Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_DeadOnceFlag(CHARACTERGUID_S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4,(FLAG)CRA_MindflayerDead_ae34ec9f-d08a-4f58-8eac-18c891c897be);

PROC_SetHitpointsPercentage_BlockSelfHealing(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, 3.0);
SetCanJoinCombat(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, 0);

DB_DialogBlockAttackButton((DIALOGRESOURCE)CRA_Escape_Mindflayer_91f3b889-e31d-f687-2f57-b18c2e8a5048);
DB_Dialogs(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, DIALOGRESOURCEGUID_CRA_Escape_Mindflayer_91f3b889-e31d-f687-2f57-b18c2e8a5048);

DB_SpotPlayers(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4,"CRA_Escape_MindFlayerSpottedPlayer",CRA_Escape_MindFlayer_StartSpotting_e60ad2c9-4990-4c0e-8a7a-0d2a35ce86a3,CRA_Escape_MindFlayer_StopSpotting_d4082360-1ca9-43dc-a052-ca5fd0df7187);
DB_SpotPlayers_CustomRadius(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4,"CRA_Escape_MindFlayerSpottedPlayer",12.0);
// Kill flag for the mind flayer
DB_KilledEvent(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4,(FLAG)CRA_Escape_State_KilledMindflayer_85d96811-0978-3afa-d7c3-56035295c2dd);

DB_GlobalFlagReactionAfterDialog((FLAG)CRA_Escape_Event_GiveInToMindflayer_6d7d1810-b42a-475b-95cb-85c6a6cdf6ac, (DIALOGRESOURCE)CRA_Escape_Mindflayer_91f3b889-e31d-f687-2f57-b18c2e8a5048);


//REGION VB about footsteps
DB_CRA_Escape_FootstepsTrigger(TRIGGERGUID_S_CRA_Escape_FootstepsBox_1_37d177db-8aa0-48e6-95d4-9ea7a7af8605);
DB_CRA_Escape_FootstepsTrigger(TRIGGERGUID_S_CRA_Escape_FootstepsBox_2_3d22b751-8353-4c42-846c-1e59b497af99);
//END_REGION

DB_OneShot_VoiceBarkTrigger((TRIGGER)S_CRA_Escape_MindflayerVB_9fdc1f34-15ac-4fa5-afef-aaae69620096, (VOICEBARKRESOURCE)CRA_Escape_VB_SawMindflayer_d0db7e14-7233-951c-c321-395067a4b717, "PerUserAndNearbyPlayers");

ApplyStatus(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, "CRA_MINDFLAYER_LYING", -1.0, 1);
PROC_CRA_Escape_MindflayerSceneInit();

KBSECTION
//REGION Init
// Can't commit crimes against the mind flayer
QRY
QRY_CRIME_BlockRegisterCrime(_, _, _, S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, _)
THEN
DB_NOOP(1);

PROC
PROC_CRA_Escape_MindflayerSceneInit()
AND
DB_CRA_Escape_FootstepsTrigger(_Trigger)
THEN
PROC_TriggerRegisterForPlayers(TRIGGERGUID_S_CRA_Escape_FootstepsBox_1_37d177db-8aa0-48e6-95d4-9ea7a7af8605);
//END_REGION

//REGION VB about footsteps
IF
EnteredTrigger(_Player, _FootstepsTrigger)
AND
DB_CRA_Escape_FootstepsTrigger(_FootstepsTrigger)
AND
QRY_OncePerUserAndNearbyPlayers(_Player,"CRA_Escape_Footsteps")
THEN
StartVoiceBark(VOICEBARKRESOURCEGUID_CRA_Escape_VB_OtherSurvivors_1a8b72d2-c0ca-5aa1-c045-80ffe2c4f1b6, _Player);
//END_REGION

//REGION Kill the mind flayer in dialog
IF
FlagSet(CRA_Escape_Event_KillMindflayer_c415a498-1598-4ac0-0bc2-c1fe5b185f7c, _Player, _) // flagType: Object
THEN
DB_CRA_Escape_KillMindflayerAfterCombat(_Player);

IF
DialogEnded(DIALOGRESOURCEGUID_CRA_Escape_Mindflayer_91f3b889-e31d-f687-2f57-b18c2e8a5048, _)
AND
DB_CRA_Escape_KillMindflayerAfterCombat(_Player)
THEN
NOT DB_CRA_Escape_KillMindflayerAfterCombat(_Player);
// Will set the appropriate flag through DB_KilledEvent()
Die(CHARACTERGUID_S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, DEATHTYPE.DoT, _Player, 1, 1);
//END_REGION

//REGION Give in to mindflayer -> you die
IF
FlagSet((FLAG)CRA_Escape_Event_MindFlayerGetUp_44a21e1e-56f5-2f5b-fa45-f50ade4e2f7f, S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, _)
THEN
RemoveStatus(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, "CRA_MINDFLAYER_LYING");

PROC
PROC_GlobalFlagReactionAfterDialog((FLAG)CRA_Escape_Event_GiveInToMindflayer_6d7d1810-b42a-475b-95cb-85c6a6cdf6ac, _, _ID)
AND
DB_DialogPlayers(_ID, _Player, 1)
AND
GetFaction(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, _MindFlayerFaction)
THEN
PROC_RemoveDialog(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4);
Die((CHARACTER)_Player, DEATHTYPE.Psychic, S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, 0, 1);
SetCanJoinCombat(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, 1);
SetEntityEvent(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4,"ClearPeaceReturn", 1);
RemoveStatus(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, "CRA_MINDFLAYER_LYING");
SetHitpointsPercentage(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, 70.0);
PROC_SetRelationToPlayers(_MindFlayerFaction, 0);
// Mind flayer is no longer dying
SetStoryDisplayName(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, "CRA_Mindflayer_Rejuvenated");
PROC_RemoveOneShotVoiceBarkTrigger((TRIGGER)S_CRA_Escape_MindflayerVB_9fdc1f34-15ac-4fa5-afef-aaae69620096,(VOICEBARKRESOURCE)CRA_Escape_VB_SawMindflayer_d0db7e14-7233-951c-c321-395067a4b717);
//END_REGION

//REGION Den confrontation finished -> mindflayer dies if it did not recuperate
PROC
PROC_DEN_RaidingParty_ConfrontationCompleted()
AND
NOT DB_GlobalFlag((FLAG)CRA_Escape_Event_GiveInToMindflayer_6d7d1810-b42a-475b-95cb-85c6a6cdf6ac)
AND
NOT DB_PermaDefeated((CHARACTER)S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4)
THEN
PROC_ForceStopDialog((CHARACTER)S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4);
Die((CHARACTER)S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, DEATHTYPE.DoT, NULL_00000000-0000-0000-0000-000000000000, 1, 0);
//END_REGION

//REGION Try to heal mindflayer -> dies
IF
StatusRemoved(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, "CRA_MINDFLAYER_LYING", _Causee, _)
AND
_Causee != NULL_00000000-0000-0000-0000-000000000000
AND
NOT DB_GlobalFlag((FLAG)CRA_MindflayerDead_ae34ec9f-d08a-4f58-8eac-18c891c897be) // flagType: Global
THEN
SetFlag((FLAG)CRA_Escape_State_TriedHelpMindflayer_b5f97a3c-ce18-c95a-8483-8804405c0a39, _Causee); // flagType: Object
Die(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, DEATHTYPE.DoT, _Causee, 1, 0);

IF
StatusApplied(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, _Status, _Causee, _)
AND
_Causee != NULL_00000000-0000-0000-0000-000000000000
AND
QRY_IsHealingStatus(_Status)
AND
NOT DB_GlobalFlag((FLAG)CRA_MindflayerDead_ae34ec9f-d08a-4f58-8eac-18c891c897be) // flagType: Global
THEN
SetFlag((FLAG)CRA_Escape_State_TriedHelpMindflayer_b5f97a3c-ce18-c95a-8483-8804405c0a39, _Causee); // flagType: Object
Die(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, DEATHTYPE.DoT, _Causee, 1, 0);
//END_REGION

//REGION Reaction to killing mind flayer
IF
KilledBy(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, _Player, _, _)
AND
DB_Players((CHARACTER)_Player)
AND
// Don't AD if we kill it with a summon and are nowhere nearby
QRY_IsInRange(_Player, S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, 20.0)
AND
NOT DB_CantTalk(_Player)
THEN
RealtimeObjectTimerLaunch(_Player,"CRA_Escape_AD_KilledMindflayer",2000);

//Adds a little bit of time between the AD and death to handle any cinematic and critical hit transitions correctly
//GUS-157076
IF
ObjectTimerFinished(_Player,"CRA_Escape_AD_KilledMindflayer")
THEN
PROC_TryStartAD(DIALOGRESOURCEGUID_CRA_Escape_AD_KilledMindflayer_4f4f79dd-9d6e-9cf6-b79d-3e0f5d126aac, _Player);
//END_REGION

//REGION Clean up
IF
FlagSet(CRA_MindflayerDead_ae34ec9f-d08a-4f58-8eac-18c891c897be, _, _)
THEN
PROC_CRA_Escape_Mindflayer_CleanUp();

IF
StatusRemoved(S_CRA_Escape_Mindflayer_d5385cd0-f371-43dc-a0a2-50381fc50ea4, "CRA_MINDFLAYER_LYING", _, _)
THEN
PROC_CRA_Escape_Mindflayer_CleanUp();

PROC
PROC_CRA_Escape_Mindflayer_CleanUp()
THEN
PROC_RemoveOneShotVoiceBarkTrigger((TRIGGER)S_CRA_Escape_MindflayerVB_9fdc1f34-15ac-4fa5-afef-aaae69620096,(VOICEBARKRESOURCE)CRA_Escape_VB_SawMindflayer_d0db7e14-7233-951c-c321-395067a4b717);

PROC
PROC_CRA_Escape_Mindflayer_CleanUp()
AND
DB_CRA_Escape_FootstepsTrigger(_Trigger)
THEN
TriggerUnregisterForPlayers((TRIGGER)_Trigger);
NOT DB_CRA_Escape_FootstepsTrigger(_Trigger);
//REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
