Version 1
SubGoalCombiner SGC_AND
INITSECTION
PROC_TriggerRegisterForPlayers(S_DEN_PrisonTrapSetBox_ffb03d40-8e8e-4907-aa52-0fc4b557979d);
PROC_TriggerRegisterForPlayers(S_DEN_PrisonTrapSpawnBox_000_4647d10e-4eac-42db-936c-e7ce807df880);
PROC_TriggerRegisterForPlayers(S_DEN_PrisonTrapSpawnBox_001_9c83e771-5d14-4042-8a69-11a39028651e);
PROC_TriggerRegisterForPlayers(S_DEN_PrisonTrapSpawnBox_002_3d7c9bb1-bccb-4db2-b1cc-6956716dfcb7);
PROC_TriggerRegisterForPlayers(S_DEN_PrisonTrapSpawnBox_003_23415d44-4e8b-439e-841e-7838d282a121);

DB_DEN_Jailbreak_AllTraps((ITEM)S_DEN_PrisonMushroomTrap_000_1eada771-1fd1-40d8-9105-f597f6e973d0);
DB_DEN_Jailbreak_AllTraps(S_DEN_PrisonMushroomTrap_001_fc1c12c8-4d08-4701-8d79-b2c45ae13387);
DB_DEN_Jailbreak_AllTraps(S_DEN_PrisonMushroomTrap_002_06db30ab-b6ac-4f98-a372-e76e5302fc79);
DB_DEN_Jailbreak_AllTraps(S_DEN_PrisonMushroomTrap_003_ea32a798-7ffd-45e1-bc84-a3c967f4c4bf);
DB_DEN_Jailbreak_AllTraps(S_DEN_PrisonMushroomTrap_004_58023cd0-80c8-4894-8e91-22b1b3c1ea18);

//Traps manually placed on the platforms, which are replaced after first long rest
DB_DEN_Jailbreak_FirstPlatformTraps((ITEM)S_DEN_PrisonMushroomTrap_000_1eada771-1fd1-40d8-9105-f597f6e973d0);
DB_DEN_Jailbreak_FirstPlatformTraps(S_DEN_PrisonMushroomTrap_002_06db30ab-b6ac-4f98-a372-e76e5302fc79);

DB_DEN_Jailbreak_TrapSpawnArea((TRIGGER)S_DEN_PrisonTrapSpawnBox_000_4647d10e-4eac-42db-936c-e7ce807df880);
DB_DEN_Jailbreak_TrapSpawnArea(S_DEN_PrisonTrapSpawnBox_001_9c83e771-5d14-4042-8a69-11a39028651e);
DB_DEN_Jailbreak_TrapSpawnArea(S_DEN_PrisonTrapSpawnBox_002_3d7c9bb1-bccb-4db2-b1cc-6956716dfcb7);
DB_DEN_Jailbreak_TrapSpawnArea(S_DEN_PrisonTrapSpawnBox_003_23415d44-4e8b-439e-841e-7838d282a121);

DB_DEN_Jailbreak_SelectedSpawn((TRIGGER)NULL_00000000-0000-0000-0000-000000000000);
NOT DB_DEN_Jailbreak_SelectedSpawn((TRIGGER)NULL_00000000-0000-0000-0000-000000000000);
DB_DEN_Jailbreak_TempSpawnList((TRIGGER)NULL_00000000-0000-0000-0000-000000000000);
NOT DB_DEN_Jailbreak_TempSpawnList((TRIGGER)NULL_00000000-0000-0000-0000-000000000000);
DB_DEN_Jailbreak_SpawnedTraps((ITEM)NULL_00000000-0000-0000-0000-000000000000);
NOT DB_DEN_Jailbreak_SpawnedTraps((ITEM)NULL_00000000-0000-0000-0000-000000000000);

SetHitpoints(S_DEN_PrisonDestructWall_e4436c10-57db-4e25-ab9f-a9abbb221be8, 4);
KBSECTION
//REGION Platform Traps
IF
EnteredTrigger(_Player, S_DEN_PrisonTrapSetBox_ffb03d40-8e8e-4907-aa52-0fc4b557979d)
AND
DB_Players(_Player)
THEN
SetFlag((FLAG)DEN_Crime_State_EscapedByPlatform_e6de8669-3209-4874-8f69-3c3ec2ec2985, NULL_00000000-0000-0000-0000-000000000000);

PROC
PROC_LongRest()
AND
DB_GlobalFlag((FLAG)DEN_Crime_State_EscapedByPlatform_e6de8669-3209-4874-8f69-3c3ec2ec2985)
THEN
NOT DB_OnlyOnce("DEN_Jailbreak_CommentOnFoundTrap");
NOT DB_OnlyOnce("DEN_Jailbreak_CommentOnTrapDeactivate");
DB_ExecuteInLevel("DEN_Jailbreak_Reset","WLD_Main_A");

PROC
PROC_ExecuteInLevel("DEN_Jailbreak_Reset","WLD_Main_A")
THEN
PROC_DEN_Jailbreak_ClearInitialTraps();
PROC_DEN_Jailbreak_ClearTraps();
PROC_DEN_Jailbreak_SetSpawnList();
PROC_DEN_Jailbreak_CheckSpawns();
PROC_DEN_Jailbreak_ClearTempList();

PROC
PROC_DEN_Jailbreak_ClearInitialTraps()
AND
DB_DEN_Jailbreak_FirstPlatformTraps(_Trap)
THEN
SetOnStage(_Trap, 0);
NOT DB_DEN_Jailbreak_AllTraps(_Trap);

PROC
PROC_DEN_Jailbreak_ClearTraps()
AND
DB_DEN_Jailbreak_SpawnedTraps((ITEM)_Trap)
THEN
RequestDelete(_Trap);
NOT DB_DEN_Jailbreak_SpawnedTraps(_Trap);
NOT DB_DEN_Jailbreak_AllTraps(_Trap);

PROC
PROC_DEN_Jailbreak_ClearTempList()
AND
DB_DEN_Jailbreak_TempSpawnList(_Trigger)
THEN
NOT DB_DEN_Jailbreak_TempSpawnList(_Trigger);

PROC
PROC_DEN_Jailbreak_SetSpawnList()
AND
DB_DEN_Jailbreak_TrapSpawnArea(_Trigger)
THEN
DB_DEN_Jailbreak_TempSpawnList(_Trigger);

IF
DB_DEN_Jailbreak_TempSpawnList(_Trigger)
THEN
DB_NOOP(1);

PROC
PROC_DEN_Jailbreak_CheckSpawns()
AND
QRY_DoNTimes(3)
AND
DB_QRY_RTN_DoNTimes(_Index)
AND
QRY_GetRandom("DB_DEN_Jailbreak_TempSpawnList", 1, "DB_DEN_Jailbreak_SelectedSpawn")
AND
DB_DEN_Jailbreak_SelectedSpawn((TRIGGER)_Trigger)
THEN
NOT DB_DEN_Jailbreak_SelectedSpawn(_Trigger);
NOT DB_DEN_Jailbreak_TempSpawnList(_Trigger);
PROC_DEN_Jailbreak_CheckForPlayer(_Trigger);

PROC
PROC_DEN_Jailbreak_CheckForPlayer((TRIGGER)_Trigger)
AND
NOT QRY_CheckOtherPlayersInTrigger(NULL_00000000-0000-0000-0000-000000000000, _Trigger)
THEN
PROC_DEN_Jailbreak_SpawnTrap(_Trigger);

PROC
PROC_DEN_Jailbreak_SpawnTrap((TRIGGER)_Trigger)
AND
TriggerGetRandomPosition(_Trigger, _X, _Y, _Z)
AND
CreateAt((ITEMROOT)PUZ_Underdark_Mushroom_Exploding_Small_A_64e2fdce-a46f-4fd6-8d9c-204f078b873d, _X, _Y, _Z, 0, 0, "", _Trap)
THEN
DB_DEN_Jailbreak_SpawnedTraps((ITEM)_Trap);
DB_DEN_Jailbreak_AllTraps((ITEM)_Trap);

IF
DB_DEN_Jailbreak_AllTraps((ITEM)_Trap)
THEN
DB_PerceptionSkillCheckADBlocked(_Trap);

PROC
PROC_HiddenPerceptionCheckSucceeded((CHARACTER)_Character,(CHARACTER) _Player,(GUIDSTRING) _Trap)
AND
DB_DEN_Jailbreak_AllTraps((ITEM)_Trap)
THEN
PROC_DEN_Jailbreak_RevealAD(_Player);

PROC
PROC_DEN_Jailbreak_RevealAD((CHARACTER)_Player)
AND
NOT DB_GlobalFlag(GLO_Mushroom_Knows_Torchstalk_274534be-89c5-40f6-ab58-f91af5a33923)
THEN
PROC_TryStartAD((DIALOGRESOURCE)DEN_Jailbreak_PAD_DiscoverTrap_3cc24fee-d6b7-7750-cbfd-f7937707f6e2, _Player);
SetFlag(GLO_Mushroom_Knows_Torchstalk_274534be-89c5-40f6-ab58-f91af5a33923);

//REGION Debug
IF
TextEvent("denresettraps")
THEN
SetFlag((FLAG)DEN_Crime_State_EscapedByPlatform_e6de8669-3209-4874-8f69-3c3ec2ec2985, NULL_00000000-0000-0000-0000-000000000000);
PROC_DEN_Jailbreak_ClearInitialTraps();
PROC_DEN_Jailbreak_ClearTraps();
PROC_DEN_Jailbreak_SetSpawnList();
PROC_DEN_Jailbreak_CheckSpawns();
PROC_DEN_Jailbreak_ClearTempList();

IF
TextEvent("denspawntraps")
THEN
PROC_DEN_Jailbreak_SetSpawnList();
PROC_DEN_Jailbreak_CheckSpawns();

IF
TextEvent("dencleartraps")
THEN
PROC_DEN_Jailbreak_ClearInitialTraps();
PROC_DEN_Jailbreak_ClearTraps();

IF
TextEvent("denrevealtraps")
AND
DB_DEN_Jailbreak_AllTraps(_Trap)
THEN
SetEntityEvent(_Trap, "StoryReveal");

IF
TextEvent("gotodenprison")
AND
DB_Players(_Player)
AND
DB_RegionPrison("WLD_Main_A_DEN", _PrisonCell, _PrisonSpawnPoint)
AND
DB_PlayerPrison(_PrisonTrigger, _, _, _FugitiveStatus)
THEN
PROC_CrimeTeleportCharacterToPrison(S_DEN_Tiefling_011_77439e86-3f60-4456-bd55-931e2f45654f, _Player, _PrisonCell, _PrisonSpawnPoint, _FugitiveStatus);


//END_REGION
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
