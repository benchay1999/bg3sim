Version 1
SubGoalCombiner SGC_AND
INITSECTION
//[Link Redacted]

//REGION Dialogs
DB_Dialogs(CHARACTERGUID_S_DEN_ZenBear_d4d4bfd2-9639-45bc-a256-2f079e53db29, (DIALOGRESOURCE)DEN_WeepingTree_Bear_aa98fb74-2a07-a047-9a1c-c7b1e8a740a2);
DB_Dialogs(S_DEN_PetWolf_63471e30-7413-4496-97de-0bdf67c2f753, DEN_DruidPet_002_fb886f7b-b763-4908-6b32-2b99d9a99a5e);
//Topaz is handled in Act1_DEN_SacredPond_Bird
DB_Dialogs(S_DEN_ExcitedBoar_5e856a8e-957a-49b1-8ec7-f430984e08ca, DEN_DruidPet_004_8c7df62f-0eec-77d3-cb12-7348e681637c);
DB_Dialogs(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, DEN_Apprentice_Bird_894b784a-c34e-8ef8-414c-e8e622551f21);

DB_Dialogs(S_DEN_BirdSender_30ee2bc0-0e69-4999-8d1c-8335c439cecf,DEN_SacredPond_BirdSender_49fd0308-d79f-c481-0441-070ab136322a);
DB_Dialogs(S_DEN_ServantIntolerant_002_6f36c7a1-edfd-4e99-bd90-6d874f68c9b8, DEN_DruidServant_002_8d0f6f2b-c878-056b-54a3-ee7ca09b3c18);
DB_Dialogs(S_DEN_ServantPatrolling_0d971a2f-5fd7-4358-8966-147ed8b106dd, DEN_DruidServant_003_9cf7d655-afce-faeb-799f-a6bfc6687196);
DB_Dialogs(S_DEN_ServantPlight_005_13db744d-8a48-4131-8801-fd5fc289fdf6, DEN_DruidServant_005_6f16f5f7-1050-ea85-7826-1f787d11d61f);
DB_Inclusion_Object(S_DEN_ServantIntolerant_002_6f36c7a1-edfd-4e99-bd90-6d874f68c9b8,(FLAG)DEN_DruidServant_Event_StartInclusion002_29667e82-3750-491d-862c-4ae61c2ae9f4,(FLAG)DEN_DruidServant_Event_EndInclusion002_c47302c7-06a0-43a0-ad57-c6f4f44152e9);

PROC_TriggerRegisterForPlayers(S_DEN_Sender_PlayerApproach_4c5333fd-c4ea-4ddd-8819-eb72e4783793);

//Ritual
DB_Dialogs(S_DEN_RitualDruid_001_9c5c4c89-890b-4e72-8698-53d36e6b4271,DEN_SacredPond_RitualDruid1_f290a927-6c77-b671-54a1-f97b53b318ad);
DB_Dialogs(S_DEN_RitualDruid_002_96e41e71-9239-498b-9cb8-8b14fd48acd0,DEN_SacredPond_RitualDruid2_3a83472c-7b32-308f-9a57-d509dbdf8243);
DB_Dialogs(S_DEN_RitualDruid_003_7cae8e24-d610-4229-859d-7b2da7c5d162,DEN_SacredPond_RitualDruid3_53e9379b-d152-f5ae-34ce-995794ec766d);
DB_Dialogs(S_DEN_RitualDruid_004_32bef45a-47ca-4c3a-b907-13a857b316fd,DEN_SacredPond_RitualDruid4_b8e5f13e-2aac-831a-42d3-64ee9f33d969);
DB_Dialogs(S_DEN_RitualDruid_005_641fc4b5-3c42-4c4d-a759-541ad437f9df,DEN_SacredPond_RitualDruid5_fb9b4400-ce5c-88c0-8cdc-fabfe4247f14);

//END_REGION
//REGION Pets
DB_DEN_DruidPets((CHARACTER)S_DEN_ZenBear_d4d4bfd2-9639-45bc-a256-2f079e53db29);
DB_DEN_DruidPets(S_DEN_VoloBear_893c5f40-a760-4b04-bd86-c8fb8f5dd08a);
DB_DEN_DruidPets(S_DEN_PetWolf_63471e30-7413-4496-97de-0bdf67c2f753);
DB_DEN_DruidPets(S_DEN_PetBird_72bd7f25-0644-4323-a428-85ba5630e6ea);
DB_DEN_DruidPets(S_DEN_ExcitedBoar_5e856a8e-957a-49b1-8ec7-f430984e08ca);
DB_DEN_DruidPets(S_DEN_ElevatorBear_04b7ef23-1002-4682-b962-33a2328bcd5c);
DB_DEN_DruidPets(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337);

DB_DoNotFace(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337);
//END_REGION
//REGION Druids
DB_DEN_IdolOwner((CHARACTER)S_DEN_RitualDruid_001_9c5c4c89-890b-4e72-8698-53d36e6b4271);

	//DB_DEN_Druids((CHARACTER)_Druid, (INTEGER)_BlockDialogSwap); 
		//_BlockDialogSwap is used in Act1_DEN_Victory after goblin hunt is completed:
		//	0 = not blocked, the character gets its AttackOnDen dialog
		//	1 = blocked, the character keeps its default dialog
		//There's no general rule anymore because some NPCs have specific needs and/or each writer handled the states differently. 
//ritual
DB_DEN_Druids((CHARACTER)S_DEN_RitualDruid_001_9c5c4c89-890b-4e72-8698-53d36e6b4271, 1); //Aelar
DB_DEN_Druids((CHARACTER)S_DEN_RitualDruid_002_96e41e71-9239-498b-9cb8-8b14fd48acd0, 1); //Karrik
DB_DEN_Druids((CHARACTER)S_DEN_RitualDruid_003_7cae8e24-d610-4229-859d-7b2da7c5d162, 1); //Inwe
DB_DEN_Druids((CHARACTER)S_DEN_RitualDruid_004_32bef45a-47ca-4c3a-b907-13a857b316fd, 1); //Elwyn
DB_DEN_Druids((CHARACTER)S_DEN_RitualDruid_005_641fc4b5-3c42-4c4d-a759-541ad437f9df, 1); //Tahan
//guards
DB_DEN_Druids((CHARACTER)S_DEN_DruidGuard_001_d1de6b4d-02d1-47f9-8e9e-55a593e3dc71, 0); //Jeorna
DB_DEN_Druids((CHARACTER)S_DEN_DruidGuard_002_8e2433db-55e9-4d03-a8fc-f8d02711907c, 0); //Maggran
DB_DEN_Druids((CHARACTER)S_DEN_DruidGuard_003_b888b138-951a-4092-b356-333b4b65123d, 0); //Mino
//at pond
DB_DEN_Druids((CHARACTER)S_DEN_BirdSender_30ee2bc0-0e69-4999-8d1c-8335c439cecf, 0); //
//in cave
DB_DEN_Druids((CHARACTER)S_DEN_ServantPlight_005_13db744d-8a48-4131-8801-fd5fc289fdf6, 0); //Arron
//secret tunnel / lair
DB_DEN_Druids((CHARACTER)S_DEN_ScoutCaptive_f5b5819f-1636-4f2e-82bb-709522cc399f, 1); //Findal
//in lair
DB_DEN_Druids((CHARACTER)S_DEN_ServantIntolerant_002_6f36c7a1-edfd-4e99-bd90-6d874f68c9b8, 0); //Marcoryl
DB_DEN_Druids((CHARACTER)S_DEN_ServantGnome_003_0d971a2f-5fd7-4358-8966-147ed8b106dd, 0); //Loic
DB_DEN_Druids((CHARACTER)S_DEN_DruidLeader_95eb2b0b-a522-4ea5-8167-c5f4d1418156, 1); //Kagha
DB_DEN_Druids((CHARACTER)S_DEN_DruidOpposed_322cf8d2-32ee-453f-987e-f12b32652b5d, 0); //Rath
DB_DEN_Druids((CHARACTER)S_DEN_Apprentice_7cabf226-e34b-4556-8903-a45d0fe26caf, 0); //Nettie
//not included: 
//shadow druids
//END_REGION
//REGION Arron
DB_ItemOwnerShipTriggers("WLD_Main_A", (TRIGGER)S_DEN_ServantPlight_005_Ownership_8e4e1b39-0e73-4d4a-bb0e-4080a86392e9, (CHARACTER)S_DEN_ServantPlight_005_13db744d-8a48-4131-8801-fd5fc289fdf6);
//END_REGION
//REGION ZenBear
SetOnStage(S_DEN_BearReward_8cedc1ea-1786-41d3-a70a-cd49aee78730,0); // hidden until the rock is moved/destroyed
TriggerRegisterForItems(S_DEN_ZenBearFishBox_c77cec9a-c6e2-491d-9a02-7bad4a880cec);
//END_REGION
//REGION Bird Sender
//druid talking to bird
//PROC_DEN_SacredPond_BirdSceneInit(S_DEN_BirdSender_30ee2bc0-0e69-4999-8d1c-8335c439cecf);

//END_REGION
//REGION Idol/Ritual
ApplyStatus(S_DEN_SilvanusIdol_a841d36c-9a00-4a26-943e-c0af6895bb16, "DEN_SILVANUS_IDOLAURA", -1.0, 1, NULL_00000000-0000-0000-0000-000000000000);

PROC_LoopEffect(VFX_Script_DEN_RiteOfThorns_Idol_DruidRitual_01_2ebc09c6-1909-4bbc-84b2-66e44378eeb2,S_DEN_RitualHelper_cf34a55b-26ba-4fca-bc0d-168e719c59e6,"DEN_SacredPond_RiteOfThorns","WLD_Main_A","");

DB_DEN_RitualDruids((CHARACTER)S_DEN_RitualDruid_001_9c5c4c89-890b-4e72-8698-53d36e6b4271);
DB_DEN_RitualDruids((CHARACTER)S_DEN_RitualDruid_005_641fc4b5-3c42-4c4d-a759-541ad437f9df);
DB_DEN_RitualDruids((CHARACTER)S_DEN_RitualDruid_004_32bef45a-47ca-4c3a-b907-13a857b316fd);
DB_DEN_RitualDruids((CHARACTER)S_DEN_RitualDruid_003_7cae8e24-d610-4229-859d-7b2da7c5d162);
DB_DEN_RitualDruids((CHARACTER)S_DEN_RitualDruid_002_96e41e71-9239-498b-9cb8-8b14fd48acd0);

DB_DEN_RitualDialogs((DIALOGRESOURCE)DEN_SacredPond_RitualDruid1_f290a927-6c77-b671-54a1-f97b53b318ad);
DB_DEN_RitualDialogs((DIALOGRESOURCE)DEN_SacredPond_RitualDruid2_3a83472c-7b32-308f-9a57-d509dbdf8243);
DB_DEN_RitualDialogs((DIALOGRESOURCE)DEN_SacredPond_RitualDruid3_53e9379b-d152-f5ae-34ce-995794ec766d);
DB_DEN_RitualDialogs((DIALOGRESOURCE)DEN_SacredPond_RitualDruid4_b8e5f13e-2aac-831a-42d3-64ee9f33d969);
DB_DEN_RitualDialogs((DIALOGRESOURCE)DEN_SacredPond_RitualDruid5_fb9b4400-ce5c-88c0-8cdc-fabfe4247f14);

DB_DEN_SacredPond_PoisonPoints(S_DEN_PoisonPoint_001_cefa2a85-11b8-48dc-ab8b-baa77cf5d349);
DB_DEN_SacredPond_PoisonPoints(S_DEN_PoisonPoint_002_cc6f8595-a8af-4e18-b8e7-73210fb3923a);
DB_DEN_SacredPond_PoisonPoints(S_DEN_PoisonPoint_003_0775bfb8-c8ba-4b79-b3e5-7a2da6891c8b);
DB_DEN_SacredPond_PoisonPoints(S_DEN_PoisonPoint_004_d1c1ff48-b2af-4ba1-8385-e2fc356ab3ef);
DB_DEN_SacredPond_PoisonPoints(S_DEN_PoisonPoint_006_56551f10-c9b3-480f-8193-dea2d2b84583);
DB_DEN_SacredPond_PoisonPoints(S_DEN_PoisonPoint_005_5dc31b9e-2795-470d-9034-e175f5fb9f77);

DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_000_d48e68e4-74eb-4e09-90a5-cf502ee18140);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_001_88517dee-39a9-402b-8bc1-32deafd193a3);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_002_dd40d50d-b465-4523-96f6-cdfdb0804184);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_003_977ffe35-20a4-464b-814d-fcfa15f95545);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_004_87ff0188-a2eb-4088-b06b-62083d26ecfe);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_005_b7f58955-e619-426b-abe0-8a2e2f9a6a3b);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_006_5eb463ea-ab20-4e66-b8e4-06724b2a03ae);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_007_2b162d15-f9a3-4133-979b-974ac59ebb60);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_008_e98c9446-48d7-49f5-9eca-67484fef3bc2);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_009_5b0c7f49-6a37-44ef-901a-932129287107);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_010_7714ed5c-1e34-444f-8d8f-12fd81b7ef66);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_011_416c6d79-cbc8-4462-b24d-93bc54d5bf27);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_012_bf920b7b-e121-4f7e-98f4-53799dbeff8c);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_013_59f1c6bf-1a8b-44e2-bc8f-86db6ec0dfe3);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_014_de5b598f-d25e-44e1-9a6f-fcb41c1d20c7);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_015_a21e6aee-0cef-4fac-93e6-a52e517fe0b8);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_016_7db7636a-f0a7-4772-b2d5-b9c428f497a5);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_017_50a1b48e-5fcd-413f-89c1-c0b34d86c676);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_018_91f4676e-550a-4f6a-a14a-bc65db91456b);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_019_d6217d07-ac71-4111-b345-5653aa7bf81b);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_020_b6b5831a-5e5c-4609-93a8-596af660424a);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_021_44370c17-8ca4-459c-a0af-5b20ff685a55);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_022_1b001ade-2461-4b1c-9cb7-4cf464ef1b87);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_023_3c31a1dd-56d2-40ca-b229-0a924127c3d0);
DB_DEN_SacredPond_SmallPoisonPoints(S_DEN_SmallPoisonPoint_024_cf39eebf-293d-4bda-97ce-c2d299bfb3ba);
//END_REGION

ApplyStatus(S_DEN_ExcitedBoar_5e856a8e-957a-49b1-8ec7-f430984e08ca, "ANIM_OVERRIDE_BOAR_EXCITED", -1.0);

DB_DoNotFaceDialog((CHARACTER)S_DEN_BirdSender_30ee2bc0-0e69-4999-8d1c-8335c439cecf, DEN_SacredPond_AD_SearchingBird_cbfff229-fd9a-8468-4ad3-2ac28d58ec94);

ClearOwnership(S_DEN_BearRock_80a525ad-80df-4473-944e-0ca6a5a84676);
ClearOwnership(S_DEN_BearReward_8cedc1ea-1786-41d3-a70a-cd49aee78730);
KBSECTION
//REGION Debug

IF
TextEvent("silvanuswrath")
THEN
PROC_DEN_SacredPond_SetDruidsKilledAtm();

//END_REGION

//REGION Trader (Arron)

IF
FlagSet(DEN_DruidServant005_Event_RaiseAttitude_3d4dc5fc-661f-b670-f717-55bf055e8c19, _Player, _)
THEN
AddAttitudeTowardsPlayer(S_DEN_ServantPlight_005_13db744d-8a48-4131-8801-fd5fc289fdf6, (CHARACTER)_Player, 50);

//END_REGION

//REGION Bird intro (Tingmiaq & Apikusis)
IF
DialogEnded(DEN_Apprentice_Bird_894b784a-c34e-8ef8-414c-e8e622551f21, _)
THEN
SetFlag(DEN_SenderBird_Event_TakeOff_ce3a4f22-36e7-1763-1eb8-a97f9f182125);

IF
FlagSet(DEN_SenderBird_Event_TakeOff_ce3a4f22-36e7-1763-1eb8-a97f9f182125, _, _)
THEN
PROC_DEN_SacredPond_InitiateBirdTakeOff();

PROC
PROC_DEN_SacredPond_InitiateBirdTakeOff()
AND
NOT DB_GlobalFlag(DEN_SacredPond_State_BirdLeft_9858f80e-2973-41cf-9118-24c9a1e0c743)
THEN
PROC_GlobalSetFlagAndCache(DEN_SacredPond_State_BirdLeft_9858f80e-2973-41cf-9118-24c9a1e0c743);
SetForceUpdate(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, 1);
LookAtEntity(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337,S_DEN_BirdLeavePoint_51b6be64-0750-47f5-9148-52fd103aa674);
SetHasDialog(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, 0);
RealtimeObjectTimerLaunch(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, "DEN_SenderBird_TakeOff", 1200);
RealtimeObjectTimerLaunch(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, "DEN_SenderBird_CheckIfBirdGone", 7000);
PROC_TriggerUnregisterForPlayers(S_DEN_Sender_PlayerApproach_4c5333fd-c4ea-4ddd-8819-eb72e4783793);

IF
ObjectTimerFinished(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, "DEN_SenderBird_TakeOff")
AND
NOT DB_Defeated(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337)
THEN
StopAnimation(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, 0);
PlayAnimation(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, CUST_TakeOff_01_5f3d7746-eb42-4599-849e-cdd8e6fecce6, "DEN_SacredPond_Event_BirdTookOff"); //anim: CUST_TakeOff_01

IF
EntityEvent(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, "DEN_SacredPond_Event_BirdTookOff")
THEN
SetForceUpdate(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, 0);
SetOnStage(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, 0);

//I don't understand why, but the bird doesn't fly away when the scene is cancelled. The call is called, but osiris never receives the event so the bird just stays there... hacking it.
IF
ObjectTimerFinished(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, "DEN_SenderBird_CheckIfBirdGone")
AND
IsOnStage(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, 1)
AND
NOT DB_PermaDefeated(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337)
THEN
SetOnStage(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, 0);

IF
ObjectTimerFinished(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, "DEN_SenderBird_CheckIfBirdGone")
THEN
SetForceUpdate(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, 0);

//Force cancel
PROC
PROC_State_Changed(S_DEN_RangerDen_SUB_50062397-bf9c-4765-9cbc-e40b5148f211, "DEN", (STRING)_)
AND
QRY_OnlyOnce("DEN_SacredPond_BirdScene")
THEN
SetFlag(DEN_SenderBird_Event_TakeOff_ce3a4f22-36e7-1763-1eb8-a97f9f182125);

//Debug reset
IF
TextEvent("DEN_SacredPond_ResetBird")
THEN
ClearFlag(DEN_SenderBird_Event_TakeOff_ce3a4f22-36e7-1763-1eb8-a97f9f182125);
ClearFlag(DEN_SacredPond_State_BirdLeft_9858f80e-2973-41cf-9118-24c9a1e0c743);
SetForceUpdate(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, 0);
SetOnStage(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, 1);
SetVisible(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, 1);
StopAnimation(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337, 1);
PROC_ForceStopDialog(S_DEN_SenderBird_bf90aa40-232b-40fd-a43e-699bb964e337);

//END_REGION

//REGION Ritual - Ownership of Idol of Silvanus

PROC
PROC_StateSet_PermaDefeated(_DeadServant)
AND
DB_DEN_IdolOwner((CHARACTER)_DeadServant)
THEN
NOT DB_DEN_IdolOwner((CHARACTER)_DeadServant);
PROC_DEN_SacredPond_UpdateOwnership();

PROC
PROC_DEN_SacredPond_UpdateOwnership()
AND
DB_DEN_Druids(_NewOwner,_)
AND
NOT DB_DEN_IdolOwner(_)
AND
NOT DB_PermaDefeated(_NewOwner)
THEN
DB_DEN_IdolOwner(_NewOwner);
DB_ItemOwnerShipTriggers("WLD_Main_A", S_DEN_PondOwnership_aff112d5-b79e-405e-9114-6b70aea65a06, _NewOwner);

//END_REGION
//REGION Ritual - Rite of Thorns

IF
DB_DEN_RitualDruids(_RitualDruid)
THEN
DB_DoNotFace(_RitualDruid);
PROC_CRIME_MusicalPerformance_DisableAllReactions(_RitualDruid);

//--- Druid VB
IF
DialogEnded(_Dialog, _Inst)
AND
DB_DEN_RitualDialogs(_Dialog)
AND
NOT DB_GlobalFlag(DEN_SacredPond_Event_RecognizedRiteVBPlayed_acf83b44-7423-e640-e8fc-b93a07f54f54)
AND
NOT DB_GlobalFlag(DEN_DruidAttack_State_DruidSetup_96fc1420-fa07-4235-98c3-ecb34fcb6b0a)
AND
DB_DialogPlayers(_Inst, _Player, 1)
AND
GetFlag(DEN_ShadowDruid_Knows_KaghaExplainedRite_79678e1b-2002-b357-1ebb-b3f059d1643c, _Player, 0)
THEN
StartVoiceBark(DEN_SacredPond_VB_RecognizeRite_4e4159e0-fe68-beec-bba7-dc5f767dc08b, (CHARACTER)_Player); //VB will start only if a DRUID is nearby


//--- Stopping the looping VFX
IF
FlagSet(DEN_GroveConflict_State_DruidsPacified_0f747b55-d68d-436f-ac33-9fecaf30387d,NULL_00000000-0000-0000-0000-000000000000,_)
THEN
PROC_StopLoopEffect(S_DEN_RitualHelper_cf34a55b-26ba-4fca-bc0d-168e719c59e6,"DEN_SacredPond_RiteOfThorns");

PROC
PROC_State_Changed(S_DEN_RangerDen_SUB_50062397-bf9c-4765-9cbc-e40b5148f211, "DEN", _)
AND
DB_DEN_RitualDruids(_RitualDruid)
THEN
NOT DB_DoNotFace(_RitualDruid);
PROC_CRIME_MusicalPerformance_EnableAllReactions(_RitualDruid);

PROC
PROC_State_Changed(S_DEN_RangerDen_SUB_50062397-bf9c-4765-9cbc-e40b5148f211, "DEN", _)
THEN
PROC_StopLoopEffect(S_DEN_RitualHelper_cf34a55b-26ba-4fca-bc0d-168e719c59e6,"DEN_SacredPond_RiteOfThorns");


//--- Blocking some stuff while they're concentrating
IF
StatusApplied((CHARACTER)_Char,"DEN_RITUAL",_,_)
THEN
SetDoNotFaceFlag(_Char, 1);
PROC_CharacterDisableCrime(_Char,"Distraction");

IF
StatusRemoved((CHARACTER)_Char,"DEN_RITUAL",_,_)
THEN
SetDoNotFaceFlag(_Char, 0);
PROC_CharacterEnableCrime(_Char,"Distraction");


//--- Ritual stopped
PROC
PROC_State_Changed(S_DEN_RangerDen_SUB_50062397-bf9c-4765-9cbc-e40b5148f211, "DEN", (STRING)_NewState)
AND
NOT DB_GlobalFlag(DEN_SacredPond_Event_StopRitual_c9f24eda-2847-4620-9fa2-974b004ac147)
AND
DB_State_Priority(S_DEN_RangerDen_SUB_50062397-bf9c-4765-9cbc-e40b5148f211, "DEN", _NewState, _NewStatePrio)
AND
DB_State_Priority(S_DEN_RangerDen_SUB_50062397-bf9c-4765-9cbc-e40b5148f211, "DEN", "DEN_State_RitualStopped", _RitualStoppedPrio)
AND
_NewStatePrio >= _RitualStoppedPrio
THEN
SetFlag(DEN_SacredPond_Event_StopRitual_c9f24eda-2847-4620-9fa2-974b004ac147,NULL_00000000-0000-0000-0000-000000000000);

//END_REGION
//REGION Ritual - Wrath of Silvanus

PROC
PROC_DEN_DruidAttack_ResetGrove(DEN_DruidAttack_State_PrepAfterDeathOfDruids_b4754af7-07ec-433c-87d2-e78ef4c79e6e)
THEN
PROC_DEN_SacredPond_SetDruidsKilledAtm();

PROC
PROC_DEN_SacredPond_SetDruidsKilledAtm()
THEN
CreateSurface(S_DEN_PoisonPondPoint_9b6c3ec0-d3ac-4ca4-b088-0375f6338d40, "SurfacePoisonCloud", 8.5, -1.0);
CreateSurface(S_DEN_PoisonPondPoint_9b6c3ec0-d3ac-4ca4-b088-0375f6338d40, "SurfaceNone", 5.8, -1.0);
//todo: need the correct trigger...
//TriggerSetAtmosphere(S_DEN_ATM_WLD_RangerCamp_I_7b4cdef6-eee6-7a58-dadc-6a0492a85fd3,"0f26e9db-09b3-96f3-b1be-03ae9c87c026"); //ATM_WLD_Forest_A_Gardens
TriggerSetAtmosphere(S_DEN_ATM_DruidGrove_3e34e106-2905-490c-89c9-b28a63fcae99,"0f26e9db-09b3-96f3-b1be-03ae9c87c026"); //ATM_WLD_Forest_A_Gardens

PROC
PROC_DEN_SacredPond_SetDruidsKilledAtm()
AND
DB_DEN_SacredPond_PoisonPoints(_Point)
THEN
CreatePuddle(_Point,"SurfacePoisonCloud",30,50,1,5,1.0);

PROC
PROC_DEN_SacredPond_SetDruidsKilledAtm()
AND
DB_DEN_SacredPond_SmallPoisonPoints(_Point)
THEN
CreatePuddle(_Point,"SurfacePoisonCloud",5,10,1,2,1.0);

PROC
PROC_DEN_SacredPond_SetDruidsKilledAtm()
THEN
TriggerSetSoundState((TRIGGER)Amb_SV_RangerCamp_DruidPond_QD_27563daa-f195-467c-957a-41dbf6b70cab,"S_DEN_SacredPond","DruidsKilled",0);

//END_REGION

//REGION ZenBear (Ormn)
//--- Fish
IF
EntityEvent(S_DEN_ZenBear_d4d4bfd2-9639-45bc-a256-2f079e53db29,"DEN_ZenBear_Fish")
THEN
TimerLaunch("DEN_ZenBear_Fish",800);

IF
EntityEvent(_Fish,"DEN_ZenBear_DeleteOffStageFish")
THEN
RequestDelete((ITEM)_Fish);

QRY
QRY_DEN_ZenBear_Fish(0,(INTEGER)_Prev)
AND
TriggerGetRandomPosition(S_DEN_ZenBearFishBox_c77cec9a-c6e2-491d-9a02-7bad4a880cec,_DX,_DY,_DZ)
AND
PositionIsInTrigger(_DX,_DY,_DZ,S_DEN_ZenBearFishBox_c77cec9a-c6e2-491d-9a02-7bad4a880cec, 1) //No fish if position not in trigger.
AND
GetPosition(S_DEN_ZenBearFishHelper_ec560fe6-dd5a-4fe2-958d-5405afbce994,_SX,_SY,_SZ)
AND
Random(360,_RY)
AND
IntegerToReal(_RY,_AY)
AND
CreateAt(CONS_GEN_Food_Fish_A_7dfc4b31-a0fb-48a4-8950-83b61c03fae5,_SX,_SY,_SZ,0,0,"",_Fish)
THEN
ToTransform((ITEM)_Fish,_SX,_SY,_SZ,0.0,_AY,0.0);
ItemDragToPosition((ITEM)_Fish,_DX,_DY,_DZ);

IF
TimerFinished("DEN_ZenBear_Fish")
AND
NOT DB_CantAct(S_DEN_ZenBear_d4d4bfd2-9639-45bc-a256-2f079e53db29)
AND
QRY_DEN_ZenBear_Fish(0,0)
THEN
DB_NOOP(1);

//--- Reward
IF
Moved(S_DEN_BearRock_80a525ad-80df-4473-944e-0ca6a5a84676)
AND
DB_Players(_Player)
THEN
SetFlag(DEN_ZenBear_Event_RevealReward_c6fef36d-20b4-3f21-a2e5-e7fcedcbf316, NULL_00000000-0000-0000-0000-000000000000, 0);
SetOnStage(S_DEN_BearReward_8cedc1ea-1786-41d3-a70a-cd49aee78730,1);
PROC_HideMarker((CHARACTER)_Player,"DEN_DruidPond_BearReward");

IF
DestroyedBy(S_DEN_BearRock_80a525ad-80df-4473-944e-0ca6a5a84676, _, _, _)
AND
DB_Players(_Player)
THEN
SetFlag(DEN_ZenBear_Event_RevealReward_c6fef36d-20b4-3f21-a2e5-e7fcedcbf316, NULL_00000000-0000-0000-0000-000000000000, 0);
SetOnStage(S_DEN_BearReward_8cedc1ea-1786-41d3-a70a-cd49aee78730,1);
PROC_HideMarker((CHARACTER)_Player,"DEN_DruidPond_BearReward");

IF
FlagSet(DEN_ZenBear_Event_RevealReward_c6fef36d-20b4-3f21-a2e5-e7fcedcbf316, NULL_00000000-0000-0000-0000-000000000000, _Inst)
AND
DB_DialogPlayers(_Inst, _Player, _)
THEN
PROC_ShowMarker((CHARACTER)_Player,"DEN_DruidPond_BearReward");

//END_REGION

//REGION Druids don't cower

IF
DB_DEN_Druids(_Druid, _)
THEN
SetFlag((FLAG)GLO_State_NoCower_8180293e-4d0d-4e9b-9dfc-b195d4c67ddb, _Druid, 0, 1);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
