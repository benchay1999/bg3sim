Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION Register Triggers 
PROC_TriggerRegisterForPlayers(S_FOR_Bottomless_UnderdarkJumpVB_12940f79-db65-4c11-9106-276f8bc37f9a);
PROC_TriggerRegisterForPlayers(S_FOR_Bottomless_EnterWellVB_1252637b-513c-4b0c-89fa-24dc21980ca1);
PROC_TriggerRegisterForPlayers(S_FOR_Bottomless_SpiderQueenVB_ac466251-ebc9-4a5e-9681-af04f62abf43);
PROC_TriggerRegisterForPlayers(S_FOR_Bottomless_SpiderNest_2e7f4b31-7b9c-498a-909f-dde459d76776);
PROC_TriggerRegisterForParty((TRIGGER)S_FOR_Bottomless_Queen_WebArea_9befd44f-43a6-4ca5-a962-90ce55aad023);
PROC_TriggerRegisterForParty((TRIGGER)S_FOR_Bottomless_WellCave_2ae9c8b4-c714-49aa-97e4-675050ce3a6d);
PROC_TriggerRegisterForParty((TRIGGER)S_FOR_BottomlessWell_SUB_f54505ef-59fd-4aed-b3b2-0e5d9262c52d);
PROC_TriggerRegisterForParty((TRIGGER)S_FOR_Bottomless_UnderdarkEntrance_de806dd8-ca47-4f20-b7d0-6d50b1328ae5);
//END_REGION

//REGION Hidden Well Entrance
SetOnStage(S_FOR_VillageWell_Teleporter_695ed0cb-8afb-499b-83f4-289f0eddb23e, 0);
DB_IgnoreMutingStatussesDialog((DIALOGRESOURCE)FOR_BottomlessWell_InteractWithWell_0ce91155-2193-8c8f-4734-976a495808d3);
DB_DialogMoneyTransfer(2, (DIALOGRESOURCE)FOR_BottomlessWell_InteractWithWell_0ce91155-2193-8c8f-4734-976a495808d3, 1); // If the player has a coin to throw in the well
//END_REGION

//REGION Ettercap Disturbances
DB_CRIME_Bottomless_EttercapHasWarned(0);
DB_Bottomless_Ettercaps((CHARACTER)S_FOR_Bottomless_Ettercap_01_1c167d87-048b-40ac-9117-c87d5a2471fd);
DB_Bottomless_Ettercaps((CHARACTER)S_FOR_Bottomless_Ettercap_02_5c57bfc0-cd82-4b9f-9b70-b5475fb121db);

DB_Bottomless_ForbiddenSurfaces("SurfaceWeb");
DB_Bottomless_ForbiddenSurfaces("SurfaceInvisibleWeb");

DB_ItemOwnerShipTriggers("WLD_Main_A", (TRIGGER)S_FOR_Bottomless_WellCave_2ae9c8b4-c714-49aa-97e4-675050ce3a6d, (CHARACTER)S_FOR_Bottomless_Ettercap_01_1c167d87-048b-40ac-9117-c87d5a2471fd);
DB_ItemOwnerShipTriggersFallback((TRIGGER)S_FOR_Bottomless_WellCave_2ae9c8b4-c714-49aa-97e4-675050ce3a6d, (CHARACTER)S_FOR_Bottomless_Ettercap_02_5c57bfc0-cd82-4b9f-9b70-b5475fb121db);

// Exclude non-webbed items for ettercap ownership
DB_ItemOwnerShipClearItem("WLD_Main_A", (ITEM)S_FOR_Bottomless_VictimBackpack_59744953-6a46-4367-890b-567e9d798d1f);
DB_ItemOwnerShipClearItem("WLD_Main_A", (ITEM)S_FOR_Bottomless_Backpack_000_70c80071-9d2a-d817-13c4-4050f8bc4be4);
DB_ItemOwnerShipClearItem("WLD_Main_A", (ITEM)S_FOR_Bottomless_Backpack_001_37794ed6-e548-decb-072a-b109a7e1922f);
DB_ItemOwnerShipClearItem("WLD_Main_A", (ITEM)S_FOR_Bottomless_Backpack_002_c57205b5-da9d-47ab-9917-c316b007735a);
DB_ItemOwnerShipClearItem("WLD_Main_A", (ITEM)S_FOR_Bottomless_Backpack_003_42f8ef86-935f-41cf-99c9-b57fd78b07a7);
DB_ItemOwnerShipClearItem("WLD_Main_A", (ITEM)S_FOR_Bottomless_MurderWeap_b51adef1-b17e-463b-b009-c71db48bd195);
DB_ItemOwnerShipClearItem("WLD_Main_A", (ITEM)S_FOR_Bottomless_Shield_5442e240-8f04-49c4-936a-7d4b0f311141);
DB_ItemOwnerShipClearItem("WLD_Main_A", (ITEM)S_FOR_Bottomless_Greatsword_abd9f395-5451-49ed-bbbb-4f167ccd24d4);
DB_ItemOwnerShipClearItem("WLD_Main_A", (ITEM)S_FOR_Bottomless_Bonepile_000_2de3acc6-8338-d242-1464-40afcf7d8a4d);
DB_ItemOwnerShipClearItem("WLD_Main_A", (ITEM)S_FOR_Bottomless_Bonepile_001_7193158f-6da7-d074-325e-3881de065954);
DB_ItemOwnerShipClearItem("WLD_Main_A", (ITEM)S_FOR_Bottomless_Skull_000_54df6cb9-8ea9-d089-3b7c-974866b2affb);
DB_ItemOwnerShipClearItem("WLD_Main_A", (ITEM)S_FOR_Bottomless_Skeleton_000_110c4086-d841-d659-160a-99b4bfda9c69);
DB_ItemOwnerShipClearItem("WLD_Main_A", (ITEM)S_FOR_Bottomless_Skeleton_001_736161e2-d963-d933-3a37-9fa3e02d9a68);
DB_ItemOwnerShipClearItem("WLD_Main_A", (ITEM)S_FOR_Bottomless_Torch_000_54164ec6-55f8-4fe9-851b-5cc56c0fcc97);
DB_ItemOwnerShipClearItem("WLD_Main_A", (ITEM)S_FOR_Bottomless_Torch_001_132c29ff-df2b-4ef8-9bac-7b35ebd3af82);
//END_REGION

//REGION Matriarch Disturbances
SetOwner(BLD_GEN_Platform_SpiderWeb_A_4D_H0n5_12W_A_Dynamic_000_99b35f7d-b026-185a-c184-eb3933d7cb3e, S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b);
SetOwner(BLD_GEN_Platform_SpiderWeb_A_4D_H0n5_12W_A_Dynamic_000_5c5cffb5-ee6f-8d51-2e75-bc4d8662a1d7, S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b);
SetOwner(BLD_GEN_Platform_SpiderWeb_A_4D_H0n5_12W_A_Dynamic_000_8bf96aba-cfb6-8fcf-27c6-92aa9f2331a6, S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b);
SetOwner(BLD_GEN_Platform_SpiderWeb_A_4D_H0n5_12W_A_Dynamic_000_829ea11b-056a-8bd2-339a-bf26bb0143f0, S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b);
SetOwner(BLD_GEN_Platform_SpiderWeb_A_4D_H0n5_12W_A_Dynamic_000_c0f55c6e-b02b-8feb-5727-011c52cb6885, S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b);
//END_REGION


DB_KnowledgeCheckTrigger_AD("FOR_Bottomless_EnterWellVB", (TRIGGER)S_FOR_Bottomless_EnterWellVB_1252637b-513c-4b0c-89fa-24dc21980ca1, "Survival", (DIFFICULTYCLASS)DC_Legacy_15_bddbb9b8-a242-4c3e-a2eb-3fd274c0c539, (DIALOGRESOURCE)FOR_BottomlessWell_PAD_WebWarning_b3570619-4a8d-4c2b-0534-4b342d58119c, (FLAG)FOR_Bottomless_State_SurvivalCheckSuccess_63ef62fd-cce6-496b-b7cc-204af051c8f0);
DB_PermaDefeatedFlag(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b, (FLAG)FOR_Bottomless_SpiderQueen_State_PermaDefeated_b749271f-fbee-43ec-bbc7-b1199d0f794a);

// In Tactician Spider Matriarch shouldn't fall into chasms
PROC_FOR_BottomlessWell_SetChasmImmunity(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b);
KBSECTION
//REGION Well Entrance
// Teleport the player when they select the dialog option to descend the well
IF
DialogEnded((DIALOGRESOURCE)FOR_BottomlessWell_InteractWithWell_0ce91155-2193-8c8f-4734-976a495808d3, _ID)
AND
DB_GlobalFlag((FLAG)FOR_Bottomless_Event_DescendedWell_919fd0e7-ae61-4520-b869-9b19db7b88a6)
AND
DialogGetInvolvedPlayer(_ID, 1, _Player)
THEN
SetOnStage(S_FOR_VillageWell_Teleporter_695ed0cb-8afb-499b-83f4-289f0eddb23e, 1);
SetOnStage(S_FOR_VillageWell_21be1469-8f1c-4934-9235-112364aa3df9, 0);
Use((CHARACTER)_Player, S_FOR_VillageWell_Teleporter_695ed0cb-8afb-499b-83f4-289f0eddb23e, "FOR_Bottomless_DescendedWellDialog");

// If the player uses the climbable wall inside the well set flag to prevent dialog and allow Teleport action
// Replace well with a version that is always visible on the minimap since we can't set the visibility without directly interacting with the original well
// Accounts for the player finding the way out of the well after going through the Blacksmith's Cellar
IF
UseStarted(_Player, S_FOR_Bottomless_WellExit_1a2eebce-4d20-47ce-a6a9-997aa1dafe2e)
AND
NOT DB_GlobalFlag((FLAG)FOR_Bottomless_Event_DescendedWell_919fd0e7-ae61-4520-b869-9b19db7b88a6)
THEN
SetFlag((FLAG)FOR_Bottomless_Event_DescendedWell_919fd0e7-ae61-4520-b869-9b19db7b88a6, NULL_00000000-0000-0000-0000-000000000000);
SetOnStage(S_FOR_VillageWell_21be1469-8f1c-4934-9235-112364aa3df9, 0);
SetOnStage(S_FOR_VillageWell_Teleporter_695ed0cb-8afb-499b-83f4-289f0eddb23e, 1);

IF
FlagSet((FLAG)FOR_BottomlessWell_InteractWithWell_Knows_ThrewCopper_0d5a8f56-969a-487d-724c-60d559e76806, _Player, _)
AND
DB_Players((CHARACTER)_Player)
THEN
RemoveGoldFromMagicPockets(_Player, 1);

PROC
PROC_BlockUseOfItem(_Player, (ITEM)S_FOR_VillageWell_21be1469-8f1c-4934-9235-112364aa3df9)
AND
DB_Players(_Player)
AND
QRY_SpeakerIsAvailable(_Player, 0, 1)
AND
NOT DB_Is_WildShaped(_Player)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)FOR_BottomlessWell_InteractWithWell_0ce91155-2193-8c8f-4734-976a495808d3, (ITEM)S_FOR_VillageWell_21be1469-8f1c-4934-9235-112364aa3df9, _Player)
THEN
DB_NOOP(1);
//END_REGION

//REGION Well Cave 
IF
FlagSet((FLAG)FOR_BottomlessWell_InteractWithWell_Knows_ThrewCopper_0d5a8f56-969a-487d-724c-60d559e76806, _, _)
AND
GetPosition(S_FOR_Bottomless_ThrowCoinHelper_c2bda10b-d487-4332-9696-9cda70ff97cf, _X, _Y, _Z)
AND
CreateAt(LOOT_Gold_A_1c3c9c74-34a1-4685-989e-410dc080be6f, _X, _Y, _Z, 0, 0, "", _ThrownCoin)
THEN
ScatterAt((ITEM)_ThrownCoin, _X, _Y, _Z);
//END_REGION

//REGION Ettercap Disturbances
// Disturbance queries
QRY
QRY_CRIME_BlockRegisterCrime((CHARACTER)_Criminal, (STRING)_Crime, (ITEM)_Item, _, _)
AND
DB_InRegion((CHARACTER)_Criminal, (TRIGGER)S_FOR_Bottomless_WellCave_2ae9c8b4-c714-49aa-97e4-675050ce3a6d)
AND
QRY_CRIME_IsCrimeFamilyMember(_Crime, "UseForbiddenItem")
AND
GetOwner(_Item, _Ettercap)
AND
DB_Bottomless_Ettercaps(_Ettercap)
AND
QRY_CRIME_Bottomless_WarningIssued()
THEN
PROC_CRIME_Bottomless_RegisterInvestigationCrime(_Criminal, _Ettercap, _Item);

QRY
QRY_CRIME_BlockRegisterCrime((CHARACTER)_Criminal, (STRING)_Crime, (ITEM)_Item, _, _)
AND
DB_InRegion((CHARACTER)_Criminal, (TRIGGER)S_FOR_Bottomless_WellCave_2ae9c8b4-c714-49aa-97e4-675050ce3a6d)
AND
QRY_CRIME_IsCrimeFamilyMember(_Crime, "UseForbiddenItem")
AND
GetOwner(_Item, _Ettercap)
AND
DB_Bottomless_Ettercaps(_Ettercap)
AND
NOT QRY_CRIME_Bottomless_WarningIssued()
THEN
PROC_CRIME_Bottomless_RegisterWarningCrime(_Criminal, _Ettercap, _Item);

QRY
QRY_CRIME_BlockRegisterCrime((CHARACTER)_Criminal, (STRING)_Crime, (ITEM)_Item, (CHARACTER)_Victim, _)
AND
DB_InRegion((CHARACTER)_Criminal, (TRIGGER)S_FOR_Bottomless_WellCave_2ae9c8b4-c714-49aa-97e4-675050ce3a6d)
AND
DB_Bottomless_Ettercaps(_Victim)
AND
QRY_CRIME_IsCrimeFamilyMember(_Crime, "Vandalise")
AND
QRY_CRIME_Bottomless_WarningIssued()
THEN
PROC_CRIME_Bottomless_RegisterInvestigationCrime(_Criminal, _Victim, _Item);

QRY
QRY_CRIME_BlockRegisterCrime((CHARACTER)_Criminal, (STRING)_Crime, (ITEM)_Item, (CHARACTER)_Victim, _)
AND
DB_InRegion((CHARACTER)_Criminal, (TRIGGER)S_FOR_Bottomless_WellCave_2ae9c8b4-c714-49aa-97e4-675050ce3a6d)
AND
DB_Bottomless_Ettercaps(_Victim)
AND
QRY_CRIME_IsCrimeFamilyMember(_Crime, "Vandalise")
AND
NOT QRY_CRIME_Bottomless_WarningIssued()
THEN
PROC_CRIME_Bottomless_RegisterWarningCrime(_Criminal, _Victim, _Item);

/* Temporarily commenting out: 	- this is creating a rogue query definition that could be problematic elsewhere
									- doesn't do anything in current state and actual fix isn't scheduled for Patch 4 (requires SFX)

QRY
QRY_CRIME_BlockRegisterCrime((CHARACTER)_Criminal, (STRING)_Crime, (ITEM)_ITEM, (CHARACTER)S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b, _)
AND
DB_InRegion(_Criminal, (TRIGGER)S_FOR_Bottomless_Queen_WebArea_9befd44f-43a6-4ca5-a962-90ce55aad023)
AND
QRY_CRIME_IsCrimeFamilyMember(_Crime, "Vandalise")
THEN
PROC_CRIME_Bottomless_RegisterInvestigationCrime_Matriarch(_Criminal);*/

QRY
QRY_CRIME_Bottomless_WarningIssued()
AND
DB_CRIME_Bottomless_EttercapHasWarned(1)
THEN
DB_NOOP(1);

// Region too large for ettercaps to consistently be active when a web sense crime is registered
// Ettercaps will force update when player enters region to work around this
IF
DB_InRegion(_Player, S_FOR_BottomlessWell_SUB_f54505ef-59fd-4aed-b3b2-0e5d9262c52d)
AND
DB_PartyMembers(_Player)
THEN
PROC_Bottomless_ForceUpdateAllEttercaps(1);

IF
LeftTrigger(_Player, S_FOR_BottomlessWell_SUB_f54505ef-59fd-4aed-b3b2-0e5d9262c52d)
AND
DB_PartyMembers(_Player)
AND
NOT QRY_TriggerEvents_AnyPartyMemberInTrigger((TRIGGER)S_FOR_BottomlessWell_SUB_f54505ef-59fd-4aed-b3b2-0e5d9262c52d)
THEN
PROC_Bottomless_ForceUpdateAllEttercaps(0);

// Register crimes if a player is enwebbed by a web surface
IF
StatusApplied(_Criminal, "WEB", _, _)
AND
DB_InRegion((CHARACTER)_Criminal, (TRIGGER)S_FOR_Bottomless_WellCave_2ae9c8b4-c714-49aa-97e4-675050ce3a6d)
AND
GetSurfaceGroundAt(_Criminal, _Surface)
AND
DB_Bottomless_ForbiddenSurfaces(_Surface)
AND
QRY_CRIME_Bottomless_WarningIssued()
AND
DB_Bottomless_Ettercaps(_Ettercap)
THEN
PROC_CRIME_Bottomless_RegisterInvestigationCrime(_Criminal, _Ettercap, _Criminal);

IF
StatusApplied(_Criminal, "WEB", _, _)
AND
DB_InRegion((CHARACTER)_Criminal, (TRIGGER)S_FOR_Bottomless_WellCave_2ae9c8b4-c714-49aa-97e4-675050ce3a6d)
AND
GetSurfaceGroundAt(_Criminal, _Surface)
AND
DB_Bottomless_ForbiddenSurfaces(_Surface)
AND
NOT QRY_CRIME_Bottomless_WarningIssued()
AND
DB_Bottomless_Ettercaps(_Ettercap)
THEN
PROC_CRIME_Bottomless_RegisterWarningCrime(_Criminal, _Ettercap, _Criminal);

IF
StatusApplied(_Criminal, "WEB", _, _)
AND
DB_InRegion((CHARACTER)_Criminal, (TRIGGER)S_FOR_Bottomless_Queen_WebArea_9befd44f-43a6-4ca5-a962-90ce55aad023)
AND
GetSurfaceGroundAt(_Criminal, _Surface)
AND
DB_Bottomless_ForbiddenSurfaces(_Surface)
THEN
PROC_CRIME_Bottomless_RegisterInvestigationCrime_Matriarch(_Criminal);

// Player hears ettercap warning AD
IF
AutomatedDialogEnded(FOR_BottomlessWell_AD_EttercapDisturbanceWarning_a6280b6f-a145-37ae-9ac8-772ad8020a7a, _)
AND
DB_Players(_Player)
AND
DB_InRegion(_Player, (TRIGGER)S_FOR_Bottomless_WellCave_2ae9c8b4-c714-49aa-97e4-675050ce3a6d)
AND
DB_Bottomless_CharacterWithWarning(_Player)
AND
QRY_OnlyOnce("FOR_Bottomless_PlayerHeardWarning")
THEN
PROC_TryStartAD((DIALOGRESOURCE)FOR_Bottomless_PAD_PlayerHeardEttercapWarning_6170bd34-1aa4-2bda-687b-efe53ec27ed3, _Player);

// If a summon triggers the warning its owner plays the AD
IF
AutomatedDialogEnded(FOR_BottomlessWell_AD_EttercapDisturbanceWarning_a6280b6f-a145-37ae-9ac8-772ad8020a7a, _)
AND
DB_Bottomless_CharacterWithWarning(_Summon)
AND
QRY_IsSummonOrPartyFollower(_Summon)
AND
CharacterGetOwner(_Summon, _Owner)
AND
DB_InRegion(_Owner, (TRIGGER)S_FOR_Bottomless_WellCave_2ae9c8b4-c714-49aa-97e4-675050ce3a6d)
AND
QRY_OnlyOnce("FOR_Bottomless_PlayerHeardWarning")
THEN
PROC_TryStartAD((DIALOGRESOURCE)FOR_Bottomless_PAD_PlayerHeardEttercapWarning_6170bd34-1aa4-2bda-687b-efe53ec27ed3, _Owner);

// Procs for crime registration
PROC
PROC_CRIME_Bottomless_RegisterWarningCrime((CHARACTER)_Criminal, (CHARACTER)_Ettercap, (GUIDSTRING)_CrimePos)
AND
GetPosition(_CrimePos,_X,_Y,_Z)
AND
CrimeGetNewID(_CrimeID)
THEN
PROC_CharacterRegisterCrimeWithPosition(_Criminal, "FOR_Bottomless_WebSenseWarning", NULL_00000000-0000-0000-0000-000000000000, _X, _Y, _Z, NULL_00000000-0000-0000-0000-000000000000, _CrimeID);
DB_CRIME_CrimeInvestigationPos(_CrimeID, _X, _Y, _Z); // Force sprint
NOT DB_CRIME_Bottomless_EttercapHasWarned(0);
DB_CRIME_Bottomless_EttercapHasWarned(1);
DB_Bottomless_CharacterWithWarning(_Criminal);
PlayEffect(_Criminal, VFX_Script_WebSense_01_cdc1fda9-2b2d-4c06-e658-9542332de6e6);

PROC
PROC_CRIME_Bottomless_RegisterInvestigationCrime((CHARACTER)_Criminal, (CHARACTER)_Ettercap, (GUIDSTRING)_CrimePos)
AND
GetPosition(_CrimePos,_X,_Y,_Z)
AND
CrimeGetNewID(_CrimeID)
THEN
PROC_CharacterRegisterCrimeWithPosition(_Criminal, "FOR_Bottomless_WebSenseInvestigate", NULL_00000000-0000-0000-0000-000000000000, _X, _Y, _Z, NULL_00000000-0000-0000-0000-000000000000, _CrimeID);
DB_CRIME_CrimeInvestigationPos(_CrimeID, _X, _Y, _Z); // Force sprint
PlayEffect(_Criminal, VFX_Script_WebSense_01_cdc1fda9-2b2d-4c06-e658-9542332de6e6);

PROC
PROC_CRIME_Bottomless_RegisterInvestigationCrime_Matriarch((CHARACTER)_Criminal)
AND
GetPosition(_Criminal,_X,_Y,_Z)
AND
CrimeGetNewID(_CrimeID)
THEN
PROC_CharacterRegisterCrime(_Criminal, "FOR_Bottomless_WebSenseInvestigate_Matriarch", NULL_00000000-0000-0000-0000-000000000000, (CHARACTER)S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b, _CrimeID);
DB_CRIME_CrimeInvestigationPos(_CrimeID, _X, _Y, _Z); // Force sprint
PlayEffect(_Criminal, VFX_Script_WebSense_01_cdc1fda9-2b2d-4c06-e658-9542332de6e6);

PROC
PROC_Bottomless_ForceUpdateEttercap((CHARACTER)_Ettercap, 1)
AND
DB_Bottomless_Ettercaps(_Ettercap)
AND
NOT DB_PermaDefeated(_Ettercap)
AND
NOT DB_Bottomless_Ettercaps_ForceUpdating(_Ettercap)
THEN
DB_Bottomless_Ettercaps_ForceUpdating(_Ettercap);
SetForceUpdate(_Ettercap, 1);

PROC
PROC_Bottomless_ForceUpdateEttercap((CHARACTER)_Ettercap, 0)
AND
DB_Bottomless_Ettercaps(_Ettercap)
AND
DB_Bottomless_Ettercaps_ForceUpdating(_Ettercap)
THEN
NOT DB_Bottomless_Ettercaps_ForceUpdating(_Ettercap);
SetForceUpdate(_Ettercap, 0);

PROC
PROC_Bottomless_ForceUpdateAllEttercaps((INTEGER)_Toggle)
AND
DB_Bottomless_Ettercaps(_Ettercap)
THEN
PROC_Bottomless_ForceUpdateEttercap(_Ettercap, _Toggle);

PROC
PROC_StateSet_PermaDefeated(_Character)
AND
DB_Bottomless_Ettercaps((CHARACTER)_Character)
THEN
PROC_Bottomless_ForceUpdateEttercap((CHARACTER)_Character, 0);
//END_REGION

//REGION Spider Nest
// VB on entering combat with the spider queen
IF
EnteredCombat(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b, _CombatId)
THEN
DB_FOR_Bottomless_SpiderQueenFightID(_CombatId);

IF
SwitchedCombat(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b, _OldCombatId, _NewCombatId)
AND
DB_FOR_Bottomless_SpiderQueenFightID(_OldCombatId)
THEN
NOT DB_FOR_Bottomless_SpiderQueenFightID(_OldCombatId);
DB_FOR_Bottomless_SpiderQueenFightID(_NewCombatId);

IF
LeftCombat(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b, _CombatId)
THEN
NOT DB_FOR_Bottomless_SpiderQueenFightID(_CombatId);

IF
TurnStarted(_Player)
AND
DB_Players((CHARACTER)_Player)
AND
DB_FOR_Bottomless_SpiderQueenFightID(_CombatId)
AND
CombatGetGuidFor(_Player, _CombatId)
AND
QRY_OnlyOnce("S_FOR_Bottomless_VB_SpiderQueenFight")
THEN
StartVoiceBark((VOICEBARKRESOURCE)FOR_BottomlessWell_VB_EnterNest_27859479-236d-b9ae-a848-6f9ff4555564, _Player);
//END_REGION

//REGION Web Crime

IF 
TemplateDestroyedBy(BLD_GEN_Platform_SpiderWeb_A_4D_H0n5_12W_A_Dynamic_3037524c-ddf4-49b2-8d99-131a8285b85d, _Web, _Player, _,_)
AND
DB_InRegion(_Player, S_FOR_Bottomless_Queen_WebArea_9befd44f-43a6-4ca5-a962-90ce55aad023)
AND
NOT DB_PermaDefeated((CHARACTER)S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b)
THEN
PROC_CRIME_Bottomless_RegisterInvestigationCrime_Matriarch(_Player);

//END_REGION

//REGION Underdark Pit
IF
DB_InRegion(_Player, S_FOR_Bottomless_UnderdarkJumpVB_12940f79-db65-4c11-9106-276f8bc37f9a)
AND
NOT DB_CantTalk(_Player)
AND
QRY_OncePerUserAndNearbyPlayers(_Player, "S_FOR_Bottomless_UnderdarkJumpVB_Once", 6.0)
THEN
StartVoiceBark((VOICEBARKRESOURCE)FOR_BottomlessWell_VB_DiscoverUnderdarkEntrance_e7e22368-4eae-4097-0d76-02b48d03444c, _Player);

// Teleport character to Underdark when they enter the pit
IF
ItemEnteredTrigger(_Item, S_FOR_Bottomless_UnderdarkEntrance_de806dd8-ca47-4f20-b7d0-6d50b1328ae5, _)
AND
Exists(_Item,1)
THEN
TeleportTo(_Item, S_FOR_Bottomless_UnderdarkTPIn_4e7780af-5bf3-4486-8e0b-5beb496baf85, "", 0, 0, 1);

// due to GUS-182819 the character might actually enter the trigger several times while falling
// so we need to have a delay before responding to EnteredTrigger event
IF
EnteredTrigger(_Character, S_FOR_Bottomless_UnderdarkEntrance_de806dd8-ca47-4f20-b7d0-6d50b1328ae5)
AND
NOT DB_FOR_Bottomless_UnderdarkEntrance_Entered(_Character)
AND
NOT DB_FOR_BottomlessWell_ImmuneToChasms(_Character)
THEN
DB_FOR_Bottomless_UnderdarkEntrance_Entered((CHARACTER)_Character);
RealtimeObjectTimerLaunch(_Character, "FOR_Bottomless_UnderdarkEntrance_EnteredTrigger", 4000);
PROC_FOR_Bottomless_UnderdarkEntrance_Entered(_Character);

PROC
PROC_FOR_Bottomless_UnderdarkEntrance_Entered((CHARACTER)_Character)
AND
IsControlled(_Character, _IsControlled)
AND
NOT QRY_FOR_Bottomless_Cinematic(_Character, _IsControlled)
THEN
PROC_FOR_Bottomless_UnderdarkEntrance(_Character);

// if the player ends up back in the UnderdarkEntrance due to the code bug mentioned above, then teleport him manually
IF
ObjectTimerFinished((CHARACTER)_Character, "FOR_Bottomless_UnderdarkEntrance_EnteredTrigger")
AND
DB_FOR_Bottomless_UnderdarkEntrance_Entered(_Character)
AND
IsInTrigger(_Character, S_FOR_Bottomless_UnderdarkEntrance_de806dd8-ca47-4f20-b7d0-6d50b1328ae5, 1)
AND
IsImmuneToFallDamage(_Character, 0)
AND
HasActiveStatus(_Character, "FLY", 0)
THEN
PROC_FOR_Bottomless_UnderdarkEntrance_DoDamage((CHARACTER)_Character);

IF
ObjectTimerFinished((CHARACTER)_Character, "FOR_Bottomless_UnderdarkEntrance_EnteredTrigger")
AND
DB_FOR_Bottomless_UnderdarkEntrance_Entered(_Character)
AND
IsInTrigger(_Character, S_FOR_Bottomless_UnderdarkEntrance_de806dd8-ca47-4f20-b7d0-6d50b1328ae5, 1)
THEN
TeleportTo(_Character, S_FOR_Bottomless_UnderdarkTPIn_4e7780af-5bf3-4486-8e0b-5beb496baf85, "", 0, 0, 1);

IF
ObjectTimerFinished((CHARACTER)_Character, "FOR_Bottomless_UnderdarkEntrance_EnteredTrigger")
AND
DB_FOR_Bottomless_UnderdarkEntrance_Entered(_Character)
THEN
NOT DB_FOR_Bottomless_UnderdarkEntrance_Entered(_Character);

PROC
PROC_FOR_Bottomless_UnderdarkEntrance((CHARACTER)_Character)
AND
QRY_FOR_Bottomless_UnderdarkEntrance_TeleportWithFade(_Character)
THEN
PROC_FOR_Bottomless_TeleportPlayerWithFade(_Character, S_FOR_Bottomless_UnderdarkTPIn_4e7780af-5bf3-4486-8e0b-5beb496baf85);

PROC
PROC_FOR_Bottomless_UnderdarkEntrance((CHARACTER)_Character)
AND
NOT QRY_FOR_Bottomless_UnderdarkEntrance_TeleportWithFade(_Character)
THEN
TeleportTo(_Character, S_FOR_Bottomless_UnderdarkTPIn_4e7780af-5bf3-4486-8e0b-5beb496baf85, "FOR_TeleportToUNDFromSpiderNest_NoFade", 0, 0, 1);

IF
EntityEvent(_Character, "FOR_TeleportToUNDFromSpiderNest_NoFade")
AND
IsImmuneToFallDamage(_Character, 0)
AND
HasActiveStatus(_Character, "FLY", 0)
THEN
PROC_FOR_Bottomless_UnderdarkEntrance_DoDamage((CHARACTER)_Character);

PROC
PROC_FOR_Bottomless_UnderdarkEntrance_DoDamage((CHARACTER)_Victim)
AND
DB_FOR_Bottomless_ForceMover(_Killer, _Victim)
THEN
Die(_Victim, DEATHTYPE.Falling, _Killer, 1, 0, 0.0);

PROC
PROC_FOR_Bottomless_UnderdarkEntrance_DoDamage((CHARACTER)_Victim)
AND
NOT DB_FOR_Bottomless_ForceMover(_, _Victim)
THEN
Die(_Victim, DEATHTYPE.Falling, NULL_00000000-0000-0000-0000-000000000000, 1, 0, 0.0);

QRY
QRY_FOR_Bottomless_UnderdarkEntrance_TeleportWithFade((CHARACTER)_Character)
AND
DB_Players(_Character) // used to be checking for DB_PartyMembers, problem with this is that if a familiar/summon was used, it'll be treated like whole party is following a companion into the hole - which isn't expected behavior.
AND
IsControlled(_Character, 1)
THEN
DB_NOOP(1);

//END_REGION

//REGION XP fallback

IF
KilledBy(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b, _Player, _, _)
AND
DB_PartyMembers((CHARACTER)_Player)
THEN
DB_OnlyOnce("FOR_Bottomless_GiveXP");

IF
Died(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b)
AND
QRY_OnlyOnce("FOR_Bottomless_GiveXP")
AND
DB_Players(_Player)
THEN
AddExplorationExperience(_Player, 250); //Do NOT use this call without express permission.

//END_REGION

//REGION Teleport with Fade
// Teleport player to target location with fade
// Player has separate teleport to prevent fade from happening after teleport
PROC
PROC_FOR_Bottomless_TeleportPlayerWithFade((CHARACTER)_Character, (GUIDSTRING)_Target)
AND
GetUUID(_Character, _UserIDString)
AND
GUIDToString(_Target, _TargetID)
AND
Concatenate(_TargetID, _UserIdString, _FadeId)
THEN
DB_FOR_Bottomless_FadeEvents(_Character, _FadeId);
ScreenFadeTo(_Character, 2.0, 0, _FadeId);

PROC
PROC_FOR_Bottomless_TeleportPlayerWithFade((CHARACTER)_Character, (GUIDSTRING)_Target)
AND
NOT DB_Is_InCombat(_Character, _)
THEN
TeleportTo(_Character, _Target, "FOR_TeleportToUNDFromSpiderNest", 1, 1, 1);

PROC
PROC_FOR_Bottomless_TeleportPlayerWithFade((CHARACTER)_Character, (GUIDSTRING)_Target)
AND
DB_Is_InCombat(_Character, _)
THEN
TeleportTo(_Character, _Target, "FOR_TeleportToUNDFromSpiderNest", 0, 0, 1);

IF
EntityEvent(_Char, "FOR_TeleportToUNDFromSpiderNest")
AND
IsImmuneToFallDamage(_Char, 0)
AND
HasActiveStatus(_Char, "FLY", 0)
THEN
Die(_Char, DEATHTYPE.Physical, 1);

IF
ScreenFadeDone(_UserId, _FadeId)
AND
DB_FOR_Bottomless_FadeEvents(_Character, _FadeId)
THEN
NOT DB_FOR_Bottomless_FadeEvents(_Character, _FadeId);
ClearScreenFade(_Character, 2.0, _FadeId);
//END_REGION

//REGION Debug
IF 
TextEvent("S_FOR_Bottomless_OpenWell")
THEN
SetFlag((FLAG)FOR_Bottomless_Event_DescendedWell_919fd0e7-ae61-4520-b869-9b19db7b88a6, NULL_00000000-0000-0000-0000-000000000000);
SetOnStage(S_FOR_VillageWell_21be1469-8f1c-4934-9235-112364aa3df9, 0);
SetOnStage(S_FOR_VillageWell_Visible_695ed0cb-8afb-499b-83f4-289f0eddb23e, 1);

IF
TextEvent("S_FOR_Bottomless_GoToWellAmbush")
AND
DB_Players(_Player)
THEN
TeleportTo(_Player, S_FOR_Bottomless_SpiderAmbushTrigger_33243453-9c0a-4a36-89c1-171841b445a9);

IF
TextEvent("S_FOR_Bottomless_GoToNest")
AND
DB_Players(_Player)
THEN
TeleportTo(_Player, S_FOR_Bottomless_SpiderTrapLookAtTrigger_cbec8fb0-3be2-4740-8e41-fb084c199c22);

IF
TextEvent("S_FOR_Bottomless_ThrowCoin")
AND
DB_Players(_Player)
THEN
SetFlag((FLAG)FOR_BottomlessWell_InteractWithWell_Knows_ThrewCopper_0d5a8f56-969a-487d-724c-60d559e76806, _Player);
//END_REGION

//REGION Immunity to chasms

PROC
PROC_FOR_BottomlessWell_SetChasmImmunity((CHARACTER)_Character)
AND
CheckRulesetModifierString(SCRIPTED_COMBAT_MECHANICS_cac2d8bd-c197-4a84-9df1-f86f54ad4521,"HARD",1)
THEN
DB_FOR_BottomlessWell_ImmuneToChasms(_Character);
//SetTag(_Character,(TAG)IGNORE_CHASMS_a50cb0b1-29a6-4711-a71d-0dc3e3642287);
TriggerRegisterForCharacter(S_FOR_Bottomless_DropIntoUnderground_beb67594-da8d-4e99-9216-e61f19066e8c, _Character);

IF
RulesetModifierChangedString(SCRIPTED_COMBAT_MECHANICS_cac2d8bd-c197-4a84-9df1-f86f54ad4521,"HARD",_)
AND
DB_FOR_BottomlessWell_ImmuneToChasms(_Character)
THEN
//ClearTag(_Character,(TAG)IGNORE_CHASMS_a50cb0b1-29a6-4711-a71d-0dc3e3642287);
TriggerUnregisterForCharacter(S_FOR_Bottomless_DropIntoUnderground_beb67594-da8d-4e99-9216-e61f19066e8c, _Character);
NOT DB_FOR_BottomlessWell_ImmuneToChasms(_Character);

IF
RulesetModifierChangedString(SCRIPTED_COMBAT_MECHANICS_cac2d8bd-c197-4a84-9df1-f86f54ad4521,_,"HARD")
THEN
PROC_FOR_BottomlessWell_SetChasmImmunity(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b);

IF
ForceMoveStarted(_, (CHARACTER)_Character, _)
AND
DB_FOR_BottomlessWell_ImmuneToChasms(_Character)
AND
NOT DB_FOR_BottomlessWell_RememberPosBeforeForcedMove(_Character,_,_,_)
AND
GetPosition(_Character, _X, _Y, _Z)
THEN
DB_FOR_BottomlessWell_RememberPosBeforeForcedMove(_Character,_X,_Y,_Z);

IF
ForceMoveStarted(_, (CHARACTER)_Character, _)
AND
DB_FOR_BottomlessWell_ImmuneToChasms(_Character)
AND
DB_FOR_BottomlessWell_RememberPosBeforeForcedMove(_Character,_OldX,_OldY,_OldZ)
AND
GetPosition(_Character, _X, _Y, _Z)
THEN
NOT DB_FOR_BottomlessWell_RememberPosBeforeForcedMove(_Character,_OldX,_OldY,_OldZ);
DB_FOR_BottomlessWell_RememberPosBeforeForcedMove(_Character,_X,_Y,_Z);

IF
EnteredTrigger(_Character, S_FOR_Bottomless_UnderdarkEntrance_de806dd8-ca47-4f20-b7d0-6d50b1328ae5)
AND
DB_FOR_BottomlessWell_ImmuneToChasms(_Character)
AND
DB_FOR_BottomlessWell_RememberPosBeforeForcedMove(_Character,_X,_Y,_Z)
THEN
PROC_FOR_BottomlessWell_ImmuneToChasms(_Character, _X, _Y, _Z);

IF
EnteredChasm((CHARACTER)_Character,_,_,_X,_Y,_Z)
AND
DB_FOR_BottomlessWell_ImmuneToChasms(_Character)
AND
NOT DB_PermaDefeated(_Character)
THEN
EnterChasmProcessed(_Character,1);
PROC_FOR_BottomlessWell_ImmuneToChasms(_Character, _X, _Y, _Z);

PROC
PROC_FOR_BottomlessWell_ImmuneToChasms((CHARACTER)_Character, (REAL)_X, (REAL)_Y, (REAL)_Z)
THEN
RemoveStatus(_Character, "OFF_BALANCED", _Character);
ApplyStatus(_Character,"CHASM_IMMUNITY_VFX_POOF",0.0,1,NULL_00000000-0000-0000-0000-000000000000);
TeleportToPosition(_Character,_X,_Y,_Z,"FOR_BottomlessWell_ReturnFromChasm",0,0,0,0,1);


//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
