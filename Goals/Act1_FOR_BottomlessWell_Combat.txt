Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_FOR_BottomlessWell_Ettercaps(S_FOR_Bottomless_Ettercap_01_1c167d87-048b-40ac-9117-c87d5a2471fd);
DB_FOR_BottomlessWell_Ettercaps(S_FOR_Bottomless_Ettercap_02_5c57bfc0-cd82-4b9f-9b70-b5475fb121db);

TriggerRegisterForItems(S_FOR_Bottomless_SpiderQueen_CombatArea_595a078d-1c1d-4496-a57c-058bc964d66d);

DB_FOR_CMB_PhaseSpiderEggs((ITEM)S_FOR_Bottomless_CMB_SpiderEgg_000_127bab56-fd82-460a-af3f-7f068a1dc2e6,1);
DB_FOR_CMB_PhaseSpiderEggs((ITEM)S_FOR_Bottomless_CMB_SpiderEgg_001_7116e9b2-ae0f-4966-8b8d-e83041643eb8,1);
DB_FOR_CMB_PhaseSpiderEggs((ITEM)S_FOR_Bottomless_CMB_SpiderEgg_002_e84260c9-037c-4670-befa-dea1d077d6e9,1);
DB_FOR_CMB_PhaseSpiderEggs((ITEM)S_FOR_Bottomless_CMB_SpiderEgg_003_d865b547-8794-4bad-ae5f-7e027e3d526d,1);
DB_FOR_CMB_PhaseSpiderEggs((ITEM)S_FOR_Bottomless_CMB_SpiderEgg_004_33cbc6fd-4338-411d-bf89-f819ea22a05d,1);
DB_FOR_CMB_PhaseSpiderEggs((ITEM)S_FOR_Bottomless_CMB_SpiderEgg_005_b37eb3ae-36f5-446e-8702-cd7807f02b5a,2);
DB_FOR_CMB_PhaseSpiderEggs((ITEM)S_FOR_Bottomless_CMB_SpiderEgg_006_10a79948-e09c-40c9-9c45-650ac8629503,2);
DB_FOR_CMB_PhaseSpiderEggs((ITEM)S_FOR_Bottomless_CMB_SpiderEgg_007_855336cb-f0a1-4aa0-b23a-af021a735d97,2);
DB_FOR_CMB_PhaseSpiderEggs((ITEM)S_FOR_Bottomless_CMB_SpiderEgg_008_17bcc154-bd91-41d9-9e9c-764d988fd5e3,2);
DB_FOR_CMB_PhaseSpiderEggs((ITEM)S_FOR_Bottomless_CMB_SpiderEgg_009_142d4eca-193a-4218-8d77-2a6c93913c58,2);
DB_FOR_CMB_PhaseSpiderEggs((ITEM)S_FOR_Bottomless_CMB_SpiderEgg_010_96bc51c7-9528-4c61-8e7f-eb8a835f2ee3,2);
DB_FOR_CMB_PhaseSpiderEggs((ITEM)S_FOR_Bottomless_CMB_SpiderEgg_011_483406be-9ab9-4718-b951-14005337b6ef,3);
DB_FOR_CMB_PhaseSpiderEggs((ITEM)S_FOR_Bottomless_CMB_SpiderEgg_012_3f2e4864-773d-43ca-9485-f0d7807622ee,3);
DB_FOR_CMB_PhaseSpiderEggs((ITEM)S_FOR_Bottomless_CMB_SpiderEgg_013_f46475a8-3071-45a3-a646-6aa5b3899bf1,3);
DB_FOR_CMB_PhaseSpiderEggs((ITEM)S_FOR_Bottomless_CMB_SpiderEgg_014_1bf5f486-6e8e-402f-82a8-290b20e51af6,3);
DB_FOR_CMB_PhaseSpiderEggs((ITEM)S_FOR_Bottomless_CMB_SpiderEgg_015_88d3ccb9-f92a-408c-9a7f-28ee032528d5,3);
DB_FOR_CMB_PhaseSpiderEggs((ITEM)S_FOR_Bottomless_CMB_SpiderEgg_016_fcca37e3-2632-42c3-8c5d-33cc5ccfcbd6,3);


DB_FOR_CMB_PhaseSpiderQueen_WebPlatforms((GUIDSTRING)PLT_SpiderWeb_12x4_Medium_5cef142f-e0ae-8d70-1285-44e116e28afc);
DB_FOR_CMB_PhaseSpiderQueen_WebPlatforms((GUIDSTRING)PLT_SpiderWeb_12x4_45657009-5949-80bd-253b-ceec6fb113ec);
DB_FOR_CMB_PhaseSpiderQueen_WebPlatforms((GUIDSTRING)PLT_SpiderWeb_12x4_5c8a2e6b-0405-1d35-b325-faffe7879b2a);
DB_FOR_CMB_PhaseSpiderQueen_WebPlatforms((GUIDSTRING)PLT_SpiderWeb_12x4_4ec039a8-1395-84aa-1967-a16043d30192);
KBSECTION
IF
DB_FOR_BottomlessWell_Ettercaps(_Ettercap)
THEN
SetEntityEvent(_Ettercap,"SpiderInfestationStart");

IF
UsingSpell(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b,"Shout_FOR_SpiderQueen_Enrage",_,_,_)
THEN
PlaySound(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b,"SE_FOR_Shout_SpiderQueen_Enrage");

IF
CastedSpell(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b,"Shout_FOR_SpiderQueen_Enrage",_,_,_)
THEN
SetAiHint(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b,NULL_00000000-0000-0000-0000-000000000000);
TriggerSetSoundState((TRIGGER)Amb_SV_VillageSubs_Cave_FZ_000_919c8b33-2c58-34e7-b85a-70f1ca9b8a33,"CombatPhase_FOR_Bottomless_SpiderQueen","Enraged",1);

//REGION Hatch Eggs Spell
IF
CastedSpell(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b,"Target_FOR_SpiderQueen_HatchEggs",_,_,_)
THEN
SetEntityEventReal(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b,"GLO_CombatWait",3.8);

IF
StatusApplied((ITEM)_Egg,"FOR_SPIDEREGGHATCH",_,_)
AND
DB_FOR_CMB_PhaseSpiderEggs(_Egg,_Cluster)
AND
DB_FOR_CMB_PhaseSpiderEggs(_OtherEggs,_Cluster)
AND
_Egg != _OtherEggs
AND
HasActiveStatus(_OtherEggs,"FOR_SPIDEREGGHATCH",0)
THEN
ApplyStatus(_OtherEggs,"FOR_SPIDEREGGHATCH",1.0,1,S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b);

IF
StatusApplied(_Egg,"FOR_SPIDEREGGHATCH",_,_)
AND
IsTagged(_Egg,ACT1_FOR_SPIDERQUEENEGGS_49458de3-20c1-4471-a8a6-2febeaf89e96,1)
AND
Random(1000,_Rand)
AND
IntegerSum(_Rand,100,_Timer)
THEN
SetCanInteract((ITEM)_Egg,0);
RealtimeObjectTimerLaunch(_Egg,"FOR_CMB_PhaseSpiderEggs_DelayRemoveStatus",_Timer);


IF
ObjectTimerFinished((ITEM)_Egg,"FOR_CMB_PhaseSpiderEggs_DelayRemoveStatus")
THEN
RemoveStatus(_Egg,"FOR_SPIDEREGGHATCH");
AiRemoveInterestingItem(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b,_Egg);
RealtimeObjectTimerLaunch(_Egg,"FOR_CMB_PhaseSpiderEggs_DelayDestroyEgg",1500);

IF
ObjectTimerFinished((ITEM)_Egg,"FOR_CMB_PhaseSpiderEggs_DelayDestroyEgg")
AND
GetPosition(_Egg,_X,_Y,_Z)
AND
DB_FOR_CMB_PhaseSpiderEggs(_Egg,_Cluster)
THEN
PlayEffectAtPosition(VFX_Script_Spider_EggHatch_Exploding_01_8331601c-3ca5-b359-91fb-e36933af5755,_X,_Y,_Z);
Die(_Egg);
DB_FOR_CMB_PhaseSpiderEggs_SpawnPos(_Egg,_X,_Y,_Z,_Cluster);

IF
DestroyedBy(_Egg,_,_,_)
AND
DB_FOR_CMB_PhaseSpiderEggs_SpawnPos(_Egg,_X,_Y,_Z,_Cluster)
AND
ConcatenateInteger("FOR_Spiderlings_",_Cluster,_SwarmGroup)
AND
CreateAt(FOR_PhaseSpider_Spawn_6d111437-6a82-439a-b961-adaa85103512,_X,_Y,_Z,0,1,"FOR_CMB_PhaseSpiderEggs_SpawnFinished",_Spider)
THEN
NOT DB_FOR_CMB_PhaseSpiderEggs_SpawnPos(_Egg,_X,_Y,_Z,_Cluster);
RequestSetSwarmGroup(_Spider,_SwarmGroup);

IF
EntityEvent(_Spider,"FOR_CMB_PhaseSpiderEggs_SpawnFinished")
AND
GetCombatGroupID(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b,_CombatGroup)
AND
GetFaction(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b,_Faction)
THEN
SetFaction(_Spider,_Faction);
SetCombatGroupID(_Spider,_CombatGroup);
PROC_EnterCombat(_Spider,S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b);

//END_REGION

//REGION Select Main Egg for AI optimisation

IF
EnteredCombat(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b,_)
THEN
PROC_FOR_CMB_PhaseSpiderEggs_UpdateClusterTargets();

PROC
PROC_FOR_CMB_PhaseSpiderEggs_UpdateClusterTargets()
AND
DB_FOR_CMB_PhaseSpiderEggs(_Egg,_Int)
AND
NOT DB_FOR_CMB_PhaseSpiderEggs_ClusterTarget(_,_Int)
THEN
DB_FOR_CMB_PhaseSpiderEggs_ClusterTarget(_Egg,_Int);


IF
DB_FOR_CMB_PhaseSpiderEggs_ClusterTarget(_Egg,_Int)
THEN
AiAddInterestingItem(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b,_Egg);

IF
DestroyingBy(_Egg,_,_,_)
AND
DB_FOR_CMB_PhaseSpiderEggs(_Egg,_Int)
THEN
NOT DB_FOR_CMB_PhaseSpiderEggs(_Egg,_Int);

IF
DestroyedBy(_Egg,_,_,_)
AND
DB_FOR_CMB_PhaseSpiderEggs_ClusterTarget(_Egg,_Int)
THEN
AiRemoveInterestingItem(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b,_Egg);
NOT DB_FOR_CMB_PhaseSpiderEggs_ClusterTarget(_Egg,_Int);
PROC_FOR_CMB_PhaseSpiderEggs_UpdateClusterTargets();

//END_REGION

//REGION Egg Bookkeeping

IF
Died(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b)
THEN
TriggerUnregisterForItems(S_FOR_Bottomless_SpiderQueen_CombatArea_595a078d-1c1d-4496-a57c-058bc964d66d);
SysClear("DB_FOR_CMB_PhaseSpiderEggs",2);

IF
DB_FOR_CMB_PhaseSpiderEggs(_Egg,_)
THEN
SetTag(_Egg,ACT1_FOR_SPIDERQUEENEGGS_49458de3-20c1-4471-a8a6-2febeaf89e96);

//END_REGION


IF
PlatformDestroyed(_WebPlatform)
AND
DB_FOR_CMB_PhaseSpiderQueen_WebPlatforms(_WebPlatform)
THEN
NOT DB_FOR_CMB_PhaseSpiderQueen_WebPlatforms(_WebPlatform);
PROC_FOR_CMB_PhaseSpiderQueen_WebPlatforms();

PROC
PROC_FOR_CMB_PhaseSpiderQueen_WebPlatforms()
AND
NOT DB_FOR_CMB_PhaseSpiderQueen_WebPlatforms(_)
AND
HasActiveStatus(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b,"FOR_SPIDERQUEEN_ENRAGE",0)
THEN
SetAiHint(S_FOR_Bottomless_SpiderQueen_e6b2f3ba-2d02-4507-8680-6047322e1a4b,AI_HINT_RANGER_3cae4ff5-e9c0-485b-b695-f8830961a781);




EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
