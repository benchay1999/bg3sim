Version 1
SubGoalCombiner SGC_AND
INITSECTION
PROC_TriggerRegisterForPlayers(S_FOR_CambionDinnerAdd_beef42a3-d14d-4f2f-acf9-e0573f0e6e2e);

//REGION Candles
DB_FOR_Monitor_Candles((ITEM)S_FOR_CambionHearth_b781e423-dea9-4421-8ff2-a1417eacccaf);

DB_FOR_Monitor_Candles((ITEM)S_FOR_Monitor_Candle_000_a419e4ad-8aee-47e5-a6f3-e1734d5f02e9);
DB_FOR_Monitor_Candles((ITEM)S_FOR_Monitor_Candle_001_281553d5-08f2-4391-ace5-890d3a5276b8);
DB_FOR_Monitor_Candles((ITEM)S_FOR_Monitor_Candle_002_87aebe77-714f-48a6-a307-5a8a8fbd4587);
DB_FOR_Monitor_Candles((ITEM)S_FOR_Monitor_Candle_003_17273a2d-0fca-43b9-a991-454b825d3652);
DB_FOR_Monitor_Candles((ITEM)S_FOR_Monitor_Candle_004_52051d47-f1ea-4a89-ac42-4e73f45e6025);
DB_FOR_Monitor_Candles((ITEM)S_FOR_Monitor_Candle_005_00165325-cd97-4498-a558-d8b5e674c070);
DB_FOR_Monitor_Candles((ITEM)S_FOR_Monitor_Candle_006_f9bcc0cb-3e46-4929-859b-b92a9758322b);
DB_FOR_Monitor_Candles((ITEM)S_FOR_Monitor_Candle_007_18aac16a-e5f0-455c-8972-5ed58d3ab8df);
DB_FOR_Monitor_Candles((ITEM)S_FOR_Monitor_Candle_008_dbdbc1c0-06bf-48b6-bfdb-f6663d943653);

DB_FOR_Monitor_CandleEffects((ITEM)LTS_DUN_Candlestick_G_Gold_ba7b4244-4d7d-4a5f-a052-621dc345a7e6, "VFX_LightSources_DUN_Hellfire_Candlestick_G_01");
DB_FOR_Monitor_CandleEffects((ITEM)LTS_DUN_Bowls_F_Gold_b535cd0b-4259-4757-ba93-982d832143c1, "VFX_LightSources_DUN_Hellfire_Bowls_F_01");
DB_FOR_Monitor_CandleEffects((ITEM)LTS_Camp_Campfire_A_827e5d0f-eeb0-42ca-a237-012033c95c83, "VFX_LightSources_Hellfire_Campfire_B_01");


//END_REGION

//REGION Banquet Selection
DB_FlagReactionAfterDialog((FLAG)FOR_Monitor_Event_TakeFood_1f6daf35-dee7-436b-bb7a-d7c1c8b86725, (DIALOGRESOURCE)FOR_Monitor_Dinner_c6618ea4-f1a5-086f-936e-402f4d74fa38);

DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_GEN_Food_Bread_Loaf_B_d4c6879f-7042-4f1a-9fd7-3c7a1092a79a, 0.0, 25.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_FOOD_Fruit_Apple_A_e8bbe73a-e1dc-4d2e-910f-318db7aee382, 0.0, 25.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_Drink_Wine_A_Wicker_3525b96e-81ef-4411-960a-b1b305e139c6, 0.0, 25.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_GEN_Food_Cheese_A_8cca8f05-63b2-4bff-8494-9609bf29af66, 0.0, 25.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_GEN_Food_Meat_Sausage_B_Rope_A_e044aa11-73ed-46b2-a202-e73e187c5c98, 0.0, 25.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_FOOD_Fruit_Grapes_A_Green_A_9d9665eb-89d4-4cdd-aa04-235a8ac51205, 0.0, 25.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_GEN_Food_Tart_Treacle_A_80e17dfc-f29e-4149-9c91-7b617efb0c77, 0.0, 25.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_GEN_Food_Bread_Bun_A_e604f4cc-4c17-4f40-9864-c7de41d9c211, 0.0, 25.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_FOOD_Fruit_Orange_A_0206650c-76b0-4ca3-9060-8efb5b739ede, 0.0, 25.0);

DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_GEN_Food_Bread_Loaf_B_d4c6879f-7042-4f1a-9fd7-3c7a1092a79a, 25.0, 50.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_FOOD_Fruit_Apple_A_e8bbe73a-e1dc-4d2e-910f-318db7aee382, 25.0, 50.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_Drink_Wine_A_Wicker_3525b96e-81ef-4411-960a-b1b305e139c6, 25.0, 50.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_GEN_Food_Cheese_Piece_A_06474f97-0dd0-45e6-8db8-5f75009bf336, 25.0, 50.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_GEN_Food_Meat_Sausage_B_Rope_A_e044aa11-73ed-46b2-a202-e73e187c5c98, 25.0, 50.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_FOOD_Fruit_Grapes_A_Green_A_9d9665eb-89d4-4cdd-aa04-235a8ac51205, 25.0, 50.0);

DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_GEN_Food_Bread_Loaf_B_d4c6879f-7042-4f1a-9fd7-3c7a1092a79a, 50.0, 75.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_FOOD_Fruit_Apple_A_e8bbe73a-e1dc-4d2e-910f-318db7aee382, 50.0, 75.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_Drink_Wine_A_Wicker_3525b96e-81ef-4411-960a-b1b305e139c6, 50.0, 75.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_GEN_Food_Cheese_Piece_A_06474f97-0dd0-45e6-8db8-5f75009bf336, 50.0, 75.0);

DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_GEN_Food_Bread_Loaf_B_d4c6879f-7042-4f1a-9fd7-3c7a1092a79a, 75.0, 100.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_FOOD_Fruit_Apple_A_e8bbe73a-e1dc-4d2e-910f-318db7aee382, 75.0, 100.0);
DB_FOR_MeetingTheMonitor_BanquetSelection((ITEMROOT)CONS_Drink_Wine_A_Wicker_3525b96e-81ef-4411-960a-b1b305e139c6, 75.0, 100.0);

//END_REGION
KBSECTION
//REGION Cambion Dinner

//REGION Player Takes Food

PROC
PROC_FlagReactionAfterDialog(_Player, (FLAG)FOR_Monitor_Event_TakeFood_1f6daf35-dee7-436b-bb7a-d7c1c8b86725)
AND
GetHitpointsPercentage(_Player, _Health)
AND
DB_FOR_MeetingTheMonitor_BanquetSelection(_Food, _MinHp, _MaxHP)
AND
_Health > _MinHP
AND
_Health <= _MaxHP
THEN
TemplateAddTo(_Food, _Player, 1, 1);


//END_REGION

//REGION Cambion Transformation

//HACK: disabled for GoldenPath for now, due to issue with timeline dummy popping up on transform
/*
IF
FlagSet((FLAG)FOR_Monitor_State_TransformToDevil_9caa5158-7491-4df3-b3cb-ffc25db37a66, NULL_00000000-0000-0000-0000-000000000000, _)
THEN
PlayEffect(S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8, (EFFECTRESOURCE)VFX_Script_Stub_Poof_01_f0cf792a-0f74-d17e-ad0d-6052a6131416);
Transform(S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8, (CHARACTER)QUEST_Monitor_DevilForm_1f9da419-8224-42e8-894a-8907cb978360, DISGUISE_ceccc4eb-d774-4cd5-9147-12322b81b763);
*/

IF
FlagSet((FLAG)FOR_Monitor_State_HellLight_180537d5-7f47-42d6-957a-65697bfd0165, NULL_00000000-0000-0000-0000-000000000000, _)
THEN
TimerLaunch("FOR_Monitor_SnapDelay", 2600);

IF
TimerFinished("FOR_Monitor_SnapDelay")
THEN
PROC_FOR_Monitor_AlterCandleLight();

PROC
PROC_FOR_Monitor_AlterCandleLight()
AND
DB_FOR_Monitor_Candles(_Candle)
AND
GetTemplate(_Candle, _Template)
AND
DB_FOR_Monitor_CandleEffects((ITEM)_Template, _Effect)
THEN
RemoveStatus(_Candle, "BURNING");
SetVarString(_Candle, "BurnEffect", _Effect);
ApplyStatus(_Candle, "BURNING", -1.0, 1);

//END_REGION


QRY
QRY_StartDialog_BlockAttackButton((DIALOGRESOURCE)FOR_Monitor_Dinner_c6618ea4-f1a5-086f-936e-402f4d74fa38,(GUIDSTRING)_Speaker1,(GUIDSTRING)_Speaker2,(GUIDSTRING)_Speaker3,(GUIDSTRING)_Speaker4,(GUIDSTRING)_Speaker5,(GUIDSTRING)_Speaker6)
THEN
DB_NOOP(1);

IF
DialogStarted((DIALOGRESOURCE)FOR_Monitor_Dinner_c6618ea4-f1a5-086f-936e-402f4d74fa38,_)
AND
DB_FOR_Monitor_Candles(_Candle)
THEN
SetForceUpdate(_Candle, 1);

IF
DialogEnded((DIALOGRESOURCE)FOR_Monitor_Dinner_c6618ea4-f1a5-086f-936e-402f4d74fa38,_)
AND
DB_FOR_Monitor_Candles(_Candle)
THEN
SetForceUpdate(_Candle, 0);

IF
DialogEnded((DIALOGRESOURCE)FOR_Monitor_Dinner_c6618ea4-f1a5-086f-936e-402f4d74fa38, _)
THEN
PROC_State_Progress((CHARACTER)S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8, "GLO_Monitor", "GLO_Monitor_HasAppeared");

PROC
PROC_State_Changed((CHARACTER)S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8, "GLO_Monitor",_,_)
AND
NOT QRY_State_IsBeforeState(S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8, "GLO_Monitor", "GLO_Monitor_WaitingMolAtLanceboard")
THEN
GoalCompleted;

//END_REGION

//REGION Cambion Combat

IF
TextEvent("mon_appear")
AND
GetHostCharacter(_Player)
THEN
TeleportTo(S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8,_Player,"",0,0,0,1,1);

IF
TextEvent("mon_van")
THEN
PROC_GLO_Monitor_AttackedReaction();

IF
AttackedBy(S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8, _, _, _, _, _, _)
AND
NOT DB_PermaDefeated(S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8)
AND
NOT DB_Camp_RequiredTalks(S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8,_)
AND
QRY_OnlyOnce("FOR_Monitor_MonitorAttackedReaction")
THEN
PROC_GLO_Monitor_AttackedReaction();

PROC
PROC_GLO_Monitor_AttackedReaction()
THEN
PROC_RemoveDialog(S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8);
PROC_ForceStopDialog(S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8);

PROC
PROC_GLO_Monitor_AttackedReaction()
AND
NOT QRY_FOR_MeetingTheMonitor_VanishAD()
THEN
PROC_FOR_MeetingTheMonitor_Vanish();
TimerLaunch("FOR_Monitor_TeleportEffectTimer", 500);

IF
TimerFinished("FOR_Monitor_TeleportEffectTimer")
THEN
PlayEffect(S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8, (EFFECTRESOURCE)VFX_Script_Monitor_Teleport_Out_42b96e0a-4e5f-111e-4d74-92cbb84512bc, "Dummy_ChestFX");
TimerLaunch("FOR_Monitor_TeleportEnd", 4000);

IF
TimerFinished("FOR_Monitor_TeleportEnd")
THEN
PROC_FOR_MeetingTheMonitor_Vanish();

QRY
QRY_FOR_MeetingTheMonitor_VanishAD()
AND
QRY_StartDialog((DIALOGRESOURCE)FOR_Monitor_AD_Vanish_f1fc2c8b-5f8b-4f67-9852-30a48181dff7, S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8)
THEN
TimerLaunch("FOR_Monitor_TeleportEffectTimer", 5000);

IF
AutomatedDialogEnded((DIALOGRESOURCE)FOR_Monitor_AD_Vanish_f1fc2c8b-5f8b-4f67-9852-30a48181dff7, _)
THEN
PROC_FOR_MeetingTheMonitor_Vanish();

PROC
PROC_FOR_MeetingTheMonitor_Vanish()
AND
QRY_OnlyOnce("FOR_Monitor_MonitorVanish")
THEN
SetOnStage(S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8, 0);

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
