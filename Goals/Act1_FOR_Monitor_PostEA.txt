Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialog_AddAllNearbyPlayersAtStart(CAMP_MonitorIntro_CFM_6aa2bfc3-d9e6-a2d8-d017-25d59d9f757a);
DB_Dialog_AddAllNearbyPlayersAtStart(FOR_Monitor_Dinner_c6618ea4-f1a5-086f-936e-402f4d74fa38);

 //Triggers to start the scene outside of CAMP
DB_GLO_MonitorAppear_Trigger((TRIGGER)S_CRA_MonitorDialog_6397e6b0-37cc-42d4-94f6-666d4c70db18);
DB_GLO_MonitorAppear_Trigger(S_DEN_MonitorDialog_9163d5d3-42be-49f5-a0a9-6cc1a60ec557);
DB_GLO_MonitorAppear_Trigger(S_GOB_MonitorDialog_06fb3585-32cb-4204-86a1-f79147dc88b0);
DB_GLO_MonitorAppear_Trigger(S_PLA_MonitorDialog_e4baef0a-4560-4a38-9ee7-af5e27c5c147);
DB_GLO_MonitorAppear_Trigger(S_UND_MonitorDialog_472f2d45-8eae-4ff3-a947-d3a7c2e93715);

DB_GLO_MonitorAppear_Requirement((FLAG)DEN_Apprentice_State_MonitorIntroRequirementMet_fdf075ba-072b-4788-9edb-1ad85949d06d);
DB_GLO_MonitorAppear_Requirement((FLAG)GLO_Hag_State_FailedTadpole_155a938a-5fb7-725b-9b00-6d6f44c451ef);
DB_GLO_MonitorAppear_Requirement((FLAG)GOB_GoblinPriest_Quest_CureIsHoax_9296acbd-c107-4fe4-ae63-7d5f89cd7099);
DB_GLO_MonitorAppear_Requirement((FLAG)UND_SocietyOfBrilliance_MindFlayer_State_ConnectionPerformed_804aadb8-783b-4d78-9b41-85030a0ba11e);
DB_GLO_MonitorAppear_Requirement((FLAG)PLA_GithChokepoint_State_MonitorIntroRequirementMet_4d226b35-cc1b-4750-a06b-30f231b0633c);
DB_GLO_MonitorAppear_Requirement((FLAG)GLO_Halsin_State_Returned_5af86099-47f9-ba69-199a-fe91292a41bd);
DB_GLO_MonitorAppear_Requirement((FLAG)GLO_Halsin_Quest_AskedKilledLeaders_853122c7-f2e2-a2a0-a2b4-67ae09c3ac73);
DB_GLO_MonitorAppear_Requirement((FLAG)GLO_Halsin_Quest_ToldAboutMoonrise_05fdd7a0-000f-f16f-e0b6-ed35c6a6a239); //Removed redunded condition for (FLAG)GLO_2NightsSinceDaisyDream1_0d62bf15-2a08-4bc3-b045-fcad912065a4
DB_GLO_MonitorAppear_Requirement((FLAG)SCL_State_RegionVisited_6bde55fb-78bb-47e4-9ce6-f91cec93219d); //TODO: The CAMP scene should trigger in this case, but it needs to be converted for SCL.
KBSECTION
/*
IF
DB_GLO_MonitorAppear_Requirement(_Flag1,_Flag2)
AND
DB_GlobalFlag(_Flag1)
AND
DB_GlobalFlag(_Flag2)
THEN
PROC_GLO_MonitorAppearRegister();*/

IF
DB_GLO_MonitorAppear_Requirement(_Flag)
AND
DB_GlobalFlag(_Flag)
THEN
PROC_GLO_MonitorAppearRegister();

//TODO: Create debug commands for all the possible conditions
PROC
PROC_GLO_MonitorAppearRegister()
AND
NOT DB_GLO_MonitorIntro_HadIntro(1)
AND
DB_GLO_MonitorAppear_Trigger(_Trigger)
THEN
PROC_TriggerRegisterForPlayers(_Trigger);
DB_AddCharactersInTriggerToDialog(CAMP_MonitorIntro_CFM_6aa2bfc3-d9e6-a2d8-d017-25d59d9f757a,_Trigger,1);
DB_AddCharactersInTriggerToDialog(FOR_Monitor_Dinner_c6618ea4-f1a5-086f-936e-402f4d74fa38,_Trigger,0);

PROC
PROC_GLO_MonitorAppearUnregisterAll()
AND
DB_GLO_MonitorAppear_Trigger(_Trigger)
THEN
PROC_TriggerUnregisterForPlayers(_Trigger);
PROC_AddCharactersInTriggerToDialog_Clear(CAMP_MonitorIntro_CFM_6aa2bfc3-d9e6-a2d8-d017-25d59d9f757a,_Trigger);
PROC_AddCharactersInTriggerToDialog_Clear(FOR_Monitor_Dinner_c6618ea4-f1a5-086f-936e-402f4d74fa38,_Trigger);


PROC
PROC_FOR_MeetingTheMonitor_Vanish()
THEN
PROC_GLO_MonitorAppearUnregisterAll();


PROC
PROC_GLO_MonitorAppearUnregisterAll()
AND
DB_GLO_MonitorAppear_Requirement(_Flag)
THEN
NOT DB_GLO_MonitorAppear_Requirement(_Flag);

IF
EnteredTrigger(_Player,_Trigger)
AND
DB_GLO_MonitorAppear_Trigger(_Trigger)
AND
NOT DB_GLO_MonitorIntro_Player(_)
AND
IsInCombat(_Player, 1)
THEN
PROC_NotifyWhenReadyToTalk(_Player, "GLO_Monitor_CanAppear");

IF
EnteredTrigger(_Player,_Trigger)
AND
DB_GLO_MonitorAppear_Trigger(_Trigger)
AND
NOT DB_GLO_MonitorIntro_Player(_)
AND
NOT QRY_SpeakerIsAvailable(_Player)
THEN
PROC_NotifyWhenReadyToTalk(_Player, "GLO_Monitor_CanAppear");

PROC
PROC_ReadyToTalk(_Player, "GLO_Monitor_CanAppear")
AND
DB_InRegion(_Player, _Trigger)
AND
DB_GLO_MonitorAppear_Trigger(_Trigger)
AND
NOT DB_GLO_MonitorIntro_Player(_)
AND
IsInCombat(_Player, 0)
AND
QRY_SpeakerIsAvailable(_Player)
THEN
TeleportTo(S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8,_Player,"MonitorTeleportedToPlayer");
PROC_GLO_Monitor_Foop();
DB_GLO_MonitorIntro_Player(_Player);

IF
EnteredTrigger(_Player,_Trigger)
AND
DB_GLO_MonitorAppear_Trigger(_Trigger)
AND
NOT DB_GLO_MonitorIntro_Player(_)
AND
IsInCombat(_Player, 0)
AND
QRY_SpeakerIsAvailable(_Player)
THEN
TeleportTo(S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8,_Player,"MonitorTeleportedToPlayer");
PROC_GLO_Monitor_Foop();
DB_GLO_MonitorIntro_Player(_Player);


IF
DB_GLO_Monitor_DismissToCamp(1)
THEN
PROC_RemoveAllDialogEntriesForSpeaker(S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8);
DB_Dialogs(S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8,CAMP_MonitorIntro_CFM_6aa2bfc3-d9e6-a2d8-d017-25d59d9f757a);
PROC_GLO_Monitor_Foop();
PROC_GLO_MonitorAppearUnregisterAll();

IF
DB_GLO_Monitor_DismissToCamp(1)
AND
DB_ActiveCamp("WLDMAIN")
THEN
TeleportTo(S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8,S_CAMP_WLDMAIN_Monitor_70b24e09-502e-421a-a19a-0887ba879014,"",0,0,0,1,1);

IF
DB_GLO_Monitor_DismissToCamp(1)
AND
DB_ActiveCamp("WLDDUNSHA")
THEN
TeleportTo(S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8,S_CAMP_WLDDUNSHA_Monitor_c3e91be7-24f6-4d49-b067-f068c918817e,"",0,0,0,1,1);

//TODO: Change to behavior with ADing to a squirrel-imp-in-disguise, dialog trigger on player interaciton
IF
EntityEvent((CHARACTER)S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8,"MonitorTeleportedToPlayer")
AND
DB_GLO_MonitorIntro_Player(_Player)
AND
QRY_StartDialog((DIALOGRESOURCE)CAMP_MonitorIntro_CFM_6aa2bfc3-d9e6-a2d8-d017-25d59d9f757a,S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8,_Player)
THEN
DB_NOOP(1);

PROC
PROC_StartDialog_AddExtraSpeakers((DIALOGRESOURCE)CAMP_MonitorIntro_CM_6aa2bfc3-d9e6-a2d8-d017-25d59d9f757a,(INTEGER)_ID)
THEN
PROC_DialogAddSpeakingActor(_ID, (CHARACTER)S_Dummy_Monitor_DevilForm_4a4326cc-be91-4903-9bd6-31bbf5a9ff42);

IF
DialogStarted((DIALOGRESOURCE)CAMP_MonitorIntro_CFM_6aa2bfc3-d9e6-a2d8-d017-25d59d9f757a,_DialogId)
AND
NOT DB_GLO_MonitorIntro_Player(_)
AND
DB_DialogPlayers(_DialogId,_Player,1)
THEN
DB_GLO_MonitorIntro_Player((CHARACTER)_Player);

IF
InstanceDialogChanged(_, CAMP_MonitorIntro_CFM_6aa2bfc3-d9e6-a2d8-d017-25d59d9f757a, FOR_Monitor_Dinner_c6618ea4-f1a5-086f-936e-402f4d74fa38, _)
THEN
PROC_State_Progress((CHARACTER)S_GLO_Monitor_f65becd6-5cd7-4c88-b85e-6dd06b60f7b8, "GLO_Monitor", "GLO_Monitor_MonitorVisit");
PROC_GLO_Monitor_Poof();
DB_GLO_MonitorIntro_HadIntro(1);
SetFlag((FLAG)NIGHT_MonitorIntro_a9297517-ab4d-482d-b287-fc6e7001d93e);
PROC_GLO_MonitorAppearUnregisterAll();

QRY
QRY_StartDialog_BlockAttackButton((DIALOGRESOURCE)CAMP_MonitorIntro_CFM_6aa2bfc3-d9e6-a2d8-d017-25d59d9f757a,(GUIDSTRING)_Speaker1,(GUIDSTRING)_Speaker2,(GUIDSTRING)_Speaker3,(GUIDSTRING)_Speaker4,(GUIDSTRING)_Speaker5,(GUIDSTRING)_Speaker6)
THEN
DB_NOOP(1);

IF
DialogEnded((DIALOGRESOURCE)CAMP_MonitorIntro_CFM_6aa2bfc3-d9e6-a2d8-d017-25d59d9f757a,_)
AND
DB_GLO_MonitorIntro_Player(_Player)
THEN
NOT DB_GLO_MonitorIntro_Player(_Player);

IF
NestedDialogPlayed((DIALOGRESOURCE)FOR_Monitor_Dinner_c6618ea4-f1a5-086f-936e-402f4d74fa38,_)
THEN
PROC_TopicalGreeting_UnlockTopic((FLAG)TG_MonitorIntro_e35b7017-eb09-4319-ba1e-934f1862c7ef);

IF
DB_GLO_MonitorAppear_Trigger(_Trigger)
THEN
DB_TopicalGreetingEndCondition_LeaveTrigger((FLAG)TG_MonitorIntro_e35b7017-eb09-4319-ba1e-934f1862c7ef,_Trigger);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
