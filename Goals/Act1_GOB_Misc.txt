Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_PermaDefeatedFlag(S_GOB_GoblinKing_11337af0-6a57-426b-a820-c4b00923dd54,(FLAG)GLO_GoblinKing_State_PermaDefeated_d985480d-6158-0b0f-adfc-4bf3db785bf1);

DB_GLO_DefeatCounter(S_GOB_DrunkGoblin_0c3404d4-af6f-4c3c-8873-101a79cc4d86, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_PartyingGoblin_000_69ab2a7d-9035-4e4e-97e5-2d005c9662ee, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_PartyingGoblin_001_4aa22ae0-1f97-42e3-8fd7-474f2f3b7269, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_PartyingGoblin_002_bedb5fe5-2fa7-42f4-82c0-dda23883ff7c, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_Festivities_Goblin_000_031e9fb7-136a-46c7-91d7-77ce3e20236a, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_Festivities_Goblin_001_774c78ed-1227-48f9-812e-aac0021c71bf, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_Festivities_Goblin_002_bb9c82a2-7ed9-45ed-99b6-58f84520fb8c, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_Festivities_Goblin_003_0296a9f6-b2bc-46a0-8632-ba72679a36d3, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_Festivities_Goblin_004_e63401de-04ea-40b6-a988-b2f90a90f748, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_Festivities_Goblin_005_a955aea9-9040-4d4c-9264-e46e385dea94, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_Festivities_Goblin_006_f3f416d1-b946-4019-95ba-7053f63dd776, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_Festivities_Goblin_007_10a00987-1dbe-4540-aa34-e96976c7bac0, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_VoloGuard_c25f0e79-b9f7-453c-8138-964bca267d93, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_RoastingDwarfOperator_000_1fe911f8-c9c9-4405-83fa-d343d59c7be9, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_RoastingDwarfOperator_001_3837d519-dc92-459c-ab2d-1eac0b208fc7, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_ChickenChase_Commentator_eb2c434d-1833-485e-bca2-f181eed6010b,"GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_Festivities_Trader_3c9f9d17-835c-46bf-929d-240e3b4adb55, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_Festivities_Ogre_00_ffe2ab58-fddc-47ce-b369-bdb93b4a52dd, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_Festivities_Bugbear_Wanderer_c45936ea-2e38-4cf5-b5e1-1ff382888737, "GOB_FestivitiesArea");

DB_GOB_Festivities_Kids((CHARACTER)S_GOB_Festivities_GoblinKid_002_f11d525d-e94d-4e96-a4ae-e43f34430518);
DB_GOB_Festivities_Kids((CHARACTER)S_GOB_Festivities_GoblinKid_000_c8de613d-be02-42ab-ae91-3ed7ba5edafa);
DB_GOB_Festivities_Kids((CHARACTER)S_GOB_Festivities_GoblinKid_001_73b27c74-5462-463d-ac82-67a40b87db2c);

DB_Dialogs((CHARACTER)S_GOB_Festivities_GoblinKid_000_c8de613d-be02-42ab-ae91-3ed7ba5edafa, (DIALOGRESOURCE)GOB_Festivities_GoblinKid000_e95670e5-77d1-3154-2bec-4f7a1d2a26ae);
DB_Dialogs((CHARACTER)S_GOB_Festivities_GoblinKid_001_73b27c74-5462-463d-ac82-67a40b87db2c, (DIALOGRESOURCE)GOB_Festivities_GoblinKid001_d9f09071-6102-eb93-7018-447098669e1e);
DB_Dialogs((CHARACTER)S_GOB_Festivities_GoblinKid_002_f11d525d-e94d-4e96-a4ae-e43f34430518, (DIALOGRESOURCE)GOB_Festivities_GoblinKid002_1f537b03-3118-20bc-b056-6bb78a0b1947);
 
DB_GOB_ShadowHeart_Trigger((TRIGGER)S_GOB_MuralSphere_85551ba5-6916-4532-87fa-1f06045286b0, (VOICEBARKRESOURCE)GOB_ShadowHeart_VB_Mural_6712960e-a6aa-cebe-d137-c913ad513884);
DB_GOB_ShadowHeart_Trigger(S_GOB_TempleBox_002_568b8a67-dfb6-4531-868f-dda7a05036f5, GOB_ShadowHeart_VB_Temple_09346a27-fd3a-8539-9160-d3b070fb936a);
DB_GOB_ShadowHeart_Trigger(S_GOB_StatueSphere_000_cbb7b039-445e-4657-b8b2-7f3965ed5450, GOB_ShadowHeart_VB_Statue_155b6052-2abf-a7c3-3f7e-10bece45a253);
DB_GOB_ShadowHeart_Trigger(S_GOB_StatueSphere_001_426b340d-9ef1-4631-b7f3-0759caad041b, GOB_ShadowHeart_VB_Statue_155b6052-2abf-a7c3-3f7e-10bece45a253);
DB_GOB_ShadowHeart_Trigger(S_GOB_StatueSphere_002_410c761f-ec00-4de3-abbe-2983251b55cf, GOB_ShadowHeart_VB_Statue_155b6052-2abf-a7c3-3f7e-10bece45a253);
PROC_TriggerRegisterForPlayers(S_GOB_MuralSphere_85551ba5-6916-4532-87fa-1f06045286b0);
PROC_TriggerRegisterForPlayers(S_GOB_TempleBox_002_568b8a67-dfb6-4531-868f-dda7a05036f5);
PROC_TriggerRegisterForPlayers(S_GOB_StatueSphere_000_cbb7b039-445e-4657-b8b2-7f3965ed5450);
PROC_TriggerRegisterForPlayers(S_GOB_StatueSphere_001_426b340d-9ef1-4631-b7f3-0759caad041b);
PROC_TriggerRegisterForPlayers(S_GOB_StatueSphere_002_410c761f-ec00-4de3-abbe-2983251b55cf);
DB_Singleton("GOB_ShadowHeart_VB",0);

//REGION Hanging coal baskets

DB_GLO_FireBowls((ITEM)S_GOB_Firebowl_Bowl_001_2e21e856-f46c-405e-bdcb-9feaaf39a0a7,(ITEM)S_GOB_Firebowl_Hinge_001_e2dcc9da-c548-4937-a9b4-cc011b4aa23c);
DB_GLO_FireBowls(S_GOB_Firebowl_Bowl_002_369396b4-4cf3-4403-b274-9dc0264b2328,S_GOB_Firebowl_Hinge_002_d73a89da-d573-40f5-bdd3-0ec6d4c8fbc7);
DB_GLO_FireBowls(S_GOB_Firebowl_Bowl_003_03665d71-23e2-4bc3-be99-1bca7fedca9f,S_GOB_Firebowl_Hinge_003_d3080fb3-2020-4e40-af32-1a1e9443ce97);
DB_GLO_FireBowls(S_GOB_Firebowl_Bowl_004_1e8a5515-543a-4d41-b750-42e2614f95cc,S_GOB_Firebowl_Hinge_004_bc199917-d1f2-44f1-9d0d-ee485388de63);
DB_GLO_FireBowls(S_GOB_Firebowl_Bowl_005_0ad92e4a-8d39-4bb0-888d-84c30bd19785,S_GOB_Firebowl_Hinge_005_174ca644-8d06-4a20-bb0c-9a82c9835b60);
DB_GLO_FireBowls(S_GOB_Firebowl_Bowl_007_8b1cbb19-a89a-40ee-8fcf-9410d3efe0b5,S_GOB_Firebowl_Hinge_007_f2f01421-eb38-492e-b984-82213c413237);
DB_GLO_FireBowls(S_GOB_Firebowl_Bowl_008_5715caa0-6f6a-46b4-a99c-a2e93045f603,S_GOB_Firebowl_Hinge_008_566d43d5-8833-4060-a89c-1bac263c8ed2);
DB_GLO_FireBowls(S_GOB_Firebowl_Bowl_009_931531b8-0115-4022-bec1-0d15d8d1bd11,S_GOB_Firebowl_Hinge_009_85ce5dee-d72d-43ee-8093-36d43f5efacd);
DB_GLO_FireBowls(S_GOB_Firebowl_Bowl_010_7d52aeb0-ed1a-47a4-b194-950472bfae7b,S_GOB_Firebowl_Hinge_010_32da5c3c-05b2-4a79-949d-ca2558116a13);

//END_REGION

//REGION Kids surrender and crime
PROC_SetCorpseOwner((CHARACTER)S_GOB_GeneralFestivities_DeadBody_f8d0a86d-4df7-443c-81fd-b267703b48ab, S_GOB_Festivities_GoblinKid_002_f11d525d-e94d-4e96-a4ae-e43f34430518);
DB_GOB_Festivities_PlayerCanLootBody(0);

DB_GOB_Festivities_KidsSurrenderAD((CHARACTER)S_GOB_Festivities_GoblinKid_000_c8de613d-be02-42ab-ae91-3ed7ba5edafa, (DIALOGRESOURCE)GOB_Festivities_AD_Kid_000_Booyahg_2cccf563-029a-613a-8475-2a30415ec9a3, "Booyahg");
DB_GOB_Festivities_KidsSurrenderAD((CHARACTER)S_GOB_Festivities_GoblinKid_001_73b27c74-5462-463d-ac82-67a40b87db2c, (DIALOGRESOURCE)GOB_Festivities_AD_Kid_001_Damaged_ff1a4d18-ba53-309d-dd9e-fb98345a71d6, "Damage");
DB_GOB_Festivities_KidsSurrenderAD((CHARACTER)S_GOB_Festivities_GoblinKid_000_c8de613d-be02-42ab-ae91-3ed7ba5edafa, (DIALOGRESOURCE)GOB_Festivities_AD_Kid_000_Damaged_4583c47e-65f9-f64f-1bf9-a8b34d82684c, "Damage");
DB_GOB_Festivities_KidsSurrenderAD((CHARACTER)S_GOB_Festivities_GoblinKid_001_73b27c74-5462-463d-ac82-67a40b87db2c, (DIALOGRESOURCE)GOB_Festivities_AD_Kid_001_Booyahg_c5f353f1-f9de-b844-4cef-d40205e1029a, "Booyahg");

DB_GOB_Festivities_KidsSurrenderDialogs((CHARACTER)S_GOB_Festivities_GoblinKid_002_f11d525d-e94d-4e96-a4ae-e43f34430518, (DIALOGRESOURCE)GOB_Festivities_GoblinKid002_1f537b03-3118-20bc-b056-6bb78a0b1947);
DB_GOB_Festivities_KidsSurrenderDialogs((CHARACTER)S_GOB_Festivities_GoblinKid_001_73b27c74-5462-463d-ac82-67a40b87db2c, (DIALOGRESOURCE)GOB_Festivities_GoblinKid001_d9f09071-6102-eb93-7018-447098669e1e);
DB_GOB_Festivities_KidsSurrenderDialogs((CHARACTER)S_GOB_Festivities_GoblinKid_000_c8de613d-be02-42ab-ae91-3ed7ba5edafa, (DIALOGRESOURCE)GOB_Festivities_GoblinKid000_e95670e5-77d1-3154-2bec-4f7a1d2a26ae);

DB_GOB_Festivities_Booyahg("CALM");
DB_GOB_Festivities_Booyahg("FRIGHTENED");
DB_GOB_Festivities_Booyahg("PARALYZED");
DB_GOB_Festivities_Booyahg("PETRIFIED");
DB_GOB_Festivities_Booyahg("BLINDNESS");
DB_GOB_Festivities_Booyahg("CHILL_TOUCH");
DB_GOB_Festivities_Booyahg("COMMAND_FLEE");
DB_GOB_Festivities_Booyahg("CONFUSION");
DB_GOB_Festivities_Booyahg("CHARMED");
DB_GOB_Festivities_Booyahg("MADNESS");
DB_GOB_Festivities_Booyahg("SLEEP");
DB_GOB_Festivities_Booyahg("FROZEN");
DB_GOB_Festivities_Booyahg("SLEEPING");

PROC_CharacterDisableCrime(S_GOB_Festivities_GoblinKid_000_c8de613d-be02-42ab-ae91-3ed7ba5edafa, "GOB_Festivities_Kids_AdventurerCorpseLooting");
PROC_CharacterDisableCrime(S_GOB_Festivities_GoblinKid_001_73b27c74-5462-463d-ac82-67a40b87db2c, "GOB_Festivities_Kids_AdventurerCorpseLooting");
PROC_CharacterEnableCrime(S_GOB_Festivities_GoblinKid_002_f11d525d-e94d-4e96-a4ae-e43f34430518, "GOB_Festivities_Kids_AdventurerCorpseLooting");

DB_CRIME_AddressOwnerWithCombatFallback("GOB_Festivities_Kids_AdventurerCorpseLooting");

DB_GlobalFlagReactionAfterDialog((FLAG)GOB_Festivities_State_KidsFighting_fdc4b841-296b-4f3a-88df-a70c30cd838b, (DIALOGRESOURCE)GOB_Festivities_GoblinKid002_LootingCrime_29f3c83c-83d8-76a1-dc60-a50f1b8fbab2);
//END_REGION Kids surrender and crime

DB_GOB_General_HostilityVBAreas((TRIGGER)S_GOB_AfterDenHelp_VB_Zone_00_f292fe11-5b93-409c-b3d0-d234a32204c2);
DB_GOB_General_HostilityVBAreas((TRIGGER)S_GOB_AfterDenHelp_VB_Zone_01_a52fb246-d99b-4e4f-bc33-e8923ef28533);

SetTag(S_GOB_GoblinThrone_SecretEntrance_LadderFrom_1e4c968c-370c-4c8d-a074-ab227d85b28d, NO_PATHFINDING_PORTAL_d9793375-20f0-437f-8033-011014b0b9ca);

//REGION Shapeshift crime
PROC_CharacterDisableCrime(S_GOB_Festivities_Ogre_00_ffe2ab58-fddc-47ce-b369-bdb93b4a52dd, "DangerousMonsterReactionFallback");
PROC_CharacterDisableCrime(S_GOB_Festivities_Bugbear_Wanderer_c45936ea-2e38-4cf5-b5e1-1ff382888737, "DangerousMonsterReactionFallback");
PROC_CharacterDisableCrime(S_GOB_DrunkGoblin_0c3404d4-af6f-4c3c-8873-101a79cc4d86, "DangerousMonsterReactionFallback");
PROC_CharacterDisableCrime(S_GOB_DrowCommander_25721313-0c15-4935-8176-9f134385451b, "DangerousMonsterReactionFallback");
PROC_CharacterDisableCrime(S_GOB_GoblinPriest_b983c336-9a14-4e9b-adb9-4689e7e0afa9, "DangerousMonsterReactionFallback");
PROC_CharacterDisableCrime(S_GOB_GoblinKing_11337af0-6a57-426b-a820-c4b00923dd54, "DangerousMonsterReactionFallback");
PROC_CharacterDisableCrime(S_GOB_GoblinKing_11337af0-6a57-426b-a820-c4b00923dd54, "DangerousMonsterReaction");
//END_REGION

//SharTempleMap
DB_BookFlags(S_GOB_SharTempleMap_f01b15a1-e670-43ac-abeb-e7ec9d5fc803,GOB_TempleAccess_State_Puzzle_Check2Made_972e520a-6271-46a5-8f20-679924d4eb63);

DB_CorpseLooting_ClearRedOutlineOnFactionGroupTagged("WLD_Main_A","Act1_GOB_Festivities",(FACTION)ACT1_GOB_FestivitiesAreaGoblins_0e01ffb1-ecd5-834b-edc4-f19a61dcd405,(TAG)PERMA_DEFEATED_867f3a1e-1e4b-48c2-869e-343415231727);
DB_CorpseLooting_ClearRedOutlineOnFactionGroupTagged("WLD_Main_A","Act1_GOB_Festivities",(FACTION)ACT1_GOB_DrunkGoblin_FistFight_381ddd56-5aa8-dd7b-bb2e-b1d1fba64a8f,(TAG)PERMA_DEFEATED_867f3a1e-1e4b-48c2-869e-343415231727);
DB_CorpseLooting_ClearRedOutlineOnFactionGroupTagged("WLD_Main_A","Act1_GOB_Festivities",(FACTION)ACT1_GOB_PatrolBugbear_4675c262-b1fe-4055-84a3-acabc3dac69a,(TAG)PERMA_DEFEATED_867f3a1e-1e4b-48c2-869e-343415231727);
DB_CorpseLooting_ClearRedOutlineOnFactionGroupTagged("WLD_Main_A","Act1_GOB_Festivities",(FACTION)ACT1_GOB_VoloGuard_5e50ffef-d250-4a18-a3d4-b3fe4f574a4b,(TAG)PERMA_DEFEATED_867f3a1e-1e4b-48c2-869e-343415231727);


DB_GOB_AllCampAreas((TRIGGER)S_GOB_Festivities_SUB_217424e5-f9b7-4441-ab09-715076696a12);
DB_GOB_AllCampAreas((TRIGGER)S_GOB_ThroneRoom_SUB_bb81678b-641c-43fc-bd41-53295d59be03);
DB_GOB_AllCampAreas((TRIGGER)S_GOB_PriestDungeon_SUB_d693268c-84c9-44c2-ac7d-11ebc5524764);
DB_GOB_AllCampAreas((TRIGGER)S_GOB_WolfPens_SUB_a9a4a7c2-87c6-4031-9859-7eddb0528ff6);
DB_GOB_AllCampAreas((TRIGGER)S_GOB_PainPenance_SUB_3189cec7-0f63-468c-a147-6a5fe426f518);

DB_Dialogs(S_GOB_AftermathTrader_5e3467b4-9186-4ab2-81b8-cf379bcbd982,GOB_AftermathTrader_53e95029-a8e0-73c5-9de3-c61f40109a68);
SetOnStage(S_GOB_AftermathTrader_5e3467b4-9186-4ab2-81b8-cf379bcbd982,0);
KBSECTION
IF
DB_GOB_AllCampAreas(_Trigger)
THEN
DB_TriggerEvents_TriggerSet("GOB_GoblinCamp",_Trigger);

//REGION Festivities
IF
DB_InRegion(_Player,(TRIGGER)S_GOB_Festivities_SUB_217424e5-f9b7-4441-ab09-715076696a12)
AND
NOT DB_GlobalFlag(GLO_GoblinCamp_EverEnteredBefore_7a0172dd-df95-448a-aa2e-a04acdf7eabe)
THEN
PROC_GlobalSetFlagAndCache(GLO_GoblinCamp_EverEnteredBefore_7a0172dd-df95-448a-aa2e-a04acdf7eabe);

PROC
PROC_GLO_DefeatCounter_AllPermaDefeated("GOB_FestivitiesArea")
THEN
SetFlag((FLAG)GOB_FestivitiesArea_State_Swept_01863b85-a514-473a-9fa9-2869612480a2, NULL_00000000-0000-0000-0000-000000000000, 0); // flagType: Global
PROC_RegisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_GOB_CampCleared_e45070fa-4c29-47bf-b1fd-00050702dcab);

PROC
PROC_GOB_SetFactionGroupHostileToPlayers((STRING)_FactionGroupID)
AND
DB_CorpseLooting_ClearRedOutlineOnFactionGroupTagged("WLD_Main_A",_FactionGroupID,_Faction,(TAG)PERMA_DEFEATED_867f3a1e-1e4b-48c2-869e-343415231727)
THEN
PROC_SetRelationToPlayers(_Faction,0);
PROC_FactionSetLootOwned(_Faction,0);

PROC
PROC_GOB_MakeFestivitiesAreaHostile()
AND
QRY_OnlyOnce("GOB_MakeFestivitiesAreaHostile")
THEN
PROC_GOB_SetFactionGroupHostileToPlayers("Act1_GOB_Festivities");
PROC_GOB_SetFactionGroupHostileToPlayers("Act1_GOB_Checkpoint");
PROC_GOB_SetFactionGroupHostileToPlayers("Act1_GOB_Checkpoint_Backup");
PROC_GlobalSetFlagAndCache((FLAG)GOB_FestivitiesArea_State_Hostile_d5ee4e10-d88b-4226-9607-41b7399a837f);
//END_REGION

//REGION Kids surrender
IF
DB_GOB_Festivities_Kids(_Kid)
THEN
DB_SurrenderManually(_Kid);

// Defeat Reaction
IF
DB_GOB_Festivities_Kids((CHARACTER)_GoblinKid)
THEN
DB_GLO_DefeatCounter(_GoblinKid, "GOB_Festivities_Kids");

PROC
PROC_StateSet_Defeated((CHARACTER)_Kid)
AND
DB_GOB_Festivities_Kids(_Kid)
AND
NOT QRY_GOB_Festivities_PlayerSeesKid(_Kid)
THEN
PROC_GOB_Festivities_RemoveKids();

PROC
PROC_StateSet_Defeated((CHARACTER)_Kid)
AND
DB_GOB_Festivities_Kids(_Kid)
AND
QRY_GOB_Festivities_PlayerSeesKid(_Kid)
AND
QRY_Surrender_GetSurrenderCause(_Kid)
AND
DB_QRYRTN_Surrender_GetSurrenderCause(_Cause)
THEN
PROC_GOB_Festivities_KidsSurrender("Damage",_Cause);

QRY
QRY_GOB_Festivities_PlayerSeesKid((CHARACTER)_Kid)
AND
DB_GOB_Festivities_Kids(_Kid)
AND
DB_PartyMembers((CHARACTER)_Player)
AND
DB_Sees(_Player, _Kid)
THEN
DB_NOOP(1);

IF
StatusApplied((CHARACTER)_Kid, _Status, (CHARACTER)_Caster, _)
AND
DB_PartyMembers(_Caster)
AND
DB_GOB_Festivities_Kids(_Kid)
AND
DB_Is_InCombat(_Kid, _)
AND
DB_GOB_Festivities_Booyahg(_Status)
THEN
PROC_GOB_Festivities_KidsSurrender("Booyahg",_Caster);

PROC
PROC_GOB_Festivities_KidsSurrender((STRING)_Reason,(CHARACTER)_Cause)
AND
NOT DB_GlobalFlag(GOB_Festivities_State_KidsSurrendered_d9a99c62-f755-4719-aaca-4e82ad0de126)
AND
NOT DB_OnlyOnce("GOB_Festivities_RemoveKids")
AND
DB_GOB_Festivities_Kids((CHARACTER)_Kid)
AND
NOT DB_Defeated(_Kid)
AND
IsOnStage(_Kid, 1)
AND
DB_Is_InCombat(_Kid, _)
AND
DB_GOB_Festivities_KidsSurrenderAD(_Kid, (DIALOGRESOURCE)_Dialog, _Reason)
AND
QRY_OnlyOnce("GOB_Festivities_SurrenderADPlayed")
THEN
PROC_TryStartAD(_Dialog, _Kid);

PROC
PROC_GOB_Festivities_KidsSurrender((STRING)_Reason,(CHARACTER)_Cause)
AND
NOT DB_GlobalFlag(GOB_Festivities_State_KidsSurrendered_d9a99c62-f755-4719-aaca-4e82ad0de126)
AND
NOT DB_OnlyOnce("GOB_Festivities_RemoveKids")
AND
DB_GOB_Festivities_Kids((CHARACTER)_Kid)
AND
DB_Is_InCombat(_Kid, (GUIDSTRING)_CombatID)
AND
NOT DB_GOB_Gestivities_PausedCombat(_CombatID)
AND
QRY_OnlyOnce("GOB_Festivities_KidsSurrenderTimerLaunched")
THEN
DB_GOB_Gestivities_PausedCombat(_CombatID);
PauseCombat(_CombatID);
DB_GOB_Festivities_Kids_SurrenderCause(_Cause);
TimerLaunch("GOB_Festivities_KidsSurrender_Timer", 2300);

IF
TimerFinished("GOB_Festivities_KidsSurrender_Timer")
AND
DB_GOB_Gestivities_PausedCombat(_CombatID)
THEN
NOT DB_GOB_Gestivities_PausedCombat(_CombatID);
ResumeCombat(_CombatID);

IF
TimerFinished("GOB_Festivities_KidsSurrender_Timer")
AND
DB_GOB_Festivities_Kids((CHARACTER)_Kid)
AND
NOT DB_Defeated(_Kid)
AND
IsOnStage(_Kid, 1)
AND
DB_Is_InCombat(_Kid, (GUIDSTRING)_CombatID)
AND
DB_GOB_Festivities_KidsSurrenderDialogs(_Kid, (DIALOGRESOURCE)_StdDialog)
AND
QRY_OnlyOnce("GOB_Festivities_SurrenderStarted")
AND
DB_GOB_Festivities_Kids_SurrenderCause(_Cause)
THEN
NOT DB_GOB_Festivities_Kids_SurrenderCause(_Cause);
PROC_GlobalSetFlagAndCache((FLAG)GOB_Festivities_State_KidsSurrendered_d9a99c62-f755-4719-aaca-4e82ad0de126);
PROC_Surrender(_Kid, _Cause, _StdDialog);

IF
CombatStarted(_CombatID)
AND
DB_GOB_Festivities_Kids((CHARACTER)_Kid)
AND
DB_Is_InCombat(_Kid, _CombatID)
AND
DB_PartyMembers((CHARACTER)_Player)
AND
DB_Is_InCombat(_Player, _CombatID)
AND
IsInTrigger(_Kid, S_GOB_Festivities_KidsArea_0ebcc246-af8b-4305-b5c6-b01d19e5a6fd, 1)
AND
NOT DB_GlobalFlag((FLAG)GOB_Festivities_State_KidsFighting_fdc4b841-296b-4f3a-88df-a70c30cd838b)
THEN
PROC_GlobalSetFlagAndCache((FLAG)GOB_Festivities_State_KidsFighting_fdc4b841-296b-4f3a-88df-a70c30cd838b);

IF
LeftCombat(_Kid, _CombatID)
AND
DB_GOB_Festivities_Kids((CHARACTER)_Kid)
AND
DB_GlobalFlag((FLAG)GOB_Festivities_State_KidsFighting_fdc4b841-296b-4f3a-88df-a70c30cd838b)
AND
DB_GOB_Festivities_Kids((CHARACTER)_OtherKid)
AND
_OtherKid != _Kid
AND
NOT DB_Is_InCombat(_OtherKid, _CombatID)
THEN
PROC_GlobalClearFlagAndCache((FLAG)GOB_Festivities_State_KidsFighting_fdc4b841-296b-4f3a-88df-a70c30cd838b);

PROC
PROC_GlobalFlagReactionAfterDialog((FLAG)GOB_Festivities_State_KidsFighting_fdc4b841-296b-4f3a-88df-a70c30cd838b, (DIALOGRESOURCE)GOB_Festivities_GoblinKid002_LootingCrime_29f3c83c-83d8-76a1-dc60-a50f1b8fbab2)
AND
NOT DB_PermaDefeated((CHARACTER)S_GOB_SecludedSpot_Goblin_b811fc94-bcd7-4be3-a53a-0663cbc16c34)
THEN
PROC_SetRelation((FACTION)ACT1_GOB_SecludedGoblin_9782ad5f-54be-40cb-ba2d-5f8ec4d0fcf6, (FACTION)ACT1_GOB_FestivitiesKids_021b58fe-a0de-4cef-af2c-ca03b475de50, 50);

IF
FlagCleared((FLAG)GOB_Festivities_State_KidsFighting_fdc4b841-296b-4f3a-88df-a70c30cd838b, _, _)
AND
NOT DB_PermaDefeated((CHARACTER)S_GOB_SecludedSpot_Goblin_b811fc94-bcd7-4be3-a53a-0663cbc16c34)
THEN
PROC_SetRelation((FACTION)ACT1_GOB_SecludedGoblin_9782ad5f-54be-40cb-ba2d-5f8ec4d0fcf6, (FACTION)ACT1_GOB_FestivitiesKids_021b58fe-a0de-4cef-af2c-ca03b475de50, 100);
//END_REGION Kids surrender

//REGION Kids crime reaction
IF
DB_GOB_Festivities_Kids(_Kid)
THEN
PROC_CharacterEnableCrime(_Kid, "GOB_Checkpoint_WarPaint");

PROC
PROC_BlockLootCorpse(_Player, S_GOB_GeneralFestivities_DeadBody_f8d0a86d-4df7-443c-81fd-b267703b48ab)
AND
DB_PartyMembers(_Player)
AND
NOT DB_GOB_Festivities_PlayerCanLootBody(1)
AND
QRY_GOB_Festivities_AnyKidCanBlockCorpseLooting(_Player)
THEN
DB_CustomLootCorpseResponse(_Player, S_GOB_GeneralFestivities_DeadBody_f8d0a86d-4df7-443c-81fd-b267703b48ab, 0);
PROC_CharacterRegisterCrime(_Player, "GOB_Festivities_Kids_AdventurerCorpseLooting", S_GOB_GeneralFestivities_DeadBody_f8d0a86d-4df7-443c-81fd-b267703b48ab, S_GOB_Festivities_GoblinKid_002_f11d525d-e94d-4e96-a4ae-e43f34430518, 0);

QRY
QRY_CRIME_BlockRegisterCrime(_Player, _CrimeType, S_GOB_GeneralFestivities_DeadBody_f8d0a86d-4df7-443c-81fd-b267703b48ab, _Kid, _CrimeID)
AND
DB_PartyMembers(_Player)
AND
DB_GOB_Festivities_Kids(_Kid)
AND
NOT DB_GOB_Festivities_PlayerCanLootBody(1)
AND
QRY_CRIME_IsCrimeFamilyMember(_CrimeType, "LootOwnedCorpse")
THEN
DB_NOOP(1);

QRY
QRY_CRIME_BlockRegisterCrime(_Player, "MoveOwnedCorpse", S_GOB_GeneralFestivities_DeadBody_f8d0a86d-4df7-443c-81fd-b267703b48ab, _Kid, _CrimeID)
AND
DB_GOB_Festivities_Kids(_Kid)
THEN
PROC_GlobalSetFlagAndCache((FLAG)GOB_Festivities_Event_CorpseShoved_cba2b424-30dc-4039-b7a9-6dc8f05c3763);

QRY
QRY_CRIME_BlockRegisterCrime(_Player, "MoveOwnedCorpse", S_GOB_GeneralFestivities_DeadBody_f8d0a86d-4df7-443c-81fd-b267703b48ab, _Kid, _CrimeID)
AND
DB_PartyMembers(_Player)
AND
DB_GOB_Festivities_Kids(_Kid)
AND
NOT DB_GOB_Festivities_PlayerCanLootBody(1)
AND
QRY_GOB_Festivities_AnyKidCanBlockCorpseLooting(_Player)
THEN
PROC_CharacterRegisterCrime(_Player, "GOB_Festivities_Kids_AdventurerCorpseLooting", S_GOB_GeneralFestivities_DeadBody_f8d0a86d-4df7-443c-81fd-b267703b48ab, S_GOB_Festivities_GoblinKid_002_f11d525d-e94d-4e96-a4ae-e43f34430518, 0);

IF
FlagSet(GOB_Festivities_State_KidsSurrendered_d9a99c62-f755-4719-aaca-4e82ad0de126, _, _)
AND
NOT DB_GOB_Festivities_PlayerCanLootBody(1)
THEN
PROC_ClearCorpseOwner(S_GOB_GeneralFestivities_DeadBody_f8d0a86d-4df7-443c-81fd-b267703b48ab);
DB_GOB_Festivities_PlayerCanLootBody(1);

PROC
PROC_GOB_Festivities_RemoveKids_Internal()
AND
NOT DB_GOB_Festivities_PlayerCanLootBody(1)
THEN
PROC_ClearCorpseOwner(S_GOB_GeneralFestivities_DeadBody_f8d0a86d-4df7-443c-81fd-b267703b48ab);
DB_GOB_Festivities_PlayerCanLootBody(1);

PROC
PROC_StateSet_PermaDefeated((CHARACTER)S_GOB_Festivities_GoblinKid_002_f11d525d-e94d-4e96-a4ae-e43f34430518)
AND
NOT DB_GOB_Festivities_PlayerCanLootBody(1)
THEN
PROC_ClearCorpseOwner(S_GOB_GeneralFestivities_DeadBody_f8d0a86d-4df7-443c-81fd-b267703b48ab);
DB_GOB_Festivities_PlayerCanLootBody(1);

IF
CharacterOnCrimeSensibleActionNotification(_Kid,_,_,_Reaction,_,_,_,_,_,_)
AND
DB_GOB_Festivities_Kids((CHARACTER)_Kid)
AND
DB_CRIME_FleeReaction(_Reaction)
THEN
PROC_GOB_Festivities_RemoveKids();

IF
KilledBy(_Kid, _AttackOwner, _, _)
AND
DB_GOB_Festivities_Kids(_Kid)
AND
DB_PartyMembers((CHARACTER)_AttackOwner)
THEN
DB_GOB_Festitivies_KidKiller(_Kid, _AttackOwner);

IF
CombatEnded(_CombatID)
AND
DB_GOB_Festitivies_KidKiller(_Kid, _AttackOwner)
AND
DB_Was_InCombat(_Kid, _CombatID)
THEN
NOT DB_GOB_Festitivies_KidKiller(_Kid, _AttackOwner);
PROC_CharacterRegisterCrime(_AttackOwner, "Murder", NULL_00000000-0000-0000-0000-000000000000, _Kid, 0);

QRY
QRY_GOB_Festivities_AnyKidCanBlockCorpseLooting((CHARACTER)_Player)
AND
DB_GOB_Festivities_Kids((CHARACTER)_Kid)
AND
NOT DB_Is_InCombat(_Kid, _)
AND
CanSee(_Kid, _Player, 1)
THEN
DB_NOOP(1);

IF
DB_Was_InCombat(_Kid, _CombatID)
AND
NOT DB_Is_InCombat(_Kid, _)
AND
DB_GOB_Festivities_Kids((CHARACTER)_Kid)
AND
DB_CurrentLevel("WLD_Main_A")
AND
DB_GlobalFlag(GOB_Festivities_Event_CorpseShoved_cba2b424-30dc-4039-b7a9-6dc8f05c3763)
AND
NOT DB_GlobalFlag(GOB_Festivities_State_KidsSurrendered_d9a99c62-f755-4719-aaca-4e82ad0de126)
THEN
PROC_GOB_Festivities_RemoveKids();
//END_REGION Kids crime reaction

//REGION Kids disappearing from the area
IF
DialogEnded(GOB_Festivities_GoblinKid002_LootingCrime_29f3c83c-83d8-76a1-dc60-a50f1b8fbab2, _)
AND
DB_GlobalFlag(GOB_Festivities_Event_KidsRun_8e6522c7-e0c1-8e17-1346-97ea30a05135)
THEN
PROC_GOB_Festivities_RemoveKids();

IF
TurnStarted((CHARACTER)_Player)
AND
DB_CurrentLevel("WLD_Main_A")
AND
DB_Players(_Player)
AND
DB_InRegion(_Player, (TRIGGER)S_GOB_Festivities_SUB_217424e5-f9b7-4441-ab09-715076696a12)
AND
DB_Is_InCombat(_Player, (GUIDSTRING)_CombatID)
AND
DB_GOB_Festivities_Kids((CHARACTER)_Kid)
AND
NOT DB_Is_InCombat(_Kid, _)
AND
CanSee(_Kid, _Player, 1)
THEN
PROC_GOB_Festivities_RemoveKids();

PROC
PROC_GOB_MakeFestivitiesAreaHostile()
THEN
PROC_GOB_Festivities_RemoveKids();

IF
FlagSet(GOB_GoblinToast_Event_ToStations_0b555ee8-2b8e-3881-b35e-9811f6baf77a, NULL_00000000-0000-0000-0000-000000000000, _)
THEN
PROC_GOB_Festivities_RemoveKids();

PROC
PROC_GOB_Festivities_RemoveKids()
AND
QRY_OnlyOnce("GOB_Festivities_RemoveKids")
THEN
PROC_GOB_Festivities_RemoveKids_Internal();

PROC
PROC_GOB_Festivities_RemoveKids_Internal()
AND
DB_GOB_Festivities_Kids(_Kid)
AND
DB_Is_InCombat(_Kid, _)
THEN
LeaveCombat(_Kid);
SetImmortal(_Kid, 1);
SetCanJoinCombat(_Kid, 0);
PROC_SetCanFight(_Kid, 0);

PROC
PROC_GOB_Festivities_RemoveKids_Internal()
AND
DB_GOB_Festivities_Kids(_Kid)
AND
NOT DB_Defeated(_Kid)
THEN
PROC_UnSurrender(_Kid, NULL_00000000-0000-0000-0000-000000000000);
PROC_ForceStopDialog(_Kid);
PROC_DisappearOutOfSight(_Kid, "Run", 1, "");
//END_REGION Kids disappearing from the area

//REGION Goblinocide
IF
FlagSet(GOB_FestivitiesArea_State_Swept_01863b85-a514-473a-9fa9-2869612480a2,_,_)
AND
QRY_OnlyOnce("GLO_Camp_SetSoundStateDead")
THEN
PROC_GOB_Misc_MakeFestivitiesDead();

IF
FlagSet(GOB_GoblinToast_State_RealizedPoison_0e867c08-b2b9-4f43-8260-168026663e2c,_,_)
AND
QRY_OnlyOnce("GLO_Camp_SetSoundStatePoisoned")
THEN
PROC_GOB_Misc_MakeFestivitiesPoisoned();

PROC
PROC_GOB_Misc_MakeFestivitiesPoisoned()
THEN
TriggerSetSoundState((TRIGGER)Amb_SV_Forest_Dense_FZ_001_727b95fa-f30c-3d41-cbe2-dc0d67f8f105,"S_GOB_Festivities_States","Poisoned",1);
TriggerSetSoundState((TRIGGER)Amb_SV_GoblinCamp_FZ_001_68aa2d33-5615-5c33-4a4d-7892c2c8f108,"S_GOB_Festivities_States","Poisoned",1);
TriggerSetSoundState((TRIGGER)Amb_SV_Plains_FZ_000_9878e6ab-03c9-dca0-a345-193a021d1906,"S_GOB_Festivities_States","Poisoned",1);

PROC
PROC_GOB_Misc_MakeFestivitiesDead()
THEN
TriggerSetSoundState((TRIGGER)Amb_SV_Forest_Dense_FZ_001_727b95fa-f30c-3d41-cbe2-dc0d67f8f105,"S_GOB_Festivities_States","Dead",1);
TriggerSetSoundState((TRIGGER)Amb_SV_GoblinCamp_FZ_001_68aa2d33-5615-5c33-4a4d-7892c2c8f108,"S_GOB_Festivities_States","Dead",1);
TriggerSetSoundState((TRIGGER)Amb_SV_Plains_FZ_000_9878e6ab-03c9-dca0-a345-193a021d1906,"S_GOB_Festivities_States","Dead",1);
//END_REGION

//REGION Drunk status for goblins
IF
DB_GLO_DefeatCounter(_Goblin, "GOB_FestivitiesArea")
THEN
ApplyStatus(_Goblin, "GOB_FESTIVITIES_DRUNK", -1.0);
//END_REGION Drunk status for goblins

//REGION VoloGuard moving to throne room
IF
UseStarted(S_GOB_VoloGuard_c25f0e79-b9f7-453c-8138-964bca267d93,S_GOB_ThroneRoom_Door_Entrance_bf8a7b29-383d-4344-bbe6-b72f10c1ba50)
THEN
NOT DB_GLO_DefeatCounter(S_GOB_VoloGuard_c25f0e79-b9f7-453c-8138-964bca267d93, "GOB_FestivitiesArea");
DB_GLO_DefeatCounter(S_GOB_VoloGuard_c25f0e79-b9f7-453c-8138-964bca267d93, "GOB_GoblinThroneGeneral");
//END_REGION

//REGION ShadowHeart VB
IF
EnteredTrigger(S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679,_Trigger)
AND
DB_GOB_ShadowHeart_Trigger(_Trigger,_VB)
AND
DB_Singleton("GOB_ShadowHeart_VB",_Int)
AND
_Int < 2
AND
IntegerSum(_Int,1,_Count)
THEN
DB_Singleton("GOB_ShadowHeart_VB",_Count);
StartVoiceBark(_VB,S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679);
NOT DB_GOB_ShadowHeart_Trigger(_Trigger,_VB);
PROC_TriggerUnregisterForPlayers(_Trigger);

IF
FlagSet(GOB_TorturedAdventurer_Knows_ToldHiddenSharTemple_802ac505-3d90-7eba-d822-4d5e73b65d07,_,_)
THEN
DB_Singleton("GOB_ShadowHeart_VB",1);

IF
DB_GlobalFlag(GOB_TorturedAdventurer_Knows_ToldHiddenSharTemple_802ac505-3d90-7eba-d822-4d5e73b65d07)
AND
DB_Singleton("GOB_ShadowHeart_VB",2)
AND
DB_GOB_ShadowHeart_Trigger(_Trigger,_VB)
THEN
NOT DB_GOB_ShadowHeart_Trigger(_Trigger,_VB);
PROC_TriggerUnregisterForPlayers(_Trigger);
NOT DB_Singleton("GOB_ShadowHeart_VB",2);
//END_REGION

//REGION Teleport to Creche

IF
UseStarted(_Player, S_WLD_RoadToMountainPass_a81232d7-af15-4c77-b0e1-d7e791fd463b)
THEN
PROC_TeleportPartiesTo(efe2f85a-c4ed-4299-bd73-3eac80962da6,"");

//END_REGION

//REGION Camp hostility, triggered according to various resolutions of the AoD quest line

IF
DB_GlobalFlag((FLAG)DEN_AttackOnDen_State_RaidersInDen_5e839d5f-3e25-4b56-b758-81d25c9cbd33)
AND
NOT DB_GlobalFlag(GOB_General_State_Hostile_9816a6fb-54d7-4b9c-a5c7-bc6cda6bb276)
AND
DB_Is_InCombat(S_GOB_DrowCommander_25721313-0c15-4935-8176-9f134385451b, _ID)
AND
DB_Is_InCombat(_Player, _ID)
AND
DB_Players((CHARACTER)_Player)
AND
NOT DB_OnlyOnce("DEN_AttackOnDen_EnterCombatWithDrow")
AND
IsEnemy(_Player, S_GOB_DrowCommander_25721313-0c15-4935-8176-9f134385451b, 1)
THEN
SetImmortal(S_GOB_DrowCommander_25721313-0c15-4935-8176-9f134385451b, 0);
DB_OnlyOnce("DEN_AttackOnDen_EnterCombatWithDrow");
SetFlag(GOB_General_State_Hostile_9816a6fb-54d7-4b9c-a5c7-bc6cda6bb276, NULL_00000000-0000-0000-0000-000000000000, 0);

IF
FlagSet(DEN_AttackOnDen_State_DenVictory_71c7f23e-3ff1-c9b8-3ef5-d75fa1b42c8d, _, _)
AND
DB_GlobalFlag(GLO_AttackOnDen_State_RaidReady_eb361773-da88-dee4-f4c8-a22acae60948)
AND
NOT DB_GlobalFlag(GOB_General_State_Hostile_9816a6fb-54d7-4b9c-a5c7-bc6cda6bb276)
THEN
SetFlag(GOB_General_State_Hostile_9816a6fb-54d7-4b9c-a5c7-bc6cda6bb276, NULL_00000000-0000-0000-0000-000000000000, 0);


IF
FlagSet(GOB_General_State_Hostile_9816a6fb-54d7-4b9c-a5c7-bc6cda6bb276, _, _)
THEN
PROC_FactionSetLootOwned((FACTION)ACT1_GOB_Goblins_General_633245e8-cbb8-b0c4-12ec-32d2ff9424e2,0);
PROC_GOB_MakeRegionHostile();

PROC
PROC_GOB_MakeRegionHostile()
THEN
DebugBreak("Goblin Camp hostile!");

PROC
PROC_GOB_MakeRegionHostile()
THEN
PROC_GOB_Checkpoint_MakeCheckpointHostile();
PROC_GOB_MakeFestivitiesAreaHostile();
SetFlag(GOB_BattleStations_Event_ToStation_03a8bd66-c23f-4989-9a7b-775cf475104b,NULL_00000000-0000-0000-0000-000000000000);

//END_REGION Attack On Den makes camp hostile, if tieflings were not betrayed

//REGION VB on GobCamp approach once Camp is hostile.

PROC
PROC_GOB_MakeRegionHostile()
AND
DB_GOB_General_HostilityVBAreas(_Trigger)
THEN
PROC_TriggerRegisterForPlayers(_Trigger);

IF
EnteredTrigger(_Player,_Trigger)
AND
DB_GOB_General_HostilityVBAreas(_Trigger)
AND
QRY_OncePerUserAndNearbyPlayers(_Player,"GOB_PAD_WarningAfterDenHelp")
THEN
PROC_TryStartAD(GOB_PAD_WarningAfterDenHelp_bbb1a1af-53f4-bbb6-4558-3b907cb2a7b1,_Player);
PROC_GOB_General_UnregisterHostilityVBTriggers();
PROC_GOB_PostAoD_GoblinCampHostile_JournalHook();
PROC_CAMP_GoblinHuntCelebration_RaidersBetrayal_JournalHook();

IF
DB_Is_InCombat(_Player,_CombatID)
AND
DB_DialogPlayers(_DialogID,_Player,_Slot)
AND
DB_DialogName(GOB_PAD_WarningAfterDenHelp_bbb1a1af-53f4-bbb6-4558-3b907cb2a7b1,_DialogID)
THEN
PROC_ForceStopSpecificDialog(_Player,GOB_PAD_WarningAfterDenHelp_bbb1a1af-53f4-bbb6-4558-3b907cb2a7b1);

PROC
PROC_GOB_PostAoD_GoblinCampHostile_JournalHook()
THEN
DB_NOOP(1);

PROC
PROC_GOB_General_UnregisterHostilityVBTriggers()
AND
DB_GOB_General_HostilityVBAreas(_Trigger)
THEN
PROC_TriggerUnregisterForPlayers(_Trigger);

//END_REGION VB on GobCamp approach after helping tieflings at DEN

//REGION Killing the king send the goblins to battle stations
IF
DB_GlobalFlag(GLO_GoblinKing_State_PermaDefeated_d985480d-6158-0b0f-adfc-4bf3db785bf1)
THEN
PROC_GOB_CheckSetKingDeadNoPoisonState();
SetFlag(GOB_GoblinToast_State_RealizedPoison_0e867c08-b2b9-4f43-8260-168026663e2c, NULL_00000000-0000-0000-0000-000000000000);
SetFlag(GOB_GoblinToast_Event_ToStations_0b555ee8-2b8e-3881-b35e-9811f6baf77a, NULL_00000000-0000-0000-0000-000000000000);
PROC_GOB_MakeFestivitiesAreaHostile();

PROC
PROC_GOB_CheckSetKingDeadNoPoisonState()
AND
NOT DB_GlobalFlag(GOB_GoblinToast_State_RealizedPoison_0e867c08-b2b9-4f43-8260-168026663e2c)
THEN
SetFlag(GOB_Festivities_State_KingDeadNoPoison_74d1d1b5-b8de-4685-8c30-4325aedbab03, NULL_00000000-0000-0000-0000-000000000000);
//END_REGION Killing the king send the goblins to battle stations


//REGION Goblin Hunt aftermath
//Clearing goblins when Minthara is loaded at moonrise (if attack on den did not happen, which in that case they would have already been cleared)
PROC
PROC_GOB_ClearGoblinFestivitiesFallback()
AND
NOT DB_GlobalFlag(DEN_AttackOnDen_State_RaiderVictory_abe1bce8-c234-4afe-a490-76210d98a078)
AND
NOT DB_GOB_ClearGoblinsOnLevelLoad(1)
THEN
DB_GOB_ClearGoblinsOnLevelLoad(1);

//TriggerLaunchIterator only works on level that is loaded
IF
LevelLoaded("WLD_Main_A")
AND
DB_GOB_ClearGoblinsOnLevelLoad(1)
THEN
NOT DB_GOB_ClearGoblinsOnLevelLoad(1);
PROC_GOB_GoblinHuntAftermath_Pacified();
RealtimeObjectTimerLaunch(S_GLO_Volo_2af25a85-5b9a-4794-85d3-0bd4c4d262fa, "GOB_Volo_ActiveCampBuffer", 500); //ActiveCamp DB not set in time for teleport

IF
ObjectTimerFinished(S_GLO_Volo_2af25a85-5b9a-4794-85d3-0bd4c4d262fa, "GOB_Volo_ActiveCampBuffer")
THEN
DB_GOB_SendVoloToCamp(1); //DB due to potentially being deferred if being teleported from DEN if entering GOB camp for the first time

IF
DB_GOB_SendVoloToCamp(1)
AND
DB_GlobalFlag(GLO_Volo_State_AtGoblinCamp_fc815506-028d-582d-a953-4182d682aa07)
THEN
NOT DB_GOB_SendVoloToCamp(1);
PROC_GOB_VoloBallad_MoveVoloToCamp(CAMP_VoloOperation_ba7c506d-c743-5dc9-7d98-eeea7b4ef2a2);

PROC
PROC_GOB_GoblinHuntAftermath_Pacified()
THEN
PROC_GOB_General_UnregisterHostilityVBTriggers();
PROC_GOB_RemoveAllGoblins();

PROC
PROC_GOB_RemoveAllGoblins()
AND
DB_GOB_AllCampAreas(_Trigger)
THEN
TriggerLaunchIterator(_Trigger,"GOB_GoblinHuntCelebration_Aftermath_ClearCamp","GOB_GoblinHuntCelebration_Aftermath_ClearCamp_Finish");

IF
EntityEvent(_Npc,"GOB_GoblinHuntCelebration_Aftermath_ClearCamp")
AND
NOT DB_PartOfTheTeam((CHARACTER)_Npc)
AND
NOT DB_PermaDefeated(_Npc)
THEN
SetOnStage(_Npc,0);

IF
EntityEvent(S_GOB_Festivities_SUB_217424e5-f9b7-4441-ab09-715076696a12, "GOB_GoblinHuntCelebration_Aftermath_ClearCamp_Finish")
THEN
PROC_GOB_Misc_MakeFestivitiesDead();
SetOnStage(S_GOB_AftermathTrader_5e3467b4-9186-4ab2-81b8-cf379bcbd982,1);

//END_REGION Goblin Hunt aftermath

//REGION Alt way into shattered sanctum once been inside
// Torture cave exit has interaction disabled by default
IF
UseFinished(_PartyMember,S_DOOR_GoblinCaveTortureEntrance_b98ef42f-c7c8-499b-a57a-a5df8d9f4d76,1)
AND
DB_PartyMembers(_PartyMember)
AND
QRY_OnlyOnce("GOB_GoblinCaveTortureExitEnabled_OnlyOnce")
THEN
SetCanInteract((ITEM)S_DOOR_GoblinCaveTortureExit_000_bbabfc3e-a508-4973-8726-e7e3e80e47a0,1);
//END_REGION

//REGION Closing the goal
IF
FlagSet((FLAG)Act2_PointOfNoReturnReached_a3155f30-b8f3-4db5-ac21-d3036f4426e3,_,_)
THEN
PROC_GOB_Festivities_CompleteGoal();

PROC
PROC_GOB_Festivities_CompleteGoal()
THEN
NOT DB_GOB_ClearGoblinsOnLevelLoad(1);
NOT DB_GOB_SendVoloToCamp(1);
GoalCompleted;
//END_REGION
EXITSECTION
PROC_GOB_General_UnregisterHostilityVBTriggers();
ENDEXITSECTION
ParentTargetEdge "Act1"
