Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_DropMutingStatussesDialog((DIALOGRESOURCE)Gale_Recruitment_4b3ad930-fb84-09ff-eced-37265b7ba8c6);

SetOnStage(S_ORI_Gale_DeathMephit_4b90d334-3646-43f8-94f4-e29b13ac75d4, 0);
SetOnStage(S_ORI_Gale_DeathPouch_72ab0321-5a3f-499f-8d7e-3ecfe72157e3, 0);

DB_DropMutingStatussesDialog((DIALOGRESOURCE)ORI_Gale_DeathFlute_c02f1420-fc9b-c969-229b-28edd6fb0347);
DB_DropMutingStatussesDialog((DIALOGRESOURCE)ORI_Gale_DeathPouch_44e0c868-01f3-e07d-8bf1-e6b4095aa311);
DB_HasItemEvent(S_ORI_Gale_DeathNote_5bc398e6-d238-475d-85e0-de985d61c564, ORI_Gale_State_HasDeathNote_fd1b196a-606b-4454-9dd2-eb43639fd346);

DB_ORI_AvatarFlag(DEN_RaidingParty_Event_Gale_PeacefulResolutionApproval_8501f655-6536-1de7-2b2f-9393300d1d67);
DB_ORI_AvatarFlag(DEB_CapturedGoblin_Event_Gale_SteppedInfrontOfCrossbowApproval_4b9cc492-0764-abbc-04e1-151b2024ad91);
DB_ORI_AvatarFlag(DEN_ShadowDruid_Event_Gale_FreedChildApproval_250f8422-5dbf-6594-807f-6735efa02dca);
DB_ORI_AvatarFlag(DEN_Apprentice_State_Gale_GotAntidoteLocationApproval_bec5378f-e125-7aa6-0653-ba21770d0ba3);
DB_ORI_AvatarFlag(DEN_HarpyMeal_State_Gale_GotChildRescueApproval_6a474068-b647-4d6c-9501-e6ea8110604f);

DB_ORI_Gale_TrustMoments(DEN_RaidingParty_State_ReachedPeacefulResolution_ae289a97-f3c5-9adc-f7a5-8639676789a0, (DIALOGRESOURCE)NULL_00000000-0000-0000-0000-000000000000, (FLAG)DEN_RaidingParty_State_ReachedPeacefulResolution_ae289a97-f3c5-9adc-f7a5-8639676789a0);
DB_ORI_Gale_TrustMoments(DEB_CapturedGoblin_State_SteppedInfrontOfCrossbow_dff39def-ac63-8851-1330-239b310f090f, (DIALOGRESOURCE)DEN_CapturedGoblin_SteppedInFrontOfCrossbow_WRD_Gale_6af89a3e-f317-acfe-2f7a-85ba6cd8bfb8, DEB_CapturedGoblin_Event_Gale_SteppedInfrontOfCrossbowApproval_4b9cc492-0764-abbc-04e1-151b2024ad91);
DB_ORI_Gale_TrustMoments(DEN_ShadowDruid_State_FreedChild_e0db3e8d-497d-33bb-ce67-91dff9fb1c67, DEN_ShadowDruid_WRD_Gale_10b419a6-f589-09aa-c3df-6224160b3696, DEN_ShadowDruid_Event_Gale_FreedChildApproval_250f8422-5dbf-6594-807f-6735efa02dca);
DB_ORI_Gale_TrustMoments(DEN_Apprentice_State_GotAntidoteLocation_96b24528-88a0-7274-9d94-38c1c87d58f0, (DIALOGRESOURCE)DEN_Apprentice_WRD_Gale_8d54b95f-1c2c-8f89-144f-18f66d00fabd, DEN_Apprentice_State_Gale_GotAntidoteLocationApproval_bec5378f-e125-7aa6-0653-ba21770d0ba3);
DB_ORI_Gale_TrustMoments(DEN_HarpyMeal_State_RescuedChild_7a90f576-f762-4710-aa4d-9a81905bd971, (DIALOGRESOURCE)DEN_HarpyMeal_WRD_Gale_d6eb7a2e-b521-87cb-298f-628550704c3b, DEN_HarpyMeal_Event_Gale_GotChildRescueApproval_6a474068-b647-4d6c-9501-e6ea8110604f);

DB_OriginMayLeaveDialog((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, (DIALOGRESOURCE)CAMP_GoblinHuntCelebration_SD_ROM_NightWithGale_9e267aa8-0f95-1364-c97d-a13a12f30386);


SetOnStage(S_ORI_Gale_DeathPouch_Open_30ad0401-7872-429a-809c-42c753eb69f2, 0);

PROC_SetInvulnerable((CHARACTER)S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, 1);
SetDetached(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, 1);
PROC_CharacterDisableAllCrimes(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe,1);
SetOnStage(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, 0);

DB_RelationshipDialog_WRD_TriggerInCamp((DIALOGRESOURCE)ORI_Resurrected_WRD_Gale_6ed0f4c2-5324-08bd-bf27-22237a90da64,(FLAG)NULL_00000000-0000-0000-0000-000000000000);

DB_OriginMayLeaveDialog((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, CAMP_Betrayal_CRD_Gale_4a473faa-041e-6f9c-d5e5-185e002d40a5);
NOT DB_ORI_Gale_MagicItemApprovalIPRD(1);
KBSECTION
PROC
PROC_GLO_Origins_SetupRecruitment("WLD_Main_A")
AND
NOT DB_Players((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
THEN
SetDetached((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 1);
SetCanSpotSneakers((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 0);
PROC_SetInvulnerable(S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 1);
PROC_CharacterDisableAllCrimes((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604);
SetVisible(S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 0);
DB_SpotPlayers((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, "ORI_Gale_FindAvatar", NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);
DB_SpotPlayers_HasTag((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, "ORI_Gale_FindAvatar", (TAG)AVATAR_306b9b05-1057-4770-aa17-01af21acd650, 1);
DB_SpotPlayers_SpotTrigger((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, "ORI_Gale_FindAvatar", S_ORI_Gale_RecruitmentTrigger_22824abf-0652-4313-8caa-3cfc54d0089d);
DB_SpotPlayers_Continuous((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, "ORI_Gale_FindAvatar");

PROC
PROC_GLO_Origins_SetupRecruitment("WLD_Main_A")
AND
DB_Players((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
THEN
PROC_ORI_Gale_PlayerSetup(NULL_00000000-0000-0000-0000-000000000000);


//REGION Recruitment
//FindValidPosition Fallback
PROC
PROC_SpotPlayers_Spotted((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, "ORI_Gale_FindAvatar", _Avatar)
AND
GetPosition(S_CHA_WaypointTrigger_Top_4141c0a2-5ba9-42c0-ab18-082426df45e7, _X, _Y, _Z)
AND
FindValidPosition(_X, _Y, _Z, 4.0, (CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 1, _Vx, _Vy, _Vz)
THEN
DB_ORI_Gale_TeleportPosition(_Vx, _Vy, _Vz);

//FindValidPosition Fallback
PROC
PROC_SpotPlayers_Spotted((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, "ORI_Gale_FindAvatar", _Avatar)
AND
NOT DB_ORI_Gale_TeleportPosition(_, _, _)
AND
GetPosition(S_CHA_WaypointTrigger_Top_4141c0a2-5ba9-42c0-ab18-082426df45e7, _X, _Y, _Z)
THEN
DB_ORI_Gale_TeleportPosition(_X, _Y, _Z);

PROC
PROC_SpotPlayers_Spotted((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, "ORI_Gale_FindAvatar", _Avatar)
AND
DB_ORI_Gale_TeleportPosition(_X, _Y, _Z)
AND
QRY_StartDialog(Gale_Recruitment_4b3ad930-fb84-09ff-eced-37265b7ba8c6, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, _Avatar)
THEN
PROC_SpotPlayers_StopSpotting((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, "ORI_Gale_FindAvatar");
NOT DB_SpotPlayers((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, "ORI_Gale_FindAvatar", NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);
NOT DB_SpotPlayers_HasTag((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, "ORI_Gale_FindAvatar", (TAG)AVATAR_306b9b05-1057-4770-aa17-01af21acd650, 1);
NOT DB_SpotPlayers_SpotTrigger((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, "ORI_Gale_FindAvatar", S_ORI_Gale_RecruitmentTrigger_22824abf-0652-4313-8caa-3cfc54d0089d);
NOT DB_SpotPlayers_Continuous((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, "ORI_Gale_FindAvatar");
NOT DB_ORI_Gale_TeleportPosition(_X, _Y, _Z);
TeleportToPosition(S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, _X, _Y, _Z, "", 0, 0, 0, 1, 1);
LookFromTrigger(S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, S_CHA_WaypointTrigger_Top_4141c0a2-5ba9-42c0-ab18-082426df45e7, 1);
PlayEffect(S_CHA_WaypointShrine_Top_8ebd584c-97e3-42fd-b81f-80d7841ebdf3, VFX_Script_Waypoint_Portal_01_f545f66c-87c8-8dde-cd27-16539cb0dc45, "", 1.0);
PlayAnimation(S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, CUST_WaypointAppear_01_b6aa4aaa-66d5-4564-833c-7308dc1c52e3);
SetDetached((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 0);
SetCanSpotSneakers((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 1);
PROC_SetInvulnerable(S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 0);
PROC_CharacterEnableAllCrimes((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604);
SetVisible(S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 1);

IF
DialogRequestFailed(Gale_Recruitment_4b3ad930-fb84-09ff-eced-37265b7ba8c6, _)
THEN
SetFlag(ORI_Gale_State_DidFirstRecruitment_d6ea1f56-d6ab-dbae-4438-22d877c8faf0, NULL_00000000-0000-0000-0000-000000000000, 0);

PROC
PROC_KnockedOut((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604,(GUIDSTRING)_Cause)
THEN
PROC_PurgeLevelPartyFlag(ORI_Gale_State_AttackedDuringRecruitment_d79bad11-eca1-2688-0c5d-ecd64c1b64bf);

IF
FlagSet(ORI_Gale_State_AttackedDuringRecruitment_d79bad11-eca1-2688-0c5d-ecd64c1b64bf, _Character, _)
THEN
ClearFlag((FLAG)ORI_Gale_State_Rescuer_4cf4fadb-1c69-ed8f-b28e-105fc665fc79, _Character, 0);


//END_REGION

//REGION Death Note
IF
DialogEnded(ORI_Gale_DeathPouch_44e0c868-01f3-e07d-8bf1-e6b4095aa311, _ID)
AND
DB_GlobalFlag(ORI_Gale_State_OpenedPouch_a533861d-a4f9-482a-8be2-7c9523df9c90)
AND
DB_DialogPlayers(_ID, _Player, 1)
THEN
PROC_TeleportToAndOffStage(S_ORI_Gale_DeathPouch_72ab0321-5a3f-499f-8d7e-3ecfe72157e3, _Player); //Ensure it's no longer in player inventory.
PROC_ToInventoryAndOnStage(S_ORI_Gale_DeathPouch_Open_30ad0401-7872-429a-809c-42c753eb69f2, _Player, -1, 0, 1);
Use((CHARACTER)_Player, S_ORI_Gale_DeathPouch_Open_30ad0401-7872-429a-809c-42c753eb69f2, 1, 0, "");

IF
FlagSet(ORI_Gale_State_DestroyedPouch_5b7b8189-7b89-4cf5-a4a1-a80cc992a654, NULL_00000000-0000-0000-0000-000000000000, _)
THEN
Die(S_ORI_Gale_DeathPouch_72ab0321-5a3f-499f-8d7e-3ecfe72157e3, DEATHTYPE.DoT, NULL_00000000-0000-0000-0000-000000000000, 0, 1);

IF
DestroyingBy(S_ORI_Gale_DeathPouch_72ab0321-5a3f-499f-8d7e-3ecfe72157e3, _, _, _)
AND
IsInInventoryOf(S_ORI_Gale_DeathFlute_550dea32-6b55-4451-820a-6cf0086faa72, S_ORI_Gale_DeathPouch_72ab0321-5a3f-499f-8d7e-3ecfe72157e3, 1)
THEN
SetOnStage(S_ORI_Gale_DeathFlute_550dea32-6b55-4451-820a-6cf0086faa72, 0);

IF
DestroyingBy(S_ORI_Gale_DeathPouch_72ab0321-5a3f-499f-8d7e-3ecfe72157e3, _, _, _)
AND
IsInInventoryOf(S_ORI_Gale_DeathNote_5bc398e6-d238-475d-85e0-de985d61c564, S_ORI_Gale_DeathPouch_72ab0321-5a3f-499f-8d7e-3ecfe72157e3, 1)
THEN
SetOnStage(S_ORI_Gale_DeathNote_5bc398e6-d238-475d-85e0-de985d61c564, 0);

IF
Died(S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
AND
QRY_OnlyOnce("ORI_Gale_DropDeathNote")
AND
GetPosition(S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, _X, _Y, _Z)
THEN
PROC_ToInventoryAndOnStage(S_ORI_Gale_DeathPouch_72ab0321-5a3f-499f-8d7e-3ecfe72157e3, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, -1, 1, 1);

PROC
PROC_BlockUseOfItem(_Player, (ITEM)S_ORI_Gale_DeathFlute_550dea32-6b55-4451-820a-6cf0086faa72)
AND
DB_Players(_Player)
AND
NOT DB_Defeated(S_ORI_Gale_DeathMephit_4b90d334-3646-43f8-94f4-e29b13ac75d4)
AND
NOT DB_GlobalFlag(ORI_Gale_DeathNote_State_Open_6e3af093-fce9-fcb1-87ec-bd6c45788ac5)
AND
QRY_StartDialog_Fixed(ORI_Gale_DeathFlute_c02f1420-fc9b-c969-229b-28edd6fb0347, S_ORI_Gale_DeathFlute_550dea32-6b55-4451-820a-6cf0086faa72, S_ORI_Gale_DeathMephit_4b90d334-3646-43f8-94f4-e29b13ac75d4, _Player)
THEN
DB_CustomUseItemResponse(_Player, S_ORI_Gale_DeathFlute_550dea32-6b55-4451-820a-6cf0086faa72, 0);

PROC
PROC_BlockUseOfItem(_Player, (ITEM)S_ORI_Gale_DeathFlute_550dea32-6b55-4451-820a-6cf0086faa72)
AND
DB_Players(_Player)
AND
DB_Defeated(S_ORI_Gale_DeathMephit_4b90d334-3646-43f8-94f4-e29b13ac75d4)
AND
NOT DB_GlobalFlag(ORI_Gale_DeathNote_State_Open_6e3af093-fce9-fcb1-87ec-bd6c45788ac5)
AND
QRY_StartDialog_Fixed(ORI_Gale_DeathFlute_c02f1420-fc9b-c969-229b-28edd6fb0347, S_ORI_Gale_DeathFlute_550dea32-6b55-4451-820a-6cf0086faa72, NULL_00000000-0000-0000-0000-000000000000, _Player)
THEN
DB_CustomUseItemResponse(_Player, S_ORI_Gale_DeathFlute_550dea32-6b55-4451-820a-6cf0086faa72, 0);

IF
DialogEnded(ORI_Gale_DeathFlute_c02f1420-fc9b-c969-229b-28edd6fb0347, _ID)
AND
DB_GlobalFlag(ORI_Gale_DeathNote_State_Open_6e3af093-fce9-fcb1-87ec-bd6c45788ac5)
AND
QRY_OnlyOnce("ORI_Gale_TransformDeathNote")
AND
DB_DialogPlayers(_ID, _Player, 1)
THEN
PROC_GLO_Backgrounds_CompleteGoal((CHARACTER)_Player, "Act1_Sage_GaleResurrection");
PROC_TeleportToAndOffStage(S_ORI_Gale_DeathNote_5bc398e6-d238-475d-85e0-de985d61c564, _Player); //Ensure it's no longer in player inventory.
TemplateAddTo(LOOT_SCROLL_TrueResurrection_01d1b2b2-9e6b-4ad2-8d63-59f9caf1d389, _Player, 1, 0);

IF
StatusAttempt(S_ORI_Gale_DeathNote_5bc398e6-d238-475d-85e0-de985d61c564, "BURNING_LAVA", _, _)
AND
GetPosition(S_ORI_Gale_DeathNote_5bc398e6-d238-475d-85e0-de985d61c564, _X, _Y, _Z)
AND
CreateAt(LOOT_SCROLL_TrueResurrection_01d1b2b2-9e6b-4ad2-8d63-59f9caf1d389, _X, _Y, _Z, 0, 1, "", _)
THEN
SetFlag(ORI_Gale_DeathNote_State_Open_6e3af093-fce9-fcb1-87ec-bd6c45788ac5);
PROC_ORI_Gale_ManualScrollDestruction();
SetOnStage(S_ORI_Gale_DeathNote_5bc398e6-d238-475d-85e0-de985d61c564, 0);

PROC
PROC_ORI_Gale_ManualScrollDestruction()
AND
QRY_GetClosestAvailableCharacterTo(S_ORI_Gale_DeathNote_5bc398e6-d238-475d-85e0-de985d61c564, 0, 0, NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000, 1, 0, 1)
AND
DB_ClosestAvailableCharacterTo(_Player, S_ORI_Gale_DeathNote_5bc398e6-d238-475d-85e0-de985d61c564, _Distance)
AND
_Distance <= 6.0
THEN
PROC_GLO_Backgrounds_CompleteGoal(_Player, "Act1_Sage_GaleResurrection");
StartVoiceBark(ORI_Gale_VB_DeathNoteLava_47760dc4-a2e2-387b-2b6c-dddcf6455c96, _Player);

IF
FlagSet(ORI_Gale_DeathMephit_Event_Appear_01b22cad-bf94-72a1-dad4-5a890c230777, _, _ID)
AND
NOT DB_Dead(S_ORI_Gale_DeathMephit_4b90d334-3646-43f8-94f4-e29b13ac75d4)
AND
DB_DialogPlayers(_ID, _Player, 1)
AND
GetPosition(_Player, _X1, _Y1, _Z1)
AND
FindValidPosition(_X1, _Y1, _Z1, 4.0, S_ORI_Gale_DeathMephit_4b90d334-3646-43f8-94f4-e29b13ac75d4, 0, _X2, _Y2, _Z2)
THEN
DB_ORI_Gale_MephitAddPlayer(_Player, _ID);
AppearAtPosition(S_ORI_Gale_DeathMephit_4b90d334-3646-43f8-94f4-e29b13ac75d4, _X2, _Y2, _Z2, 1, NULL_00000000-0000-0000-0000-000000000000, "ORI_Gale_MephitAt");

//FindValidPosition Fallback
IF
FlagSet(ORI_Gale_DeathMephit_Event_Appear_01b22cad-bf94-72a1-dad4-5a890c230777, _, _ID)
AND
NOT DB_ORI_Gale_MephitAddPlayer(_, _ID)
AND
NOT DB_Dead(S_ORI_Gale_DeathMephit_4b90d334-3646-43f8-94f4-e29b13ac75d4)
AND
DB_DialogPlayers(_ID, _Player, 1)
AND
GetPosition(_Player, _X1, _Y1, _Z1)
THEN
DB_ORI_Gale_MephitAddPlayer(_Player, _ID);
AppearAtPosition(S_ORI_Gale_DeathMephit_4b90d334-3646-43f8-94f4-e29b13ac75d4, _X1, _Y1, _Z1, 1, NULL_00000000-0000-0000-0000-000000000000, "ORI_Gale_MephitAt");

IF
FlagSet(ORI_Gale_DeathMephit_Event_Appear_01b22cad-bf94-72a1-dad4-5a890c230777, _, _ID)
THEN
ClearFlag(ORI_Gale_DeathMephit_Event_Appear_01b22cad-bf94-72a1-dad4-5a890c230777, NULL_00000000-0000-0000-0000-000000000000, _ID);

IF
EntityEvent(S_ORI_Gale_DeathMephit_4b90d334-3646-43f8-94f4-e29b13ac75d4, "ORI_Gale_MephitAt")
AND
DB_ORI_Gale_MephitAddPlayer(_Player, _ID)
AND
NOT DialogGetInvolvedPlayer(_ID, 1, _Player) //Make sure dialogue is still ongoing when the mephit actually appears.
THEN
PROC_Poof(S_ORI_Gale_DeathMephit_4b90d334-3646-43f8-94f4-e29b13ac75d4);

IF
EntityEvent(S_ORI_Gale_DeathMephit_4b90d334-3646-43f8-94f4-e29b13ac75d4, "ORI_Gale_MephitAt")
AND
DB_ORI_Gale_MephitAddPlayer(_Player, _ID)
THEN
NOT DB_ORI_Gale_MephitAddPlayer(_Player, _ID);

QRY
QRY_SpeakerIsAvailable(S_ORI_Gale_DeathMephit_4b90d334-3646-43f8-94f4-e29b13ac75d4, _, _, _)
AND
IsOnStage(S_ORI_Gale_DeathMephit_4b90d334-3646-43f8-94f4-e29b13ac75d4, 0)
THEN
DB_NOOP(1);

IF
DialogEnded(ORI_Gale_DeathFlute_c02f1420-fc9b-c969-229b-28edd6fb0347, _)
AND
IsOnStage(S_ORI_Gale_DeathMephit_4b90d334-3646-43f8-94f4-e29b13ac75d4, 1)
THEN
PROC_Poof(S_ORI_Gale_DeathMephit_4b90d334-3646-43f8-94f4-e29b13ac75d4);

IF
TextEvent("ORI_Gale_TestFlute")
AND
GetHostCharacter(_Character)
THEN
PROC_ToInventoryAndOnStage(S_ORI_Gale_DeathFlute_550dea32-6b55-4451-820a-6cf0086faa72, _Character, -1, 1, 1);

//END_REGION

//REGION WRD
IF
FlagSet(_Flag, _Speaker, _ID)
AND
_ID != 0
AND
DB_ORI_Gale_TrustMoments(_Flag, _WRD, _)
THEN
DB_ORI_Gale_WRDAfterDialog(_ID, _WRD);

IF
FlagSet(_Flag, _Character, _)
AND
DB_ORI_Gale_TrustMoments(_, _WRD, _Flag)
AND
DB_Avatars((CHARACTER)_Character)
AND
DB_ORI_Gale_TrustMoments(_, _WRD, _OtherFlag)
AND
_Flag != _OtherFlag
AND
GetFlag(_OtherFlag, _Character, 1)
THEN	
SetFlag(ORI_Gale_State_CreditedForTwoGoodDeeds_d1a2d94e-8dce-4d33-ac2b-bfac306d62e1, _Character, 0);

IF
FlagSet(_Flag, _Character, _ID)
AND
DB_ORI_Gale_TrustMoments(_, _WRD, _Flag)
AND
DB_Avatars((CHARACTER)_Character)
THEN
PROC_ORI_Gale_TrustMoment_GiveApproval(_ID, _Character);

PROC
PROC_ORI_Gale_TrustMoment_GiveApproval((INTEGER)_ID, (CHARACTER)_Avatar)
AND
DB_DialogSpeakers(_ID, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, _)
AND
ChangeApprovalRating((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, (CHARACTER)_Avatar, 0, 8,_)
THEN
DB_NOOP(1);

PROC
PROC_ORI_Gale_TrustMoment_GiveApproval((INTEGER)_ID, (CHARACTER)_Avatar)
AND
NOT DB_DialogSpeakers(_ID, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, _)
THEN
DB_ORI_Gale_TrustMoment_DelayedApproval(_ID, _Avatar);

IF
DialogEnded(_, _ID)
AND
DB_ORI_Gale_TrustMoment_DelayedApproval(_ID, _Avatar)
AND
ChangeApprovalRating((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, (CHARACTER)_Avatar, 0, 6,_)
THEN
DB_NOOP(1);

IF
DialogEnded(_, _ID)
AND
DB_ORI_Gale_WRDAfterDialog(_ID, _WRD)
AND
_WRD != NULL_00000000-0000-0000-0000-000000000000
AND
DB_DialogPlayers(_ID, _Player, 1)
AND
NOT DB_ORI_Gale_MagicItemApprovalIPRD(1)
AND
NOT DB_Avatars((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
AND
QRY_SameUser(S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, (CHARACTER)_Player)
THEN
PROC_RelationshipDialog((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, _WRD, _Player);

//END_REGION

//REGION Night 3 trigger
IF
ApprovalRatingChanged(S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, _Avatar, _)
AND
NOT DB_GlobalFlag(NIGHT_Ceremorphosis3_Gale_3af72f14-8ecf-41ff-8bd0-0ea78b5d715b)
AND
NOT QRY_ORI_Gale_HasApprovalForNight3()
THEN
ClearFlag(ORI_Gale_State_ApproveNight3_95f06c00-cfc7-4cad-b11e-98cb86eaca6d, NULL_00000000-0000-0000-0000-000000000000, 0);

QRY
QRY_ORI_Gale_HasApprovalForNight3()
AND
DB_ApprovalRating(S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, _Avatar, _Approval)
AND
_Approval >= 20
THEN
SetFlag(ORI_Gale_State_ApproveNight3_95f06c00-cfc7-4cad-b11e-98cb86eaca6d, NULL_00000000-0000-0000-0000-000000000000, 0);

//END_REGION

//REGION For the trust moment with the Harpy Meal, we also consider anyone who joined the fight to have directly rescued the kid.
IF
FlagSet(DEN_HarpyMeal_State_RescuedChild_7a90f576-f762-4710-aa4d-9a81905bd971, _Player, _)
AND
DB_Was_InCombat(S_DEN_CharmedKid_3b92c689-6024-4446-a6c9-584e9e8d77ca, _ID)
AND
DB_DEN_HarpyMeal_Harpies(_Harpy, _)
AND
DB_Was_InCombat(_Harpy, _ID)
AND
DB_Players(_AnyPlayer)
AND
DB_Was_InCombat(_AnyPlayer, _ID)
THEN
SetFlag(DEN_HarpyMeal_State_RescuedChild_7a90f576-f762-4710-aa4d-9a81905bd971, _AnyPlayer, 0);

//END_REGION

//REGION Camp behaviour

PROC
PROC_DialogFlagSetup((DIALOGRESOURCE)CAMP_Gale_CRD_SpellTeaching_4d0a0d4e-0812-98c1-f2a6-94013578ce97, _, _)
THEN
SetFlag(ORI_Gale_State_CAMP_Gale_CRD_SpellTeachingStarted_8c0a51e6-480e-4425-b17c-eb105ce51554, NULL_00000000-0000-0000-0000-000000000000, 0);

//END_REGION

//REGION 

IF
DialogEnded((DIALOGRESOURCE)CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, _)
THEN
SetFlag(ORI_Gale_State_CAMP_Night2_CRDOver_2154190b-e9ed-464e-a361-f1e597d2b3c9, NULL_00000000-0000-0000-0000-000000000000, 0);

IF
DialogStarted((DIALOGRESOURCE)CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, _)
THEN
NOT DB_ORI_Gale_CAMP_Night2_CRDStarting(1);

PROC
PROC_DialogFlagSetup((DIALOGRESOURCE)CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, _, _, _)
THEN
DB_ORI_Gale_CAMP_Night2_CRDStarting(1);

IF
DB_SelectedDialog(CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, (CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, _Player)
AND
NOT DB_ORI_Gale_DoubleOnStage(1)
THEN
Transform(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, DISGUISE_KEEPICON_KEEPNAME_b40d9ab4-57a7-4632-b8d7-188904b00606);
CopyCharacterEquipment(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604);
PROC_Foop(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, VFX_Script_Gale_Double_Poof_01_5aec34ad-c864-c27d-1c4e-bb941014b75d);
TeleportTo(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, "ORI_Gale_DoubleInPlace", 0, 0, 1);
DB_SelectedDialog(CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, (CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, (CHARACTER)S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, _Player);
NOT DB_SelectedDialog(CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, (CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, _Player);


IF
DB_SelectedDialog(CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, _Avatar)
THEN
DB_SelectedDialog(CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, _Avatar);
NOT DB_SelectedDialog(CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, _Avatar);


IF
DB_GlobalFlag(ORI_Gale_State_ShowGaleDouble_ad1aabf8-690d-4f54-a674-717b515dc08d)
AND
NOT DB_GlobalFlag((FLAG)ORI_Gale_State_CAMP_Night2_CRDOver_2154190b-e9ed-464e-a361-f1e597d2b3c9)
AND
NOT DB_ORI_Gale_DoubleOnStage(1)
AND
QRY_GetArmourSet((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
AND
DB_QRYRTN_GetArmourSet(_ArmourSet)
THEN
PROC_SetArmourSet(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, _ArmourSet);
CopyCharacterEquipment(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604); //Keep equipment updated.
PROC_Foop(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, VFX_Script_Gale_Double_Poof_01_5aec34ad-c864-c27d-1c4e-bb941014b75d);
TeleportTo(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, "ORI_Gale_DoubleInPlace", 0, 0, 1);

IF
DB_DialogName((DIALOGRESOURCE)CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, _)
AND
NOT DB_GlobalFlag((FLAG)ORI_Gale_State_CAMP_Night2_CRDOver_2154190b-e9ed-464e-a361-f1e597d2b3c9)
AND
QRY_GetArmourSet((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
AND
DB_QRYRTN_GetArmourSet(_ArmourSet)
AND
NOT DB_ORI_Gale_DoubleOnStage(1)
THEN
PROC_SetArmourSet(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, _ArmourSet);
CopyCharacterEquipment(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604); //Keep equipment updated.
PROC_Foop(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, VFX_Script_Gale_Double_Poof_01_5aec34ad-c864-c27d-1c4e-bb941014b75d);
TeleportTo(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, "ORI_Gale_DoubleInPlace", 0, 0, 1);

IF
EntityEvent(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, "ORI_Gale_DoubleInPlace")
THEN
PROC_Foop(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, VFX_Script_Gale_Double_Poof_01_5aec34ad-c864-c27d-1c4e-bb941014b75d);
SteerTo(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 1);
SteerTo(S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, 1);

IF
ArmorSetChanged((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, _ArmourSet)
AND
NOT DB_Offstage(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe)
THEN
PROC_SetArmourSet(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, _ArmourSet);

PROC
PROC_Camp_SetModeToDay()
AND
DB_GlobalFlag(ORI_Gale_State_ShowGaleDouble_ad1aabf8-690d-4f54-a674-717b515dc08d)
THEN
PROC_GlobalClearFlagAndCache(ORI_Gale_State_ShowGaleDouble_ad1aabf8-690d-4f54-a674-717b515dc08d);

PROC
PROC_Camp_SetModeToDay()
AND
DB_ORI_Gale_CAMP_Night2_CRDStarting(1)
THEN
NOT DB_ORI_Gale_CAMP_Night2_CRDStarting(1);

PROC
PROC_Camp_SetModeToDay()
AND
DB_ORI_Gale_DoubleOnStage(1)
THEN
PROC_Poof(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, VFX_Script_Gale_Double_Poof_01_5aec34ad-c864-c27d-1c4e-bb941014b75d);
NOT DB_ORI_Gale_DoubleOnStage(1);

IF
Teleported((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, _, _, _, _, _, _, _, _)
AND
DB_GlobalFlag(ORI_Gale_State_ShowGaleDouble_ad1aabf8-690d-4f54-a674-717b515dc08d)
AND
NOT DB_DialogName((DIALOGRESOURCE)CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, _)
AND
NOT DB_SelectedDialog(CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, _, _)
AND
NOT DB_SelectedDialog(CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, _, _, _)
AND
NOT DB_ORI_Gale_CAMP_Night2_CRDStarting(1)
THEN
ClearFlag(ORI_Gale_State_ShowGaleDouble_ad1aabf8-690d-4f54-a674-717b515dc08d, NULL_00000000-0000-0000-0000-000000000000, 0);


IF
GainedControl((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
AND
DB_GlobalFlag(ORI_Gale_State_ShowGaleDouble_ad1aabf8-690d-4f54-a674-717b515dc08d)
AND
NOT DB_DialogName((DIALOGRESOURCE)CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, _)
AND
NOT DB_SelectedDialog(CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, _, _)
AND
NOT DB_SelectedDialog(CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, _, _, _)
AND
NOT DB_ORI_Gale_CAMP_Night2_CRDStarting(1)
THEN
ClearFlag(ORI_Gale_State_ShowGaleDouble_ad1aabf8-690d-4f54-a674-717b515dc08d, NULL_00000000-0000-0000-0000-000000000000, 0);

IF
DB_ORI_Gale_DoubleOnStage(1)
AND
NOT DB_GlobalFlag(ORI_Gale_State_ShowGaleDouble_ad1aabf8-690d-4f54-a674-717b515dc08d)
AND
NOT DB_DialogName((DIALOGRESOURCE)CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, _)
AND
NOT DB_SelectedDialog(CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, _, _)
AND
NOT DB_SelectedDialog(CAMP_Night2_CRD_Gale_adae2da4-11a7-2966-db5a-8342d8e64e15, _, _, _)
AND
NOT DB_ORI_Gale_CAMP_Night2_CRDStarting(1)
THEN
PROC_Poof(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, VFX_Script_Gale_Double_Poof_01_5aec34ad-c864-c27d-1c4e-bb941014b75d);

IF
DB_GlobalFlag((FLAG)ORI_Gale_State_CAMP_Night2_CRDOver_2154190b-e9ed-464e-a361-f1e597d2b3c9)
AND
DB_ORI_Gale_DoubleOnStage(1)
THEN
PROC_Poof(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, VFX_Script_Gale_Double_Poof_01_5aec34ad-c864-c27d-1c4e-bb941014b75d);

IF
WentOnStage(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, 1)
THEN
DB_ORI_Gale_DoubleOnStage(1);

IF
WentOnStage(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, 0)
THEN
NOT DB_ORI_Gale_DoubleOnStage(1);

IF
Unequipped(_, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
AND
DB_ORI_Gale_DoubleOnStage(1)
THEN
CopyCharacterEquipment(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604); //Keep equipment updated.

IF
Equipped(_, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
AND
DB_ORI_Gale_DoubleOnStage(1)
THEN
CopyCharacterEquipment(S_ORI_GaleDouble_ddf3dd37-fa65-4351-9f55-e50b1211fcfe, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604); //Keep equipment updated.
//END_REGION

//REGION You can point out to Gale that by betraying the Druids, you got him a magic item.
IF
AddedTo(S_DEN_SilvanusIdol_a841d36c-9a00-4a26-943e-c0af6895bb16, _Player, _)
AND
DB_Players((CHARACTER)_Player)
AND
NOT DB_GlobalFlag(DEN_AttackOnDen_State_HostileTieflings_c5641caa-4409-4739-a1fe-8263b8271f4e)
THEN
DB_ORI_Gale_GotIdolBeforeAttackOnDen(1);


IF
AddedTo(S_DEN_SilvanusIdol_a841d36c-9a00-4a26-943e-c0af6895bb16, _Player, _)
AND
DB_Players((CHARACTER)_Player)
AND
DB_GlobalFlag(DEN_AttackOnDen_State_HostileTieflings_c5641caa-4409-4739-a1fe-8263b8271f4e)
AND
NOT DB_ORI_Gale_GotIdolBeforeAttackOnDen(1)
AND
NOT DB_GlobalFlag(NIGHT_GoblinHunt_RaiderCelebration_86fee25f-1069-4f17-89fa-4c5b69f82e0b)
THEN
SetFlag(ORI_Gale_Event_TookIdolAfterTieflingBetrayal_11a06674-3353-41b6-8275-d127b7a7c260, NULL_00000000-0000-0000-0000-000000000000, 0);

//END_REGION

//REGION GUS-308727
// Fix for Rain Dancer Staff not being consumable but showing the status
IF
TemplateAddedTo((ROOT)UNI_DEN_StaffOfRain_28b38ae1-8d66-4d8e-a4f0-5d4c6c342c59, _Staff, _, _)
AND
NOT DB_BUGFIX_Marker("GUS-308727", _Staff)
THEN
RemoveStatus(_Staff, "ORI_GALE_CONSUMABLE", NULL_00000000-0000-0000-0000-000000000000);
DB_BUGFIX_Marker("GUS-308727", _Staff);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
