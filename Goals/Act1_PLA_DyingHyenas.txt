Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION Sick hyenas
DB_Dialogs((CHARACTER)S_PLA_DyingHyena_01_dd45b23f-7b4d-4859-9113-4df21492fffe,(DIALOGRESOURCE)PLA_DyingHyena_DeathEvent_3f3088b1-2846-4f64-4a09-00f95004f617);
DB_Dialogs((CHARACTER)S_PLA_DyingHyena_02_e77699da-afc5-4a3c-b151-417fb2ff724d,(DIALOGRESOURCE)PLA_DyingHyena_02_0ab1d34b-9931-e19f-b247-223531339473);
DB_Dialogs((CHARACTER)S_PLA_DyingHyena_03_72708c44-205d-4fd0-8217-c4a0a8935e92,(DIALOGRESOURCE)PLA_DyingHyena_03_e27d3262-a9ca-b0eb-f443-1aa4465e34e7);
DB_Dialogs((CHARACTER)S_PLA_DyingHyena_04_44be6770-8eab-42a7-b0d0-994658d7b820,(DIALOGRESOURCE)PLA_DyingHyena_04_fa0a840c-5ca9-97c3-fffd-0f142b158366);

DB_PLA_DyingHyenas((CHARACTER)S_PLA_DyingHyena_01_dd45b23f-7b4d-4859-9113-4df21492fffe);
DB_PLA_DyingHyenas((CHARACTER)S_PLA_DyingHyena_02_e77699da-afc5-4a3c-b151-417fb2ff724d);
DB_PLA_DyingHyenas((CHARACTER)S_PLA_DyingHyena_03_72708c44-205d-4fd0-8217-c4a0a8935e92);
DB_PLA_DyingHyenas((CHARACTER)S_PLA_DyingHyena_04_44be6770-8eab-42a7-b0d0-994658d7b820);

DB_DropMutingStatussesDialog((DIALOGRESOURCE)PLA_DyingHyena_DeathEvent_3f3088b1-2846-4f64-4a09-00f95004f617);
//END_REGION Sick hyenas

//REGION Gnolls and hyena runners
DB_Dialogs(S_PLA_DyingHyena_Healthy_01_8c993005-f18e-4a69-a96f-74a0f0400ca6,(DIALOGRESOURCE)PLA_DyingHyena_Healthy_01_86c20274-564a-d14a-a10b-3fce65937085);

DB_PLA_DyingHyena_GnollPack((CHARACTER)S_PLA_DyingHyena_Healthy_01_8c993005-f18e-4a69-a96f-74a0f0400ca6, (TRIGGER)S_PLA_DyingHyena_Bridge_PackArea_a443f8d5-8b6b-4aa0-8880-6adf3fea89ec);
DB_PLA_DyingHyena_GnollPack((CHARACTER)S_PLA_DyingHyena_GnollPack_C_Fang_01_00fc2e9b-f93c-422d-8a17-7cdaa292f29c, (TRIGGER)S_PLA_DyingHyena_GnollPack_C_PackArea_2e69af4c-4388-43e7-8ab2-2493d726ff9d);
DB_PLA_DyingHyena_GnollPack((CHARACTER)S_PLA_DyingHyena_GnollPack_C_Gnawer_01_3348d3cd-745e-419e-a0a7-e4ae94cf496e, (TRIGGER)S_PLA_DyingHyena_GnollPack_C_PackArea_2e69af4c-4388-43e7-8ab2-2493d726ff9d);
DB_PLA_DyingHyena_GnollPack((CHARACTER)S_PLA_DyingHyena_GnollPack_C_Ranger_02_81c8cbc3-ac1c-4873-9b8e-0b234bf9ef74, (TRIGGER)S_PLA_DyingHyena_GnollPack_C_PackArea_2e69af4c-4388-43e7-8ab2-2493d726ff9d);
DB_PLA_DyingHyena_GnollPack((CHARACTER)S_PLA_DyingHyena_GnollPack_C_Ranger_01_e502091b-79a1-479b-9f5f-69937502a355, (TRIGGER)S_PLA_DyingHyena_GnollPack_C_PackArea_2e69af4c-4388-43e7-8ab2-2493d726ff9d);

DB_PLA_DyingHyena_HyenaRunner((CHARACTER)S_PLA_DyingHyena_Healthy_01_8c993005-f18e-4a69-a96f-74a0f0400ca6, (TRIGGER)S_PLA_DyingHyena_GnollPack_C_PackArea_2e69af4c-4388-43e7-8ab2-2493d726ff9d);
//END_REGION Gnolls and hyena runners

//REGION Healthy Hyena spotting
DB_SpotPlayers((CHARACTER)S_PLA_DyingHyena_Healthy_01_8c993005-f18e-4a69-a96f-74a0f0400ca6, "PLA_DyingHyena_HealthySpotter", NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);
DB_SpotPlayers_CustomRadius((CHARACTER)S_PLA_DyingHyena_Healthy_01_8c993005-f18e-4a69-a96f-74a0f0400ca6, "PLA_DyingHyena_HealthySpotter", 12.0);
DB_SpotPlayers_Continuous((CHARACTER)S_PLA_DyingHyena_Healthy_01_8c993005-f18e-4a69-a96f-74a0f0400ca6, "PLA_DyingHyena_HealthySpotter");
DB_SpotPlayers_IncludeSummons((CHARACTER)S_PLA_DyingHyena_Healthy_01_8c993005-f18e-4a69-a96f-74a0f0400ca6, "PLA_DyingHyena_HealthySpotter");
DB_SpotPlayers_IncludeFollowers((CHARACTER)S_PLA_DyingHyena_Healthy_01_8c993005-f18e-4a69-a96f-74a0f0400ca6, "PLA_DyingHyena_HealthySpotter");
DB_SpotPlayers_IncludeWildshapedPlayers((CHARACTER)S_PLA_DyingHyena_Healthy_01_8c993005-f18e-4a69-a96f-74a0f0400ca6, "PLA_DyingHyena_HealthySpotter");
DB_SpotPlayers_TargetIgnoreCantTalk((CHARACTER)S_PLA_DyingHyena_Healthy_01_8c993005-f18e-4a69-a96f-74a0f0400ca6, "PLA_DyingHyena_HealthySpotter");
DB_SpotPlayers_SpotterIgnoreCantTalk((CHARACTER)S_PLA_DyingHyena_Healthy_01_8c993005-f18e-4a69-a96f-74a0f0400ca6, "PLA_DyingHyena_HealthySpotter");
//END_REGION

//REGION AD manager
DB_PLA_DyingHyena_ADManager_AD((DIALOGRESOURCE)PLA_DyingHyena_SickHyena_AD_Groaning_553e9e53-79de-98e6-64ae-b3a482c5d365);
DB_PLA_DyingHyena_ADManager_Players((CHARACTER)S_PLA_DyingHyena_01_dd45b23f-7b4d-4859-9113-4df21492fffe);
DB_PLA_DyingHyena_ADManager_Players((CHARACTER)S_PLA_DyingHyena_02_e77699da-afc5-4a3c-b151-417fb2ff724d);
DB_PLA_DyingHyena_ADManager_Players((CHARACTER)S_PLA_DyingHyena_03_72708c44-205d-4fd0-8217-c4a0a8935e92);
DB_PLA_DyingHyena_ADManager_Players((CHARACTER)S_PLA_DyingHyena_04_44be6770-8eab-42a7-b0d0-994658d7b820);
//END_REGION AD manager

DB_GLO_DefeatCounter_AllPermaDefeatedGlobalFlag("PLA_DyingHyena_Creatures",(FLAG)PLA_DyingHyena_State_Gone_62e3eb5c-d0c8-48ca-be82-b6c64008b093);
KBSECTION
//REGION Crimes
QRY
QRY_CRIME_BlockGenericInvestigation(_Character)
AND
DB_PLA_DyingHyenas(_Character)
THEN
DB_NOOP(1);

IF
KilledBy(S_PLA_DyingHyena_Healthy_01_8c993005-f18e-4a69-a96f-74a0f0400ca6, _AttackOwner, _, _)
AND
DB_PartyMembers((CHARACTER)_AttackOwner)
THEN
PROC_PLA_DyingHyena_StopSpotting();
PROC_SetRelationToPlayers((FACTION)ACT1_PLA_DyingHyena_d6fd5106-8d14-dba6-cc4e-7f1fbb7a4da8, 0);

IF
KilledBy(_Hyena, _AttackOwner, _, _)
AND
DB_PLA_DyingHyenas(_Hyena)
AND
DB_PartyMembers((CHARACTER)_AttackOwner)
THEN
PROC_PLA_DyingHyena_StopSpotting();
PROC_SetRelationToPlayers((FACTION)ACT1_PLA_DyingHyena_d6fd5106-8d14-dba6-cc4e-7f1fbb7a4da8, 0);

PROC
PROC_PLA_DyingHyena_StopSpotting()
AND
DB_PLA_DyingHyenas(_Hyena)
AND
DB_SpotPlayers(_Hyena, (STRING)_SpotID, _, _)
THEN
PROC_SpotPlayers_StopSpotting(_Hyena, _SpotID);

IF
AttackedBy((CHARACTER)_Hyena, (CHARACTER)_Player, _, _, _, _, _)
AND
DB_PartyMembers(_Player)
AND
DB_PLA_DyingHyenas(_Hyena)
AND
NOT DB_Is_InCombat(_Hyena, _)
AND
NOT DB_DyingHyeanas_MassHungerStarted(1)
THEN
PROC_PLA_DyingHyena_HyenasStartMassHunger();

PROC
PROC_PLA_DyingHyena_HyenasStartMassHunger()
THEN
DB_DyingHyeanas_MassHungerStarted(1);
PROC_PLA_DyingHyena_StopSpotting();

PROC
PROC_PLA_DyingHyena_HyenasStartMassHunger()
AND
DB_PLA_DyingHyenas(_Hyena)
AND
NOT DB_Is_InCombat(_Hyena, _)
AND
HasActiveStatus(_Hyena, "PLA_YEENOGHU_HUNGER", 0)
AND
Random(100, _RandomInt)
AND
IntegerToReal(_RandomInt, _RandomReal)
AND
RealDivide(_RandomReal, 100.0, _RandomRealAdj)
AND
RealSum(_RandomRealAdj, 5.0, _HungerTime)
THEN
ApplyStatus(_Hyena, "PLA_YEENOGHU_HUNGER", _HungerTime, 1);
//END_REGION Crimes

//REGION Dialog encounter
IF
DB_PLA_DyingHyenas(_Hyena)
THEN
ApplyStatus(_Hyena, "PLA_DYINGHYENA_BLOATED", -1.0, 1);

IF
DB_PLA_DyingHyenas(_Hyena)
THEN
DB_SpotPlayers(_Hyena, "PLA_DyingHyena_Spotting", NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);
DB_SpotPlayers_CustomRadius(_Hyena,"PLA_DyingHyena_Spotting", 10.0);

PROC
PROC_SpotPlayers_Spotted(_Hyena, "PLA_DyingHyena_Spotting",_Player)
AND
NOT DB_PLA_DyingHyenas_DeathEventStarted(_)
AND
QRY_StartDialog((DIALOGRESOURCE)PLA_DyingHyena_DeathEvent_3f3088b1-2846-4f64-4a09-00f95004f617, S_PLA_DyingHyena_01_dd45b23f-7b4d-4859-9113-4df21492fffe, _Player)
THEN
ApplyStatus(S_PLA_DyingHyena_01_dd45b23f-7b4d-4859-9113-4df21492fffe, "PLA_YEENOGHU_HUNGER", 6.0, 1);
DB_PLA_DyingHyenas_DeathEventStarted(_Player);

QRY
QRY_SelectCustomDialog((CHARACTER)_Hyena,_Player)
AND
DB_PLA_DyingHyenas(_Hyena)
AND
NOT DB_PLA_DyingHyenas_DeathEventStarted(_)
THEN
ApplyStatus(S_PLA_DyingHyena_01_dd45b23f-7b4d-4859-9113-4df21492fffe, "PLA_YEENOGHU_HUNGER", 6.0, 1);
DB_SelectedDialog((DIALOGRESOURCE)PLA_DyingHyena_DeathEvent_3f3088b1-2846-4f64-4a09-00f95004f617,S_PLA_DyingHyena_01_dd45b23f-7b4d-4859-9113-4df21492fffe,_Player);
DB_PLA_DyingHyenas_DeathEventStarted((CHARACTER)_Player);

IF
FlagSet((FLAG)PLA_DyingHyena_PlayerAdvantage_96fa8cc6-5dce-09b3-82de-eabd54094d64,_Player,_ID)
AND
DB_PLA_DyingHyenas(_Hyena)
THEN
PROC_MakeSurprised(_Hyena);

IF
FlagSet((FLAG)PLA_DyingHyena_PaladinSurge_10028606-b4cf-d558-c0bc-cc338d2ece59,_Player,_ID)
THEN
ApplyStatus(_Player, "STUNNED", 6.0, 1, NULL_00000000-0000-0000-0000-000000000000);

//Event_Death
IF
DialogEnded(PLA_DyingHyena_DeathEvent_3f3088b1-2846-4f64-4a09-00f95004f617,_ID)
AND
DB_DialogNPCs(_ID,_DyingHyena,_)
AND
DB_GlobalFlag((FLAG)PLA_DyingHyena_Event_Death_d9c6cee2-e57b-0275-db29-f640f8d3753d)
THEN
RemoveStatus(_DyingHyena,"PLA_YEENOGHU_HUNGER");

//PeacefulDeath
IF
DialogEnded(PLA_DyingHyena_DeathEvent_3f3088b1-2846-4f64-4a09-00f95004f617,_ID)
AND
DB_DialogPlayers(_ID,_Player,1)
AND
DB_DialogNPCs(_ID,_DyingHyena,_)
AND
DB_GlobalFlag((FLAG)PLA_DyingHyena_Event_PeacefulDeath_7af9106e-473e-9146-cf96-e9be7acacfca)
AND
GetPosition(_DyingHyena, _X, _Y, _Z)
THEN
Die((CHARACTER)_DyingHyena,DEATHTYPE.DoT,(CHARACTER)_Player,0,0);
CreateSurfaceAtPosition(_X, _Y, _Z, "SurfaceBlood", 2.0, -1.0);

IF
DialogEnded(PLA_DyingHyena_DeathEvent_3f3088b1-2846-4f64-4a09-00f95004f617,_ID)
AND
DB_DialogPlayers(_Id,_Player,1)
THEN
PROC_SetRelationToPlayers((FACTION)ACT1_PLA_DyingHyena_d6fd5106-8d14-dba6-cc4e-7f1fbb7a4da8, 0);

IF
CombatEnded(_CombatID)
AND
DB_Was_InCombat(_Hyena,_CombatID)
AND
DB_PLA_DyingHyenas((CHARACTER)_Hyena)
AND
DB_PartyMembers((CHARACTER)_Player)
AND
DB_Was_InCombat(_Player,_CombatID)
AND
NOT DB_CantTalk(_Player)
AND
QRY_OnlyOnce("PLA_DyingHyenas_CombatedHyenas")
THEN
StartVoiceBark((VOICEBARKRESOURCE)PLA_DyingHyena_VB_PostFight_02c98d7b-86e4-00d8-f988-33527819adf1,_Player);
//END_REGION Dialog encounter

//REGION Combat: normal hyena runs to call the gnolls
IF
KilledBy(_KilledHyena, _AttackOwner, _, _)
AND
DB_PartyMembers((CHARACTER)_AttackOwner)
AND
DB_PLA_DyingHyenas(_KilledHyena)
AND
DB_PLA_DyingHyena_HyenaRunner(_Hyena, _PackArea)
AND
QRY_PLA_DyingHyena_CanRunToThePack(_PackArea)
AND
NOT DB_PLA_DyingHyena_OnTheRun(_Hyena, _)
THEN
PROC_PLA_DyingHyena_StartRun(_Hyena, _PackArea);

IF
EnteredCombat((CHARACTER)_Hyena, (GUIDSTRING)_CombatID)
AND
DB_PLA_DyingHyena_HyenaRunner(_Hyena, (TRIGGER)_PackArea)
AND
QRY_PLA_DyingHyena_CanRunToThePack(_PackArea)
AND
NOT DB_PLA_DyingHyena_OnTheRun(_Hyena, _)
THEN
PROC_PLA_DyingHyena_StartRun(_Hyena, _PackArea);

PROC
PROC_PLA_DyingHyena_StartRun((CHARACTER)_Hyena, (TRIGGER)_PackArea)
AND
NOT DB_PLA_DyingHyena_OnTheRun(_Hyena, _PackArea)
THEN
TriggerRegisterForCharacter(_PackArea, _Hyena);
DB_PLA_DyingHyena_OnTheRun(_Hyena, _PackArea);
SetDualEntityEvent(_Hyena, _PackArea, "PLA_DyingHyena_Event_RunToPack");

PROC
PROC_PLA_DyingHyena_StartRun(_Hyena, _)
AND
NOT DB_PLA_DyingHyena_AreaPlayerSpotted(_Hyena, _)
AND
DB_PLA_DyingHyena_GnollPack(_Hyena, (TRIGGER)_OriginArea)
THEN
DB_PLA_DyingHyena_AreaPlayerSpotted(_Hyena, _OriginArea);

// checks if we should run to the pack (we don't need to, if everyone of the pack has been defeated)
QRY
QRY_PLA_DyingHyena_CanRunToThePack((TRIGGER)_PackArea)
AND
DB_PLA_DyingHyena_GnollPack((CHARACTER)_Gnoll, _PackArea)
AND
NOT DB_Is_InCombat(_Gnoll, _)
AND
NOT DB_Defeated(_Gnoll)
AND
IsInTrigger(_Gnoll, _PackArea, 1)
THEN
DB_NOOP(1);

IF
EnteredCombat((CHARACTER)_Gnoll, _)
AND
NOT DB_PLA_DyingHyena_HyenaRunner(_Gnoll, _)
AND
DB_PLA_DyingHyena_GnollPack(_Gnoll, (TRIGGER)_PackArea)
AND
DB_PLA_DyingHyena_OnTheRun((CHARACTER)_Hyena, _PackArea)
THEN
PROC_PLA_DyingHyena_EndRun(_Hyena, _PackArea);

IF
EnteredTrigger(_Hyena, _PackArea)
AND
DB_PLA_DyingHyena_OnTheRun(_Hyena, _PackArea)
THEN
PROC_PLA_DyingHyena_EndRun(_Hyena, _PackArea);
PROC_PLA_DyingHyena_CallForGnolls(_Hyena);

PROC
PROC_PLA_DyingHyena_CallForGnolls((CHARACTER)_Hyena)
AND
DB_PLA_DyingHyena_AreaPlayerSpotted(_Hyena, (TRIGGER)_PlayerSpottedArea)
AND
DB_PLA_DyingHyena_GnollPack((CHARACTER)_Gnoll, _)
AND
NOT DB_PLA_DyingHyena_CalledToArea(_Gnoll, _)
AND
NOT DB_Is_InCombat(_Gnoll, _)
AND
GetDistanceTo(_Hyena, _Gnoll, (REAL)_Distance)
AND
_Distance < 17.0
AND
Random(3000, (INTEGER)_Random)
THEN
DB_PLA_DyingHyena_CalledToArea(_Gnoll, _PlayerSpottedArea);
ObjectTimerLaunch(_Gnoll, "PLA_DyingHyena_RushToPlayersArea_Timer", _Random);

IF
ObjectTimerFinished((CHARACTER)_Gnoll, "PLA_DyingHyena_RushToPlayersArea_Timer")
AND
DB_PLA_DyingHyena_CalledToArea(_Gnoll, (TRIGGER)_PlayerSpottedArea)
THEN
SetDualEntityEvent(_Gnoll, _PlayerSpottedArea, "PLA_DyingHyena_Event_RushToPlayersArea");

PROC
PROC_PLA_DyingHyena_EndRun((CHARACTER)_Hyena, (TRIGGER)_PackArea)
AND
DB_PLA_DyingHyena_OnTheRun(_Hyena, _PackArea)
THEN
TriggerUnregisterForCharacter(_PackArea, _Hyena);
NOT DB_PLA_DyingHyena_OnTheRun(_Hyena, _PackArea);
SetEntityEvent(_Hyena, "PLA_DyingHyena_Event_StopRunning");

IF
LeftCombat((CHARACTER)_Hyena, _)
AND
DB_PLA_DyingHyena_AreaPlayerSpotted(_Hyena, _Area)
THEN
NOT DB_PLA_DyingHyena_AreaPlayerSpotted(_Hyena, _Area);

IF
LeftCombat((CHARACTER)_Gnoll, _)
AND
DB_PLA_DyingHyena_CalledToArea(_Gnoll, _Area)
THEN
NOT DB_PLA_DyingHyena_CalledToArea(_Gnoll, _Area);

IF
DB_Is_InCombat(_Gnoll, _CombatID)
AND
DB_PLA_DyingHyena_GnollPack((CHARACTER)_Gnoll, _)
AND
NOT DB_GlobalFlag((FLAG)PLA_ConflictedFlind_Knows_Gnolls_c353a7d3-7561-05dc-c725-32e363ce6bf3)
AND
IsTagged(_Gnoll, GNOLL_1350164f-85e1-42f9-89c9-eb0c6703e890, 1)
AND
DB_PartyMembers((CHARACTER)_Player)
AND
DB_Is_InCombat(_Player, _CombatID)
THEN
PROC_GlobalSetFlagAndCache(PLA_ConflictedFlind_Knows_Gnolls_c353a7d3-7561-05dc-c725-32e363ce6bf3);

// voice bark about hyena running
IF
TurnEnded((CHARACTER)_Hyena)
AND
DB_PLA_DyingHyena_OnTheRun(_Hyena, _)
AND
DB_Is_InCombat(_Hyena, _HyenaCombat)
AND
NOT DB_DyingHyena_HyenaRunning_PADAwaiting(_)
AND
NOT DB_DyingHyena_HyenaRunning_PADPlayed(1)
THEN
DB_DyingHyena_HyenaRunning_PADAwaiting(_HyenaCombat);

IF
TurnStarted((CHARACTER)_Character)
AND
NOT DB_Players(_Character)
AND
DB_DyingHyena_HyenaRunning_PADAwaiting(_HyenaCombat)
AND
QRY_GetClosestAvailableCharacterTo(_Character, 0, 1, NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000, 1)
AND
DB_ClosestAvailableCharacterTo(_Player, _Character, _)
THEN
NOT DB_DyingHyena_HyenaRunning_PADAwaiting(_HyenaCombat);
RealtimeObjectTimerLaunch(_Player, "PLA_DyingHyena_VB_HyenaRunning_Timer", 500);

IF
TurnStarted((CHARACTER)_Player)
AND
DB_Players(_Player)
AND
DB_DyingHyena_HyenaRunning_PADAwaiting(_HyenaCombat)
AND
DB_Is_InCombat(_Player, _HyenaCombat)
THEN
NOT DB_DyingHyena_HyenaRunning_PADAwaiting(_HyenaCombat);
RealtimeObjectTimerLaunch(_Player, "PLA_DyingHyena_VB_HyenaRunning_Timer", 200);

IF
ObjectTimerFinished((CHARACTER)_Player, "PLA_DyingHyena_VB_HyenaRunning_Timer")
AND
NOT DB_DyingHyena_HyenaRunning_PADPlayed(1)
THEN
DB_DyingHyena_HyenaRunning_PADPlayed(1);
StartVoiceBark(PLA_DyingHyena_VB_HyenaRunning_d8cd0b3a-7a54-f0a5-765e-862974c6c438, _Player);
//END_REGION Combat: normal hyena runs to call gnolls

//REGION Healthy Hyena spotter
IF
DB_PLA_DyingHyenas_DeathEventStarted((CHARACTER)_Player)
AND
NOT DB_PLA_DyingHynea_CurrentHyenaSpotter(S_PLA_DyingHyena_Healthy_01_8c993005-f18e-4a69-a96f-74a0f0400ca6, _)
THEN
SetDualEntityEvent(S_PLA_DyingHyena_Healthy_01_8c993005-f18e-4a69-a96f-74a0f0400ca6, _Player, "DB_PLA_DyingHyena_Event_PlayerSpotted");

PROC
PROC_SpotPlayers_Spotted(_Hyena, "PLA_DyingHyena_HealthySpotter", _Character)
AND
DB_PartyMembers(_Character)
AND
DB_PLA_DyingHyena_GnollPack(_Hyena, _)
AND
NOT DB_PLA_DyingHynea_CurrentHyenaSpotter(_Hyena, _)
THEN
DB_PLA_DyingHynea_CurrentHyenaSpotter(_Hyena, _Character);
SetDualEntityEvent(_Hyena, _Character, "DB_PLA_DyingHyena_Event_PlayerSpotted");

PROC
PROC_SpotPlayers_Unspotted(_Hyena, "PLA_DyingHyena_HealthySpotter", _Character)
AND
DB_PLA_DyingHynea_CurrentHyenaSpotter(_Hyena, _Character)
THEN
NOT DB_PLA_DyingHynea_CurrentHyenaSpotter(_Hyena, _Character);
SetDualEntityEvent(_Hyena, _Character, "DB_PLA_DyingHyena_Event_PlayerUnspotted");

IF
EnteredCombat((CHARACTER)_Hyena, _)
AND
DB_PLA_DyingHynea_CurrentHyenaSpotter(_Hyena, (CHARACTER)_Character)
AND
DB_SpotPlayers(_Hyena, (STRING)_SpotID, _, _)
THEN
NOT DB_PLA_DyingHynea_CurrentHyenaSpotter(_Hyena, _Character);
PROC_SpotPlayers_StopSpotting(_Hyena, _SpotID);
//END_REGION Healthy Hyena spotter

//REGION Kill (Defeat) counter
IF
DB_PLA_DyingHyenas((CHARACTER)_Creature)
THEN
DB_GLO_DefeatCounter(_Creature, "PLA_DyingHyena_Creatures");

IF
DB_PLA_DyingHyena_GnollPack((CHARACTER)_Creature, _)
THEN
DB_GLO_DefeatCounter(_Creature, "PLA_DyingHyena_Creatures");

//END_REGION Kill (Defeat) counter

//REGION AD Manager
IF
AutomatedDialogStarted((DIALOGRESOURCE)_Dialog, (INTEGER)_InstanceID)
AND
DB_PLA_DyingHyena_ADManager_AD(_Dialog)
AND
DB_DialogNPCs(_InstanceID, (GUIDSTRING)_Character, _)
AND
DB_PLA_DyingHyena_ADManager_Players((CHARACTER)_Character)
AND
DB_PLA_DyingHyena_ADManager_Players((CHARACTER)_OtherCharacter)
AND
_Character != _OtherCharacter
THEN
SetEntityEvent(_OtherCharacter, "DB_PLA_DyingHyena_Event_ADBlocked");

IF
AutomatedDialogEnded((DIALOGRESOURCE)_Dialog, (INTEGER)_InstanceID)
AND
DB_PLA_DyingHyena_ADManager_AD(_Dialog)
THEN
PROC_PLA_DyingHyena_ADManager_DialogEnded(_InstanceID);

PROC
PROC_PLA_DyingHyena_ADManager_DialogEnded((INTEGER)_InstanceID)
AND
DB_DialogNPCs(_InstanceID, (GUIDSTRING)_Character, _)
AND
DB_PLA_DyingHyena_ADManager_Players((CHARACTER)_Character)
AND
NOT DB_PLA_DyingHyena_ADManager_OnCooldown(_Character)
THEN
DB_PLA_DyingHyena_ADManager_OnCooldown(_Character);

PROC
PROC_PLA_DyingHyena_ADManager_DialogEnded(_)
AND
NOT DB_PLA_DyingHyena_ADManager_TimerLaunched(1)
THEN
DB_PLA_DyingHyena_ADManager_TimerLaunched(1);
TimerLaunch("PLA_DyingHyena_ADManager_Blocking_Timer", 8000);

IF
TimerFinished("PLA_DyingHyena_ADManager_Blocking_Timer")
AND 
DB_PLA_DyingHyena_ADManager_TimerLaunched(1)
THEN
NOT DB_PLA_DyingHyena_ADManager_TimerLaunched(1);
PROC_PLA_DyingHyena_ADManager_Unblock();

PROC
PROC_PLA_DyingHyena_ADManager_Unblock()
AND
DB_PLA_DyingHyena_ADManager_Players((CHARACTER)_Character)
AND
NOT DB_PLA_DyingHyena_ADManager_OnCooldown(_Character)
THEN
SetEntityEvent(_Character, "DB_PLA_DyingHyena_Event_ADAvailable");

// Cycle clearing (when cooldown list is full, clear it but not the new character)
IF
DB_PLA_DyingHyena_ADManager_OnCooldown((CHARACTER)_Character)
AND
SysCount("DB_PLA_DyingHyena_ADManager_OnCooldown", 1, _CountOnCooldown)
AND
SysCount("DB_PLA_DyingHyena_ADManager_Players", 1, _CountPlayers)
AND
_CountOnCooldown >= _CountPlayers
AND
DB_PLA_DyingHyena_ADManager_OnCooldown(_OtherCharacter)
AND
_OtherCharacter != _Character
THEN
NOT DB_PLA_DyingHyena_ADManager_OnCooldown(_OtherCharacter);

IF
DB_PermaDefeated(_Character)
AND
DB_PLA_DyingHyena_ADManager_Players((CHARACTER)_Character)
THEN
NOT DB_PLA_DyingHyena_ADManager_Players((CHARACTER)_Character);

IF
DB_PermaDefeated(_Character)
AND
DB_PLA_DyingHyena_ADManager_OnCooldown((CHARACTER)_Character)
THEN
NOT DB_PLA_DyingHyena_ADManager_OnCooldown((CHARACTER)_Character);

IF
EnteredCombat((CHARACTER)_Hyena, _)
AND
DB_PLA_DyingHyena_ADManager_Players(_Hyena)
THEN
PROC_TryStopDialogFor(_Hyena);
TimerCancel("PLA_DyingHyena_ADManager_Blocking_Timer");
//END_REGION AD Manager

//REGION Banter
IF
DB_GlobalFlag(PLA_DyingHyena_State_Gone_62e3eb5c-d0c8-48ca-be82-b6c64008b093)
AND
NOT DB_DyingHyena_SafeForBanter(1)
THEN
DB_DyingHyena_SafeForBanter(1);

PROC
PROC_LongRest()
AND
DB_DyingHyena_SafeForBanter(1)
THEN
PROC_RegisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_PLA_HyenasCleared_26df6c23-9940-4210-a0ea-3755182a681e);
//END_REGION Banter

//REGION Bridge Trigger adds Jump
IF
EntityEvent((CHARACTER)_NewbornGnoll, "PLA_DyingHyena_Event_NewbornGnollBorn")
AND
NOT DB_PLA_DyingHyena_NewbornGnolls(_NewbornGnoll)
THEN
DB_PLA_DyingHyena_NewbornGnolls(_NewbornGnoll);

IF
DB_PLA_DyingHyena_NewbornGnolls(_NewbornGnoll)
THEN
TriggerRegisterForCharacter(S_PLA_Gnolls_Jump_Trigger_01_56bcc9c6-6dec-41b2-a5ef-f11930e41c6f, _NewbornGnoll);
DebugText(_NewbornGnoll,"Can Jump!");

IF
DB_PLA_DyingHyena_GnollPack(_Gnoll, _)
AND
IsTagged(_Gnoll,(TAG)GNOLL_1350164f-85e1-42f9-89c9-eb0c6703e890,1)
THEN
TriggerRegisterForCharacter(S_PLA_Gnolls_Jump_Trigger_01_56bcc9c6-6dec-41b2-a5ef-f11930e41c6f, _Gnoll);
DebugText(_Gnoll,"Can Jump!");

IF
EnteredTrigger(_Gnoll, S_PLA_Gnolls_Jump_Trigger_01_56bcc9c6-6dec-41b2-a5ef-f11930e41c6f)
AND
IsTagged(_Gnoll,(TAG)GNOLL_1350164f-85e1-42f9-89c9-eb0c6703e890,1)
THEN
AddSpell(_Gnoll,"Projectile_Jump_Gnoll",0,0);

IF
LeftTrigger(_Gnoll, S_PLA_Gnolls_Jump_Trigger_01_56bcc9c6-6dec-41b2-a5ef-f11930e41c6f)
AND
IsTagged(_Gnoll,(TAG)GNOLL_1350164f-85e1-42f9-89c9-eb0c6703e890,1)
THEN
RemoveSpell(_Gnoll,"Projectile_Jump_Gnoll",0);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
