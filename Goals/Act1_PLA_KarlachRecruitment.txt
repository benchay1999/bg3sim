Version 1
SubGoalCombiner SGC_AND
INITSECTION
SetOnStage(S_GLO_KarlachHead_931f6df2-b546-422c-beff-fe91682bdf49, 0);

DB_KilledEvent((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (FLAG)PLA_KarlachRecruitment_State_KilledKarlach_ee2f4a40-6290-4a2b-ba3a-54da7dc3f69f);
DB_HasItemEvent(S_GLO_KarlachHead_931f6df2-b546-422c-beff-fe91682bdf49,(FLAG)GLO_Karlach_State_HasHead_611d2fd1-91df-43c2-9af0-f7db11e7460d);
DB_GiveItemToEvent(S_GLO_KarlachHead_931f6df2-b546-422c-beff-fe91682bdf49, (FLAG)PLA_KarlachRecruitmentTollhouse_Event_GiveHead_d4e8a331-084c-42b7-a2b3-99e587f8679e);
DB_HasItemEvent((ITEM)S_PLA_TollhouseMagicalWeapon_5e3bb83a-1d07-4598-80bb-850bb1bbd23b,(FLAG)PLA_KarlachRecruitmentTollhouse_State_HasWeapon_ed70a103-6223-47c4-98de-f4d221154554);
DB_GiveItemToEvent((ITEM)S_PLA_TollhouseMagicalWeapon_5e3bb83a-1d07-4598-80bb-850bb1bbd23b, (FLAG)PLA_KarlachRecruitmentTollhouse_Event_GiveWeapon_22ae1f42-07bc-4f15-8ea0-26e71487b85a);

DB_FlagReactionAfterDialog((FLAG)PLA_KarlachRecruitmentTollhouse_State_CrimeSpotted_5bd6baa7-91ca-430b-9ba7-c887d6626c09, (DIALOGRESOURCE)PLA_Refugee_SpokeWithDeadCrime_6e6bfe2a-3e85-3aa6-3cb1-fed280670565);
DB_FlagReactionAfterDialog((FLAG)PLA_KarlachRecruitment_Event_TakeHead_8690f0aa-eb70-4676-8069-32c049a504b5, (DIALOGRESOURCE)PLA_KarlachRecruitment_HeadLoot_25793061-8650-7d0e-9b79-d98ecaee9ca7);

PROC_TriggerRegisterForPlayers(S_PLA_SlaughterSite_c358ea55-a404-4745-baae-8bbe50ec8e33);
PROC_TriggerRegisterForPlayers(S_PLA_SpotCorpseTrigger_99a01991-5f05-47ac-ae24-a87f2f304306);
PROC_TriggerRegisterForParty(S_PLA_TollhouseBounds_9b185c3f-f82f-4315-8c5d-aaead5ae362c);
PROC_TriggerRegisterForPlayers(S_PLA_EavesdropBounds_da95acfe-61c3-4832-b6f2-80b3af95a1bb);
PROC_TriggerRegisterForPlayers(S_PLA_TollhouseInterior_59a65f0c-6a16-4710-a058-509834036439);
TriggerRegisterForCharacter(S_PLA_KarlachRecruitmentTollhouse_CorpseMoveBounds_3763ead9-4340-42ce-9df8-42b1c30dd36b, S_PLA_Refugee_006_eecfdb49-f726-4d23-9081-dca9b2481e7a);
TriggerRegisterForItems(S_PLA_BucketBounds_5470338f-4ace-4381-9cf9-86ee271359e7);

DB_SpotPlayers((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, "PLA_Karlach_SpotAtRecruitment", (FLAG)NULL_00000000-0000-0000-0000-000000000000, (FLAG)NULL_00000000-0000-0000-0000-000000000000);
DB_SpotPlayers_Continuous((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, "PLA_Karlach_SpotAtRecruitment");
DB_SpotPlayers_SpotTrigger((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, "PLA_Karlach_SpotAtRecruitment", (TRIGGER)S_PLA_KarlachRiverSpotRange_806bd95f-0207-4361-a1c9-3d9ba7de642d);
DB_AddCharactersInTriggerToDialog((DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61, (TRIGGER)S_PLA_KarlachRiverSpot_IncludePlayers_22fcba99-9315-46f5-9fcf-95b88f3e6b6f, 1, 0);

DB_PLA_KarlachRecruitment_BloodTrail((TRIGGER)S_PLA_KarlachBloodTrail_000_91d6b00a-a8eb-4d54-9901-e621eaca5c12, 0.9);
DB_PLA_KarlachRecruitment_BloodTrail((TRIGGER)S_PLA_KarlachBloodTrail_001_455e584d-6b4a-46ce-b0d3-551e4d493bd5, 0.9);
DB_PLA_KarlachRecruitment_BloodTrail((TRIGGER)S_PLA_KarlachBloodTrail_002_e5c2ed1e-d7b2-4a8a-8b1f-6f804671a7d4, 0.9);
DB_PLA_KarlachRecruitment_BloodTrail((TRIGGER)S_PLA_KarlachBloodTrail_003_0555c901-d337-4d7b-aab9-37cdcd54e11a, 0.9);
DB_PLA_KarlachRecruitment_BloodTrail((TRIGGER)S_PLA_KarlachBloodTrail_004_115fc9ac-b161-4f3c-8e28-5d932a79c028, 0.9);

DB_GLO_CharacterCorpseDialog(S_PLA_TollCollector_001_43421d3b-74b7-4107-b0a3-c765ee282700, (DIALOGRESOURCE)PLA_TollCollector001_Dead_745d880a-de52-9b3e-d049-80c975c9be50);
DB_GLO_CharacterCorpseDialog(S_PLA_TollCollector_002_d11a407d-9d16-40f1-a4de-fce659462383, (DIALOGRESOURCE)PLA_TollCollector002_Dead_37a3862d-0b09-f084-40d3-08dcf8024848);

DB_Dialogs(S_PLA_Refugee_001_a43d1d6c-d397-4d2d-adb4-ead3b10cb189, (DIALOGRESOURCE)PLA_Refugee_001_PostEA_16dd4720-bb82-70a6-4567-215546346b0d);

DB_Dialogs(S_PLA_Refugee_002_97ae38c5-550d-4585-9f9e-a0494ecb863a, (DIALOGRESOURCE)PLA_Refugee_002_PostEA_c0d179f4-42cb-4ee4-8448-0cdcb926ad3c);
DB_Dialogs(S_PLA_Refugee_003_bf6a657e-091c-43e5-b18a-a2ca7ca7e511, (DIALOGRESOURCE)PLA_Refugee_003_PostEA_7f2cdc2c-465d-6c6f-a77a-761c7f20bd33);

DB_PLA_KarlachRecruitmentTollhouse_Gnoll((CHARACTER)S_PLA_TollhouseGnoll_005_540a1de2-0b7b-4146-b3eb-369f636e6ca9);
DB_PLA_KarlachRecruitmentTollhouse_Gnoll((CHARACTER)S_PLA_TollhouseGnoll_006_4a0b46d1-6e90-4449-b750-c4dda67ff4a1);
DB_PLA_KarlachRecruitmentTollhouse_Gnoll((CHARACTER)S_PLA_TollhouseGnoll_007_fd8fe54e-020b-416b-9866-db64e59b96e3);

DB_PLA_KarlachRecruitmentTollhouse_Refugee(S_PLA_Refugee_001_a43d1d6c-d397-4d2d-adb4-ead3b10cb189);
DB_PLA_KarlachRecruitmentTollhouse_Refugee(S_PLA_Refugee_002_97ae38c5-550d-4585-9f9e-a0494ecb863a);
DB_PLA_KarlachRecruitmentTollhouse_Refugee(S_PLA_Refugee_003_bf6a657e-091c-43e5-b18a-a2ca7ca7e511);

DB_PLA_KarlachRecruitmentTollhouse_TalkingRefugee(S_PLA_Refugee_001_a43d1d6c-d397-4d2d-adb4-ead3b10cb189);
DB_PLA_KarlachRecruitmentTollhouse_TalkingRefugee(S_PLA_Refugee_002_97ae38c5-550d-4585-9f9e-a0494ecb863a);

DB_PLA_KarlachRecruitmentTollhouse_PressurePlates((ITEM)S_PLA_WallPressurePlate_001_bf566bf6-78c9-49a4-9dc9-49d8ef210e98);
DB_PLA_KarlachRecruitmentTollhouse_PressurePlates((ITEM)S_PLA_WallPressurePlate_002_0e497cd8-6b56-4989-823d-ba594207534a);

DB_GLO_DefeatCounter_IgnoreWaitForCombatEnd("PLA_KarlachRecruitmentTollhouse_Cultists");
DB_GLO_DefeatCounter(S_PLA_Refugee_001_a43d1d6c-d397-4d2d-adb4-ead3b10cb189, "PLA_KarlachRecruitmentTollhouse_Cultists");
DB_GLO_DefeatCounter(S_PLA_Refugee_002_97ae38c5-550d-4585-9f9e-a0494ecb863a, "PLA_KarlachRecruitmentTollhouse_Cultists");
DB_GLO_DefeatCounter(S_PLA_Refugee_003_bf6a657e-091c-43e5-b18a-a2ca7ca7e511, "PLA_KarlachRecruitmentTollhouse_Cultists");
DB_GLO_DefeatCounter_AllPermaDefeatedGlobalFlag("PLA_KarlachRecruitmentTollhouse_Cultists", (FLAG)PLA_KarlachRecruitmentTollhouse_State_PaladinsPermaDefeated_d78dbc95-8d8e-4b3a-b618-ccfb48c31375);
DB_GLO_DefeatCounter_AllDeadGlobalFlag("PLA_KarlachRecruitmentTollhouse_Cultists", (FLAG)PLA_KarlachRecruitmentTollhouse_State_PaladinsDead_1e26e9d9-7e48-419c-b3c4-d7a042483aad);

DB_Inclusion_Object(S_PLA_Refugee_002_97ae38c5-550d-4585-9f9e-a0494ecb863a, (FLAG)PLA_KarlachRecruitment_Refugee002_StartInclusion_1aebb3e9-60c5-4dac-80f3-f9503be20ef3, (FLAG)PLA_KarlachRecruitment_Refugee002_EndInclusion_98f25e61-0e56-4a62-96a0-8d0180db8602);
DB_Inclusion_NPCDialog((DIALOGRESOURCE)PLA_Refugee_001_PostEA_16dd4720-bb82-70a6-4567-215546346b0d,S_PLA_Refugee_002_97ae38c5-550d-4585-9f9e-a0494ecb863a);
SetHasDialog(S_PLA_Refugee_002_97ae38c5-550d-4585-9f9e-a0494ecb863a, 0);

Equip(S_PLA_Refugee_001_a43d1d6c-d397-4d2d-adb4-ead3b10cb189,S_PLA_TollhouseMagicalWeapon_5e3bb83a-1d07-4598-80bb-850bb1bbd23b);

DB_GLO_FireBowls(S_PLA_Firebowl_Bowl_001_4f1c542a-4a61-4d5b-ad8a-720cb93b669e,S_PLA_Firebowl_Hinge_000_45b71538-954d-421e-9ca9-8144341479ce);	//Plains, Zariel Cultists

DB_QuestDef_ChainedState("PLA_TollhouseHunters", "AcceptedBoth", "AcceptedAnders_AlsoKarlach"); 
DB_QuestDef_ChainedState("PLA_TollhouseHunters", "AcceptedBoth", "AcceptedKarlach_AlsoAnders"); 
DB_QuestDef_LevelLoaded("SCL_Main_A", "PLA_TollhouseHunters", "LeftRegion");
KBSECTION
//REGION Debug

IF
TextEvent("Tollhouse_KnowsCultist")
THEN
SetFlag((FLAG)PLA_KarlachRecruitmentTollhouse_Knows_RefugeesAreCultists_8b18c014-7558-4bab-aa35-37d135fc8630, NULL_00000000-0000-0000-0000-000000000000, 0);


//END_REGION

//REGION Karlach Meeting

PROC
PROC_GLO_Origins_SetupRecruitment("WLD_Main_A")
THEN
PROC_PLA_KarlachRecruitment_KarlachStateCheck();

//REGION Karlach State

PROC
PROC_PLA_KarlachRecruitment_KarlachStateCheck()
AND
DB_Avatars(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
SetFlag((FLAG)PLA_KarlachRecruitment_State_KarlachAbsent_2ce4fc18-5ba4-4c18-aefd-42a499713f29, NULL_00000000-0000-0000-0000-000000000000, 0);
SetOnStage(S_PLA_KarlachBackpack_e6b331da-d5e2-481f-8db6-8c36ddb9562d, 0);

PROC
PROC_PLA_KarlachRecruitment_KarlachStateCheck()
AND
NOT DB_Avatars(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
SetFlag((FLAG)PLA_KarlachRecruitment_State_KarlachWounded_4cd908ef-0bc9-41fa-84f1-18bc7c64ed2d, NULL_00000000-0000-0000-0000-000000000000);
PROC_RemoveDialog(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c);
DB_Dialogs(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61);
DB_ReportKiller((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, PLA_KarlachRecruitment_Event_KarlachDied_f90c799d-a643-7d25-04d4-79f5c6e9d96b);

PROC
PROC_PLA_KarlachRecruitment_KarlachStateCheck()
AND
NOT DB_Avatars(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
DB_PLA_KarlachRecruitment_BloodTrail(_Blood, _Size)
THEN
CreateSurface((TRIGGER)_Blood, "SurfaceBlood", _Size, -1.0);

IF
FlagCleared((FLAG)PLA_KarlachRecruitment_State_KarlachWounded_4cd908ef-0bc9-41fa-84f1-18bc7c64ed2d, NULL_00000000-0000-0000-0000-000000000000, _)
THEN
PROC_SelfHealing_Enable(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c);
//END_REGION

PROC
PROC_SpotPlayers_Spotted((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, "PLA_Karlach_SpotAtRecruitment", _Char)
AND
NOT DB_Players((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
NOT DB_OnlyOnce("PLA_KarlachRecruitment_InitialConfrontation")
THEN
PROC_PLA_KarlachRecruitment_CheckCharacter(_Char);

PROC
PROC_PLA_KarlachRecruitment_CheckCharacter((CHARACTER)_Char)
AND
DB_Players(_Char)
AND
NOT DB_Is_WildShaped(_Char)
AND
QRY_PLA_KarlachRecruitment_RemoveBlockers((CHARACTER)_Char)
THEN
PROC_PLA_KarlachRecruitment_StartConfrontation((CHARACTER)_Char);

PROC
PROC_PLA_KarlachRecruitment_CheckCharacter((CHARACTER)_Char)
AND
DB_Is_WildShaped(_Char)
AND
DB_Players(_Char)
THEN
PROC_PLA_KarlachRecruitment_FindAlternative();

PROC
PROC_PLA_KarlachRecruitment_FindAlternative()
AND
QRY_PLA_KarlachRecruitment_CheckParty()
AND
DB_PLA_KarlachRecruitment_PlayerSelected((CHARACTER)_Char)
THEN
NOT DB_PLA_KarlachRecruitment_PlayerSelected(_Char);
PROC_PLA_KarlachRecruitment_CheckCharacter((CHARACTER)_Char);

PROC
PROC_PLA_KarlachRecruitment_FindAlternative()
AND
NOT QRY_PLA_KarlachRecruitment_CheckParty()
AND
DB_InRegion(_Player, (TRIGGER)S_PLA_KarlachRiverSpotRange_806bd95f-0207-4361-a1c9-3d9ba7de642d)
AND
DB_Is_WildShaped(_Player)
AND
GetFaction(_Player, _PlayerFaction)
AND
GetFaction(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _KarlachFaction)
THEN
PROC_SetRelationMutual(_PlayerFaction, _KarlachFaction, 0);

QRY
QRY_PLA_KarlachRecruitment_CheckParty()
AND
DB_Players(_Char)
AND
NOT DB_Defeated(_Char)
AND
NOT DB_Is_WildShaped(_Char)
AND
GetDistanceTo(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Char, _Dist)
AND
_Dist < 18.0
AND
NOT DB_PLA_KarlachRecruitment_PlayerSelected(_)
THEN
DB_PLA_KarlachRecruitment_PlayerSelected(_Char);

QRY
QRY_PLA_KarlachRecruitment_RemoveBlockers((CHARACTER)_Player)
AND
HasActiveStatus(_Player, "SILENCED", 1)
THEN
RemoveStatus(_Player, "SILENCED");

QRY
QRY_PLA_KarlachRecruitment_RemoveBlockers((CHARACTER)_Player)
THEN
DB_NOOP(1);

PROC
PROC_PLA_KarlachRecruitment_CheckCharacter((CHARACTER)_Char)
AND
NOT DB_Players(_Char)
AND
GetClosestAlivePlayer(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Player, _Dist)
AND
_Dist < 18.0
THEN
PROC_PLA_KarlachRecruitment_CheckCharacter((CHARACTER)_Player);

PROC
PROC_PLA_KarlachRecruitment_StartConfrontation((CHARACTER)_Char)
AND
NOT DB_OnlyOnce("PLA_KarlachRecruitment_InitialConfrontation")
AND
QRY_StartDialog((DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61, (CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Char)
AND
QRY_OnlyOnce("PLA_KarlachRecruitment_InitialConfrontation")
THEN
PROC_SpotPlayers_StopSpotting((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, "PLA_Karlach_SpotAtRecruitment");

IF
DialogEnded((DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61, _DialogID)
AND
NOT DB_DialogRequestFailed((DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61, _DialogID)
AND
DB_AddCharactersInTriggerToDialog((DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61, (TRIGGER)S_PLA_KarlachRiverSpot_IncludePlayers_22fcba99-9315-46f5-9fcf-95b88f3e6b6f, 1, 0)
THEN
PROC_AddCharactersInTriggerToDialog_Clear((DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61, (TRIGGER)S_PLA_KarlachRiverSpot_IncludePlayers_22fcba99-9315-46f5-9fcf-95b88f3e6b6f);
//REGION Karlach Healing

IF
FlagSet((FLAG)PLA_KarlachRecruitment_Event_TakeHealingPotion_909cad91-a267-40bd-adb5-e65e44a369e6, _Player, _)
THEN
MagicPocketsMoveToByTag((CHARACTER)_Player, "HEALING_POTION_1879a93d-2edf-4f54-85dd-81a3724d677f", 1, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 0, 1);

IF
StatusApplied(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Status, _Player, _)
AND
NOT DB_Players((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
DB_Players((CHARACTER)_Player)
AND
QRY_IsHealingStatus(_Status)
AND
NOT DB_Defeated(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
DB_GlobalFlag((FLAG)PLA_KarlachRecruitment_State_KarlachWounded_4cd908ef-0bc9-41fa-84f1-18bc7c64ed2d)
THEN
SetFlag((FLAG)PLA_KarlachRecruitment_Event_HealedKarlach_54f22394-ff39-407f-9b2c-ab2e38c9c128, _Player);
ClearFlag((FLAG)PLA_KarlachRecruitment_State_KarlachWounded_4cd908ef-0bc9-41fa-84f1-18bc7c64ed2d, NULL_00000000-0000-0000-0000-000000000000);
PROC_PLA_KarlachRecruitment_HealedAD((CHARACTER)_Player);

IF
DialogEnded((DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61, _)
AND
NOT DB_OnlyOnce("PLA_KarlachRecruitment_KarlachHealAD")
AND
DB_Players(_Player)
AND
GetFlag((FLAG)PLA_KarlachRecruitment_Event_HealedKarlach_54f22394-ff39-407f-9b2c-ab2e38c9c128, _Player, 1)
THEN
ObjectTimerLaunch(_Player, "PLA_KarlachRecruitment_HealADDelay", 500);

IF
ObjectTimerFinished(_Player, "PLA_KarlachRecruitment_HealADDelay")
THEN
PROC_PLA_KarlachRecruitment_HealedAD((CHARACTER)_Player);

PROC
PROC_PLA_KarlachRecruitment_HealedAD((CHARACTER)_Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Player)
AND
QRY_OnlyOnce("PLA_KarlachRecruitment_KarlachHealAD")
THEN
LookAtEntity((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Player);
PROC_TryStartAD((DIALOGRESOURCE)PLA_KarlachRecruitment_AD_KarlachHealed_cd758a00-bc77-22a7-077d-f72472f78376, (CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c);

IF
AddedTo(_Potion, (CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _)
AND
NOT DB_Players((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
Exists(_Potion,1)
AND
IsTagged((ITEM)_Potion, (TAG)HEALING_POTION_1879a93d-2edf-4f54-85dd-81a3724d677f, 1)
AND
DB_Players(_Player)
AND
GetFlag((FLAG)PLA_KarlachRecruitment_Event_HealedKarlach_54f22394-ff39-407f-9b2c-ab2e38c9c128, _Player, 1)
THEN
Use((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Potion, 1, 0, "PLA_KarlachRecruitment_UsePotion");


//END_REGION

//REGION Karlach Death
IF
DB_GlobalFlag((FLAG)PLA_KarlachRecruitmentTollhouse_Quest_AcceptedSearch_3b379a18-9b46-4f66-9baf-0e7ce938c1d0)
AND
NOT DB_PartOfTheTeam((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
DB_GLO_HeadRemoval_Dialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)PLA_KarlachRecruitment_HeadLoot_25793061-8650-7d0e-9b79-d98ecaee9ca7);
DB_GLO_HeadRemoval((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (CHARACTERROOT)UNI_PLA_Karlach_Headless_3c3bedb4-917a-4663-9a8a-d18611aaac95, (ITEM)S_GLO_KarlachHead_931f6df2-b546-422c-beff-fe91682bdf49);
DB_GLO_HeadRemoval_InaccessibleIfOffstage((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c);

PROC
PROC_Origins_CompanionLeavePermanently((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _)
AND
DB_GlobalFlag((FLAG)PLA_KarlachRecruitmentTollhouse_Quest_AcceptedSearch_3b379a18-9b46-4f66-9baf-0e7ce938c1d0)
THEN
DB_GLO_HeadRemoval_Dialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)PLA_KarlachRecruitment_HeadLoot_25793061-8650-7d0e-9b79-d98ecaee9ca7);
DB_GLO_HeadRemoval((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (CHARACTERROOT)UNI_PLA_Karlach_Headless_3c3bedb4-917a-4663-9a8a-d18611aaac95, (ITEM)S_GLO_KarlachHead_931f6df2-b546-422c-beff-fe91682bdf49);
DB_GLO_HeadRemoval_InaccessibleIfOffstage((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c);

IF
DB_PartOfTheTeam((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
NOT DB_GLO_HeadRemoval((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (CHARACTERROOT)UNI_PLA_Karlach_Headless_3c3bedb4-917a-4663-9a8a-d18611aaac95, (ITEM)S_GLO_KarlachHead_931f6df2-b546-422c-beff-fe91682bdf49);
NOT DB_GLO_HeadRemoval_Dialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)PLA_KarlachRecruitment_HeadLoot_25793061-8650-7d0e-9b79-d98ecaee9ca7);
NOT DB_GLO_HeadRemoval_InaccessibleIfOffstage((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c);

QRY
QRY_GLO_HeadRemoval_Block((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
DB_PartOfTheTeam((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
DB_NOOP(1);

PROC
PROC_GLO_HeadRemoval_OnHeadPicked((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _)
THEN
SetTag(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, BLOCK_RESURRECTION_22a75dbb-1588-407e-b559-5aa4e6d4e6a6);
SetFlag((FLAG)GLO_Karlach_State_Dead_26f3c3b0-a9c8-40d6-8950-67c9b8e134e2, NULL_00000000-0000-0000-0000-000000000000); // Karlach is dead for good.

IF
DB_GLO_HeadRemoval_HeadInaccessible((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
SetFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_HeadNotAvailable_5f22237d-806b-4111-bfd6-fca3c62c283b, NULL_00000000-0000-0000-0000-000000000000);
//END_REGION

//END_REGION

//REGION Tollhouse
// If Karlach is part of the team always consider the paladins permadefeated if KO.
IF
DB_PartOfTheTeam((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
DB_PLA_KarlachRecruitmentTollhouse_Refugee(_Paladin)
THEN
DB_ForceRemoveKnockedOutCharacterOnLongRest((CHARACTER)_Paladin);

// If players agree to help her always consider the paladins permadefeated if KO.
IF
FlagSet((FLAG)PLA_KarlachRecruitment_State_HelpingKarlach_b7cd48bc-b9e3-419e-8ad0-c4837bc0cec0, _, _)
AND
DB_PLA_KarlachRecruitmentTollhouse_Refugee(_Paladin)
THEN
DB_ForceRemoveKnockedOutCharacterOnLongRest((CHARACTER)_Paladin);

PROC
PROC_Origins_CompanionLeavePermanently((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _)
AND
DB_PLA_KarlachRecruitmentTollhouse_Refugee(_Paladin)
THEN
NOT DB_ForceRemoveKnockedOutCharacterOnLongRest((CHARACTER)_Paladin);

IF
FlagSet((FLAG)PLA_KarlachRecruitmentTollhouse_State_PaladinsPermaDefeated_d78dbc95-8d8e-4b3a-b618-ccfb48c31375, _, _)
AND
DB_Players((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
SetFlag((FLAG)PLA_PaladinsOfTyr_State_KarlachInPartyWhenPermadefeated_96991d97-9ce8-4c88-8bff-01f4abd99fc8, NULL_00000000-0000-0000-0000-000000000000);

//REGION Voicebarks

IF
EnteredTrigger(_Player, S_PLA_SlaughterSite_c358ea55-a404-4745-baae-8bbe50ec8e33)
AND
DB_PLA_KarlachRecruitmentTollhouse_Gnoll(_Gnoll)
AND
DB_Sees(_Player, (CHARACTER)_Gnoll)
AND
QRY_OnlyOnce("PLA_KarlachRecruitmentTollhouse_SeenDeadGnolls")
THEN
SetFlag((FLAG)PLA_KarlachRecruitmentTollhouse_Knows_GnollsDead_9dab2061-e12d-435e-99f0-d9dbe075813d, NULL_00000000-0000-0000-0000-000000000000, 0);
StartVoiceBark((VOICEBARKRESOURCE)PLA_KarlachRecruitmentTollhouse_VB_GnollsDead_9ce72981-0f18-f7ac-d69b-7c4769984666, _Player);

IF
EnteredTrigger(_Player, S_PLA_SpotCorpseTrigger_99a01991-5f05-47ac-ae24-a87f2f304306)
AND
QRY_OnlyOnce("PLA_KarlachRecruitmentTollhouse_SeenCollector")
THEN
StartVoiceBark((VOICEBARKRESOURCE)PLA_KarlachRecruitmentTollhouse_VB_CorpseSmell_5b046757-6cd8-8cb4-7293-06650a4af753, _Player);
//END_REGION

//REGION Tollhouse vault

IF
UseFinished(_Player, S_PLA_TollDoor_8fc6c3fc-61b8-4dce-a91d-8bffb81ae65e, 0)
AND
IsLocked(S_PLA_TollDoor_8fc6c3fc-61b8-4dce-a91d-8bffb81ae65e, 1)
AND
QRY_OnlyOnce("PLA_KarlachRecruitmentTollhouse_DoorInteraction")
THEN
SetFlag((FLAG)PLA_KarlachRecruitmentTollhouse_Knows_VaultDoor_db455146-8d4a-4819-a9a2-698f918de90a, NULL_00000000-0000-0000-0000-000000000000, 0);

IF
AddedTo(S_PLA_TollhouseKey_d2f08a74-8e8a-413a-b17e-da0bbfb4492f, _Player, _)
AND
DB_Players((CHARACTER)_Player)
AND
QRY_OnlyOnce("PLA_KarlachRecruitmentTollhouse_LootedKey")
THEN
StartVoiceBark((VOICEBARKRESOURCE)PLA_KarlachRecruitmentTollhouse_VB_FoundKey_bfd58349-b4d3-60ab-5efa-7d4756a48907, _Player);
PROC_HideMarker((CHARACTER)_Player, "PLA_Tollhouse_Key");

IF
DialogActorJoined((DIALOGRESOURCE)GLO_AD_MechanicalClick_fc8d3dc5-b947-6354-3aa9-d1f02fd51af4,_,_Plate,_)
AND
DB_PLA_KarlachRecruitmentTollhouse_PressurePlates((ITEM)_Plate)
THEN
PROC_PLA_KarlachRecruitmentTollhouse_PlatePressed((ITEM)_Plate);

PROC
PROC_PLA_KarlachRecruitmentTollhouse_PlatePressed((ITEM)_Plate)
AND
NOT DB_OnlyOnce("PLA_KarlachRecruitment_PlatePressed")
AND
NOT IsOpened((ITEM)S_PLA_Tollhouse_SecretDoor_efd35cb2-7198-49a3-92da-72d3b5e42781,1)
AND
GetClosestAlivePlayer(_Plate, _Player, _Dist)
AND
_Dist < 8.0
AND
QRY_OnlyOnce("PLA_KarlachRecruitment_PlatePressed")
THEN
StartVoiceBark((VOICEBARKRESOURCE)PLA_KarlachRecruitment_VB_DiscoveredPressurePlate_88c02da6-ecd8-4255-7c5f-0597c3dfcf55, _Player);

//END_REGION

//REGION  Move Events

IF
LeftTrigger(S_PLA_Refugee_006_eecfdb49-f726-4d23-9081-dca9b2481e7a, S_PLA_KarlachRecruitmentTollhouse_CorpseMoveBounds_3763ead9-4340-42ce-9df8-42b1c30dd36b)
THEN
SetFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_CorpseLeftBounds_4741a054-8532-42f1-bff4-a25b746e8714, NULL_00000000-0000-0000-0000-000000000000, 0);
TriggerUnregisterForCharacter(S_PLA_KarlachRecruitmentTollhouse_CorpseMoveBounds_3763ead9-4340-42ce-9df8-42b1c30dd36b, S_PLA_Refugee_006_eecfdb49-f726-4d23-9081-dca9b2481e7a);

IF
ItemLeftTrigger(S_PLA_WaterBucket_2e142774-8664-4fe2-9954-b74a78770038, S_PLA_BucketBounds_5470338f-4ace-4381-9cf9-86ee271359e7, _)
THEN
SetFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_BucketOutOfBounds_2ef1c013-9069-4dba-b201-21c72b2bb8d1, NULL_00000000-0000-0000-0000-000000000000, 0);
TriggerUnregisterForItems(S_PLA_BucketBounds_5470338f-4ace-4381-9cf9-86ee271359e7);

//END_REGION

//REGION Refugees Leave
IF
LeftTrigger(_, S_PLA_TollhouseBounds_9b185c3f-f82f-4315-8c5d-aaead5ae362c)
AND
NOT DB_InRegion(_, S_PLA_TollhouseBounds_9b185c3f-f82f-4315-8c5d-aaead5ae362c)
AND
GetFlag(PLA_KarlachRecruitmentTollhouse_Event_GiveHead_d4e8a331-084c-42b7-a2b3-99e587f8679e, S_PLA_Refugee_001_a43d1d6c-d397-4d2d-adb4-ead3b10cb189, 1)
AND
QRY_OnlyOnce("PLA_KarlachRecruitmentTollhouse_RefugeesLeft")
THEN
SetFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_RefugeesLeft_04c9162c-0ade-45d8-87a7-40f88796219a, NULL_00000000-0000-0000-0000-000000000000, 0);
DB_PLA_PaladinsOfTyr_TryResolve(1);

PROC
PROC_ReadyToMoveOn(_Refugee, "PLA_KarlachRecruitmentTollhouse_LeaveArea")
THEN
PROC_DisappearOutOfSight(_Refugee, "Run", 0, "");

PROC
PROC_PLA_KarlachRecruitmentTollhouse_DealWithDead()
AND
IsInTrigger(S_PLA_Refugee_006_eecfdb49-f726-4d23-9081-dca9b2481e7a, S_PLA_TollhouseBounds_9b185c3f-f82f-4315-8c5d-aaead5ae362c, 1)
AND
CreateAtObject(PUZ_GEN_Dirt_Pile_Gravel_A_ea591a45-ed05-4881-b4dc-4204ee9cf064, S_PLA_BurialMound_10c8f134-a0b4-458b-b16c-a0319c3a6f4c, 0, 0, "", 0, _Mound)
THEN
SetOnStage(S_PLA_Refugee_006_eecfdb49-f726-4d23-9081-dca9b2481e7a, 0);

//END_REGION

//REGION Eavesdropping

IF
EnteredTrigger(_Player, S_PLA_EavesdropBounds_da95acfe-61c3-4832-b6f2-80b3af95a1bb)
AND
QRY_OnlyOnce("PLA_KarlachRecruitmentTollhouse_DiscussionStarted")
THEN
PROC_TryStartAD((DIALOGRESOURCE)PLA_KarlachRecruitmentTollhouse_AD_Refugees_0641c6ca-45b0-3ec8-bb7d-01f3f2bf6a96, S_PLA_Refugee_001_a43d1d6c-d397-4d2d-adb4-ead3b10cb189, S_PLA_Refugee_002_97ae38c5-550d-4585-9f9e-a0494ecb863a);

IF
DB_Sees(_Refugee, _Player)
AND
DB_PLA_KarlachRecruitmentTollhouse_TalkingRefugee(_Refugee)
AND
DB_Players(_Player)
AND
QRY_OnlyOnce("PLA_KarlachRecruitment_Seen")
AND
DB_PLA_KarlachRecruitmentTollhouse_TalkingRefugee(_Refugees)
AND
NOT DB_InteractiveDialogSpeaker(_, _Refugees)
THEN 
SetFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_SpottedPlayer_51dcca7b-61dc-41de-b515-29d8970b56bb, NULL_00000000-0000-0000-0000-000000000000, 0);
PROC_ForceStopDialog(_Refugee);

IF
AutomatedDialogEnded((DIALOGRESOURCE)PLA_KarlachRecruitmentTollhouse_AD_Refugees_0641c6ca-45b0-3ec8-bb7d-01f3f2bf6a96, _)
AND
DB_GlobalFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_SpottedPlayer_51dcca7b-61dc-41de-b515-29d8970b56bb)
THEN
TimerLaunch("PLA_KarlachRecruitment_GreetPlayer", 1000); // Give some time before trying to start the AD.

IF
TimerFinished("PLA_KarlachRecruitment_GreetPlayer")
AND
NOT DB_CantTalk(S_PLA_Refugee_001_a43d1d6c-d397-4d2d-adb4-ead3b10cb189)
THEN
PROC_TryStartAD((DIALOGRESOURCE)PLA_KarlachRecruitmentTollhouse_AD_RefugeeGreeting_eaf2c800-c561-150c-67b6-2e7b487734f3, S_PLA_Refugee_001_a43d1d6c-d397-4d2d-adb4-ead3b10cb189);

IF
AutomatedDialogEnded((DIALOGRESOURCE)PLA_KarlachRecruitmentTollhouse_AD_Refugees_0641c6ca-45b0-3ec8-bb7d-01f3f2bf6a96, _)
THEN
ClearFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_SpottedPlayer_51dcca7b-61dc-41de-b515-29d8970b56bb, NULL_00000000-0000-0000-0000-000000000000, 0);
SetFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_FinishedDiscussion_8c460213-e3b8-4304-95e6-6d9fe6cb85a2, NULL_00000000-0000-0000-0000-000000000000, 0);
SetHasDialog(S_PLA_Refugee_002_97ae38c5-550d-4585-9f9e-a0494ecb863a, 1);
PROC_NotifyWhenReadyToMoveOn(S_PLA_Refugee_002_97ae38c5-550d-4585-9f9e-a0494ecb863a, "PLA_KarlachRecruitment_MoveTrynn");

IF
AutomatedDialogRequestFailed((DIALOGRESOURCE)PLA_KarlachRecruitmentTollhouse_AD_Refugees_0641c6ca-45b0-3ec8-bb7d-01f3f2bf6a96, _)
THEN
ClearFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_SpottedPlayer_51dcca7b-61dc-41de-b515-29d8970b56bb, NULL_00000000-0000-0000-0000-000000000000, 0);
SetFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_FinishedDiscussion_8c460213-e3b8-4304-95e6-6d9fe6cb85a2, NULL_00000000-0000-0000-0000-000000000000, 0);
SetHasDialog(S_PLA_Refugee_002_97ae38c5-550d-4585-9f9e-a0494ecb863a, 1);
PROC_NotifyWhenReadyToMoveOn(S_PLA_Refugee_002_97ae38c5-550d-4585-9f9e-a0494ecb863a, "PLA_KarlachRecruitment_MoveTrynn");

PROC
PROC_ReadyToMoveOn(S_PLA_Refugee_002_97ae38c5-550d-4585-9f9e-a0494ecb863a, "PLA_KarlachRecruitment_MoveTrynn")
THEN
PROC_CharacterMoveTo(S_PLA_Refugee_002_97ae38c5-550d-4585-9f9e-a0494ecb863a, S_PLA_HatchPoint_f9a1051c-5e80-4cab-8464-b1cada13e6c7, "Walk", "PLA_KarlachRecruitment_TrynnMoved");

//END_REGION

//REGION Zariel Cultist Reflection

IF //Cultists Alive - Learned they are cultists
LeftTrigger(_Player, S_PLA_TollhouseInterior_59a65f0c-6a16-4710-a058-509834036439)
AND
DB_GlobalFlag((FLAG)PLA_KarlachRecruitmentTollhouse_Knows_RefugeesAreCultists_8b18c014-7558-4bab-aa35-37d135fc8630)
AND
NOT DB_GlobalFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_HostilitiesBegan_23df3000-4672-b146-14a5-14d24107d05e)
AND
NOT DB_PartOfTheTeam((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
NOT DB_CantTalk(_Player)
AND
QRY_OnlyOnce("PLA_KarlachRecruitmentTollhouse_ReflectionDefined")
THEN
StartVoiceBark((VOICEBARKRESOURCE)PLA_Tollhouse_VB_RefugeesAreCultists_78a50ff3-3e5e-8cdf-7d6d-d8e904fa1f5e, _Player);

//END_REGION

//REGION Secret Marker

IF
FlagSet((FLAG)PLA_KarlachRecruitmentTollhouse_Quest_LearnedKeyFell_0c5297df-cab5-4786-af06-7026aca78e5d, _Player, _)
AND
QRY_OnlyOnce("PLA_KarlachRecruitmentTollhouse_LootedKey")
AND
QRY_OnlyOnce("PLA_KarlachRecruitment_FoundKeyLocation")
THEN
PROC_ShowMarker((CHARACTER)_Player, "PLA_Tollhouse_Key");
//END_REGION

//REGION Crime - Spoke with Dead
QRY
QRY_CRIME_BlockRegisterCrime(_Player, "PLA_Tollhouse_SpokeWithDead", NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000, _)
AND
DB_GlobalFlag((FLAG)PLA_KarlachRecruitment_State_CultistsKnown_eae24292-5c76-4b96-86da-08694f238203)
THEN
DB_NOOP(1);

IF
FlagSet((FLAG)PLA_KarlachRecruitment_State_CultistsKnown_eae24292-5c76-4b96-86da-08694f238203, _, _)
AND
DB_Players(_Player)
THEN
CharacterStopCrime(_Player, "PLA_Tollhouse_SpokeWithDead", NULL_00000000-0000-0000-0000-000000000000);

PROC
PROC_FlagReactionAfterDialog(_Refugee, (FLAG)PLA_KarlachRecruitmentTollhouse_State_CrimeSpotted_5bd6baa7-91ca-430b-9ba7-c887d6626c09)
AND
QRY_StartDialog((DIALOGRESOURCE)PLA_KarlachRecruitmentTollhouse_AD_RefugeeAlarm_71e5040c-e050-d879-00d5-853f920d2349, _Refugee)
THEN
DB_NOOP(1);

//END_REGION
//END_REGION

//REGION Banter
PROC
PROC_LongRest()
AND
DB_GlobalFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_PaladinsPermaDefeated_d78dbc95-8d8e-4b3a-b618-ccfb48c31375)
THEN
PROC_RegisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_PLA_TollhouseCleared_683e29e2-5840-41d1-8fd2-626f92852f53);

PROC
PROC_LongRest()
AND
DB_PermaDefeated((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
PROC_RegisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_PLA_KarlachResolved_9784be16-5816-488c-bc33-a3783bed67bf);

PROC
PROC_LongRest()
AND
DB_GlobalFlag((FLAG)PLA_KarlachRecruitment_Quest_ToldKarlach_0f7ebbd3-2774-4b63-a292-c93212c32d1c)
THEN
PROC_RegisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_PLA_KarlachResolved_9784be16-5816-488c-bc33-a3783bed67bf);
//END_REGION Banter

//REGION Karlach reactions to dying
IF
Died((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
SetFlag((FLAG)PLA_KarlachRecruitment_Event_KarlachDied_f90c799d-a643-7d25-04d4-79f5c6e9d96b, NULL_00000000-0000-0000-0000-000000000000, 0);
ClearFlag((FLAG)PLA_KarlachRecruitment_State_KarlachKnockedOut_6f218edf-4ddd-418a-82ba-d2937f08961d, NULL_00000000-0000-0000-0000-000000000000, 0);

PROC
PROC_WitnessedKiller((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Player, _, _, (CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
SetFlag((FLAG)PLA_KarlachRecruitment_State_KarlachKiller_fc644931-c7a3-4b6c-8b01-1438e44118c4, _Player, 0);

IF
StatusApplied(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, "KNOCKED_OUT", _Attacker, _) 
AND
QRY_CharacterGetOwnerOrSelf((CHARACTER)_Attacker)
AND
DB_QRYRTN_CharacterGetOwnerOrSelf(_AttackerOwner)
AND
DB_Players(_AttackerOwner)
AND
DB_Sees((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _AttackerOwner)
THEN
SetFlag((FLAG)PLA_KarlachRecruitment_State_KarlachKnockout_213fbc58-e34c-4205-a50e-04bc2c38d30d, _AttackerOwner, 0);

IF
StatusApplied(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, "KNOCKED_OUT", _, _) 
AND
DB_Is_InCombat((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _ID)
AND
DB_Players(_Player)
AND
DB_Is_InCombat(_Player, _ID)
AND
DB_Sees((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Player)
AND
QRY_ReportKiller_WasEnemy((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Player, _ID)
THEN
SetFlag((FLAG)PLA_KarlachRecruitment_State_KarlachKnockout_213fbc58-e34c-4205-a50e-04bc2c38d30d, _Player, 0);

PROC
PROC_KnockedOut((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (GUIDSTRING)_Cause)
THEN
SetFlag(PLA_KarlachRecruitment_State_KarlachKnockedOut_6f218edf-4ddd-418a-82ba-d2937f08961d, NULL_00000000-0000-0000-0000-000000000000, 0);

IF
DialogEnded((DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61, _ID)
AND
NOT DB_DialogRequestFailed(PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61, _ID)
THEN
PROC_PurgeLevelPartyFlag((FLAG)PLA_KarlachRecruitment_State_KarlachKiller_fc644931-c7a3-4b6c-8b01-1438e44118c4);
PROC_PurgeLevelPartyFlag((FLAG)PLA_KarlachRecruitment_State_KarlachKnockout_213fbc58-e34c-4205-a50e-04bc2c38d30d);
ClearFlag((FLAG)PLA_KarlachRecruitment_State_KarlachKnockedOut_6f218edf-4ddd-418a-82ba-d2937f08961d, NULL_00000000-0000-0000-0000-000000000000);
ClearFlag((FLAG)PLA_KarlachRecruitment_Event_KarlachDied_f90c799d-a643-7d25-04d4-79f5c6e9d96b, NULL_00000000-0000-0000-0000-000000000000, 0);

//END_REGION

//REGION Closing goal
IF
FlagSet((FLAG)Act2_PointOfNoReturnReached_a3155f30-b8f3-4db5-ac21-d3036f4426e3, _, _)
THEN
GoalCompleted;
//END_REGION Closing goal
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
