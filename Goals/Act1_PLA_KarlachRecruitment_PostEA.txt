Version 1
SubGoalCombiner SGC_AND
INITSECTION
PROC_GLO_Origins_SetRecruitmentDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61);
DB_OriginMayLeaveDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61);

DB_Inclusion_NPCDialog((DIALOGRESOURCE)PLA_Refugee_001_PostEA_16dd4720-bb82-70a6-4567-215546346b0d, S_PLA_Refugee_002_97ae38c5-550d-4585-9f9e-a0494ecb863a);

DB_GLO_CharacterCorpseDialog(S_PLA_Refugee_006_eecfdb49-f726-4d23-9081-dca9b2481e7a, (DIALOGRESOURCE)PLA_Refugee_Dead_PostEA_601c206b-f4c1-4798-1d80-51347309c965);

DB_AddCharactersInTriggerToDialog((DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61, (TRIGGER)S_PLA_KarlachRiverSpot_IncludePlayers_22fcba99-9315-46f5-9fcf-95b88f3e6b6f, 1, 0);

DB_SpotPlayers((CHARACTER)S_PLA_Refugee_001_a43d1d6c-d397-4d2d-adb4-ead3b10cb189, "PLA_KarlachRecruitmentTollhouse_AndersSpotsKarlach", (FLAG)PLA_KarlachRecruitmentTollhouse_Event_StartAndersSightChecks_e9434e80-45a4-4e9b-aadc-fe05373c9f2f, (FLAG)PLA_KarlachRecruitmentTollhouse_Event_StopAndersSightChecks_410ccafc-0203-43c8-b2a3-4d3f3b55a477);
DB_SpotPlayers_HasTag((CHARACTER)S_PLA_Refugee_001_a43d1d6c-d397-4d2d-adb4-ead3b10cb189, "PLA_KarlachRecruitmentTollhouse_AndersSpotsKarlach", (TAG)KARLACH_9aab4bcd-e1f7-4008-8f7b-f1203789ae9b, 1);

DB_PLA_KarlachRecruitment_RecruitmentDialogues((DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61);
DB_PLA_KarlachRecruitment_RecruitmentDialogues((DIALOGRESOURCE)PLA_WyllConfrontation_OM_Wyll_COM_dfc8919e-e6a5-d01d-e048-3853196629be);
DB_PLA_KarlachRecruitment_RecruitmentDialogues((DIALOGRESOURCE)PLA_WyllConfrontation_OM_Wyll_AOM_OOM_d52e58c1-6130-bf0e-6a11-e631c2864947);

PROC_TriggerRegisterForParty((TRIGGER)S_PLA_PaladinsOfTyr_TollHouseAndKarlachArea_61d97a5b-9a75-44d1-a009-042797b9481c);

//REGION Dammon dies
DB_GLO_CompoundFlag((FLAG)DEN_AttackOnDen_State_HostileTieflings_c5641caa-4409-4739-a1fe-8263b8271f4e, (FLAG)PLA_KarlachRecruitment_State_DammonUnavailable_96af4ae3-496d-497f-afb5-fec042fe6f27);
DB_GLO_CompoundFlag((FLAG)DEN_AttackOnDen_State_HostileTieflings_c5641caa-4409-4739-a1fe-8263b8271f4e, (FLAG)PLA_KarlachRecruitment_State_PartyPermadefeatedDammon_2712e0c3-e498-4d0b-a0c2-20053c64e9ed); // Always consider party was responsible if tieflings go hostile.
DB_PermaDefeatedFlag(S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79, (FLAG)PLA_KarlachRecruitment_State_DammonUnavailable_96af4ae3-496d-497f-afb5-fec042fe6f27);
DB_ReportKiller(S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79, PLA_KarlachRecruitment_State_PartyPermadefeatedDammon_2712e0c3-e498-4d0b-a0c2-20053c64e9ed);
//END_REGION Players kill tieflings before recruiting her

//REGION Topical Greetings
DB_GlobalFlagReactionAfterDialog((FLAG)PLA_KarlachRecruitment_Event_UnlockTGNoWyll_1081c6eb-cf46-7d81-9a68-a51aed4f4aae, (DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61);
DB_TopicalGreeting((FLAG)TG_PLA_KarlachRecruitment_NoWyll_f3448ace-822c-47ca-9570-3bbc0a600f3c);
DB_TopicalGreetingEndCondition_FlagSet((FLAG)TG_PLA_KarlachRecruitment_NoWyll_f3448ace-822c-47ca-9570-3bbc0a600f3c,(FLAG)GLO_Karlach_State_Dead_26f3c3b0-a9c8-40d6-8950-67c9b8e134e2);

DB_GlobalFlagReactionAfterDialog((FLAG)PLA_KarlachRecruitment_Event_UnlockTGWyllAvatar_28e14b86-550a-4100-8838-301805f8961b, (DIALOGRESOURCE)PLA_WyllConfrontation_OM_Wyll_AOM_OOM_d52e58c1-6130-bf0e-6a11-e631c2864947);
DB_TopicalGreeting((FLAG)TG_PLA_KarlachRecruitment_WyllAvatar_73acd5a3-cfd4-47cb-984f-c55ecda5664c);
DB_TopicalGreetingEndCondition_FlagSet((FLAG)TG_PLA_KarlachRecruitment_WyllAvatar_73acd5a3-cfd4-47cb-984f-c55ecda5664c,(FLAG)GLO_Karlach_State_Dead_26f3c3b0-a9c8-40d6-8950-67c9b8e134e2);

DB_GlobalFlagReactionAfterDialog((FLAG)PLA_KarlachRecruitment_Event_UnlockTGWyllCompanion_3a998190-c475-4d5a-ad5f-2ec8e439bc64, (DIALOGRESOURCE)PLA_WyllConfrontation_OM_Wyll_COM_dfc8919e-e6a5-d01d-e048-3853196629be);
DB_TopicalGreeting((FLAG)TG_PLA_KarlachRecruitment_WyllCompanion_d694775c-fe26-41c2-9322-337f4e63818a);
DB_TopicalGreetingEndCondition_FlagSet((FLAG)TG_PLA_KarlachRecruitment_WyllCompanion_d694775c-fe26-41c2-9322-337f4e63818a,(FLAG)GLO_Karlach_State_Dead_26f3c3b0-a9c8-40d6-8950-67c9b8e134e2);

//END_REGION Topical Greetings

//REGION Journal - Paladins of Tyr
DB_QuestDef_State((FLAG)PLA_PaladinsOfTyr_Event_KarlachLeavesHostile_f178f9f1-cf9e-2956-20aa-2a5857f1c285, "PLA_PaladinsOfTyr", "KarlachBetrayed");
DB_QuestDef_State((FLAG)PLA_KarlachRecruitmentTollhouse_State_HostilitiesBegan_23df3000-4672-b146-14a5-14d24107d05e, "PLA_PaladinsOfTyr", "KillThePaladins");
DB_QuestDef_State((FLAG)PLA_KarlachRecruitmentTollhouse_Event_CorpseToldZarielFollowers_92a42412-66f7-f6df-6006-6cd6ae45acf9, "PLA_PaladinsOfTyr", "LearntZarielFollowers_Corpse"); 

// Closing entries
DB_QuestDef_State((FLAG)PLA_KarlachRecruitmentTollhouse_Event_GiveWeapon_22ae1f42-07bc-4f15-8ea0-26e71487b85a, "PLA_PaladinsOfTyr", "KilledKarlach_Reward");
DB_QuestDef_State((FLAG)PLA_KarlachRecruitmentTollhouse_Quest_CompletedNoReward_207bba38-6d17-cfed-8ba0-3f02f34d8fec, "PLA_PaladinsOfTyr", "KilledKarlach_NoReward");

DB_PLA_KarlachRecruitmentTollhouse_LeaveReasons("ORI_LeftAfterHostility");
DB_PLA_KarlachRecruitmentTollhouse_LeaveReasons("GLO_CompanionLeaves_LowRelation");
//END_REGION Journal - Paladins of Tyr
KBSECTION
//REGION Init
PROC
PROC_PLA_KarlachRecruitment_KarlachStateCheck()
AND
NOT DB_Players((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
SetLevel(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 3);
ApplyStatus(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, "ORI_KARLACH_BURNING_LOWLEVEL_VFX", -1.0, 1, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c);
DB_PLA_KarlachRecruitment_Burning(1);

//END_REGION

//REGION Dialog overrides

// Paladins
IF
DialogStarted((DIALOGRESOURCE)PLA_Refugee_Dead_PostEA_601c206b-f4c1-4798-1d80-51347309c965, _ID)
AND
DB_DialogPlayers(_ID, _Player, _)
THEN
PROC_CharacterRegisterCrime((CHARACTER)_Player, "PLA_Tollhouse_SpokeWithDead", NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000, 0);

IF
DialogEnded((DIALOGRESOURCE)PLA_Refugee_Dead_PostEA_601c206b-f4c1-4798-1d80-51347309c965, _)
AND
DB_Players(_Player)
THEN
CharacterStopCrime(_Player, "PLA_Tollhouse_SpokeWithDead", NULL_00000000-0000-0000-0000-000000000000);
//END_REGION Dialog overrides

//REGION Anders sight checks
PROC
PROC_SpotPlayers_Spotted((CHARACTER)S_PLA_Refugee_001_a43d1d6c-d397-4d2d-adb4-ead3b10cb189, "PLA_KarlachRecruitmentTollhouse_AndersSpotsKarlach", _Player)
AND
NOT QRY_StartDialog((DIALOGRESOURCE)PLA_Refugee_001_PostEA_16dd4720-bb82-70a6-4567-215546346b0d, S_PLA_Refugee_001_a43d1d6c-d397-4d2d-adb4-ead3b10cb189, _Player)
AND
NOT DB_QRYRTN_StartDialog_FailReason("OriginMomentOverride")
AND
GetFaction(S_PLA_Refugee_001_a43d1d6c-d397-4d2d-adb4-ead3b10cb189, _Faction)
THEN
SetFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_HostilitiesBegan_23df3000-4672-b146-14a5-14d24107d05e, NULL_00000000-0000-0000-0000-000000000000);
PROC_SetRelationToPlayers(_Faction, 0);

//END_REGION Anders sight checks

IF
DialogStarted((DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61, _)
THEN
DB_OnlyOnce("PLA_KarlachRecruitment_InitialConfrontation");
PROC_SpotPlayers_StopSpotting((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, "PLA_Karlach_SpotAtRecruitment");

IF
DialogEnded((DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61, _DialogID)
AND
NOT DB_DialogRequestFailed((DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61, _DialogID)
AND
DB_AddCharactersInTriggerToDialog((DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61, (TRIGGER)S_PLA_KarlachRiverSpot_IncludePlayers_22fcba99-9315-46f5-9fcf-95b88f3e6b6f, 1, 0, _IgnoreStatus, _IncludeAllPartyMembers)
THEN
PROC_AddCharactersInTriggerToDialog_Clear((DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61, (TRIGGER)S_PLA_KarlachRiverSpot_IncludePlayers_22fcba99-9315-46f5-9fcf-95b88f3e6b6f);

IF
DialogEnded((DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61, _)
AND
DB_GlobalFlag((FLAG)PLA_KarlachRecruitment_Event_KarlachLeaves_3c7fc104-976e-4573-b18e-f1ffd4eebab2)
THEN
SetHasDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 0);

IF
DialogEnded((DIALOGRESOURCE)PLA_KarlachRecruitment_Karlach_PostEA_0f598b1a-b8b9-10cf-4d0d-0cd958586f61, _)
AND
NOT DB_OnlyOnce("PLA_KarlachRecruitment_KarlachHealAD")
AND
DB_Players(_Player)
AND
GetFlag((FLAG)PLA_KarlachRecruitment_Event_HealedKarlach_54f22394-ff39-407f-9b2c-ab2e38c9c128, _Player, 1)
THEN
ObjectTimerLaunch(_Player, "PLA_KarlachRecruitment_HealADDelay", 500);

IF
DB_PLA_KarlachRecruitment_RecruitmentDialogues(_Dialog)
THEN
DB_GlobalFlagReactionAfterDialog((FLAG)PLA_KarlachRecruitment_Event_KarlachHostile_cb16919b-08da-485c-a9df-7911ba1572d1, _Dialog);

PROC
PROC_GlobalFlagReactionAfterDialog((FLAG)PLA_KarlachRecruitment_Event_KarlachHostile_cb16919b-08da-485c-a9df-7911ba1572d1)
AND
GetBaseFaction(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Faction)
THEN
PROC_SetRelationToPlayers(_Faction, 0);

//REGION Burning state
IF
DB_PartOfTheTeam((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
DB_PLA_KarlachRecruitment_Burning(1)
THEN
SetFlag(ORI_Karlach_Event_StopBurning_eb256a3f-aaa4-46e3-bbc6-28a71d3268ac, NULL_00000000-0000-0000-0000-000000000000);

IF
StatusRemoved(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, "ORI_KARLACH_BURNING_LOWLEVEL_VFX", _, _)
AND
DB_PLA_KarlachRecruitment_Burning(1)
THEN
NOT DB_PLA_KarlachRecruitment_Burning(1);

//END_REGION

//REGION Dammon dies
PROC
PROC_ReportKiller(_, _AttackerOwner, (CHARACTER)S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79, PLA_KarlachRecruitment_State_PartyPermadefeatedDammon_2712e0c3-e498-4d0b-a0c2-20053c64e9ed)
AND
DB_Players(_AttackerOwner)
THEN
SetFlag((FLAG)PLA_KarlachRecruitment_State_PartyPermadefeatedDammon_2712e0c3-e498-4d0b-a0c2-20053c64e9ed, NULL_00000000-0000-0000-0000-000000000000); // Used for persuasion/deception in Karlach's recruitment.

// Used to test persuasion check
IF
TextEvent("KillDammon")
THEN
Die(S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79, DEATHTYPE.DoT, NULL_00000000-0000-0000-0000-000000000000, 1, 0);
//END_REGION

//REGION Topical Greetings
// Clear other exclusive Topical Greetings
PROC
PROC_GlobalFlagReactionAfterDialog(PLA_KarlachRecruitment_Event_UnlockTGNoWyll_1081c6eb-cf46-7d81-9a68-a51aed4f4aae)
AND
QRY_OnlyOnce("PLA_KarlachRecruitment_UnlockedRecruitmentTG")
THEN
PROC_TopicalGreeting_UnlockTopic((FLAG)TG_PLA_KarlachRecruitment_NoWyll_f3448ace-822c-47ca-9570-3bbc0a600f3c);

PROC
PROC_GlobalFlagReactionAfterDialog(PLA_KarlachRecruitment_Event_UnlockTGWyllAvatar_28e14b86-550a-4100-8838-301805f8961b)
AND
QRY_OnlyOnce("PLA_KarlachRecruitment_UnlockedRecruitmentTG")
THEN
PROC_TopicalGreeting_UnlockTopic((FLAG)TG_PLA_KarlachRecruitment_WyllAvatar_73acd5a3-cfd4-47cb-984f-c55ecda5664c);

PROC
PROC_GlobalFlagReactionAfterDialog(PLA_KarlachRecruitment_Event_UnlockTGWyllCompanion_3a998190-c475-4d5a-ad5f-2ec8e439bc64)
AND
QRY_OnlyOnce("PLA_KarlachRecruitment_UnlockedRecruitmentTG")
THEN
PROC_TopicalGreeting_UnlockTopic((FLAG)TG_PLA_KarlachRecruitment_WyllCompanion_d694775c-fe26-41c2-9322-337f4e63818a);
//END_REGION Topical Greetings

//REGION Leaving the area while Paladins are still alive
// She needs to be part of the party to register the trigger.
PROC
PROC_GLO_PartyMembers_AddHook((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _)
AND
QRY_OnlyOnce("ORI_Karlach_RegisterTollHouseAndRecruitmentArea")
THEN
PROC_TriggerRegisterForPlayers((TRIGGER)S_PLA_PaladinsOfTyr_TollHouseAndKarlachRecruitmentArea_2b87b34c-b603-4c90-9477-906ed5f60082);

// Small PAD to remind players that they have to kill the paladins. Only trigger if there's someone else nearby and outside of combat.
IF
LeftTrigger((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (TRIGGER)S_PLA_PaladinsOfTyr_TollHouseAndKarlachRecruitmentArea_2b87b34c-b603-4c90-9477-906ed5f60082)
AND
DB_Players((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c) // Avoid if being dismissed to camp. She still has a branch as long as players have not defeated the paladins.
AND
NOT DB_Is_InCombat(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _)
AND
NOT DB_GlobalFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_PaladinsPermaDefeated_d78dbc95-8d8e-4b3a-b618-ccfb48c31375) //Avoid triggering it if trigger is unregistered because of permadefeating the paladins.
AND
QRY_GetClosestAvailableCharacterTo(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 0, 0, NULL_00000000-0000-0000-0000-000000000000, (CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 0)
AND
DB_ClosestAvailableCharacterTo(_Player, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Dist)
AND
_Dist < 10.0
THEN
PROC_TryStartAD((DIALOGRESOURCE)ORI_Karlach_PAD_RemindsAboutPaladins_50d67871-3d24-36ee-22be-292147a61150, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c);

// Main IPRD moment in which she reminds them to go back and deal with the paladins.
IF
LeftTrigger((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (TRIGGER)S_PLA_PaladinsOfTyr_TollHouseAndKarlachRecruitmentArea_2b87b34c-b603-4c90-9477-906ed5f60082)
AND
DB_Players((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c) // Avoid if being dismissed to camp. She still has a branch as long as players have not defeated the paladins.
AND
NOT DB_GlobalFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_PaladinsPermaDefeated_d78dbc95-8d8e-4b3a-b618-ccfb48c31375) //Avoid triggering it if trigger is unregistered because of permadefeating the paladins.
THEN
PROC_TriggerUnregisterForPlayers((TRIGGER)S_PLA_PaladinsOfTyr_TollHouseAndKarlachRecruitmentArea_2b87b34c-b603-4c90-9477-906ed5f60082);
PROC_RelationshipDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_PaladinsReminder_2b39b93f-3376-45e0-9c07-116d913fa512, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 0);

IF
DB_GlobalFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_PaladinsPermaDefeated_d78dbc95-8d8e-4b3a-b618-ccfb48c31375)
THEN
PROC_TriggerUnregisterForPlayers((TRIGGER)S_PLA_PaladinsOfTyr_TollHouseAndKarlachRecruitmentArea_2b87b34c-b603-4c90-9477-906ed5f60082);

IF
FlagSet((FLAG)PLA_KarlachRecruitmentTollhouse_State_PaladinsPermaDefeated_d78dbc95-8d8e-4b3a-b618-ccfb48c31375, _, _)
AND
DB_HandlingRelationshipDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_PaladinsReminder_2b39b93f-3376-45e0-9c07-116d913fa512,_,_,_)
THEN
PROC_CancelRelationshipDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_PaladinsReminder_2b39b93f-3376-45e0-9c07-116d913fa512);
//END_REGION Leaving the area while Paladins are still alive

//REGION Karlach leaves the region if all the tieflings die
IF
TagSet(S_Player_Wyll_c774d764-4a17-48dc-b470-32ace9ce447d, (TAG)WYLL_DEVIL_5e844f0e-d822-4210-a28e-6af149a7bd4b)
THEN
DB_PLA_PaladinsOfTyr_TryResolve(1);

IF
DB_PLA_PaladinsOfTyr_TryResolve(1)
AND
NOT DB_InRegion(_, S_PLA_PaladinsOfTyr_TollHouseAndKarlachArea_61d97a5b-9a75-44d1-a009-042797b9481c)
AND
NOT DB_PartOfTheTeam((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
PROC_PLA_PaladinsOfTyr_Resolve();

// Karlach joins the party: we won't need to resolve the situation.
IF
DB_PartOfTheTeam((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
QRY_OnlyOnce("PLA_PaladinsOfTyr_Resolve")
THEN
PROC_TriggerUnregisterForParty((TRIGGER)S_PLA_PaladinsOfTyr_TollHouseAndKarlachArea_61d97a5b-9a75-44d1-a009-042797b9481c);

PROC
PROC_PLA_PaladinsOfTyr_Resolve()
AND
QRY_OnlyOnce("PLA_PaladinsOfTyr_Resolve")
THEN
QuestUpdate("PLA_PaladinsOfTyr", "PointOfNoReturn"); // Use the point of no return as fallback
PROC_TriggerUnregisterForParty((TRIGGER)S_PLA_PaladinsOfTyr_TollHouseAndKarlachArea_61d97a5b-9a75-44d1-a009-042797b9481c);
PROC_NotifyWhenReadyToMoveOn((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, "PLA_PaladinsOfTyr_KarlachLeaves");

PROC
PROC_ReadyToMoveOn((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, "PLA_PaladinsOfTyr_KarlachLeaves")
THEN
PROC_DisappearOutOfSight((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, "Run", 0, "PLA_PaladinsOfTyr_KarlachKillsPaladins");

PROC
PROC_ReadyToMoveOn((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, "PLA_PaladinsOfTyr_KarlachLeaves")
AND
IsInTrigger(S_PLA_KarlachBackpack_e6b331da-d5e2-481f-8db6-8c36ddb9562d, (TRIGGER)S_PLA_KarlachBagBounds_d34ae8de-76c4-4708-aefb-b193d1be2ce3, 1)
THEN
SetOnStage(S_PLA_KarlachBackpack_e6b331da-d5e2-481f-8db6-8c36ddb9562d, 0);

// Karlach leaves and kills them
IF
EntityEvent(_, "PLA_PaladinsOfTyr_KarlachKillsPaladins")
AND
DB_PLA_KarlachRecruitmentTollhouse_Refugee(_Refugee)
AND
NOT DB_Dead((CHARACTER)_Refugee)
THEN
Die(_Refugee, DEATHTYPE.Incinerate, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 1, 0);

IF
EntityEvent(_Refugee, "PLA_PaladinsOfTyr_Dies")
AND
QRY_OnlyOnce("PLA_PaladinsOfTyr_Explosion")
THEN
CreateExplosion(S_PLA_FireballExplosion_000_fc1f56c4-a345-4f22-bef8-54c79ea1c1a7, "Projectile_Fireball", 10);
CreateExplosion(S_PLA_FireballExplosion_001_aa08bed7-b5f2-4894-ae71-6b7e8311701a, "Projectile_Fireball", 10);

// If Karlach can't leave, paladins will leave instead.
PROC
PROC_ReadyToMoveOn_Failed((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, "PLA_PaladinsOfTyr_KarlachLeaves")
AND
DB_PLA_KarlachRecruitmentTollhouse_Refugee(_Refugee)
THEN
PROC_NotifyWhenReadyToMoveOn((CHARACTER)_Refugee, "PLA_PaladinsOfTyr_RefugeeLeaves");

PROC
PROC_ReadyToMoveOn(_Refugee, "PLA_PaladinsOfTyr_RefugeeLeaves")
THEN
PROC_DisappearOutOfSight(_Refugee, "Run", 0, "");

PROC
PROC_ReadyToMoveOn(_Refugee, "PLA_PaladinsOfTyr_RefugeeLeaves")
AND
QRY_OnlyOnce("PLA_PaladinsOfTyr_DealWithDead")
THEN
SetFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_RefugeesLeft_04c9162c-0ade-45d8-87a7-40f88796219a, NULL_00000000-0000-0000-0000-000000000000, 0);
PROC_PLA_KarlachRecruitmentTollhouse_DealWithDead();

IF
TextEvent("PLA_PaladinsOfTyr_Resolve")
THEN
PROC_PLA_PaladinsOfTyr_Resolve();
//END_REGION

//REGION Journal - Paladins of Tyr
IF
DB_QuestIsAccepted("PLA_PaladinsOfTyr")
THEN
DB_QuestDef_State(PLA_KarlachRecruitment_Quest_ToldZarielFollowers_b3724866-2e00-14e2-7c7b-55b27f310a6e, "PLA_PaladinsOfTyr", "LearntZarielFollowers"); // TODO: Update PLA_WyllConfrontation_OM_Wyll_AOM_OOM once it's unlocked.

IF
FlagSet((FLAG)PLA_KarlachRecruitment_Quest_ToldZarielFollowers_b3724866-2e00-14e2-7c7b-55b27f310a6e, _, _)
THEN
SetFlag((FLAG)PLA_KarlachRecruitmentTollhouse_State_ToldByKarlachPaladinCultists_630fc3b5-7246-4aa4-98f1-ce5d6ce6f288, NULL_00000000-0000-0000-0000-000000000000);
SetFlag((FLAG)PLA_KarlachRecruitmentTollhouse_Knows_RefugeesAreCultists_8b18c014-7558-4bab-aa35-37d135fc8630, NULL_00000000-0000-0000-0000-000000000000);

IF
FlagSet((FLAG)PLA_KarlachRecruitmentTollhouse_Quest_AcceptedSearch_3b379a18-9b46-4f66-9baf-0e7ce938c1d0, _, _DialogID)
AND
DB_DialogPlayers(_DialogID, _Player, 1)
AND
NOT DB_Avatars((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c) // Don't start if Karlach is an avatar
THEN
QuestUpdate((CHARACTER)_Player, "PLA_PaladinsOfTyr", "MetAnders_AgreedHelp");

IF
FlagSet((FLAG)PLA_KarlachRecruitmentTollhouse_Quest_AcceptedSearch_3b379a18-9b46-4f66-9baf-0e7ce938c1d0, _, _DialogID)
AND
DB_DialogPlayers(_DialogID, _Player, 1)
AND
DB_Avatars((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c) // Don't start if Karlach is an avatar
THEN
PROC_ORI_Karlach_UpdateJournal("DealWithPaladins");

// Meeting Karlach
IF
DialogStarted(_Dialog, _DialogID)
AND
DB_PLA_KarlachRecruitment_RecruitmentDialogues(_Dialog)
AND
DB_DialogPlayers(_DialogID, _Player, 1)
AND
DB_QuestIsAccepted("PLA_PaladinsOfTyr")
THEN
QuestUpdate((CHARACTER)_Player, "PLA_PaladinsOfTyr", "MetKarlach");

//Recruiting Karlach
PROC
PROC_CheckFirstTimeRecruited((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
DB_QuestIsAccepted("PLA_PaladinsOfTyr")
AND
QRY_OnlyOnce("PLA_PaladinsOfTyr_KarlachRecruited")
THEN
QuestUpdate("PLA_PaladinsOfTyr", "KarlachRecruited");

PROC
PROC_CheckFirstTimeRecruited((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
NOT DB_QuestIsAccepted("PLA_PaladinsOfTyr")
AND
QRY_OnlyOnce("PLA_PaladinsOfTyr_KarlachRecruited")
THEN
QuestUpdate("PLA_PaladinsOfTyr", "KarlachRecruited_AndersNotMet");


IF
DB_Dead((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
NOT DB_PartOfTheTeam((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
DB_GlobalFlag((FLAG)PLA_KarlachRecruitmentTollhouse_Quest_AcceptedSearch_3b379a18-9b46-4f66-9baf-0e7ce938c1d0)
THEN
QuestUpdate("PLA_PaladinsOfTyr", "TakeHead");

PROC
PROC_GLO_HeadRemoval_OnHeadPicked((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _)
THEN
QuestUpdate("PLA_PaladinsOfTyr", "DeliverHead");

// Closing entries
IF
DB_PermaDefeated(S_PLA_Refugee_001_a43d1d6c-d397-4d2d-adb4-ead3b10cb189)
AND
NOT QRY_QuestUpdateIsUnlockedForAny("PLA_PaladinsOfTyr", "KillThePaladins")
THEN
QuestUpdate("PLA_PaladinsOfTyr", "AndersPermadefeated"); // Only if players haven't unlocked the objective to confront the paladins with Karlach or to kill him.

IF
DB_GlobalFlag((FLAG)PLA_KarlachRecruitmentTollhouse_Quest_AcceptedSearch_3b379a18-9b46-4f66-9baf-0e7ce938c1d0)
AND
DB_GLO_HeadRemoval_HeadInaccessible((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
QuestUpdate("PLA_PaladinsOfTyr", "HeadNotAvailable");

IF
FlagSet((FLAG)PLA_KarlachRecruitmentTollhouse_State_PaladinsPermaDefeated_d78dbc95-8d8e-4b3a-b618-ccfb48c31375, _, _)
AND
QRY_QuestUpdateIsUnlockedForAny("PLA_PaladinsOfTyr", "KillThePaladins")
THEN
QuestUpdate("PLA_PaladinsOfTyr", "PaladinsPermadefeated");

IF
FlagSet((FLAG)PLA_KarlachRecruitmentTollhouse_Event_GiveHead_d4e8a331-084c-42b7-a2b3-99e587f8679e, S_PLA_Refugee_001_a43d1d6c-d397-4d2d-adb4-ead3b10cb189, _DialogID)
AND
DB_Players(_Player)
THEN
QuestUpdate("PLA_PaladinsOfTyr", "DeliveredHead");

IF
FlagSet((FLAG)PLA_KarlachRecruitmentTollhouse_Event_GiveHead_d4e8a331-084c-42b7-a2b3-99e587f8679e, S_PLA_Refugee_001_a43d1d6c-d397-4d2d-adb4-ead3b10cb189, _DialogID)
AND
DB_Players(_Player)
THEN
QuestUpdate("PLA_PaladinsOfTyr", "DeliveredHead");

IF
DB_PermaDefeated(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
DB_GlobalFlag((FLAG)PLA_PaladinsOfTyr_Event_KarlachLeavesHostile_f178f9f1-cf9e-2956-20aa-2a5857f1c285)
THEN
QuestUpdate("PLA_PaladinsOfTyr", "KarlachPermaDefeated");

IF
EntityEvent(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Reason)
AND
DB_PLA_KarlachRecruitmentTollhouse_LeaveReasons(_Reason)
THEN
QuestUpdate("PLA_PaladinsOfTyr", "KarlachPermanentlyLeaves");

IF
FlagSet((FLAG)Act2_PointOfNoReturnReached_a3155f30-b8f3-4db5-ac21-d3036f4426e3, _, _)
THEN
QuestUpdate("PLA_PaladinsOfTyr", "PointOfNoReturn");
GoalCompleted;
//END_REGION Journal - Paladins of Tyr
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
