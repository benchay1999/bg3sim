Version 1
SubGoalCombiner SGC_AND
INITSECTION
SetOnStage(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3,0);

DB_UND_Bulette_BurrowHelper((CHARACTER)S_UND_Bulette_BurrowHelper_f85336f2-4fe5-46dc-acdf-7f302a82bae4);
SetAmbushing((CHARACTER)S_UND_Bulette_BurrowHelper_f85336f2-4fe5-46dc-acdf-7f302a82bae4, 1); // To hide the minimap icon and the outline

DB_UND_Bulette_SpawnZones((FLAG)UND_Bulette_State_Arena001_InProgress_21d8d894-2077-4a54-a7cd-ef28e269a988, (CHARACTER)S_UND_Bulette_SpawnHelper_001_a0cd5f18-9dcf-4bf5-a1aa-e9656f9f6e79, (TRIGGER)S_UND_Bulette_SpawnTrigger_001_b8c45b76-79f5-4eab-b097-5a3843b4d014, (TRIGGER)S_UND_Bulette_JumpTo_001_05ecd76d-b6aa-4b82-8b8f-93cdf1e42917);
DB_UND_Bulette_SpawnZones((FLAG)UND_Bulette_State_Arena002_InProgress_e2fceb55-6268-4cef-9883-ae63de160b6f, (CHARACTER)S_UND_Bulette_SpawnHelper_002_fc43f2c0-872a-416f-8b9f-be45d43275d7, (TRIGGER)S_UND_Bulette_SpawnTrigger_002_3ae721bb-9921-4031-bda2-dff932508c1f, (TRIGGER)S_UND_Bulette_JumpTo_002_3934c5d0-fe93-45fe-9f08-bbf23c211fa7);
DB_UND_Bulette_SpawnZones((FLAG)UND_Bulette_State_Arena003_InProgress_01560cbb-a144-4ba8-bffd-479278beaf33, (CHARACTER)S_UND_Bulette_SpawnHelper_003_dc98138b-ce7e-4513-b59e-d9f45f825c2c, (TRIGGER)S_UND_Bulette_SpawnTrigger_003_279db630-40b9-4462-81f1-b143dae4691e, (TRIGGER)S_UND_Bulette_JumpTo_003_9ddecc60-c145-4636-99aa-de16a71d6cb8);

DB_AddCharactersInTriggerToDialog((DIALOGRESOURCE)UND_Bulette_Intro_e3b04626-7012-699a-4ce3-d4ac1c73d18f, (TRIGGER)S_UND_Bulette_SpawnTrigger_001_Intro_ba47e230-6704-41c4-aaeb-f411a76f37f2, 1, 0);
DB_AddCharactersInTriggerToDialog((DIALOGRESOURCE)UND_Bulette_Intro_e3b04626-7012-699a-4ce3-d4ac1c73d18f, (TRIGGER)S_UND_Bulette_SpawnTrigger_002_Intro_e54a2f78-34ab-405f-80d5-734dd98f67d3, 1, 0);
DB_AddCharactersInTriggerToDialog((DIALOGRESOURCE)UND_Bulette_Intro_e3b04626-7012-699a-4ce3-d4ac1c73d18f, (TRIGGER)S_UND_Bulette_SpawnTrigger_003_Intro_728afd22-4b89-4266-bcbf-8d3bd69bfb71, 1, 0);

//TODO: Change with bigger triggers
DB_AddCharactersInTriggerToDialog((DIALOGRESOURCE)UND_Bulette_Intro_e3b04626-7012-699a-4ce3-d4ac1c73d18f, (TRIGGER)S_UND_Bulette_SpawnTrigger_001_b8c45b76-79f5-4eab-b097-5a3843b4d014, 1, 0);
DB_AddCharactersInTriggerToDialog((DIALOGRESOURCE)UND_Bulette_Intro_e3b04626-7012-699a-4ce3-d4ac1c73d18f, (TRIGGER)S_UND_Bulette_SpawnTrigger_002_3ae721bb-9921-4031-bda2-dff932508c1f, 1, 0);
DB_AddCharactersInTriggerToDialog((DIALOGRESOURCE)UND_Bulette_Intro_e3b04626-7012-699a-4ce3-d4ac1c73d18f, (TRIGGER)S_UND_Bulette_SpawnTrigger_003_279db630-40b9-4462-81f1-b143dae4691e, 1, 0);
PROC_TriggerRegisterForPlayers((TRIGGER)S_UND_Bulette_SpawnTrigger_001_Intro_ba47e230-6704-41c4-aaeb-f411a76f37f2);
PROC_TriggerRegisterForPlayers((TRIGGER)S_UND_Bulette_SpawnTrigger_002_Intro_e54a2f78-34ab-405f-80d5-734dd98f67d3);
PROC_TriggerRegisterForPlayers((TRIGGER)S_UND_Bulette_SpawnTrigger_003_Intro_728afd22-4b89-4266-bcbf-8d3bd69bfb71);

SetOnStage(S_UND_Bulette_BurrowHelper_f85336f2-4fe5-46dc-acdf-7f302a82bae4,0);
SetOnStage(S_UND_Bulette_SpawnHelper_004_8e888d77-cbe9-4e5d-96d8-0f180e7fa4cf, 0);
SetOnStage(S_UND_Bulette_SpawnHelper_005_969c081c-be32-4352-951d-a3fade24c806, 0);

DB_UND_Bulette_CombatArenas((FLAG)UND_Bulette_State_Arena001_InProgress_21d8d894-2077-4a54-a7cd-ef28e269a988, (TRIGGER)S_UND_Bulette_CombatArena_001_574e9acd-f174-4cb1-9714-5b8f25f257c5);
DB_UND_Bulette_CombatArenas((FLAG)UND_Bulette_State_Arena002_InProgress_e2fceb55-6268-4cef-9883-ae63de160b6f, (TRIGGER)S_UND_Bulette_CombatArena_002_abe46858-e46a-43c6-bb32-4c93ac3cd40b);
DB_UND_Bulette_CombatArenas((FLAG)UND_Bulette_State_Arena003_InProgress_01560cbb-a144-4ba8-bffd-479278beaf33, (TRIGGER)S_UND_Bulette_CombatArena_003_457b0463-bafa-4eed-b608-f82280debaa0);
DB_UND_Bulette_CombatArenas((FLAG)UND_Bulette_State_Arena004_InProgress_1efb30da-229c-4a92-bb6b-2e028f8751fa, (TRIGGER)S_UND_Bulette_CombatArena_004_1cc4e212-9201-4008-b16b-88764e11dd7e);
DB_UND_Bulette_CombatArenas((FLAG)UND_Bulette_State_Arena005_InProgress_8512e388-30c1-4c48-9cff-eb669734bdc9, (TRIGGER)S_UND_Bulette_CombatArena_005_3b4bb4bb-02c0-4b5b-871b-48bdec5c02df);

//REGION Burrow paths
SetFlag(UND_Bulette_State_CanBurrow_e253e789-57b4-452e-8b59-2f92b4011f58, NULL_00000000-0000-0000-0000-000000000000);
DB_UND_Bulette_BurrowPathsTriggers((TRIGGER)S_UND_Bulette_BurrowTrigger_001_End_23912b6d-59ea-486c-8ec7-832fdfc5a38a);
DB_UND_Bulette_BurrowPathsTriggers((TRIGGER)S_UND_Bulette_BurrowTrigger_001_Start_9f825997-a607-4598-a85f-2a8c854c37b6);
DB_UND_Bulette_BurrowPathsTriggers((TRIGGER)S_UND_Bulette_BurrowTrigger_002_End_ec43c361-70e5-46e9-99f6-75037e0d60ce);
DB_UND_Bulette_BurrowPathsTriggers((TRIGGER)S_UND_Bulette_BurrowTrigger_002_Start_c5ca0b54-b66f-420b-bc7b-fb29eb063dec);
DB_UND_Bulette_BurrowPathsTriggers((TRIGGER)S_UND_Bulette_BurrowTrigger_003_Start_67ed5218-236e-4034-ad73-9443bab1e77a);
DB_UND_Bulette_BurrowPathsTriggers((TRIGGER)S_UND_Bulette_BurrowTrigger_003_End_c523c9a3-5d62-4405-bbd0-9dd3931322a6);
//END_REGION

//REGION Bulette Holes PAD
DB_UND_Bulette_HolesPADTrigger((TRIGGER)S_UND_Bulette_HoleAreaTrigger_000_81d8c56c-20cc-4c1f-9069-dfc9e1da0895);
DB_UND_Bulette_HolesPADTrigger((TRIGGER)S_UND_Bulette_HoleAreaTrigger_001_feca256b-ef27-4923-8c1b-bea208d64163);
DB_UND_Bulette_HolesPADTrigger((TRIGGER)S_UND_Bulette_HoleAreaTrigger_003_faa9dfc9-26c5-4845-ab5a-be8ed60909f6);
DB_UND_Bulette_HolesPADTrigger((TRIGGER)S_UND_Bulette_HoleAreaTrigger_004_6f03364e-221c-4890-8ae5-76b11cd74ba1);
DB_UND_Bulette_HolesPADTrigger((TRIGGER)S_UND_Bulette_HoleAreaTrigger_005_7914bd56-7931-4d87-888a-a1080c815136);
DB_UND_Bulette_HolesPADTrigger((TRIGGER)S_UND_Bulette_HoleAreaTrigger_006_e4f43779-ec06-4c5e-a680-403861c690f4);
DB_UND_Bulette_HolesPADTrigger((TRIGGER)S_UND_Bulette_HoleAreaTrigger_007_241f0af4-d3aa-4cb9-97b6-4ffcc9e1dc0d);
DB_UND_Bulette_HolesPADTrigger((TRIGGER)S_UND_Bulette_HoleAreaTrigger_008_b7efda4a-5bf6-49bf-ada0-01f666178044);
DB_UND_Bulette_HolesPADTrigger((TRIGGER)S_UND_Bulette_HoleAreaTrigger_009_16d0b552-79e6-4a12-a5c9-0ac7ee674f68);
//END_REGION Bulette Holes PAD

//REGION Bulette save triggers
DB_UND_Bulette_SaveTriggers((FLAG)UND_Bulette_State_Arena001_InProgress_21d8d894-2077-4a54-a7cd-ef28e269a988, (TRIGGER)S_UND_Bulette_Arena_001_SaveTrigger_001_17a42a4a-d359-43b4-b3b4-6e366fd4500b);
DB_UND_Bulette_SaveTriggers((FLAG)UND_Bulette_State_Arena002_InProgress_e2fceb55-6268-4cef-9883-ae63de160b6f, (TRIGGER)S_UND_Bulette_Arena_002_SaveTrigger_001_6ef64045-20c8-4004-9e4a-1001a7dd76d3);
DB_UND_Bulette_SaveTriggers((FLAG)UND_Bulette_State_Arena003_InProgress_01560cbb-a144-4ba8-bffd-479278beaf33, (TRIGGER)S_UND_Bulette_Arena_003_SaveTrigger_001_decc40fe-5200-48ad-b24c-72b8f1d5fd31);
//END_REGION Bulette save triggers

AddPreferredAiTargetTag(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3,MONSTROSITY_081a2ef4-dc1b-4d5b-bae3-8db81bef1d06);
AddPreferredAiTargetTag(S_UND_Minotaur_001_19edd11d-fde0-4aa9-95f6-fb2e3f2bc570,BULETTE_06c1ef36-dac6-4224-8320-d4d38f84897f);
AddPreferredAiTargetTag(S_UND_Minotaur_002_9e58f686-0534-4234-8805-180118533810,BULETTE_06c1ef36-dac6-4224-8320-d4d38f84897f);

// Block for now the intro
PROC_UND_Bulette_BlockIntroCinematic();
KBSECTION
PROC
PROC_StateSet_PermaDefeated(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3)
AND
DB_UND_Bulette_SpawnZones(_Flag, _, _, _)
THEN
PROC_UND_Bulette_RemoveEncounter(_Flag);
PROC_UND_Bulette_RemoveSaveTriggers(_Flag);

IF
DB_UND_Bulette_SpawnZones(_, _SpawnPoint, _Region, _JumpTo)
AND
_Region != NULL_00000000-0000-0000-0000-000000000000
THEN
PROC_TriggerRegisterForPlayers(_Region);
TriggerRegisterForCharacter(_JumpTo, S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3);

IF
DB_UND_Bulette_SpawnZones((FLAG)_Flag, (CHARACTER)_SpawnPoint, _SpawnTrigger, _)
THEN
SetOnStage(_SpawnPoint,0);
PROC_SetInvulnerable(_SpawnPoint,1);
PROC_CharacterDisableAllCrimes(_SpawnPoint,1);
TeleportTo(_SpawnPoint,_SpawnTrigger);
DB_UND_Bulette_EncounterFlags(_Flag);

// Take into account EVERYONE in the party.
IF
DB_UND_Bulette_CombatArenas(_, _Arena)
THEN
PROC_TriggerRegisterForParty(_Arena);

PROC
PROC_UND_Bulette_RemoveEncounter((FLAG)_Flag)
AND
DB_UND_Bulette_SpawnZones(_Flag, _SpawnPoint, _Region, _JumpTo)
THEN
NOT DB_UND_Bulette_SpawnZones(_Flag, _SpawnPoint, _Region, _JumpTo);

PROC
PROC_UND_Bulette_RemoveEncounter((FLAG)_Flag)
AND
NOT DB_UND_Bulette_SpawnZones(_, _, _, _)
THEN
SetFlag(UND_Bulette_State_RunOutOfArenas_2b335f2b-ab46-4648-91e6-a72d07e68f23, NULL_00000000-0000-0000-0000-000000000000);

//REGION Burrow Events
IF
DB_UND_Bulette_BurrowHelper((CHARACTER)_Helper)
THEN
PROC_SetInvulnerable(_Helper,1);
SetOnStage(_Helper,0);
PROC_SetCanFight(_Helper, 0);
SetCanJoinCombat(_Helper, 0);

IF
WentOnStage((CHARACTER)_Helper,1)
AND
DB_UND_Bulette_BurrowHelper(_Helper)
AND
NOT DB_GlobalFlag(UND_Bulette_State_Burrowing_b161423b-a786-457d-a5ef-1bd06410dfe1)
THEN
SetFlag(UND_Bulette_State_ApproachArena_3ea89f6f-2ad8-46b7-8330-b4533fd8a983, NULL_00000000-0000-0000-0000-000000000000);
ClearFlag(UND_Bulette_State_LeaveArena_42194aaa-16f8-4953-a167-1d4dd2f649d6, NULL_00000000-0000-0000-0000-000000000000);

IF
WentOnStage((CHARACTER)_Helper,0)
AND
DB_UND_Bulette_BurrowHelper(_Helper)
THEN
RemoveStatus(_Helper,"UND_BULETTE_BURROWTRAIL");
//END_REGION

//REGION Spawn Events
//Trigger Enter Start
IF
EnteredTrigger(_Player,_Region)
AND
DB_UND_Bulette_SpawnZones(_, _SpawnPoint, _Region, _)
THEN
PROC_UND_Bulette_SpawnBullete(_SpawnPoint, _Player);

IF
EnteredCombat((CHARACTER)_NPC,_)
AND
DB_UND_Bulette_NPCCombatJoin(_SpawnPoint,_NPC)
AND
DB_UND_Bulette_SpawnZones(_, _SpawnPoint, _Region, _)
THEN
PROC_UND_Bulette_SpawnBullete(_SpawnPoint,_NPC);

//END_REGION

//REGION Spawning
PROC
PROC_UND_Bulette_SpawnBullete((GUIDSTRING)_SpawnPoint, (CHARACTER)_Character)
AND
QRY_UND_Bulette_CanAppear()
THEN
PROC_UND_Bulette_SpawnBullete_Internal((GUIDSTRING)_SpawnPoint, (CHARACTER)_Character);

QRY
QRY_UND_Bulette_CanAppear()
AND
NOT DB_Defeated(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3)
AND
NOT DB_GlobalFlag(UND_Bulette_State_HasBeenPermaDefeated_a7a88648-acfc-443f-80ea-f4195217f7be)
AND
NOT DB_GlobalFlag(UND_Bulette_State_Burrowing_b161423b-a786-457d-a5ef-1bd06410dfe1)
AND
IsOnStage(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, 0)
AND
IsOnStage(S_UND_Bulette_BurrowHelper_f85336f2-4fe5-46dc-acdf-7f302a82bae4, 0)
AND
NOT DB_UND_Bulette_IsActive(_)
THEN
DB_NOOP(1);


//First set the flag for the helper behaviour.
PROC
PROC_UND_Bulette_SpawnBullete_Internal((GUIDSTRING)_SpawnPoint, (CHARACTER)_Character)
AND
DB_UND_Bulette_SpawnZones(_Flag, (CHARACTER)_SpawnPoint, _, _)
THEN
NOT DB_UND_Bulette_LeaveOnTurn(1);
PROC_UND_Bulette_SetEncounterFlag(_Flag);
PROC_UND_Bulette_RemoveSaveTriggers(_Flag);

PROC
PROC_UND_Bulette_SetEncounterFlag((FLAG)_Flag)
AND
DB_UND_Bulette_EncounterFlags(_FlagToClear)
AND
DB_GlobalFlag(_FlagToClear)
THEN
ClearFlag(_FlagToClear, NULL_00000000-0000-0000-0000-000000000000);

PROC
PROC_UND_Bulette_SetEncounterFlag((FLAG)_Flag)
THEN
SetFlag(_Flag, NULL_00000000-0000-0000-0000-000000000000);

PROC
PROC_UND_Bulette_SpawnBullete_Internal((CHARACTER)_SpawnPoint, (CHARACTER)_Character)
AND
DB_OnlyOnce("UND_Bulette_IntroStarted")
THEN
PROC_UND_Bulette_SpawnBullete_NoIntro(_SpawnPoint, _Character);

PROC
PROC_UND_Bulette_SpawnBullete_Internal((CHARACTER)_SpawnPoint, (CHARACTER)_Character)
AND
QRY_OnlyOnce("UND_Bulette_IntroStarted")
THEN
PROC_UND_Bulette_SpawnBullete_Intro(_SpawnPoint, _Character);

PROC
PROC_UND_Bulette_BlockIntroCinematic()
AND
QRY_OnlyOnce("UND_Bulette_IntroStarted")
THEN
DB_NOOP(1);

//END_REGION

//REGION Intro - No Cinematic
//Hidden status for spawn
PROC
PROC_UND_Bulette_SpawnBullete_NoIntro((CHARACTER)_SpawnPoint, (CHARACTER)_Character)
AND
DB_UND_Bulette_SpawnZones(_Flag, _SpawnPoint,_Region, _)
AND
NOT DB_UND_Bulette_IsActive(_)
AND
DB_UND_Bulette_BurrowHelper(_Helper)
THEN
DB_UND_Bulette_IsActive(_Flag);
SetOnStage(_SpawnPoint,1);
SetDetached(_SpawnPoint, 1);
SetVisible(_SpawnPoint, 0);
SetOnStage(_Helper,1);
SetDetached(_Helper, 1);
PROC_TriggerUnregisterForPlayers(_Region);
PROC_UND_Bulette_SpawnBullete_NoIntro_Internal(_SpawnPoint, _Character);

//On Spawn, check to join combat with Players
PROC
PROC_UND_Bulette_SpawnBullete_NoIntro_Internal((CHARACTER)_SpawnPoint, (CHARACTER)_Character)
AND
IsInCombat(_Character, 1)
THEN
PROC_EnterCombat(_SpawnPoint, _Character);

// Apply status that will work as a timer for the bulette to spawn
IF
EntityEvent(_, "UND_Bulette_TriggerSpawn")
AND
DB_UND_Bulette_IsActive(_ActiveEncounter)
AND
DB_UND_Bulette_SpawnZones(_ActiveEncounter, _SpawnPoint, _, _)
THEN
ApplyStatus(_SpawnPoint,"UND_BULETTE_SPAWNTIMER",0.5);

// If not, FTB to allow them react
// Check if it's inside the arena trigger, is a player AND is close to the bulette
IF
EntityEvent(_, "UND_Bulette_TriggerSpawn")
AND
DB_UND_Bulette_IsActive(_ActiveEncounter)
AND
DB_UND_Bulette_CombatArenas(_ActiveEncounter, _ArenaTrigger)
AND
DB_InRegion(_Player, _ArenaTrigger)
AND
DB_Players(_Player)
AND
GetDistanceTo(_Player, S_UND_Bulette_BurrowHelper_f85336f2-4fe5-46dc-acdf-7f302a82bae4, _Dist)
AND
_Dist < 15.0
AND
IsInCombat(_Player, 0)
AND
IsInForceTurnBasedMode(_Player, 0)
THEN
ForceTurnBasedMode(_Player, 1);

//On Spawn, check to join combat with predefined NPCs
PROC
PROC_UND_Bulette_SpawnBullete_NoIntro_Internal((CHARACTER)_SpawnPoint, (CHARACTER)_Character)
AND
DB_UND_Bulette_NPCCombatJoin(_SpawnPoint, _NPC)
AND
GetCombatGroupID(_NPC, _CombatID)
THEN
SetCombatGroupID(_SpawnPoint, _CombatID);
SetCombatGroupID(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, _CombatID);
PROC_EnterCombat(_SpawnPoint, _NPC);

IF
CastedSpell(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, "Projectile_Jump_Bulette_Spawn", _, _, _)
AND
DB_UND_Bulette_SecondReaction(1)
AND
DB_Players(_Player)
AND
NOT DB_OnlyOnce("UND_Bulette_PlayerReaction")
AND
QRY_SpeakerIsInDialogRange(_Player, S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3)
AND
QRY_StartDialog(UND_Bulette_PAD_Reaction_0cf415e1-43eb-6cc3-a0e8-302a466b26db, _Player)
AND
QRY_OnlyOnce("UND_Bulette_PlayerReaction")
THEN
DB_NOOP(1);

// After casting for the first time the jump, on the next ones try to trigger the PAD
IF
CastedSpell(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, "Projectile_Jump_Bulette_Spawn", _, _, _)
THEN
DB_UND_Bulette_SecondReaction(1);
DB_UND_Bulette_BlockBurrowReaction(1);

IF
AutomatedDialogRequestFailed(UND_Bulette_PAD_Reaction_0cf415e1-43eb-6cc3-a0e8-302a466b26db, _)
AND
QRY_OnlyOnce_Reset("UND_Bulette_PlayerReaction")
THEN
DB_NOOP(1);

IF
StatusRemoved((CHARACTER)_SpawnPoint,"UND_BULETTE_SPAWNTIMER",_,_)
AND
DB_UND_Bulette_SpawnZones(_Flag, _SpawnPoint, _, _JumpTo)
THEN
ClearFlag(UND_Bulette_State_ApproachArena_3ea89f6f-2ad8-46b7-8330-b4533fd8a983, NULL_00000000-0000-0000-0000-000000000000);
PROC_UND_Bulette_UpdateSpawnAndJumpTo(_JumpTo);
PROC_UND_Bulette_SpawnBulette_PlaySpawn(_SpawnPoint);
PROC_UND_Bulette_RemoveEncounter(_Flag);
PROC_UND_Bulette_EndBurrowTrail(0);

PROC
PROC_UND_Bulette_UpdateSpawnAndJumpTo((TRIGGER)_JumpTo)
AND
DB_UND_Bulette_SpawnAndJumpToTrigger(_PrevJumpTo)
AND
_PrevJumpTo != _JumpTo
THEN
NOT DB_UND_Bulette_SpawnAndJumpToTrigger(_PrevJumpTo);

PROC
PROC_UND_Bulette_UpdateSpawnAndJumpTo((TRIGGER)_JumpTo)
THEN
DB_UND_Bulette_SpawnAndJumpToTrigger(_JumpTo);

IF
TurnStarted((CHARACTER)_SpawnPoint)
AND
DB_UND_Bulette_SpawnZones(_Flag, _SpawnPoint, _Region, _JumpTo)
THEN
SetEntityEventReal(_SpawnPoint,"GLO_CombatWait",3.0);
RealtimeObjectTimerLaunch(_SpawnPoint,"UND_Bulette_TurnEndDelay",3000);

IF
EntityEvent(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3,"UND_Bulette_Spawn")
AND
DB_UND_Bulette_BurrowHelper(_BurrowHelper)
THEN
TeleportTo(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, _BurrowHelper, "UND_Bulette_TeleportedToHelper", 0, 0, 1);

IF
EntityEvent(_, "UND_Bulette_TeleportedToHelper")
AND
DB_UND_Bulette_BurrowHelper(_BurrowHelper)
THEN
CreateExplosion(_BurrowHelper, "Projectile_UND_BuletteSpawnExplode", -1, S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3);
SetOnStage(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, 1);

//Cast jump spell immediately
IF
WentOnStage(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3,1)
AND
DB_UND_Bulette_SpawnAndJumpToTrigger(_Trigger)
AND
_Trigger != NULL_00000000-0000-0000-0000-000000000000
AND
GetPosition(_Trigger,_X,_Y,_Z)
AND
FindValidPosition(_X, _Y, _Z, 6.0, S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, 0, _NewX, _NewY, _NewZ)
THEN
DB_UND_Bulette_UsedJumpSpell(1);
UseSpellAtPosition(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, "Projectile_Jump_Bulette_Spawn", _NewX, _NewY, _NewZ);

IF
WentOnStage(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3,1)
AND
NOT DB_UND_Bulette_UsedJumpSpell(1)
AND
DB_UND_Bulette_SpawnAndJumpToTrigger(_Trigger)
AND
_Trigger != NULL_00000000-0000-0000-0000-000000000000
AND
GetPosition(_Trigger,_X,_Y,_Z)
THEN
DB_UND_Bulette_UsedJumpSpell(1);
UseSpellAtPosition(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, "Projectile_Jump_Bulette_Spawn", _X,_Y,_Z);

IF
UsingSpell(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, "Projectile_Jump_Bulette_Spawn", _, _, _)
THEN
NOT DB_UND_Bulette_UsedJumpSpell(1);
SetVisible(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3 ,1);

// Case if the bulette fails casting the jump spell
IF
CastSpellFailed(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, "Projectile_Jump_Bulette_Spawn", _, _, _)
THEN
NOT DB_UND_Bulette_UsedJumpSpell(1);
SetVisible(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, 1);

IF
EnteredCombat(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, _)
THEN
ApplyStatus(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3,"UND_BULETTE_DESPAWNTIMER",12.0,1);

IF
ObjectTimerFinished((CHARACTER)_SpawnPoint,"UND_Bulette_TurnEndDelay")
THEN
EndTurn(_SpawnPoint);

PROC
PROC_UND_Bulette_SpawnBulette_PlaySpawn((CHARACTER)_SpawnPoint)
AND
DB_UND_Bulette_SpawnAndJumpToTrigger(_Trigger)
AND
_Trigger != NULL_00000000-0000-0000-0000-000000000000
THEN
LookFromTrigger(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3,_Trigger,1);

PROC
PROC_UND_Bulette_SpawnBulette_PlaySpawn((CHARACTER)_SpawnPoint)
THEN
SetVisible(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3,0);
TeleportTo(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3,_SpawnPoint,"UND_Bulette_Spawn");
PROC_StopLoopEffect(_SpawnPoint, "BuletteSpawner");
SetOnStage(_SpawnPoint,0);
//END_REGION

//REGION Intro - Cinematic
//Plays intro when the bulette spawns for the first time
PROC
PROC_UND_Bulette_SpawnBullete_Intro((CHARACTER)_SpawnPoint, (CHARACTER)_Character)
AND
DB_UND_Bulette_SpawnZones(_Flag, _SpawnPoint, _, _)
THEN
PROC_UND_Bulette_RemoveEncounter(_Flag);
DB_UND_Bulette_IsActive(_Flag);
DB_UND_Bulette_PlayerNearBulette(_Character);
SetOnStage(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, 1);
SetVisible(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, 0);
TeleportTo(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, _SpawnPoint, "UND_Bulette_StartIntro", 0, 0, 1);

IF
EntityEvent(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, "UND_Bulette_StartIntro")
THEN
SetVisible(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, 1);

IF
EntityEvent(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, "UND_Bulette_StartIntro")
AND
DB_UND_Bulette_PlayerNearBulette(_Character)
AND
NOT QRY_StartDialogCustom((DIALOGRESOURCE)UND_Bulette_Intro_e3b04626-7012-699a-4ce3-d4ac1c73d18f, S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, _Character, 0, 0, 1, 0)
THEN
PROC_UND_Bulette_EndIntro();

IF
EntityEvent(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, "UND_Bulette_StartIntro")
AND
DB_UND_Bulette_PlayerNearBulette(_Character)
THEN
NOT DB_UND_Bulette_PlayerNearBulette(_Character);

IF
DialogEnded(UND_Bulette_Intro_e3b04626-7012-699a-4ce3-d4ac1c73d18f, _)
THEN
PROC_UND_Bulette_EndIntro();

PROC
PROC_UND_Bulette_EndIntro()
THEN
ApplyStatus(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3,"UND_BULETTE_DESPAWNTIMER",12.0,1);
PROC_TriggerUnregisterForPlayers((TRIGGER)S_UND_Bulette_SpawnTrigger_001_Intro_ba47e230-6704-41c4-aaeb-f411a76f37f2);
PROC_TriggerUnregisterForPlayers((TRIGGER)S_UND_Bulette_SpawnTrigger_002_Intro_e54a2f78-34ab-405f-80d5-734dd98f67d3);
PROC_TriggerUnregisterForPlayers((TRIGGER)S_UND_Bulette_SpawnTrigger_003_Intro_728afd22-4b89-4266-bcbf-8d3bd69bfb71);
//END_REGION

//REGION Burrow paths
IF
DB_UND_Bulette_BurrowPathsTriggers(_Trigger)
THEN
PROC_TriggerRegisterForPlayers(_Trigger);

PROC
PROC_StateSet_Defeated(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3)
THEN
ClearFlag(UND_Bulette_State_CanBurrow_e253e789-57b4-452e-8b59-2f92b4011f58, NULL_00000000-0000-0000-0000-000000000000);

PROC
PROC_StateCleared_Defeated(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3)
THEN
SetFlag(UND_Bulette_State_CanBurrow_e253e789-57b4-452e-8b59-2f92b4011f58, NULL_00000000-0000-0000-0000-000000000000);

IF
TimerFinished("UND_Bulette_CanBurrowAgainTimer")
AND
DB_UND_Bulette_BurrowHelper(_Helper)
THEN
SetEntityEvent(_Helper, "UND_Bulette_CanBurrowAgain");

// Player sees for the first time the bulette burrowing and hasn't encountered it yet: nature check.
IF
DB_Sees(_Player, S_UND_Bulette_BurrowHelper_f85336f2-4fe5-46dc-acdf-7f302a82bae4)
AND
DB_Players(_Player)
AND
DB_GlobalFlag(UND_Bulette_State_Burrowing_b161423b-a786-457d-a5ef-1bd06410dfe1)
AND
NOT DB_UND_Bulette_IsActive(_)
AND
NOT DB_UND_Bulette_BlockBurrowReaction(1)
AND
QRY_OnlyOnce("UND_Bulette_NatureCheck")
AND
NOT QRY_StartDialog((DIALOGRESOURCE)UND_Bulette_PAD_SeesBuletteBurrowing_87582ed9-f1ea-4cb1-85f9-d454f6fd54b3, _Player)
AND
QRY_OnlyOnce_Reset("UND_Bulette_NatureCheck")
THEN
DB_NOOP(1);

IF
AutomatedDialogRequestFailed(UND_Bulette_PAD_SeesBuletteBurrowing_87582ed9-f1ea-4cb1-85f9-d454f6fd54b3, _)
AND
QRY_OnlyOnce_Reset("UND_Bulette_NatureCheck")
THEN
DB_NOOP(1);

IF
AutomatedDialogEnded(UND_Bulette_PAD_SeesBuletteBurrowing_87582ed9-f1ea-4cb1-85f9-d454f6fd54b3, _ID)
AND
DB_OnlyOnce("UND_Bulette_NatureCheck")
AND
DB_DialogPlayers(_ID, _Player, 1)
THEN
PROC_KnowledgeCheck_MP((CHARACTER)_Player, "UND_BuletteNatureCheck", S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, "Nature", (DIFFICULTYCLASS)DC_Legacy_15_bddbb9b8-a242-4c3e-a2eb-3fd274c0c539, UND_Bulette_PAD_NatureCheckOutcome_47bcba0c-4b6b-e622-2dfd-569afeb93984, UND_Bulette_Knows_Bulette_ea70b015-f5f4-4470-aeda-9882010fdd98);

PROC
PROC_UND_Tremor((CHARACTER)_,S_UND_TremorTrigger_001_6aeb7bf9-8987-4bc0-a787-215a69bf05e6)
THEN
TimerLaunch("UND_Bulette_AddSharFortTriggers", 16000);

IF
TimerFinished("UND_Bulette_AddSharFortTriggers")
THEN
DB_UND_Bulette_BurrowPathsTriggers((TRIGGER)S_UND_Bulette_BurrowTrigger_004_End_0314fee2-7d78-4a2f-b359-a122b348d853);
DB_UND_Bulette_BurrowPathsTriggers((TRIGGER)S_UND_Bulette_BurrowTrigger_004_Start_e0eee0be-bdce-4282-9f61-68542d03863c);
//END_REGION

//REGION Despawning
IF
EntityEvent(_, "UND_Bulette_DespawnBurrowTrail") // Used for when the bulette is burrowing around the underdark (not when approaching an arena)
THEN
PROC_UND_Bulette_EndBurrowTrail(1);

PROC
PROC_UND_Bulette_EndBurrowTrail((INTEGER)_SetCanBurrowTimer)
AND
DB_UND_Bulette_BurrowHelper(_Helper)
THEN
RemoveStatus(_Helper, "UND_BULETTE_BURROWTRAIL");
SetDetached(_Helper, 1);
Freeze(_Helper);
ApplyStatus(_Helper, "SNEAKING", -1.0, 1); // TODO: once we have proper support for hiding map markers remove it.
RealtimeObjectTimerLaunch(_Helper, "UND_Bulette_DespawnBuletteHelper", 5000);

PROC
PROC_UND_Bulette_EndBurrowTrail(1)
AND
DB_UND_Bulette_BurrowHelper(_Helper)
THEN
DB_UND_Bulette_BlockBurrowSpawn(1);

IF
ObjectTimerFinished(_Helper, "UND_Bulette_DespawnBuletteHelper")
THEN
SetDetached((CHARACTER)_Helper, 0);
Unfreeze((CHARACTER)_Helper);
SetOnStage(_Helper, 0);
RemoveStatus(_Helper, "SNEAKING");

IF
ObjectTimerFinished(_Helper, "UND_Bulette_DespawnBuletteHelper")
AND
DB_UND_Bulette_BlockBurrowSpawn(1)
THEN
NOT DB_UND_Bulette_BlockBurrowSpawn(1);
TimerLaunch("UND_Bulette_CanBurrowAgainTimer", 180000); // 3 minutes (we may want to further increase it)

IF
StatusRemoved(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3,"UND_BULETTE_DESPAWNTIMER",_,_)
AND
QRY_OnlyOnce_Reset("UND_Bulette_Despawn")
AND
DB_UND_Bulette_SpawnZones(_, _, _, _)
AND
QRY_OnlyOnce("UND_Bulette_Despawn")
THEN
PROC_UND_Bulette_PrepareDespawn();

IF
LeftTrigger(_Char, _Trigger)
AND
DB_UND_Bulette_CombatArenas(_Flag, _Trigger)
AND
DB_UND_Bulette_IsActive(_Flag)
AND
NOT QRY_TriggerEvents_AnyPartyMemberInTrigger(_Trigger)
AND
HasActiveStatus(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, "UND_BULETTE_DESPAWNTIMER", 1)
THEN
RemoveStatus(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3,"UND_BULETTE_DESPAWNTIMER");

PROC
PROC_UND_Bulette_PrepareDespawn()
THEN
DB_UND_Bulette_LeaveOnTurn(1);

IF
TurnStarted(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3)
AND
DB_UND_Bulette_LeaveOnTurn(1)
THEN
SetEntityEvent(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, "ClearPeaceReturn", 1);
UseSpell(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, "Target_Burrow_Bulette", S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3);

// After First Despawn, Allow combat join events 
IF
CastSpell(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, "Target_Burrow_Bulette", _, _, _)
AND
QRY_OnlyOnce("UND_Bulette_FirstDespawn_OnlyOnce")
THEN
DB_UND_Bulette_SpawnZones((FLAG)UND_Bulette_State_Arena004_InProgress_1efb30da-229c-4a92-bb6b-2e028f8751fa, (CHARACTER)S_UND_Bulette_SpawnHelper_004_8e888d77-cbe9-4e5d-96d8-0f180e7fa4cf,(TRIGGER)S_UND_Bulette_SpawnTrigger_004_b8dd439d-346e-4f39-8658-1e264d1502f0, (TRIGGER)S_UND_Bulette_JumpTo_004_4af219a0-deeb-49b9-869a-86e0b0909e80);
DB_UND_Bulette_SpawnZones((FLAG)UND_Bulette_State_Arena005_InProgress_8512e388-30c1-4c48-9cff-eb669734bdc9, (CHARACTER)S_UND_Bulette_SpawnHelper_005_969c081c-be32-4352-951d-a3fade24c806,(TRIGGER)S_UND_Bulette_SpawnTrigger_005_8ec8421a-22df-479b-92a3-cc647d2be1af, (TRIGGER)S_UND_Bulette_JumpTo_005_e73d50d8-1e98-4ef0-b83e-2b1c89540f30);
DB_UND_Bulette_NPCCombatJoin((CHARACTER)S_UND_Bulette_SpawnHelper_004_8e888d77-cbe9-4e5d-96d8-0f180e7fa4cf,(CHARACTER)S_UND_HookHorror_Hunter_000_70866004-4b9d-4dde-a9c7-9891229c47c7);
DB_UND_Bulette_NPCCombatJoin((CHARACTER)S_UND_Bulette_SpawnHelper_005_969c081c-be32-4352-951d-a3fade24c806,(CHARACTER)S_UND_Minotaur_001_19edd11d-fde0-4aa9-95f6-fb2e3f2bc570);
DB_UND_Bulette_SaveTriggers((FLAG)UND_Bulette_State_Arena004_InProgress_1efb30da-229c-4a92-bb6b-2e028f8751fa, (TRIGGER)S_UND_Bulette_Arena_004_SaveTrigger_001_e80acc07-4811-4184-af7a-780dea4fb825);
DB_UND_Bulette_SaveTriggers((FLAG)UND_Bulette_State_Arena005_InProgress_8512e388-30c1-4c48-9cff-eb669734bdc9, (TRIGGER)S_UND_Bulette_Arena_005_SaveTrigger_001_51c5a5d7-658d-48d7-b016-e5f73ea64b9a);

IF
EnteredCombat(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, _)
THEN
SetFlag(UND_Bulette_State_Retreat_bae43e65-05e0-4aaf-8549-fc8743185b6c, NULL_00000000-0000-0000-0000-000000000000);

IF
CastSpell(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, "Target_Burrow_Bulette", _, _, _)
THEN
ClearFlag(UND_Bulette_State_Retreat_bae43e65-05e0-4aaf-8549-fc8743185b6c, NULL_00000000-0000-0000-0000-000000000000);
LeaveCombat(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3);
SetOnStage(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, 0);

IF
WentOnStage(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3,0)
AND
DB_UND_Bulette_IsActive(_Flag)
THEN
NOT DB_UND_Bulette_IsActive(_Flag);
ClearFlag(_Flag, NULL_00000000-0000-0000-0000-000000000000);

IF
WentOnStage(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3,0)
THEN
PurgeOsirisQueue(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3);
PROC_UND_Bulette_ClearActiveCombatArena();
// Need to deactivate the bulette after the helper leaves, not before

//Clear active combat arena
PROC
PROC_UND_Bulette_ClearActiveCombatArena()
AND
DB_UND_Bulette_EncounterFlags(_Flag)
AND
DB_GlobalFlag(_Flag)
AND
DB_UND_Bulette_CombatArenas(_Flag, _Arena)
THEN
PROC_TriggerUnregisterForParty(_Arena);
//END_REGION

//REGION Debug commands
IF
TextEvent("Bulette_SkipCutscene")
AND
QRY_OnlyOnce("UND_Bulette_IntroStarted")
THEN
DB_UND_Bulette_SpawnZones((FLAG)UND_Bulette_State_Arena004_InProgress_1efb30da-229c-4a92-bb6b-2e028f8751fa, (CHARACTER)S_UND_Bulette_SpawnHelper_004_8e888d77-cbe9-4e5d-96d8-0f180e7fa4cf,(TRIGGER)S_UND_Bulette_SpawnTrigger_004_b8dd439d-346e-4f39-8658-1e264d1502f0, (TRIGGER)S_UND_Bulette_JumpTo_004_4af219a0-deeb-49b9-869a-86e0b0909e80);
DB_UND_Bulette_SpawnZones((FLAG)UND_Bulette_State_Arena005_InProgress_8512e388-30c1-4c48-9cff-eb669734bdc9, (CHARACTER)S_UND_Bulette_SpawnHelper_005_969c081c-be32-4352-951d-a3fade24c806,(TRIGGER)S_UND_Bulette_SpawnTrigger_005_8ec8421a-22df-479b-92a3-cc647d2be1af, (TRIGGER)S_UND_Bulette_JumpTo_005_e73d50d8-1e98-4ef0-b83e-2b1c89540f30);
DB_UND_Bulette_NPCCombatJoin((CHARACTER)S_UND_Bulette_SpawnHelper_004_8e888d77-cbe9-4e5d-96d8-0f180e7fa4cf,(CHARACTER)S_UND_HookHorror_Hunter_000_70866004-4b9d-4dde-a9c7-9891229c47c7);
DB_UND_Bulette_NPCCombatJoin((CHARACTER)S_UND_Bulette_SpawnHelper_005_969c081c-be32-4352-951d-a3fade24c806,(CHARACTER)S_UND_Minotaur_001_19edd11d-fde0-4aa9-95f6-fb2e3f2bc570);


IF
TextEvent("Bulette_InstantSpawn")
THEN
DB_UND_Bulette_InstantSpawn_Debug(1);

IF
StatusApplied(_Char,"UND_BULETTE_SPAWNTIMER",_,_)
AND
DB_UND_Bulette_InstantSpawn_Debug(1)
THEN
RemoveStatus(_Char,"UND_BULETTE_SPAWNTIMER");

IF
TextEvent("Bulette_ResetSpawns")
AND
DB_UND_Bulette_SpawnZones(_Flag, _SpawnPoint,_Region,_JumpTo)
THEN
NOT DB_UND_Bulette_SpawnZones(_Flag, _SpawnPoint,_Region,_JumpTo);
NOT DB_UND_Bulette_SpawnZones((FLAG)UND_Bulette_State_Arena004_InProgress_1efb30da-229c-4a92-bb6b-2e028f8751fa, (CHARACTER)S_UND_Bulette_SpawnHelper_004_8e888d77-cbe9-4e5d-96d8-0f180e7fa4cf,(TRIGGER)S_UND_Bulette_SpawnTrigger_004_b8dd439d-346e-4f39-8658-1e264d1502f0, (TRIGGER)S_UND_Bulette_JumpTo_004_4af219a0-deeb-49b9-869a-86e0b0909e80);
NOT DB_UND_Bulette_SpawnZones((FLAG)UND_Bulette_State_Arena005_InProgress_8512e388-30c1-4c48-9cff-eb669734bdc9, (CHARACTER)S_UND_Bulette_SpawnHelper_005_969c081c-be32-4352-951d-a3fade24c806,(TRIGGER)S_UND_Bulette_SpawnTrigger_005_8ec8421a-22df-479b-92a3-cc647d2be1af, (TRIGGER)S_UND_Bulette_JumpTo_005_e73d50d8-1e98-4ef0-b83e-2b1c89540f30);

IF
TextEvent("Bulette_ResetSpawns")
THEN
DB_UND_Bulette_SpawnZones((FLAG)UND_Bulette_State_Arena001_InProgress_21d8d894-2077-4a54-a7cd-ef28e269a988, (CHARACTER)S_UND_Bulette_SpawnHelper_001_a0cd5f18-9dcf-4bf5-a1aa-e9656f9f6e79,(TRIGGER)S_UND_Bulette_SpawnTrigger_001_b8c45b76-79f5-4eab-b097-5a3843b4d014,(TRIGGER)S_UND_Bulette_JumpTo_001_05ecd76d-b6aa-4b82-8b8f-93cdf1e42917);
DB_UND_Bulette_SpawnZones((FLAG)UND_Bulette_State_Arena002_InProgress_e2fceb55-6268-4cef-9883-ae63de160b6f, (CHARACTER)S_UND_Bulette_SpawnHelper_002_fc43f2c0-872a-416f-8b9f-be45d43275d7,(TRIGGER)S_UND_Bulette_SpawnTrigger_002_3ae721bb-9921-4031-bda2-dff932508c1f,(TRIGGER)S_UND_Bulette_JumpTo_002_3934c5d0-fe93-45fe-9f08-bbf23c211fa7);
DB_UND_Bulette_SpawnZones((FLAG)UND_Bulette_State_Arena003_InProgress_01560cbb-a144-4ba8-bffd-479278beaf33, (CHARACTER)S_UND_Bulette_SpawnHelper_003_dc98138b-ce7e-4513-b59e-d9f45f825c2c,(TRIGGER)S_UND_Bulette_SpawnTrigger_003_279db630-40b9-4462-81f1-b143dae4691e,(TRIGGER)S_UND_Bulette_JumpTo_003_9ddecc60-c145-4636-99aa-de16a71d6cb8);
DB_UND_Bulette_SpawnZones((FLAG)UND_Bulette_State_Arena004_InProgress_1efb30da-229c-4a92-bb6b-2e028f8751fa, (CHARACTER)S_UND_Bulette_SpawnHelper_004_8e888d77-cbe9-4e5d-96d8-0f180e7fa4cf,(TRIGGER)S_UND_Bulette_SpawnTrigger_004_b8dd439d-346e-4f39-8658-1e264d1502f0, (TRIGGER)S_UND_Bulette_JumpTo_004_4af219a0-deeb-49b9-869a-86e0b0909e80);
DB_UND_Bulette_SpawnZones((FLAG)UND_Bulette_State_Arena005_InProgress_8512e388-30c1-4c48-9cff-eb669734bdc9, (CHARACTER)S_UND_Bulette_SpawnHelper_005_969c081c-be32-4352-951d-a3fade24c806,(TRIGGER)S_UND_Bulette_SpawnTrigger_005_8ec8421a-22df-479b-92a3-cc647d2be1af, (TRIGGER)S_UND_Bulette_JumpTo_005_e73d50d8-1e98-4ef0-b83e-2b1c89540f30);
//END_REGION

//REGION Bulette Holes PAD
IF
DB_UND_Bulette_HolesPADTrigger((TRIGGER)_Trigger)
THEN
DB_KnowledgeCheckTrigger_AD(
	"UND_Bulette_HolesPAD_Investigate",
	_Trigger, 
	"Nature", 
	(DIFFICULTYCLASS)DC_Legacy_15_bddbb9b8-a242-4c3e-a2eb-3fd274c0c539, 
	(DIALOGRESOURCE)UND_BuletteHoles_PAD_41ca0267-be34-7a27-b647-d3051d533632, 
	(FLAG)UND_Bulette_Event_NatureCheckSuccess_c0e4431c-1ea1-4082-9e22-c4befa1e5fdc);

PROC
PROC_GLO_KnowledgeCheckEnded(_Player, "UND_Bulette_HolesPAD_Investigate", _Trigger, _, _)
AND
DB_UND_Bulette_HolesPADTrigger((TRIGGER)_Trigger)
THEN
LookAtEntity((CHARACTER)_Player,_Trigger);
PROC_PlayPerceptionRevealEffect(_Trigger, "UND_Bulette_Hole_VFX");
//END_REGION Bulette Holes PAD

//REGION Bulette save triggers
IF 
DB_UND_Bulette_SpawnZones(_Flag, _, _, _)
AND
DB_UND_Bulette_SaveTriggers(_Flag, _SaveTrigger)
THEN
PROC_TriggerRegisterForPlayers(_SaveTrigger);

IF
EnteredTrigger(_Player, _Trigger)
AND
DB_UND_Bulette_SaveTriggers(_Flag, _Trigger)
AND
IsInCombat(_Player, 0)
AND
QRY_UND_Bulette_CanAppear()
THEN
AutoSave();
PROC_UND_Bulette_RemoveSaveTriggers(_Flag);

PROC
PROC_UND_Bulette_RemoveSaveTriggers((FLAG)_Flag)
AND
DB_UND_Bulette_SaveTriggers(_Flag, _SaveTrigger)
THEN
NOT DB_UND_Bulette_SaveTriggers(_Flag, _SaveTrigger);
PROC_TriggerUnregisterForPlayers(_SaveTrigger);

// If bulette was defeated or perma-defeated, disable/remove save triggers
PROC
PROC_StateSet_PermaDefeated(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3)
AND
DB_UND_Bulette_SaveTriggers(_, _SaveTrigger)
THEN
PROC_TriggerUnregisterForPlayers(_SaveTrigger);

PROC
PROC_StateSet_PermaDefeated(S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3)
THEN
SetFlag(UND_Bulette_State_HasBeenPermaDefeated_a7a88648-acfc-443f-80ea-f4195217f7be, NULL_00000000-0000-0000-0000-000000000000);
GoalCompleted;
//END_REGION Bulette save triggers

//REGION Banter 
IF
DB_PermaDefeated((CHARACTER)S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3)
THEN
PROC_RegisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_UND_BuletteLairCleared_0f78888c-acf3-41a3-9f08-10c84de1786a);

IF
DB_PermaDefeated((CHARACTER)S_UND_Minotaur_001_19edd11d-fde0-4aa9-95f6-fb2e3f2bc570)
AND
DB_PermaDefeated((CHARACTER)S_UND_Minotaur_002_9e58f686-0534-4234-8805-180118533810)
AND
DB_PermaDefeated((CHARACTER)S_UND_SharFort_Minotaur_Bait_b63c0d26-7c5f-4dc5-a3d1-2cc99367c809)
AND
NOT DB_Is_InCombat((CHARACTER)S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3, _)
AND
QRY_OnlyOnce("UND_PartyBanterTrigger_MinotaursCleared_OnlyOnce")
THEN
PROC_RegisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_UND_MinotaursCleared_e7207a90-3c21-4e0a-92c6-1c2128436e69);
//END_REGION Banter
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
