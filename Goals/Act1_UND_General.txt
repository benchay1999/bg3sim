Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_UND_TremorTriggers((TRIGGER)S_UND_TremorTrigger_000_027f9db2-a92b-4679-a755-1fdb35a9a9c2);
DB_UND_TremorTriggers(S_UND_TremorTrigger_001_6aeb7bf9-8987-4bc0-a787-215a69bf05e6);
DB_UND_TremorTriggers(S_UND_TremorTrigger_002_376ec306-d13c-4980-aff8-b6f6e0c1c3df);

SetOnStage(ITEMGUID_S_UND_Elevator_ToCaravanserai_b0bafa5b-ca00-4c94-af1c-12ced8562b4e,0);

//DB_OneShot_VoiceBarkTrigger(S_UND_ZhentStash_SkeletonsTrigger_VB_6419bfd4-cda5-41a0-aa68-ab76883874da,UND_ZhentStash_Skeletons_VB_406b853b-d9c2-6934-49c2-c00aeb3f73f4);

DB_UND_SoundEntityToTrack(S_SoundTracked_NAT_Tree_Deeproot_Sussur_A_Trunk_000_fde025c6-f99c-4c41-a7f0-548f7e216dc3);
DB_UND_SoundEntityToTrack(S_SoundTracked_NAT_Tree_Deeproot_Sussur_A_Trunk_001_8d7bcf76-2376-46ab-b3c3-7d1cb0edbd37);
DB_UND_SoundEntityToTrack(S_SoundTracked_NAT_Tree_Deeproot_Sussur_A_Trunk_002_9576b59d-2741-4c4e-9030-04f54b595da9);
DB_UND_SoundEntityToTrack(S_SoundTracked_NAT_Tree_Deeproot_Sussur_A_Trunk_003_454b53cc-40f5-4d86-91e8-75f7f1858e0c);
DB_UND_SoundEntityToTrack(S_SoundTracked_NAT_Tree_Deeproot_Sussur_A_Trunk_004_9ee052cc-0d77-4f1e-a22c-d413e8c6ed1c);
DB_UND_SoundEntityToTrack(S_SoundTracked_NAT_Tree_Deeproot_Sussur_A_Trunk_005_f518cb08-35ee-4229-8966-40e2b4bc1ef4);

//REGION VB on arriving to Zhents Port from the Hideout
PROC_TriggerRegisterForPlayers(S_UND_ZhentPort_FromHideout_VB_99c0e66d-02e5-4d2c-b736-dda71d9eea83);
//END_REGION VB on arriving to Zhents Port from the Hideout 

//REGION Notice Myconid vs Duergar fight
PROC_TriggerRegisterForPlayers(S_UND_MyconidCircle_FightSignsAD_95657559-f4c7-471d-ace1-2b513367b5ec);
//END_REGION

//REGION Zhent Port recognize VB
DB_UND_ZhentPort_RecognizeVB((TRIGGER)S_UND_ZhentPort_Recognize_VB_01_6781d159-8627-455f-91bb-adeb4a2d2221, (TRIGGER)S_UND_ZhentPort_Recognize_VFXTrigger_01_1010a762-d1af-4593-a4f6-1d0202a16634);
DB_UND_ZhentPort_RecognizeVB((TRIGGER)S_UND_ZhentPort_Recognize_VB_02_3be6fd08-f5f3-4d6c-8a62-10677720e9ca, (TRIGGER)S_UND_ZhentPort_Recognize_VFXTrigger_02_4e1339d6-98c0-4bde-88cd-d45107e49728);
//END_REGION Zhent Port recognize VB

//REGION Deep Hole
DB_Dialogs(S_UND_DeepHole_e9f966e8-9998-48af-a369-19c05665c8f5,S_UND_DeepHole_SpeakerHelper_c286da16-eabe-4bac-acd1-3e9ee1bee510,UND_DeepHole_09828f83-1f8e-303f-95d9-45023bc39c2f);
//END_REGION Deep Hole

//REGION Farmable mushrooms
DB_UND_Farmable_Reward(
	"UND_Nightlight", 
	(GUIDSTRING)LOOT_Underdark_Mushroom_Nightlight_Piece_7a164775-4a77-47a4-a8b2-5bd5bdc311cf);
//END_REGION Farmable mushrooms

//REGION Destructilbe Crystal
DB_UND_Mineable_Templates(
	(GUIDSTRING)LTS_GEN_Gem_Glowing_A_ba81625d-e066-4d76-9693-f33b149c025a, 
	(GUIDSTRING)LOOT_Alchemy_Crystal_PurpleFluoriteShard_fef1900a-402c-455f-8c63-87bdaedac6d7);
DB_UND_Mineable_Templates(
	(GUIDSTRING)LTS_GEN_Gem_Glowing_A_Purple_54251249-5d81-4734-9050-2a27f30450e7, 
	(GUIDSTRING)LOOT_Alchemy_Crystal_PurpleFluoriteShard_fef1900a-402c-455f-8c63-87bdaedac6d7);
DB_UND_Mineable_Templates(
	(GUIDSTRING)LTS_GEN_Gem_Glowing_B_f31aa231-f511-4c69-96e8-34563acb01aa, 
	(GUIDSTRING)LOOT_Alchemy_Crystal_PurpleFluoriteShard_fef1900a-402c-455f-8c63-87bdaedac6d7);
DB_UND_Mineable_Templates(
	(GUIDSTRING)LTS_GEN_Gem_Glowing_B_Purple_482ddde0-013d-4435-84bf-5d96cd6f0abb, 
	(GUIDSTRING)LOOT_Alchemy_Crystal_PurpleFluoriteShard_fef1900a-402c-455f-8c63-87bdaedac6d7);
DB_UND_Mineable_Templates(
	(GUIDSTRING)LTS_GEN_Gem_Glowing_C_73f05807-1d69-427f-b586-f61c89142ad8, 
	(GUIDSTRING)LOOT_Alchemy_Crystal_PurpleFluoriteShard_fef1900a-402c-455f-8c63-87bdaedac6d7);
DB_UND_Mineable_Templates(
	(GUIDSTRING)LTS_GEN_Gem_Glowing_C_Purple_8d0d8aac-a95f-48c3-b33e-9f4662fd00d2, 
	(GUIDSTRING)LOOT_Alchemy_Crystal_PurpleFluoriteShard_fef1900a-402c-455f-8c63-87bdaedac6d7);
DB_UND_Mineable_Templates(
	(GUIDSTRING)LTS_GEN_Gem_Glowing_D_67c050c7-ad41-4c48-83bb-e031f791d12a, 
	(GUIDSTRING)LOOT_Alchemy_Crystal_PurpleFluoriteShard_fef1900a-402c-455f-8c63-87bdaedac6d7);
DB_UND_Mineable_Templates(
	(GUIDSTRING)LTS_GEN_Gem_Glowing_D_Purple_76c2f34a-8dff-467b-b84b-e36a6c2540fe, 
	(GUIDSTRING)LOOT_Alchemy_Crystal_PurpleFluoriteShard_fef1900a-402c-455f-8c63-87bdaedac6d7);

//DB_UND_Mineable_Const_DamageStep(100.0);
//DB_UND_Mineable_Const_DropChanceDamaged(0);
DB_UND_Mineable_Const_DropCountDestroyed(0,1);
//END_REGION Destructilbe Crystal

//REGION Message in a bottle
SetOnStage(S_UND_MessageInBottle_Letter_51b336e5-c230-43a6-8939-b94f0b9e9d72, 0);
SetOnStage(S_UND_MessageInBottle_Bottle_Opened_9ff0228a-f8be-4ac6-8f9a-179ff5f52ab6, 0);
//END_REGION Message in a bottle

//REGION Hook Horror Claw Marks
DB_KnowledgeCheckTrigger_AD("UND_HookGrooves", (TRIGGER)S_UND_HookGrooves_SurvivalTrigger_42cb6a50-86d8-40c7-b4f4-992e39983413, "Survival", (DIFFICULTYCLASS)DC_Legacy_15_bddbb9b8-a242-4c3e-a2eb-3fd274c0c539, (DIALOGRESOURCE)UND_HookGrooves_PAD_512c7290-27c0-a1aa-0000-9696bdc35cfe, (FLAG)UND_HookGrooves_State_SurvivalCheckSuccess_8edb290f-fee3-42a3-91f3-eeae2a03e133);
//END_REGION

//REGION Purple Worm Nursery / Carrion Crawler Nest / Worm Wall Booster
DB_DialogBlockTradeButton((DIALOGRESOURCE)UND_WormWall_bc8a4574-02ee-bef4-c49e-cf98d4b0a61a);
DB_DialogBlockAttackButton((DIALOGRESOURCE)UND_WormWall_bc8a4574-02ee-bef4-c49e-cf98d4b0a61a);

DB_QuestDef_State((FLAG)UND_WormWall_State_RetrievedLoot_1157b599-5131-384f-3426-895c22a72420, "HIDDEN_WLD_Boosters", "UND_WormWall_SucceededCheck");

SetCanInteract(S_UND_WormWall_Hole_000_742bbb85-0daa-4368-874c-73a54b5f6fd5, 0);
//END_REGION Purple Worm Nursery / Carrion Crawler Nest / Worm Wall Booster

//REGION Drow Shrine / Sword in Stone Booster
DB_DialogBlockTradeButton((DIALOGRESOURCE)UND_SwordInStone_210ffd6c-63e7-02c1-cd28-e27207511638);
DB_DialogBlockAttackButton((DIALOGRESOURCE)UND_SwordInStone_210ffd6c-63e7-02c1-cd28-e27207511638);
SetOnStage(S_UND_SwordInStone_SwordReward_cc16c1cb-d355-47df-820a-33a83c42234b, 0);

DB_QuestDef_State((FLAG)UND_SwordInStone_State_RewardGiven_81840ad9-58d0-1714-167a-20493daed3c9, "HIDDEN_WLD_Boosters", "UND_SwordInStone_SucceededCheck");

PROC_LoopEffect((EFFECTRESOURCE)VFX_Environment_UND_Sword_01_8a31699c-0e9c-0838-3e81-71220edfd882, (ITEM)S_UND_SwordInStone_SwordTalkable_6941c335-1b36-4edb-8ba1-94dbc7302850, "UND_SwordInStone_LoopVFX", "WLD_Main_A", "");
//END_REGION Drow Shrine / Sword in Stone Booster

DB_TriggerEvents_TriggerSet("UND_Underdark",(TRIGGER)S_UND_Underdark_SUB_b379a862-a59f-4e52-9166-23fbe0e8976e);
DB_TriggerEvents_TriggerSet("UND_Underdark",(TRIGGER)S_UND_DuergarCamp_SUB_05c3bf1f-29a6-437c-9a98-c1ff0ec53a53);
KBSECTION
//REGION Tremor
IF
DB_UND_TremorTriggers(_Trigger)
THEN
PROC_TriggerRegisterForPlayers(_Trigger);

IF
EnteredTrigger(_Player,_Trigger)
AND
DB_UND_TremorTriggers(_Trigger)
AND
DB_Players(_Player)
THEN
PROC_UND_Tremor(_Player,_Trigger);


PROC
PROC_UND_Tremor((CHARACTER)_,(TRIGGER)_Trigger)
AND
DB_UND_TremorTriggers(_Trigger)
THEN
PROC_TriggerUnregisterForPlayers(_Trigger);
NOT DB_UND_TremorTriggers(_Trigger);

PROC
PROC_UND_Tremor(_Player,_)
THEN
ObjectTimerLaunch(_Player,"Timer_UND_Tremor", 300);

IF
ObjectTimerFinished(_Player,"Timer_UND_Tremor")
THEN
PROC_ShakeCameraForTime((CHARACTER)_Player, 6000, VFX_Script_Underdark_Earthquake_01_18bc69d9-c8f9-938d-7335-4d50f6782707);
ObjectTimerLaunch(_Player,"Timer_UND_Tremor_VB_Delay", 4000);

IF
ObjectTimerFinished(_Player,"Timer_UND_Tremor_VB_Delay")
AND
NOT DB_UND_Tremor_OneShotVB_Played(_)
THEN
PROC_TryStartAD(UND_Tremor_PAD_f4bfaf96-690a-0bee-9fb4-e974df00b9a6,_Player);
DB_UND_Tremor_OneShotVB_Played(1);

//END_REGION Tremor

//REGION Notice Myconid vs Duergar fight
IF
DB_InRegion(_Player, (TRIGGER)S_UND_MyconidCircle_FightSignsAD_95657559-f4c7-471d-ace1-2b513367b5ec)
AND
DB_Players(_Player)
AND
NOT DB_CantTalk(_Player)
AND
QRY_OnlyOnce("UND_PlayerNoticedMyconidFight")
THEN
PROC_TryStartAD((DIALOGRESOURCE)UND_MyconidCircle_PAD_NoticeFight_6d64be05-c2ed-374f-bf97-0ba0cf7bf33a, _Player);
//END_REGION

//REGION Elevator from the zhent Dungeon

IF
UseStarted(_Player,S_PLA_ZhentDungeon_LiftDown_2e18ea36-cf07-49df-a3f6-f98ec020e4af)
AND
NOT DB_PLA_ZhentDungeon_Portal(S_PLA_ZhentDungeon_LiftDown_2e18ea36-cf07-49df-a3f6-f98ec020e4af)
AND
IsOnStage(ITEMGUID_S_UND_Elevator_ToCaravanserai_b0bafa5b-ca00-4c94-af1c-12ced8562b4e,0)
THEN
SetOnStage(ITEMGUID_S_UND_Elevator_ToCaravanserai_b0bafa5b-ca00-4c94-af1c-12ced8562b4e,1);


PROC
PROC_UND_TransitionCinematic_PassingTo((TRIGGER)_PastTrigger)
AND
IsOnStage(ITEMGUID_S_UND_Elevator_ToCaravanserai_b0bafa5b-ca00-4c94-af1c-12ced8562b4e,0)
THEN
SetOnStage(ITEMGUID_S_UND_Elevator_ToCaravanserai_b0bafa5b-ca00-4c94-af1c-12ced8562b4e,1);

//END_REGION

IF
DB_InRegion(_Player, S_UND_Underdark_SUB_b379a862-a59f-4e52-9166-23fbe0e8976e)
AND
NOT DB_GlobalFlag(GLO_Underdark_EverEnteredBefore_dd5cd5b4-2ad5-4dbd-4972-2afaa7538994)
THEN
PROC_GlobalSetFlagAndCache(GLO_Underdark_EverEnteredBefore_dd5cd5b4-2ad5-4dbd-4972-2afaa7538994);

//REGION Sound events
IF
EnteredTrigger(_Player,S_UND_Underdark_SUB_b379a862-a59f-4e52-9166-23fbe0e8976e)
AND
DB_Players(_Player)
AND
DB_UND_SoundEntityToTrack(_Entity)
THEN
AddTrackedSoundEntity(_Entity,"");
NOT DB_UND_SoundEntityToTrack(_Entity);
DB_UND_TrackedSoundEntity(_Entity);

IF
LeftTrigger(_Player,S_UND_Underdark_SUB_b379a862-a59f-4e52-9166-23fbe0e8976e)
AND
DB_Players(_Player)
AND
NOT QRY_CheckOtherPlayersInTrigger(_Player,S_UND_Underdark_SUB_b379a862-a59f-4e52-9166-23fbe0e8976e)
AND
DB_UND_TrackedSoundEntity(_Entity)
THEN
RemoveTrackedSoundEntity(_Entity);
NOT DB_UND_TrackedSoundEntity(_Entity);
DB_UND_SoundEntityToTrack(_Entity);
//END_REGION Sound events

//REGION VB on arriving to Zhents Port from the Hideout
PROC
PROC_UND_TransitionCinematic_PassingTo((TRIGGER)S_UND_Elevetor_FromCaravanSerai_905f9fe5-b31b-4595-97cd-ea51d4f08be8)
AND
NOT DB_GlobalFlag(GLO_Underdark_EverEnteredBefore_dd5cd5b4-2ad5-4dbd-4972-2afaa7538994)
AND
NOT DB_UND_ZhentPort_AllowFromHideoutVoiceBark(1)
AND
NOT DB_UND_ZhentPort_AwaitingFromHideoutVoiceBark(1)
THEN
DB_UND_ZhentPort_AllowFromHideoutVoiceBark(1);

IF
EnteredTrigger(_Player, S_UND_ZhentPort_FromHideout_VB_99c0e66d-02e5-4d2c-b736-dda71d9eea83)
AND
DB_Players(_Player)
AND
DB_UND_ZhentPort_AllowFromHideoutVoiceBark(1)
AND
NOT DB_UND_ZhentPort_AwaitingFromHideoutVoiceBark(1)
THEN
DB_UND_ZhentPort_AwaitingFromHideoutVoiceBark(1);
TimerLaunch("UND_ZhentPort_ArrivedFromHideoutVB_Timer", 3000);

IF
TimerFinished("UND_ZhentPort_ArrivedFromHideoutVB_Timer")
AND
DB_InRegion(_Player, S_UND_ZhentPort_FromHideout_VB_99c0e66d-02e5-4d2c-b736-dda71d9eea83)
AND
DB_Players(_Player)
AND
DB_UND_ZhentPort_AwaitingFromHideoutVoiceBark(1)
AND
DB_UND_ZhentPort_AllowFromHideoutVoiceBark(1)
AND
NOT DB_CantTalk(_Player)
AND
QRY_OncePerUserAndNearbyPlayers(_Player, "UND_ZhentPort_ArrivedFromHideoutVB")
THEN
PROC_UND_ZhentPort_UnregisterRecognizeAreas();
NOT DB_UND_ZhentPort_AllowFromHideoutVoiceBark(1);
StartVoiceBark(UND_ZhentPort_VB_FromHideout_938701ee-f476-f219-3821-ef28aab93e36, _Player);

IF
TimerFinished("UND_ZhentPort_ArrivedFromHideoutVB_Timer")
AND
DB_UND_ZhentPort_AwaitingFromHideoutVoiceBark(1)
THEN
NOT DB_UND_ZhentPort_AwaitingFromHideoutVoiceBark(1);
//END_REGION VB on arriving to Zhents Port from the Hideout 

//REGION Zhent Port recognize VB
IF
DB_UND_ZhentPort_RecognizeVB((TRIGGER)_Trigger, _)
THEN
PROC_TriggerRegisterForPlayers(_Trigger);

IF
EnteredTrigger(_Player, _Trigger)
AND
DB_Players(_Player)
AND
DB_UND_ZhentPort_RecognizeVB(_Trigger, (TRIGGER)_VFXTrigger)
AND
NOT DB_CantTalk(_Player)
AND
QRY_OncePerUserAndNearbyPlayers(_Player, "UND_ZhentPort_Recognize")
THEN
LookAtEntity((CHARACTER)_Player,_VFXTrigger);
StartVoiceBark((VOICEBARKRESOURCE)UND_ZhentPort_VB_Recognize_c5fa103f-944f-cbcc-595b-cb90516e7074, _Player);
PROC_PlayPerceptionRevealEffect(_VFXTrigger, "UND_ZhentPort_RecognizeVFX");

PROC
PROC_UND_ZhentPort_UnregisterRecognizeAreas()
AND
DB_UND_ZhentPort_RecognizeVB(_Trigger, _)
THEN
PROC_TriggerUnregisterForPlayers(_Trigger);

PROC
PROC_UND_TransitionCinematic_PassingTo((TRIGGER)S_UND_Elevetor_FromCaravanSerai_905f9fe5-b31b-4595-97cd-ea51d4f08be8)
AND
NOT DB_UND_ZhentPort_CameFromHideout(1)
THEN
DB_UND_ZhentPort_CameFromHideout(1);
PROC_UND_ZhentPort_UnregisterRecognizeAreas();
//END_REGION Zhent Port recognize VB

//REGION VB within pile of bones, from beneath the Bottomless Well
IF
DB_GlobalFlag(GLO_Underdark_EverEnteredBefore_dd5cd5b4-2ad5-4dbd-4972-2afaa7538994)
THEN
PROC_TriggerRegisterForPlayers(S_UND_ZhentStash_SkeletonsTrigger_VB_6419bfd4-cda5-41a0-aa68-ab76883874da);

// On entering from Underdark
IF
DB_InRegion(_Player, S_UND_ZhentStash_SkeletonsTrigger_VB_6419bfd4-cda5-41a0-aa68-ab76883874da)
AND
DB_Players(_Player)
AND
NOT DB_CantTalk(_Player)
AND
NOT DB_FOR_Bottomless_Cinematic_Done(_)
THEN
PROC_UND_General_PlaySkeletonVB();

// On upon jumping from the Well
IF
DB_InRegion(_Player, S_UND_ZhentStash_SkeletonsTrigger_VB_6419bfd4-cda5-41a0-aa68-ab76883874da)
AND
DB_Players(_Player)
AND
NOT DB_CantTalk(_Player)
AND
DB_FOR_Bottomless_Cinematic_Done(_)
AND
NOT DB_UND_General_TryingToStartSkeletonsVB(1)
THEN
DB_UND_General_TryingToStartSkeletonsVB(1);
TimerLaunch("UND_ZhentStash_SkeletonsTrigger_VB_Timer", 3000);

IF
TimerFinished("UND_ZhentStash_SkeletonsTrigger_VB_Timer")
AND
DB_UND_General_TryingToStartSkeletonsVB(1)
THEN
NOT DB_UND_General_TryingToStartSkeletonsVB(1);
PROC_UND_General_PlaySkeletonVB();

PROC
PROC_UND_General_PlaySkeletonVB()
AND
DB_InRegion((CHARACTER)_Player, S_UND_ZhentStash_SkeletonsTrigger_VB_6419bfd4-cda5-41a0-aa68-ab76883874da)
AND
NOT DB_CantTalk(_Player)
AND
QRY_OncePerUserAndNearbyPlayers(_Player, "UND_ZhentStash_SkeletonsTrigger_VB_Once", 6.0)
THEN
StartVoiceBark((VOICEBARKRESOURCE)UND_ZhentStash_Skeletons_VB_406b853b-d9c2-6934-49c2-c00aeb3f73f4, _Player);
//END_REGION VB within pile of bones, from beneath the Bottomless Well

//REGION Farmable mushrooms
IF
DualEntityEvent(_Player, _FarmableItem, "GEN_FarmWithNature_CheckPlayer")
AND
NOT DB_NatureSkillCheck((CHARACTER)_Player, (ITEM)_FarmableItem, _)
AND
NOT DB_NatureSkillCheck_AwaitingResult(_Player, _FarmableItem)
AND
GetVarInteger(_FarmableItem, "DC", _DC)
AND
QRY_TemporaryDCsubstitute(_DC)
AND
DB_QRYRTN_TemporaryDCsubstitute(_DCsub)
THEN
DB_NatureSkillCheck_AwaitingResult(_Player, _FarmableItem);
PROC_TrySkillCheck((CHARACTER)_Player, _FarmableItem, "Nature", _DCsub, "GLO_FarmableItemCheck");

// Check Succeeded
PROC
PROC_RollResult("GLO_FarmableItemCheck", _Player, (ITEM)_FarmableItem, 1)
AND
GetVarString(_FarmableItem, "RewardId", _RewardId)
THEN
DB_NatureSkillCheck(_Player, _FarmableItem, 1);
PROC_UND_FarmMushroom(_Player, _FarmableItem, _RewardId);

// Check Failed
PROC
PROC_RollResult("GLO_FarmableItemCheck", _Player, _FarmableItem, 0)
AND
NOT DB_NatureSkillCheck((CHARACTER)_Player, (ITEM)_FarmableItem,_)
THEN
DB_NatureSkillCheck(_Player,_FarmableItem, 0);

PROC
PROC_RollResult("GLO_FarmableItemCheck", _Player, _FarmableItem, _Result)
AND
_Result != 2
THEN
SetEntityEvent(_FarmableItem, "FarmFromStory");

PROC
PROC_RollResult("GLO_FarmableItemCheck", _Player, _FarmableItem, _)
AND
DB_NatureSkillCheck_AwaitingResult(_Player, (ITEM)_FarmableItem)
THEN
NOT DB_NatureSkillCheck_AwaitingResult(_Player, _FarmableItem);

// Drop treasure
PROC
PROC_UND_FarmMushroom((CHARACTER)_Player, (ITEM)_FarmableItem, (STRING)_RewardId)
AND
NOT DB_UND_ItemFarmed(_FarmableItem)
AND
DB_UND_Farmable_Reward(_RewardId, _DropTemplate)
AND
GetPosition(_FarmableItem, _X, _Y, _Z)
AND
CreateAt(_DropTemplate, _X, _Y, _Z, 0, 0, "", _DroppedItem)
THEN
//ScatterAt((ITEM)_DroppedItem, _X, _Y, _Z); // TODO: Currently ScatterAt() is buggy and drops things underneath the surface
ToInventory((ITEM)_DroppedItem, _Player, 1, 1, 1);
DB_UND_ItemFarmed(_FarmableItem);
//END_REGION Farmable mushrooms

//REGION Mineable Crystal
/*IF
AttackedBy((ITEM)_Crystal, _, _, _, _DamageAmount, _, _)
AND
_DamageAmount > 0
AND
NOT DB_Destroyed(_Crystal)
AND
GetTemplate(_Crystal, _Template)
AND
DB_UND_Mineable_Templates(_Template, _DropTemplate)
AND
QRY_Crime_ItemHasHPorIndestructible(_Crystal)
AND
GetHitpointsPercentage(_Crystal, _HPCur)
THEN
PROC_UND_Mineable_DropSingle(_Crystal, _DropTemplate, _HPCur);

PROC
PROC_UND_Mineable_DropSingle((ITEM)_Source, (GUIDSTRING)_DropTemplate, _)
AND
NOT DB_UND_Mineable_Crystals(_Source, _)
THEN
DB_UND_Mineable_Crystals(_Source, 100.0);

PROC
PROC_UND_Mineable_DropSingle((ITEM)_Source, (GUIDSTRING)_DropTemplate, (REAL)_HPCur)
AND
DB_UND_Mineable_Crystals(_Source, _HPMined)
AND
RealSubtract(_HPMined, _HPCur, _HPDiff)
AND
DB_UND_Mineable_Const_DamageStep(_ConstStep)
AND
_HPDiff >= _ConstStep
AND
RealSubtract(_HPMined, _ConstStep, _HPMinedNew)
THEN
NOT DB_UND_Mineable_Crystals(_Source, _HPMined);
DB_UND_Mineable_Crystals(_Source, _HPMinedNew);
PROC_UND_Mineable_RollOnceToSpawnAndScatter(_Source, _DropTemplate);
PROC_UND_Mineable_DropSingle(_Source, _DropTemplate, _HPCur);

PROC
PROC_UND_Mineable_RollOnceToSpawnAndScatter((ITEM)_Source, (GUIDSTRING)_DropTemplate)
AND
Random(100, _Roll)
AND
DB_UND_Mineable_Const_DropChanceDamaged(_Chance)
AND
_Roll < _Chance
AND
GetPosition(_Source, _X, _Y, _Z)
THEN
PROC_UND_Mineable_SpawnAndScatter(_X, _Y, _Z, _DropTemplate);

// On destroy spawn additional crystals as well as the ones we've missed
IF
TemplateDestroyedBy(_CrystalTemplate, _Crystal, _, _, _)
AND
DB_UND_Mineable_Templates(_CrystalTemplate, _DropTemplate)
THEN
PROC_UND_Mineable_DropManyOnDestroy(_Crystal, _DropTemplate);
PROC_UND_Mineable_DropSingle(_Crystal, _DropTemplate, 0.0);

PROC
PROC_UND_Mineable_DropManyOnDestroy((ITEM)_Source, (GUIDSTRING)_DropTemplate)
AND
DB_UND_Mineable_Const_DropCountDestroyed(_Min, _Max)
AND
IntegerSubtract(_Max, _Min, _Length)
AND
IntegerSum(_Length, 1, _LengthPlusOne) // because Random() is within [0, Modulo-1]
AND
Random(_LengthPlusOne, _Roll)
AND
IntegerSum(_Min, _Roll, _NumToDrop)
AND
GetPosition(_Source, _X, _Y, _Z)
AND
DB_Avatars(_Player)
AND
QRY_DoNTimes(_NumToDrop)
AND
DB_QRY_RTN_DoNTimes(_I)
THEN
PROC_UND_Mineable_SpawnAndScatter(_X, _Y, _Z, _DropTemplate);

// Actually spawning the item drop
PROC
PROC_UND_Mineable_SpawnAndScatter((REAL)_X, (REAL)_Y, (REAL)_Z, (GUIDSTRING)_DropTemplate)
AND
CreateAt(_DropTemplate, _X, _Y, _Z, 0, 0, "", _DroppedItem)
THEN
ScatterAt((ITEM)_DroppedItem, _X, _Y, _Z);
*/
// Until we'll get Scatter() work properly
// and the crystal's price adjust.
IF
TemplateDestroyedBy(_CrystalTemplate, _Crystal, _, _, _)
AND
DB_UND_Mineable_Templates(_CrystalTemplate, _DropTemplate)
AND
DB_UND_Mineable_Const_DropCountDestroyed(_Min, _Max)
AND
IntegerSubtract(_Max, _Min, _Length)
AND
IntegerSum(_Length, 1, _LengthPlusOne) // because Random() is within [0, Modulo-1]
AND
Random(_LengthPlusOne, _Roll)
AND
IntegerSum(_Min, _Roll, _NumToDrop)
AND
_NumToDrop > 0
AND
GetPosition(_Crystal, _X, _Y, _Z)
AND
QRY_DoNTimes(_NumToDrop)
AND
DB_QRY_RTN_DoNTimes(_I)
THEN
PROC_CreateAt(_DropTemplate, _X, _Y, _Z);
//END_REGION Mineable Crystal

//REGION Message in a bottle
PROC
PROC_BlockUseOfItem(_Player, S_UND_S_UND_MessageInBottle_Bottle_Corked_e82b44c0-f2e9-418b-8712-21c012fae74e)
AND
NOT DB_UND_MessageInBottle_Transformed(1)
AND
IsInInventory(S_UND_S_UND_MessageInBottle_Bottle_Corked_e82b44c0-f2e9-418b-8712-21c012fae74e, 1)
AND
GetInventoryOwner(S_UND_S_UND_MessageInBottle_Bottle_Corked_e82b44c0-f2e9-418b-8712-21c012fae74e, _Owner)
THEN
DB_UND_MessageInBottle_Transformed(1);
DB_CustomUseItemResponse(_Player, S_UND_S_UND_MessageInBottle_Bottle_Corked_e82b44c0-f2e9-418b-8712-21c012fae74e, 0);
RequestDelete(S_UND_S_UND_MessageInBottle_Bottle_Corked_e82b44c0-f2e9-418b-8712-21c012fae74e);
PROC_ToInventoryAndOnStage(S_UND_MessageInBottle_Letter_51b336e5-c230-43a6-8939-b94f0b9e9d72, _Owner);
PROC_ToInventoryAndOnStage(S_UND_MessageInBottle_Bottle_Opened_9ff0228a-f8be-4ac6-8f9a-179ff5f52ab6, _Owner);

PROC
PROC_BlockUseOfItem(_Player, S_UND_S_UND_MessageInBottle_Bottle_Corked_e82b44c0-f2e9-418b-8712-21c012fae74e)
AND
NOT DB_UND_MessageInBottle_Transformed(1)
AND
IsInInventory(S_UND_S_UND_MessageInBottle_Bottle_Corked_e82b44c0-f2e9-418b-8712-21c012fae74e, 0)
AND
GetPosition(S_UND_S_UND_MessageInBottle_Bottle_Corked_e82b44c0-f2e9-418b-8712-21c012fae74e, _X, _Y, _Z)
THEN
DB_UND_MessageInBottle_Transformed(1);
DB_CustomUseItemResponse(_Player, S_UND_S_UND_MessageInBottle_Bottle_Corked_e82b44c0-f2e9-418b-8712-21c012fae74e, 0);
PROC_UND_MessageInBottle_TryReadLetter(_Player, _X, _Y, _Z);
SetOnStage(S_UND_S_UND_MessageInBottle_Bottle_Corked_e82b44c0-f2e9-418b-8712-21c012fae74e, 0);
SetOnStage(S_UND_MessageInBottle_Bottle_Opened_9ff0228a-f8be-4ac6-8f9a-179ff5f52ab6, 1);
TeleportToPosition(S_UND_MessageInBottle_Bottle_Opened_9ff0228a-f8be-4ac6-8f9a-179ff5f52ab6, _X, _Y, _Z, "", 0, 0, 0);

PROC
PROC_UND_MessageInBottle_TryReadLetter((CHARACTER)_Player, _, _, _)
AND
DB_Players(_Player)
AND
NOT DB_UND_MessageInBottle_LetterNeedsReading(_)
THEN
DB_UND_MessageInBottle_LetterNeedsReading(_Player);

PROC
PROC_UND_MessageInBottle_TryReadLetter(_, _, _, _)
THEN
SetOnStage(S_UND_MessageInBottle_Letter_51b336e5-c230-43a6-8939-b94f0b9e9d72, 1);

PROC
PROC_UND_MessageInBottle_TryReadLetter((CHARACTER)_Player, (REAL)_X, (REAL)_Y, (REAL)_Z)
AND
NOT DB_Players(_Player)
THEN
ScatterAt(S_UND_MessageInBottle_Letter_51b336e5-c230-43a6-8939-b94f0b9e9d72, _X, _Y, _Z);

IF
WentOnStage(S_UND_MessageInBottle_Letter_51b336e5-c230-43a6-8939-b94f0b9e9d72, 1)
AND
DB_UND_MessageInBottle_LetterNeedsReading(_Player)
THEN
NOT DB_UND_MessageInBottle_LetterNeedsReading(_Player);
ToInventory(S_UND_MessageInBottle_Letter_51b336e5-c230-43a6-8939-b94f0b9e9d72, _Player);
Use(_Player, S_UND_MessageInBottle_Letter_51b336e5-c230-43a6-8939-b94f0b9e9d72, "");
//END_REGION Message in a bottle

//REGION Drow patrol resupply booster
IF
Opened(S_UND_DrowResupply_HeavyChest_e8f726d4-aacd-4397-adaa-27ebdf6a0f5b)
THEN
PROC_UND_DrowResupply_OnChestOpened();

IF
Unlocked(S_UND_DrowResupply_HeavyChest_e8f726d4-aacd-4397-adaa-27ebdf6a0f5b, _Player, _)
THEN
PROC_UND_DrowResupply_OnChestOpened();

PROC
PROC_UND_DrowResupply_OnChestOpened()
AND
NOT DB_UND_DrowResupply_MessageRead(1)
AND
QRY_GetClosestAvailableCharacterTo(S_UND_DrowResupply_HeavyChest_e8f726d4-aacd-4397-adaa-27ebdf6a0f5b, 0)
AND
DB_ClosestAvailableCharacterTo(_Player, S_UND_DrowResupply_HeavyChest_e8f726d4-aacd-4397-adaa-27ebdf6a0f5b, _)
AND
QRY_SpeakerIsInDialogRange(_Player, S_UND_DrowResupply_HeavyChest_e8f726d4-aacd-4397-adaa-27ebdf6a0f5b)
AND
NOT DB_OnlyOnce("UND_DrowResupply_MessageRead")
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)UND_DrowResupply_PAD_d0988990-6a74-7f2c-5566-8a2c7964f2b4, _Player)
AND
QRY_OnlyOnce("UND_DrowResupply_MessageRead")
THEN
DB_UND_DrowResupply_MessageRead(1);
//END_REGION Drow patrol resupply booster

//REGION Carrion Crawler Wall
// Bottom hole
IF
UseStarted(_Player, (ITEM)S_UND_WormWall_Hole_002_8e08a220-6e53-44d4-bd67-a4afdb026b57)
AND
DB_Players(_Player)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)UND_WormWall_bc8a4574-02ee-bef4-c49e-cf98d4b0a61a, (ITEM)S_UND_WormWall_Hole_002_8e08a220-6e53-44d4-bd67-a4afdb026b57, _Player)
THEN
DB_NOOP(1);

IF
DialogEnded(UND_WormWall_bc8a4574-02ee-bef4-c49e-cf98d4b0a61a, _Id)
AND
DB_GlobalFlag((FLAG)UND_WormWall_State_RetrievedLoot_1157b599-5131-384f-3426-895c22a72420)
AND
DB_DialogPlayers(_Id, _Player, 1)
AND
GetFlag(UND_WormWall_Event_LowerGiveReward_de7918ce-e0e0-eed1-7f6a-c97571d56fc2, _Player, 1)
AND
NOT DB_UND_WormWall_GiveRewardToPlayer_Lower(_)
THEN
ClearFlag(UND_WormWall_Event_LowerGiveReward_de7918ce-e0e0-eed1-7f6a-c97571d56fc2, _Player, 0);
DB_UND_WormWall_GiveRewardToPlayer_Lower(_Player);
IterateInventory(S_UND_WormWall_Hole_002_8e08a220-6e53-44d4-bd67-a4afdb026b57, "UND_WormWall_LowerGiveItem", "UND_WormWall_LowerGiveItem_Finish");

IF
EntityEvent((ITEM)_Item, "UND_WormWall_LowerGiveItem")
AND
DB_UND_WormWall_GiveRewardToPlayer_Lower(_Player)
THEN
ToInventory(_Item, _Player, 1, 1, 1);

IF
EntityEvent(_, "UND_WormWall_LowerGiveItem_Finish")
AND
DB_UND_WormWall_GiveRewardToPlayer_Lower(_Player)
THEN
NOT DB_UND_WormWall_GiveRewardToPlayer_Lower(_Player);

// Empty holes
IF
UseStarted(_Player, (ITEM)S_UND_WormWall_Hole_004_23c479b4-3c45-4461-b570-618f492f13af)
AND
DB_Players(_Player)
AND
QRY_StartDialog((DIALOGRESOURCE)UND_WormWall_PAD_Empty1_8d4efee8-b922-c66d-1e18-769b58837304, _Player)
THEN
DB_NOOP(1);

IF
UseStarted(_Player, (ITEM)S_UND_WormWall_Hole_003_f3430ca7-189b-4a0d-a787-6223ef0d359b)
AND
DB_Players(_Player)
AND
QRY_StartDialog((DIALOGRESOURCE)UND_WormWall_PAD_Empty2_9e78a181-bc93-56c8-e045-58dea57781c1, _Player)
THEN
DB_NOOP(1);

IF
UseStarted(_Player, (ITEM)S_UND_WormWall_Hole_001_f21259b5-e6ef-4075-8030-9ebba60fce14)
AND
DB_Players(_Player)
AND
QRY_StartDialog((DIALOGRESOURCE)UND_WormWall_PAD_Empty3_2faf2c12-6fcb-1ec8-9eee-47a11d4c6e42, _Player)
THEN
DB_NOOP(1);

IF
AutomatedDialogEnded(UND_WormWall_PAD_Empty3_2faf2c12-6fcb-1ec8-9eee-47a11d4c6e42, _Id)
AND
DB_GlobalFlag((FLAG)UND_WormWall_State_GotBone_15d16254-8b93-60a2-dbb4-89606d58033d)
AND
DB_DialogPlayers(_Id, _Player, 1)
AND
GetFlag(UND_WormWall_Event_EmptyGiveReward_4d0ad74c-383f-42c7-38b0-b848dc3bb1f6, _Player, 1)
AND
NOT DB_UND_WormWall_GiveRewardToPlayer_Empty(_)
THEN
ClearFlag(UND_WormWall_Event_EmptyGiveReward_4d0ad74c-383f-42c7-38b0-b848dc3bb1f6, _Player, 0);
DB_UND_WormWall_GiveRewardToPlayer_Empty(_Player);
IterateInventory(S_UND_WormWall_Hole_001_f21259b5-e6ef-4075-8030-9ebba60fce14, "UND_WormWall_EmptyGiveItem", "UND_WormWall_EmptyGiveItem_Finish");

IF
EntityEvent((ITEM)_Item, "UND_WormWall_EmptyGiveItem")
AND
DB_UND_WormWall_GiveRewardToPlayer_Empty(_Player)
THEN
ToInventory(_Item, _Player, 1, 1, 1);

IF
EntityEvent(_, "UND_WormWall_EmptyGiveItem_Finish")
AND
DB_UND_WormWall_GiveRewardToPlayer_Empty(_Player)
THEN
NOT DB_UND_WormWall_GiveRewardToPlayer_Empty(_Player);
//END_REGION Carrion Crawler Wall

//REGION Sword in Stone Booster
PROC
PROC_BlockUseOfItem(_Player, S_UND_SwordInStone_SwordTalkable_6941c335-1b36-4edb-8ba1-94dbc7302850)
AND
DB_Players(_Player)
THEN
PROC_UND_SwordInStone_StartSwordDialog(_Player);
DB_CustomUseItemResponse(_Player, S_UND_SwordInStone_SwordTalkable_6941c335-1b36-4edb-8ba1-94dbc7302850, 0);

PROC
PROC_UND_SwordInStone_StartSwordDialog((CHARACTER)_Player)
AND
NOT DB_GlobalFlag((FLAG)UND_SwordInStone_State_RewardGiven_81840ad9-58d0-1714-167a-20493daed3c9)
AND
HasActiveStatus(S_UND_SwordInStone_SwordTalkable_6941c335-1b36-4edb-8ba1-94dbc7302850, "UND_SUSSURTREEANTIMAGIC", 0)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)UND_SwordInStone_210ffd6c-63e7-02c1-cd28-e27207511638, S_UND_SwordInStone_SwordTalkable_6941c335-1b36-4edb-8ba1-94dbc7302850, _Player)
THEN
DB_NOOP(1); 

PROC
PROC_UND_SwordInStone_StartSwordDialog((CHARACTER)_Player)
AND
NOT DB_GlobalFlag((FLAG)UND_SwordInStone_State_RewardGiven_81840ad9-58d0-1714-167a-20493daed3c9)
AND
HasActiveStatus(S_UND_SwordInStone_SwordTalkable_6941c335-1b36-4edb-8ba1-94dbc7302850, "UND_SUSSURTREEANTIMAGIC", 1)
THEN
PROC_TryStartAD((DIALOGRESOURCE)GEN_AD_ItemBlocked_a4eb2e7b-809a-a9e4-94da-955a8ac9b8c6, _Player);

IF
DialogEnded(UND_SwordInStone_210ffd6c-63e7-02c1-cd28-e27207511638, _Id)
AND
DB_DialogPlayers(_Id, _Player, 1)
AND
NOT DB_UND_SwordInStone_AddToInventory(_)
AND
GetFlag(UND_SwordInStone_Rewarded_f436af9d-ee3c-22e0-e677-23e3fa31e890, _Player, 1)
THEN
PROC_StopLoopEffect(S_UND_SwordInStone_SwordTalkable_6941c335-1b36-4edb-8ba1-94dbc7302850, "UND_SwordInStone_LoopVFX");
ClearFlag(UND_SwordInStone_Rewarded_f436af9d-ee3c-22e0-e677-23e3fa31e890, _Player, 0);
DB_UND_SwordInStone_AddToInventory(_Player);
SetOnStage(S_UND_SwordInStone_SwordTalkable_6941c335-1b36-4edb-8ba1-94dbc7302850, 0);
SetOnStage(S_UND_SwordInStone_SwordReward_cc16c1cb-d355-47df-820a-33a83c42234b, 1);

IF
WentOnStage(S_UND_SwordInStone_SwordReward_cc16c1cb-d355-47df-820a-33a83c42234b, 1)
AND
DB_UND_SwordInStone_AddToInventory(_Player)
THEN
NOT DB_UND_SwordInStone_AddToInventory(_Player);
ToInventory(S_UND_SwordInStone_SwordReward_cc16c1cb-d355-47df-820a-33a83c42234b, _Player, 1, 1, 1);

IF
TemplateAddedTo(_Template,S_UND_SwordInStone_SwordReward_cc16c1cb-d355-47df-820a-33a83c42234b,_Player, _)
AND
QRY_OnlyOnce("S_UND_SwordInStone_SwordPickedUp")
THEN
TriggerSetSoundState((TRIGGER)Amb_SV_Underdark_Sword_QD_000_60596127-cb77-4f2e-8b83-8643e1794a72,"S_UND_SwordInStone","SwordPickedUp",0);

IF
StatusApplied(S_UND_SwordInStone_SwordTalkable_6941c335-1b36-4edb-8ba1-94dbc7302850,"INSURFACE", _,_)
AND
NOT DB_GlobalFlag(UND_SwordInStone_SwordUnlocked_6698f98a-cde7-d515-47ff-68d269d54eba)
THEN
PROC_UND_SwordInStone_UnlockBySurface(S_UND_SwordInStone_SwordTalkable_6941c335-1b36-4edb-8ba1-94dbc7302850);

IF
StatusApplied(S_UND_SwordInStone_Helper_acce1086-1611-4436-91ad-957f093d516e,"INSURFACE", _,_)
AND
NOT DB_GlobalFlag(UND_SwordInStone_SwordUnlocked_6698f98a-cde7-d515-47ff-68d269d54eba)
THEN
PROC_UND_SwordInStone_UnlockBySurface(S_UND_SwordInStone_Helper_acce1086-1611-4436-91ad-957f093d516e);

PROC
PROC_UND_SwordInStone_UnlockBySurface((ITEM)_Item)
AND
GetSurfaceGroundAt(_Item, _Surface)
AND
_Surface == "SurfaceBlood"
THEN
PROC_GlobalSetFlagAndCache((FLAG)UND_SwordInStone_SwordUnlocked_6698f98a-cde7-d515-47ff-68d269d54eba);
PROC_LoopEffect(VFX_Script_Perception_Danger_01_100c3407-e65a-bc79-7ecb-cf33ce00b302, S_UND_SwordInStone_SwordTalkable_6941c335-1b36-4edb-8ba1-94dbc7302850, "UND_SwordInStone_BloodVFX", "WLD_Main_A", "");
ObjectTimerLaunch(S_UND_SwordInStone_SwordTalkable_6941c335-1b36-4edb-8ba1-94dbc7302850, "UND_SwordInStone_BloodVFX_Timer", 3000);

IF
ObjectTimerFinished(_Sword, "UND_SwordInStone_BloodVFX_Timer")
THEN
PROC_StopLoopEffect(_Sword, "UND_SwordInStone_BloodVFX");

IF
FlagSet(UND_SwordInStone_Event_GiveBlood_b37cf836-031f-41b4-a949-06f201d18f6c, (CHARACTER)_Player, _)
AND
DB_Players(_Player)
AND
GetHitpoints(_Player, _CurrentHP)
AND
_CurrentHP > 1
THEN
ApplyDamage(_Player, 1, "Slashing", _Player);

IF
FlagSet(UND_SwordInStone_Event_GiveBlood_b37cf836-031f-41b4-a949-06f201d18f6c, (CHARACTER)_Player, _)
AND
DB_Players(_Player)
THEN
CreateSurface(S_UND_SwordInStone_Helper_acce1086-1611-4436-91ad-957f093d516e, "SurfaceBlood", 1.0, -1.0);
//END_REGION Sword in Stone Booster
EXITSECTION
PROC_UND_ZhentPort_UnregisterRecognizeAreas();
PROC_TriggerUnregisterForPlayers(S_UND_ZhentStash_SkeletonsTrigger_VB_6419bfd4-cda5-41a0-aa68-ab76883874da);
PROC_TriggerUnregisterForPlayers(S_UND_ZhentPort_FromHideout_VB_99c0e66d-02e5-4d2c-b736-dda71d9eea83);
ENDEXITSECTION
ParentTargetEdge "Act1"
