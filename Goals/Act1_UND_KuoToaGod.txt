Version 1
SubGoalCombiner SGC_AND
INITSECTION
ApplyStatus(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, "INVISIBLE", -1.0, 1, S_UND_KuoToaGod_HighPriest_a1f879ab-8731-4889-9b7b-d91b5094dbc0);
ApplyStatus(S_UNI_SickleOfBOOOAL_78c0da13-b4f5-4451-8b16-4948f4576310, "UND_BOOOALGIFT", -1.0, 1, S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f);

PROC_TriggerRegisterForParty(S_UND_KuoToaGod_SpeakWithBOOOALTrigger_52658a25-6c89-45a2-ad12-f713b41cadf6);
PROC_TriggerRegisterForParty((TRIGGER)S_UND_KuoToa_FallToLairTrigger_43b674ed-2243-4b28-a31b-f00d3e3be483);
TriggerRegisterForItems(S_UND_KuoToa_FallToLairTrigger_43b674ed-2243-4b28-a31b-f00d3e3be483);

DB_IgnoreMutingStatussesDialog((DIALOGRESOURCE)UND_KuoToaGod_SpeakWithBOOOAL_492001e1-45fe-8c4d-18b8-8956ab45594e);
DB_IgnoreMutingStatussesDialog((DIALOGRESOURCE)UND_KuoToaGod_HighPriest_22d27cde-dc64-5170-71c9-47a20eb9c19a);

DB_GlobalFlagReactionAfterDialog((FLAG)UND_KuoToaGod_Event_BetrayedBOOOAL_279c8a6d-8278-8cff-07d7-471097bf6d1f, (DIALOGRESOURCE)UND_KuoToaGod_SpeakWithBOOOAL_492001e1-45fe-8c4d-18b8-8956ab45594e);

//REGION Give Item Events
DB_HasItemEvent((ITEM)S_UNI_SickleOfBOOOAL_78c0da13-b4f5-4451-8b16-4948f4576310, (FLAG)UND_KuoToaGod_State_HasSickle_0e0a25df-f8b0-d18a-3ff6-49a45fad8af3);
DB_GiveItemToEvent((ITEM)S_UNI_SickleOfBOOOAL_78c0da13-b4f5-4451-8b16-4948f4576310, (FLAG)UND_KuoToaGod_Event_GiveMagicItem_528f89e3-fcd8-7040-9c62-eae93df404f4);
//END_REGION

//REGION Followers
DB_UND_KuoToaGod_BOOOALFollowers((CHARACTER)S_UND_Kuotoa_001_840fd954-7c2b-41e4-b485-13b66659f598);
DB_UND_KuoToaGod_BOOOALFollowers((CHARACTER)S_UND_Kuotoa_003_db745e99-88bd-4e2c-8714-a85f7c24c145);
DB_UND_KuoToaGod_BOOOALFollowers((CHARACTER)S_UND_Kuotoa_004_b70b3d3f-8b4a-4334-a2a1-5cbbfaf5a403);
DB_UND_KuoToaGod_BOOOALFollowers((CHARACTER)S_UND_Kuotoa_006_faa76070-acc4-4e02-a89b-9a517e1e53e4);
DB_UND_KuoToaGod_BOOOALFollowers((CHARACTER)S_UND_Kuotoa_007_6d5e56d1-2c3e-4926-b3e9-f2e52b8725ef);
DB_UND_KuoToaGod_BOOOALFollowers((CHARACTER)S_UND_Kuotoa_008_bec56ac9-24b7-4bd1-8225-39f9e32eb0ca);
DB_UND_KuoToaGod_BOOOALFollowers((CHARACTER)S_UND_Kuotoa_009_ae14c77e-e623-433e-b64a-677c063599f7);
DB_UND_KuoToaGod_BOOOALFollowers((CHARACTER)S_UND_KuoToaGod_010_46233a43-62e7-4115-9e89-6bcf35012185);
DB_UND_KuoToaGod_BOOOALFollowers((CHARACTER)S_UND_KuoToaGod_HighPriest_a1f879ab-8731-4889-9b7b-d91b5094dbc0);

// Initial chanters
SetFlag(UND_KuoToaGod_State_Chanting_af777202-2b5b-4f0b-834c-be4c89184ca4, S_UND_Kuotoa_008_bec56ac9-24b7-4bd1-8225-39f9e32eb0ca);
SetFlag(UND_KuoToaGod_State_Chanting_af777202-2b5b-4f0b-834c-be4c89184ca4, S_UND_Kuotoa_003_db745e99-88bd-4e2c-8714-a85f7c24c145);
//END_REGION

//REGION Prepare Companions for Sacrifice
DB_OriginMayLeaveDialog((CHARACTER)S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679, (DIALOGRESOURCE)UND_KuoToaGod_SpeakWithBOOOAL_492001e1-45fe-8c4d-18b8-8956ab45594e);
DB_OriginMayLeaveDialog((CHARACTER)S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12, (DIALOGRESOURCE)UND_KuoToaGod_SpeakWithBOOOAL_492001e1-45fe-8c4d-18b8-8956ab45594e);
DB_OriginMayLeaveDialog((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, (DIALOGRESOURCE)UND_KuoToaGod_SpeakWithBOOOAL_492001e1-45fe-8c4d-18b8-8956ab45594e);
DB_OriginMayLeaveDialog((CHARACTER)S_Player_Wyll_c774d764-4a17-48dc-b470-32ace9ce447d, (DIALOGRESOURCE)UND_KuoToaGod_SpeakWithBOOOAL_492001e1-45fe-8c4d-18b8-8956ab45594e);
DB_OriginMayLeaveDialog((CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255, (DIALOGRESOURCE)UND_KuoToaGod_SpeakWithBOOOAL_492001e1-45fe-8c4d-18b8-8956ab45594e);
DB_OriginMayLeaveDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)UND_KuoToaGod_SpeakWithBOOOAL_492001e1-45fe-8c4d-18b8-8956ab45594e);

//END_REGION

//REGION XP Rewards
DB_QuestDef_State((FLAG)UND_KuoToaGod_Event_GiveBlessing_7b596518-1daa-44b1-0364-3e68cb730b99, "HIDDEN_WLD_Boosters", "UND_KuoToaGod_Blessed");
//END_REGION
KBSECTION
//REGION Situation Setup
// Start dialog when player approaches Kuo-Toa
// If dialog fails to start then make them hostile
IF
DB_InRegion(_Player, (TRIGGER)S_UND_KuoToaGod_SpeakWithBOOOALTrigger_52658a25-6c89-45a2-ad12-f713b41cadf6)
AND
DB_PartyMembers(_Player)
AND
QRY_OnlyOnce("UND_KuoToaGod_SpokeWithBOOOAL")
AND
NOT QRY_StartDialog((DIALOGRESOURCE)UND_KuoToaGod_SpeakWithBOOOAL_492001e1-45fe-8c4d-18b8-8956ab45594e, (CHARACTER)S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, (CHARACTER)S_UND_KuoToaGod_HighPriest_a1f879ab-8731-4889-9b7b-d91b5094dbc0, _Player)
THEN
PROC_SetRelationToPlayers((FACTION)Act1_UND_KuoToas_403d92b0-73a0-4275-a7f5-f49b717bd36b, 0);

// Kuo-Toa are lost in worship and won't react to monsters
// Also prevents them from reacting to shapeshifts during duels
IF
DB_UND_KuoToaGod_BOOOALFollowers(_Fishman)
THEN
DB_Dialogs(_Fishman, (DIALOGRESOURCE)UND_KuoToaGod_KuoToaMinion_3d06ecd5-12e5-c1b6-44a1-292116f1a320);
CharacterDisableCrime(_Fishman, "DangerousMonsterReaction");
CharacterDisableCrime(_Fishman, "DangerousMonsterReactionFallback");

IF
UseStarted(_Player, S_UND_BOOOALHotline_f98cd22e-53d9-4136-a554-fafbbcb170a7)
AND
DB_Players(_Player)
AND
NOT DB_GlobalFlag((FLAG)UND_KuoToaGod_State_FoughtGod_684933c7-b7a7-4ac5-9e54-9214711e5b11)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)UND_KuoToaGod_SpeakWithBOOOAL_492001e1-45fe-8c4d-18b8-8956ab45594e, (CHARACTER)S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, (CHARACTER)S_UND_KuoToaGod_HighPriest_a1f879ab-8731-4889-9b7b-d91b5094dbc0, _Player)
THEN
DB_NOOP(1);
//END_REGION

//REGION Character Management
IF
DB_UND_KuoToaGod_BOOOALFollowers(_Fishman)
THEN
DB_GLO_DefeatCounter(_Fishman, "UND_KuoToaGod_KillWorshippers");

IF
Died(_Fishman)
AND
DB_UND_KuoToaGod_BOOOALFollowers(_Fishman)
THEN
NOT DB_UND_KuoToaGod_BOOOALFollowers(_Fishman);
//END_REGION

//REGION Kuo-Toa Chanting
PROC
PROC_StateSet_Defeated(_DefeatedChanter)
AND
DB_UND_KuoToaGod_BOOOALFollowers((CHARACTER)_DefeatedChanter)
AND
GetFlag(UND_KuoToaGod_State_Chanting_af777202-2b5b-4f0b-834c-be4c89184ca4, _DefeatedChanter, 1)
AND
QRY_OnlyOnce_Reset("UND_KuoToaGod_SelectNewChanter")
AND
DB_UND_KuoToaGod_BOOOALFollowers(_NewChanter)
AND
GetFlag(UND_KuoToaGod_State_Chanting_af777202-2b5b-4f0b-834c-be4c89184ca4, _NewChanter, 0)
AND
NOT DB_Defeated(_NewChanter)
AND
QRY_OnlyOnce("UND_KuoToaGod_SelectNewChanter")
THEN
ClearFlag(UND_KuoToaGod_State_Chanting_af777202-2b5b-4f0b-834c-be4c89184ca4, _DefeatedChanter);
SetFlag(UND_KuoToaGod_State_Chanting_af777202-2b5b-4f0b-834c-be4c89184ca4, _NewChanter);
//END_REGION

//REGION Companion Sacrifice
IF
FlagSet(UND_KuoToaGod_State_SacrificedCompanion_1c40155d-1859-313e-cd78-c80a238d3dd5, _Companion, _)
THEN
SetTag(_Companion, BLOCK_RESURRECTION_22a75dbb-1588-407e-b559-5aa4e6d4e6a6);

IF
DB_InRegion(_Player, (TRIGGER)S_UND_KuoToaGod_SpeakWithBOOOALTrigger_52658a25-6c89-45a2-ad12-f713b41cadf6)
AND
DB_Players(_Player)
AND
DB_GlobalFlag(UND_KuoToaGod_State_SacrificeComplete_8716b20b-607b-494c-be58-ebd167512b9d)
AND
NOT DB_Is_InCombat(_Player, _)
AND
QRY_OnlyOnce("UND_KuoToaGod_CompletedSacrificeDialog")
AND
NOT QRY_StartDialog((DIALOGRESOURCE)UND_KuoToaGod_SpeakWithBOOOAL_492001e1-45fe-8c4d-18b8-8956ab45594e, (CHARACTER)S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, NULL_00000000-0000-0000-0000-000000000000, _Player)
THEN
DB_NOOP(1);

IF
KilledBy(_Companion, _, _, _)
AND
GetFlag(UND_KuoToaGod_State_SacrificedCompanion_1c40155d-1859-313e-cd78-c80a238d3dd5, _Companion, 1)
THEN
SetFlag(UND_KuoToaGod_State_SacrificeComplete_8716b20b-607b-494c-be58-ebd167512b9d, NULL_00000000-0000-0000-0000-000000000000, 0);
PlayEffect(_Companion, VFX_Script_WLD_SacrificedCompanion_01_1882f614-e7f3-03a8-3194-5b45a3ced83b, "", 1.0);
//END_REGION

//REGION Dialog Event Reactions
IF
DialogAttackRequested(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, _Player)
THEN
PROC_SetRelationMutual(Act1_UND_KuoToas_403d92b0-73a0-4275-a7f5-f49b717bd36b, Hero_a1542c81-6895-929e-4522-10ce218bb360, 0);

IF
FlagSet(UND_KuoToaGod_Event_GiveBlessing_7b596518-1daa-44b1-0364-3e68cb730b99, _Player, _)
THEN
PROC_UND_KuoToaGod_GiveBlessing();

PROC
PROC_UND_KuoToaGod_GiveBlessing()
AND
DB_Players(_Player)
THEN
ApplyStatus(_Player, "UND_BOOOALBLESSING", -1.0, 1, S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f);
DB_UND_KuoToaGod_BlessedCharacters(_Player);
//END_REGION

//REGION Dialog Fallback
// Fallback dialog has a passive skill check but there are no actual spoken lines
QRY
QRY_SpeakerIsAvailableForDialogSlot((GUIDSTRING)_Speaker,(DIALOGRESOURCE)UND_KuoToaGod_SpeakWithBOOOAL_Fallback_3631e862-d4eb-62a0-79a0-b98b5a419368,2,(INTEGER)_AllowStartIfCombat,_)
AND
DB_Players((CHARACTER)_Speaker)
AND
NOT DB_CantAct(_Speaker)
THEN
DB_NOOP(1);

QRY
QRY_SpeakerIsAvailableForDialogSlot((GUIDSTRING)_Speaker,(DIALOGRESOURCE)UND_KuoToaGod_SpeakWithBOOOAL_Fallback_3631e862-d4eb-62a0-79a0-b98b5a419368,3,(INTEGER)_AllowStartIfCombat,_)
AND
DB_Players((CHARACTER)_Speaker)
AND
NOT DB_CantAct(_Speaker)
THEN
DB_NOOP(1);
//END_REGION

//REGION Fighting BOOOAL
IF
DB_GlobalFlag(UND_KuoToaGod_Event_ChallengedBOOOAL_e181ba35-e538-4755-8c0d-c36633917448)
AND
DB_Players(_Player)
AND
QRY_OnlyOnce("UND_KuoToaGod_BOOOALArrives")
THEN
DB_SpotPlayers((CHARACTER)S_UND_KuoToaGod_HighPriest_a1f879ab-8731-4889-9b7b-d91b5094dbc0, "UND_KuoToaGod_HighPriestCongrats", NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);
PROC_UND_KuoToaGod_BOOOALAppears();

// Prevent Kuo-Toa from reacting to player shapeshifting during duel
IF
DB_GlobalFlag(UND_KuoToaGod_Event_ChallengedBOOOAL_e181ba35-e538-4755-8c0d-c36633917448)
AND
DB_UND_KuoToaGod_BOOOALFollowers(_KuoToa)
THEN
CharacterDisableCrime(_KuoToa, "DangerousMonsterReaction");
CharacterDisableCrime(_KuoToa, "DangerousMonsterReactionFallback");

IF
CombatStarted(_CombatID)
AND
DB_Is_InCombat(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, _CombatID)
AND
DB_GlobalFlag(UND_KuoToaGod_Event_ChallengedBOOOAL_e181ba35-e538-4755-8c0d-c36633917448)
AND
DB_Is_InCombat(_Player, _CombatID)
AND
DB_Players((CHARACTER)_Player)
THEN
DB_UND_KuoToaGod_BOOOALDuelCombatID(_CombatID);

IF
SwitchedCombat(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, _CombatID, _NewID)
AND
DB_UND_KuoToaGod_BOOOALDuelCombatID(_CombatID)
THEN
NOT DB_UND_KuoToaGod_BOOOALDuelCombatID(_CombatID);
DB_UND_KuoToaGod_BOOOALDuelCombatID(_NewID);

IF
CombatEnded(_CombatID)
AND
DB_UND_KuoToaGod_BOOOALDuelCombatID(_CombatID)
AND
DB_Sees((CHARACTER)S_UND_KuoToaGod_HighPriest_a1f879ab-8731-4889-9b7b-d91b5094dbc0, _Player)
AND
DB_Players(_Player)
AND
DB_GlobalFlag((FLAG)UND_KuoToaGod_State_UsurpedBOOOAL_215b8fc4-936f-4de1-a186-dfe5b88fec0a)
AND
QRY_OnlyOnce("UND_KuoToaGod_CompletedSacrificeDialog")
AND
QRY_StartDialog((DIALOGRESOURCE)UND_KuoToaGod_HighPriest_22d27cde-dc64-5170-71c9-47a20eb9c19a, (CHARACTER)S_UND_KuoToaGod_HighPriest_a1f879ab-8731-4889-9b7b-d91b5094dbc0, _Player)
THEN
DB_NOOP(1);

IF
EnteredCombat(_Fishman, _)
AND
DB_UND_KuoToaGod_BOOOALFollowers((CHARACTER)_Fishman)
AND
QRY_OnlyOnce("UND_KuoToaGod_BOOOALArrives")
THEN
RemoveStatus(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, "INVISIBLE");
TeleportTo(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, S_UND_KuoToaGod_GodAppearPoint_ae9a55db-9090-4a79-8a30-05355d824ebf, "UND_KuoToaGod_GodAppeared");
PROC_TryStartAD((DIALOGRESOURCE)UND_KuoToaGod_AD_BOOOALAppears_b3371f04-6c2d-44f1-124f-2b4c4a4f02a8, (CHARACTER)S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f);

IF
EnteredCombat(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, _)
THEN
SetFlag(UND_KuoToaGod_State_FoughtGod_684933c7-b7a7-4ac5-9e54-9214711e5b11, NULL_00000000-0000-0000-0000-000000000000, 0);
DB_ItemDialog_NarratorAD((ITEM)S_UND_BOOOALHotline_f98cd22e-53d9-4136-a554-fafbbcb170a7, (DIALOGRESOURCE)UND_KuoToaGod_AD_NoAnswerFromGod_bf1fd243-bd7a-258a-49ff-df0ad8be0890);

IF
EntityEvent(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, "UND_KuoToaGod_GodAppeared")
THEN
PlayEffect(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, VFX_Script_UND_KuoToaGod_Appears_BodyFX_01_d7291d5b-a372-8839-1b1e-d6e62675ccfb, "Dummy_BodyFX");

PROC
PROC_UND_KuoToaGod_BOOOALAppears()
THEN
RemoveStatus(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, "INVISIBLE");
SetCombatGroupID(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, "");
TeleportTo(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, S_UND_KuoToaGod_GodAppearPoint_ae9a55db-9090-4a79-8a30-05355d824ebf, "UND_KuoToaGod_GodAppeared");
ApplyStatus(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, "BLOODLUST_REDCAP", -1.0, 1, S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f);

IF
Died(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f)
AND
DB_UND_KuoToaGod_BOOOALFollowers(_Fishman)
AND
QRY_OnlyOnce("UND_KuoToaGod_BOOOALDead")
AND
QRY_UND_KuoToaGod_DeathReactionInCombat(_Fishman)
THEN
DB_NOOP(1);

PROC
PROC_KnockedOut(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f,_Cause)
THEN
Die(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f,DEATHTYPE.DoT,_Cause,1,0,0.0);

PROC
PROC_StateSet_PermaDefeated(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f)
THEN
SetFlag(UND_KuoToaGod_State_UsurpedBOOOAL_215b8fc4-936f-4de1-a186-dfe5b88fec0a, NULL_00000000-0000-0000-0000-000000000000, 0);
PROC_RemoveDialog((CHARACTER)S_UND_KuoToaGod_HighPriest_a1f879ab-8731-4889-9b7b-d91b5094dbc0);
DB_Dialogs((CHARACTER)S_UND_KuoToaGod_HighPriest_a1f879ab-8731-4889-9b7b-d91b5094dbc0, (DIALOGRESOURCE)UND_KuoToaGod_HighPriest_22d27cde-dc64-5170-71c9-47a20eb9c19a);

PROC
PROC_GlobalFlagReactionAfterDialog((FLAG)UND_KuoToaGod_Event_BetrayedBOOOAL_279c8a6d-8278-8cff-07d7-471097bf6d1f)
THEN
PROC_UND_KuoToaGod_BOOOALAppears();
PROC_SetRelationMutual((FACTION)Act1_UND_KuoToas_403d92b0-73a0-4275-a7f5-f49b717bd36b, (FACTION)Act1_UND_KuoToaGod_717865d5-c6d1-4b6e-8d3d-8454de92709a, 0);

//if not in combat, do AD immediately
QRY
QRY_UND_KuoToaGod_DeathReactionInCombat((CHARACTER)_Fishman)
AND
IsInCombat(_Fishman,0)
THEN
PROC_TryStartAD((DIALOGRESOURCE)UND_KuoToaGod_AD_GodDead_bc7f5bbf-114d-1941-06df-f3bc7a4c5f33, _Fishman);

//if in combat, delay to next fish turn
QRY
QRY_UND_KuoToaGod_DeathReactionInCombat((CHARACTER)_Fishman)
AND
IsInCombat(_Fishman,1)
THEN
DB_UND_KuoToaGod_DeathReactionInCombat(1);
//END_REGION

//REGION Kuo-Toa Magic
PROC
PROC_UND_KuoToaGod_PlayBlessingGoneAD()
AND
DB_UND_KuoToaGod_BlessedCharacters(_Player)
AND
QRY_OnlyOnce("UND_KuoToaGod_MagicGone")
THEN
StartVoiceBark(UND_KuoToaGod_VB_MagicGone_e3c1c0f6-151d-1835-96a1-c017bf76cee9, _Player);

PROC
PROC_UND_KuoToaGod_PlayWeaponMagicGoneAD()
AND
DB_Players(_Player)
AND
IsInInventoryOf(S_UNI_SickleOfBOOOAL_78c0da13-b4f5-4451-8b16-4948f4576310, _Player, 1)
AND
QRY_OnlyOnce("UND_KuoToaGod_MagicGone")
THEN
StartVoiceBark(UND_KuoToaGod_VB_MagicGone_e3c1c0f6-151d-1835-96a1-c017bf76cee9, _Player);

PROC
PROC_UND_KuoToaGod_TransformSickle()
AND
HasActiveStatus(S_UNI_SickleOfBOOOAL_78c0da13-b4f5-4451-8b16-4948f4576310, "UND_BOOOALGIFT", 1)
THEN
RemoveStatus(S_UNI_SickleOfBOOOAL_78c0da13-b4f5-4451-8b16-4948f4576310, "UND_BOOOALGIFT");
PROC_UND_KuoToaGod_PlayWeaponMagicGoneAD();

PROC
PROC_UND_KuoToaGod_RemoveBlessing()
AND
DB_UND_KuoToaGod_BlessedCharacters(_Player)
AND
HasActiveStatus(_Player, "UND_BOOOALBLESSING", 1)
THEN
PROC_UND_KuoToaGod_PlayBlessingGoneAD();
RemoveStatus(_Player, "UND_BOOOALBLESSING");
NOT DB_UND_KuoToaGod_BlessedCharacters(_Player);

// If the Kuo-Toa are killed their magic wears away
PROC
PROC_GLO_DefeatCounter_AllPermaDefeated("UND_KuoToaGod_KillWorshippers")
THEN
ClearFlag((FLAG)END_GatherYourAllies_Event_KuotoaPromisedENDSupport_099fbdae-4e4f-eecd-6747-8d5a9f59fe22);
PROC_UND_KuoToaGod_TransformSickle();
PROC_UND_KuoToaGod_RemoveBlessing();
//END_REGION

//REGION Fall into Lair
IF
EnteredTrigger(_Character, S_UND_KuoToa_FallToLairTrigger_43b674ed-2243-4b28-a31b-f00d3e3be483)
AND
DB_Players(_Character)
THEN
TeleportTo(_Character, S_UND_KuoToa_LairFallEntranceTrigger_9f26ec07-c5d4-40c9-bfba-b6fbe0855bde, "", 0, 0, 1);

IF
EnteredTrigger(_Character, S_UND_KuoToa_FallToLairTrigger_43b674ed-2243-4b28-a31b-f00d3e3be483)
AND
DB_Players(_Character)
THEN
PROC_UND_KuoToaGod_TeleportPlayerWithFade(_Character, S_UND_KuoToa_LairFallEntranceTrigger_9f26ec07-c5d4-40c9-bfba-b6fbe0855bde);

IF
ItemEnteredTrigger(_Item, S_UND_KuoToa_FallToLairTrigger_43b674ed-2243-4b28-a31b-f00d3e3be483, _)
AND
_Item != S_UND_KuoToa_EntranceLadder_0f909361-5e0c-4ca7-b06d-d3216e63d073
AND
Exists(_Item,1)
THEN
TeleportTo(_Item, S_UND_KuoToa_LairFallEntranceTrigger_9f26ec07-c5d4-40c9-bfba-b6fbe0855bde, "", 0, 0, 1);

// Player has separate teleport to prevent fade from happening after teleport
PROC
PROC_UND_KuoToaGod_TeleportPlayerWithFade((CHARACTER)_Character, (GUIDSTRING)_Target)
AND
GetUUID(_Character, _UserIDString)
AND
GUIDToString(_Target, _TargetID)
AND
Concatenate(_TargetID, _UserIdString, _FadeId)
THEN
DB_UND_KuoToaGod_FadeEvents(_Character, _FadeId);
ScreenFadeTo(_Character, 2.0, 0, _FadeId);
TeleportTo(_Character, _Target, "", 0, 0, 1);

IF
ScreenFadeDone(_UserId, _FadeId)
AND
DB_UND_KuoToaGod_FadeEvents(_Character, _FadeId)
THEN
NOT DB_UND_KuoToaGod_FadeEvents(_Character, _FadeId);
ClearScreenFade(_Character, 2.0, _FadeId);
//END_REGION

//REGION Debug
IF
TextEvent("UND_KuoToaGod_TransformSickle")
THEN
PROC_UND_KuoToaGod_TransformSickle();

IF
TextEvent("UND_KuoToaGod_GiveBlessing")
THEN
PROC_UND_KuoToaGod_GiveBlessing();

IF
TextEvent("UND_KuoToaGod_GiveSickle")
AND
QRY_OnlyOnce_Reset("UND_KuoToaGod_GiveSickleDebug")
AND
DB_Players(_Player)
AND
QRY_OnlyOnce("UND_KuoToaGod_GiveSickleDebug")
THEN
SetFlag(UND_KuoToaGod_Event_GiveMagicItem_528f89e3-fcd8-7040-9c62-eae93df404f4, _Player, 0);

IF
TextEvent("UND_KuoToaGod_ChallengeGod")
THEN
SetFlag(UND_KuoToaGod_Event_ChallengedBOOOAL_e181ba35-e538-4755-8c0d-c36633917448, NULL_00000000-0000-0000-0000-000000000000, 0);
DB_OnlyOnce("UND_KuoToaGod_SpokeWithBOOOAL");

IF
TextEvent("UND_KuoToaGod_RevealGod")
THEN
RemoveStatus(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, "INVISIBLE");
SetCombatGroupID(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, "");
TeleportTo(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, S_UND_KuoToaGod_GodAppearPoint_ae9a55db-9090-4a79-8a30-05355d824ebf, "UND_KuoToaGod_GodAppeared");
ApplyStatus(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, "BLOODLUST_REDCAP", -1.0, 1, S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f);

IF
TextEvent("UND_KuoToaGod_HideGod")
THEN
ApplyStatus(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f, "INVISIBLE", -1.0, 1);

IF
TextEvent("UND_KuoToaGod_StartHighPriestDialog")
AND
DB_Avatars(_Player)
AND
QRY_OnlyOnce_Reset("UND_KuoToadGod_Debug_StartHighPriestDialog")
AND
QRY_StartDialog((DIALOGRESOURCE)UND_KuoToaGod_HighPriest_22d27cde-dc64-5170-71c9-47a20eb9c19a, (CHARACTER)S_UND_KuoToaGod_HighPriest_a1f879ab-8731-4889-9b7b-d91b5094dbc0, _Player)
THEN
DB_NOOP(1);
//END_REGION

//REGION Peaceful Resolution XP
// If we duel & kill BOOOAL and the KuaToa become our worshippers, then we get XP for them
IF
FlagSet((FLAG)UND_KuoToaGod_State_UsurpedBOOOAL_215b8fc4-936f-4de1-a186-dfe5b88fec0a, _, _)
AND
DB_GlobalFlag((FLAG)UND_KuoToaGod_Event_ChallengedBOOOAL_e181ba35-e538-4755-8c0d-c36633917448)
AND
DB_UND_KuoToaGod_BOOOALFollowers(_KuoToa)
THEN
PROC_PeacefulResolve(_KuoToa);

IF
FlagSet((FLAG)UND_KuoToaGod_State_UsurpedBOOOAL_215b8fc4-936f-4de1-a186-dfe5b88fec0a, _, _)
AND
DB_GlobalFlag((FLAG)UND_KuoToaGod_Event_BetrayedBOOOAL_279c8a6d-8278-8cff-07d7-471097bf6d1f)
AND
DB_UND_KuoToaGod_BOOOALFollowers(_KuoToa)
THEN
PROC_PeacefulResolve(_KuoToa);

// If we become BOOOAL's choosen, then we get XP both for them and BOOOAL
IF
FlagSet((FLAG)UND_KuoToaGod_State_PlayerIsChosen_fffbe2b1-beee-a2cb-5e91-78e17bc71c05, _, _)
THEN
PROC_PeacefulResolve(S_UND_KuoToaGod_BOOOAL_abede50d-0d73-412b-8912-c9a4b900493f);

IF
FlagSet((FLAG)UND_KuoToaGod_State_PlayerIsChosen_fffbe2b1-beee-a2cb-5e91-78e17bc71c05, _, _)
AND
DB_UND_KuoToaGod_BOOOALFollowers(_KuoToa)
THEN
PROC_PeacefulResolve(_KuoToa);
//END_REGION

//REGION VFX for Weapons
IF
StatusApplied(_Follower, "UND_BOOOALSERVANT", _, _)
AND
GetEquippedWeapon((CHARACTER)_Follower, _Weapon)
THEN
DB_UND_KuoToaGod_ActivatedWeapon((GUIDSTRING)_Follower, (ITEM)_Weapon);
ApplyStatus(_Weapon, "UND_BOOOALSERVANT_WEAPON", -1.0);

IF
StatusRemoved(_Follower, "UND_BOOOALSERVANT", _, _)
AND
DB_UND_KuoToaGod_ActivatedWeapon(_Follower, _Weapon)
THEN
RemoveStatus(_Weapon, "UND_BOOOALSERVANT_WEAPON");
NOT DB_UND_KuoToaGod_ActivatedWeapon(_Follower, _Weapon);

// if we disarm a KuoToa and put the weapon
// in a container / inventory, then the status is removed
IF
AddedTo((ITEM)_Weapon, _InventoryHolder, _)
AND
DB_UND_KuoToaGod_ActivatedWeapon(_Follower, _Weapon)
AND
_Follower != _InventoryHolder
THEN
RemoveStatus(_Weapon, "UND_BOOOALSERVANT_WEAPON");
NOT DB_UND_KuoToaGod_ActivatedWeapon(_Follower, _Weapon);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
