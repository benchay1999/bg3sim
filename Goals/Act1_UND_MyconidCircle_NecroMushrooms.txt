Version 1
SubGoalCombiner SGC_AND
INITSECTION
PROC_TriggerRegisterForPlayers((TRIGGER)S_UND_MyconidCircle_MainArea_a08c9f65-06ed-4f36-95a9-2330fa9462da);

// Used to prevent Anubis configs of spore servants being overridden with DefaultCharacter
DB_SporeServantBehaviourExceptions((CHARACTER)S_UND_PlantDuergar_002_0028d0c1-1fb1-4c0f-baad-bf44eeae6dc1);
DB_SporeServantBehaviourExceptions((CHARACTER)S_UND_PlantDuergar_003_6da6d51e-8cf6-4b49-a780-3f46971d5b33);

// Used to prevent their dialogs from being removed when spore servants are ressed
DB_SporeServantDialogExceptions((CHARACTER)S_UND_PlantDuergar_002_0028d0c1-1fb1-4c0f-baad-bf44eeae6dc1);
DB_SporeServantDialogExceptions((CHARACTER)S_UND_PlantDuergar_003_6da6d51e-8cf6-4b49-a780-3f46971d5b33);

DB_SporeServantDialogExceptions(S_UND_MyconidCircle_ServantHookHorror_01_6ccbf16f-4c9f-4975-b43d-e8569fd1c566);
DB_SporeServantDialogExceptions(S_UND_PlantDuergar_000_96b1fc15-f658-4451-9dea-3692115da9a1);
DB_SporeServantDialogExceptions(S_UND_PlantDuergar_001_be7b7b75-03fb-4040-9d8c-14e1e8d9882c);

PROC_TriggerRegisterForPlayers(S_UND_MyconidCircle_StartIntroEventTrigger_471e9e76-448b-49dc-bdc2-231550fa399c);

DB_UND_MyconidCircle_AdditionsToKillCount((CHARACTER)S_UND_PlantDuergar_002_0028d0c1-1fb1-4c0f-baad-bf44eeae6dc1);
DB_UND_MyconidCircle_AdditionsToKillCount((CHARACTER)S_UND_PlantDuergar_003_6da6d51e-8cf6-4b49-a780-3f46971d5b33);

DB_UND_MyconidCircle_AnimatingSporesSpells("Target_UND_MyconidSovereign_FakeAnimatingSpores");
DB_UND_MyconidCircle_AnimatingSporesSpells("Target_AnimatingSpores");
KBSECTION
//REGION Introduction 
IF
EnteredTrigger(_Player,S_UND_MyconidCircle_MainArea_a08c9f65-06ed-4f36-95a9-2330fa9462da)
THEN
SetFlag(
    (FLAG)UND_MyconidCircle_State_Visited_5c470f79-d252-49e0-af96-94cebd100359,
    NULL_00000000-0000-0000-0000-000000000000,0);
PROC_TriggerUnregisterForPlayers((TRIGGER)S_UND_MyconidCircle_MainArea_a08c9f65-06ed-4f36-95a9-2330fa9462da);

IF
DB_InRegion(_Player,S_UND_MyconidCircle_StartIntroEventTrigger_471e9e76-448b-49dc-bdc2-231550fa399c)
AND
DB_Players(_Player)
AND
DB_UND_MyconidCircle_CurrentSovereign(_Sovereign)
AND
NOT DB_GlobalFlag(UND_MyconidCircle_State_Hostile_d0fe87bc-0b67-e446-1543-ac3b5e07e16d)
AND
NOT DB_CantAct(_Sovereign)
AND
IsInTrigger(_Sovereign, S_UND_MyconidCircle_StartIntroEventTrigger_471e9e76-448b-49dc-bdc2-231550fa399c, 1)
AND
QRY_OnlyOnce("UND_MyconidSovereign_PlayerIntro")
THEN
PROC_GlobalSetFlagAndCache((FLAG)UND_MyconidSovereign_State_PlayerSawResurrection_0fc153f3-fff5-92b6-9834-b72e920423b7);
StartVoiceBark(UND_MyconidCircle_VB_SawCorpse_dd67fc5d-0a77-36a9-21ab-670974b8478e,_Player);
PROC_TriggerUnregisterForPlayers(S_UND_MyconidCircle_StartIntroEventTrigger_471e9e76-448b-49dc-bdc2-231550fa399c);
//END_REGION


IF
Resurrected(_Char)
AND
DB_UND_MyconidCircle_AdditionsToKillCount(_Char)
THEN
DB_GLO_DefeatCounter(_Char, "UND_MyconidCircle_KillCounter");

IF
CastedSpell(S_UND_MyconidSovereign_ea0f222f-eaad-4d83-bbcd-cbae51ccf265, _Spell, _, _, _)
AND
DB_UND_MyconidCircle_AnimatingSporesSpells(_Spell)
AND
DB_Sees(_Player, S_UND_MyconidSovereign_ea0f222f-eaad-4d83-bbcd-cbae51ccf265)
AND
DB_Players(_Player)
AND
NOT DB_GlobalFlag((FLAG)UND_MyconidSovereign_State_PlayerSawResurrection_0fc153f3-fff5-92b6-9834-b72e920423b7)
THEN
PROC_GlobalSetFlagAndCache((FLAG)UND_MyconidSovereign_State_PlayerSawResurrection_0fc153f3-fff5-92b6-9834-b72e920423b7);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
