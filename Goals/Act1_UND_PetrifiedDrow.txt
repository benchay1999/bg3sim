Version 1
SubGoalCombiner SGC_AND
INITSECTION
//creature, petrified status, dialog, default faction, charmed faction, spectator's appear position, display name, title
DB_UND_PetrifiedDrow_PetrifiedCreatures((CHARACTER)S_UND_PetrifiedDrow_Wizard_1f86d2de-db96-4662-a360-6ba5ad902fd7, "UND_SPECTATOR_PETRIFIED_001", (DIALOGRESOURCE)UND_PetrifiedDrow_Wizard_7a4fc9ca-c472-ab4a-669a-549fdf1b5758,	
	(FACTION)ACT1_UND_PetrifiedDrow_Drow_6a05bdb0-d46a-4ecc-9740-f32e39928448, (FACTION)ACT1_UND_PetrifiedDrow_DrowCharmed_1a5300f3-3abd-475e-a1f8-9c1fd2b9bfe7, 
	(TRIGGER)S_UND_PetrifiedDrow_SpectatorAppearTrigger_01_7fc44a28-a2df-4646-98cb-7e0a274ea0e6, "UND_PetrifiedDrow_Unpetrified_Wizard", "UND_PetrifiedDrow_Unpetrified_Wizard_Title");

DB_UND_PetrifiedDrow_PetrifiedCreatures((CHARACTER)S_UND_PetrifiedDrow_002_34722f55-c90a-4458-8740-7e3620cb9600, "UND_SPECTATOR_PETRIFIED_002", (DIALOGRESOURCE)UND_PetrifiedDrow_002_110f033b-f902-de7e-4ed9-5fd1b9281ca7, 	
	(FACTION)ACT1_UND_PetrifiedDrow_Drow_6a05bdb0-d46a-4ecc-9740-f32e39928448, (FACTION)ACT1_UND_PetrifiedDrow_DrowCharmed_1a5300f3-3abd-475e-a1f8-9c1fd2b9bfe7,
	(TRIGGER)S_UND_PetrifiedDrow_SpectatorAppearTrigger_01_7fc44a28-a2df-4646-98cb-7e0a274ea0e6, "UND_PetrifiedDrow_Unpetrified_Drow_002", "");

DB_UND_PetrifiedDrow_PetrifiedCreatures((CHARACTER)S_UND_PetrifiedDrow_003_7d816c9b-2274-423f-bc81-7e1e4a889e12, "UND_SPECTATOR_PETRIFIED_003", (DIALOGRESOURCE)UND_PetrifiedDrow_003_b1ec0556-c902-6be6-4dee-84863deb9cdb,	
	(FACTION)ACT1_UND_PetrifiedDrow_Drow_6a05bdb0-d46a-4ecc-9740-f32e39928448, (FACTION)ACT1_UND_PetrifiedDrow_DrowCharmed_1a5300f3-3abd-475e-a1f8-9c1fd2b9bfe7,
	(TRIGGER)S_UND_PetrifiedDrow_SpectatorAppearTrigger_02_0aaf2911-6173-4bd6-81be-3329e0faef5f, "UND_PetrifiedDrow_Unpetrified_Drow_003", "");

DB_UND_PetrifiedDrow_PetrifiedCreatures((CHARACTER)S_UND_PetrifiedDrow_004_28830520-e1e4-4fa8-be35-3e9feee219c9, "UND_SPECTATOR_PETRIFIED_004", (DIALOGRESOURCE)UND_PetrifiedDrow_004_dd36835a-e414-237e-a5ed-87fcf7d1653c,
	(FACTION)ACT1_UND_PetrifiedDrow_Drow_6a05bdb0-d46a-4ecc-9740-f32e39928448, (FACTION)ACT1_UND_PetrifiedDrow_DrowCharmed_1a5300f3-3abd-475e-a1f8-9c1fd2b9bfe7,
	(TRIGGER)S_UND_PetrifiedDrow_SpectatorAppearTrigger_02_0aaf2911-6173-4bd6-81be-3329e0faef5f, "UND_PetrifiedDrow_Unpetrified_Drow_004", "");

DB_UND_PetrifiedDrow_PetrifiedCreatures((CHARACTER)S_UND_PetrifiedDrow_005_7f57e4f0-125a-4a09-85df-81465b40cde4, "UND_SPECTATOR_PETRIFIED_005", (DIALOGRESOURCE)UND_PetrifiedDrow_005_6741ce8e-cd9c-956e-4f59-6cfa54e62a8a,
	(FACTION)ACT1_UND_PetrifiedDrow_Drow_6a05bdb0-d46a-4ecc-9740-f32e39928448, (FACTION)ACT1_UND_PetrifiedDrow_DrowCharmed_1a5300f3-3abd-475e-a1f8-9c1fd2b9bfe7,
	(TRIGGER)S_UND_PetrifiedDrow_SpectatorAppearTrigger_01_7fc44a28-a2df-4646-98cb-7e0a274ea0e6, "UND_PetrifiedDrow_Unpetrified_Drow_005", "");

DB_UND_PetrifiedDrow_PetrifiedCreatures((CHARACTER)S_UND_PetrifiedDrow_006_0d5f2f17-9827-45c1-aa6e-67ff0aa4f2f1, "UND_SPECTATOR_PETRIFIED_002", (DIALOGRESOURCE)UND_PetrifiedDrow_006_f8a814cc-9b61-1fc9-6041-a3eb3a2f18f9,	
	(FACTION)ACT1_UND_PetrifiedDrow_Drow_6a05bdb0-d46a-4ecc-9740-f32e39928448, (FACTION)ACT1_UND_PetrifiedDrow_DrowCharmed_1a5300f3-3abd-475e-a1f8-9c1fd2b9bfe7,
	(TRIGGER)S_UND_PetrifiedDrow_SpectatorAppearTrigger_01_7fc44a28-a2df-4646-98cb-7e0a274ea0e6, "UND_PetrifiedDrow_Unpetrified_Drow_006", "");

SetVisible(S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0, 0);
SetOnStage(S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0, 0);
SetFlag(UND_PetrifiedDrow_State_CanShootUnpetrificationRay_880a4a7f-d2cf-4eae-b31d-52318c279672, S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0);

PROC_TriggerRegisterForPlayers(S_UND_PetrifiedDrow_CinematicTrigger_baf2974e-15bd-4da6-9e34-08ce3e6ae059);
DB_AddCharactersInTriggerToDialog(UND_PetrifiedDrow_CINE_SpectatorReveal_2d104570-1a14-53b1-b98e-97e6fc6f933a, S_UND_PetrifiedDrow_CinematicTrigger_baf2974e-15bd-4da6-9e34-08ce3e6ae059, 1, 1, 1);
DB_IgnoreMutingStatussesDialog((DIALOGRESOURCE)UND_PetrifiedDrow_CINE_SpectatorReveal_2d104570-1a14-53b1-b98e-97e6fc6f933a);
DB_DialogBlockTradeButton((DIALOGRESOURCE)UND_PetrifiedDrow_CINE_SpectatorReveal_2d104570-1a14-53b1-b98e-97e6fc6f933a);
DB_DialogBlockAttackButton((DIALOGRESOURCE)UND_PetrifiedDrow_CINE_SpectatorReveal_2d104570-1a14-53b1-b98e-97e6fc6f933a);

DB_UND_PetrifiedDrow_AmbushAreas((TRIGGER)S_UND_PetrifiedDrow_AmbushArea_01_3b117066-c0e8-4303-9d21-9fde802a2088);

DB_GLO_ExclusiveFlag("UND_PetrifiedDrow_Wizard", (FLAG)UND_PetrifiedDrow_State_WizardPetrified_acd52f8e-e154-4bd7-96ca-7c9889857ebb);
DB_GLO_ExclusiveFlag("UND_PetrifiedDrow_Wizard", (FLAG)UND_PetrifiedDrow_State_WizardUnpetrified_9dfc3375-b93e-43de-880f-4244aad43bd8);
DB_GLO_ExclusiveFlag("UND_PetrifiedDrow_Wizard", (FLAG)UND_PetrifiedDrow_State_WizardPermaDefeated_35d5f1c6-26b1-4f9a-96f7-d6498e3bfc7b);
SetFlag(UND_PetrifiedDrow_State_WizardPetrified_acd52f8e-e154-4bd7-96ca-7c9889857ebb, NULL_00000000-0000-0000-0000-000000000000);

DB_GlobalFlagReactionAfterDialog(UND_PetrifiedDrow_State_HostileResolution_0e62f611-fe1b-47f1-8bd7-e89e4068a1bd, (DIALOGRESOURCE)UND_PetrifiedDrow_Wizard_7a4fc9ca-c472-ab4a-669a-549fdf1b5758);
DB_GlobalFlagReactionAfterDialog(UND_PetrifiedDrow_State_PeacefulResolution_e5d3cf4d-3dd4-4b8d-a04e-147f24fec1d4, (DIALOGRESOURCE)UND_PetrifiedDrow_Wizard_7a4fc9ca-c472-ab4a-669a-549fdf1b5758);

DB_PermaDefeatedFlag((CHARACTER)S_UND_PetrifiedDrow_Wizard_1f86d2de-db96-4662-a360-6ba5ad902fd7, (FLAG)UND_PetrifiedDrow_State_WizardPermaDefeated_35d5f1c6-26b1-4f9a-96f7-d6498e3bfc7b);
DB_GLO_CharacterCorpseDialog((CHARACTER)S_UND_PetrifiedDrow_Wizard_1f86d2de-db96-4662-a360-6ba5ad902fd7, (DIALOGRESOURCE)UND_PetrifiedDrow_Dead_Wizard_fc9a8d4c-9ab9-7751-1a06-5e97cd36446e);
DB_GlobalFlagReactionAfterDialog((FLAG)UND_PetrifiedDrow_Event_MentionedForge_36fd33c7-f0f2-432d-ac6e-7c5bcb5b110a, (DIALOGRESOURCE)UND_PetrifiedDrow_Dead_Wizard_fc9a8d4c-9ab9-7751-1a06-5e97cd36446e);
DB_GlobalFlagReactionAfterDialog((FLAG)UND_PetrifiedDrow_Event_MentionedForge_36fd33c7-f0f2-432d-ac6e-7c5bcb5b110a, (DIALOGRESOURCE)UND_PetrifiedDrow_Wizard_7a4fc9ca-c472-ab4a-669a-549fdf1b5758);
DB_GlobalFlagReactionAfterDialog((FLAG)UND_PetrifiedDrow_Event_LearnedForgeFromMap_7ccd223a-6df5-49ea-9e62-44dace429b94, (DIALOGRESOURCE)UND_PetrifiedDrow_MemoryCrystal_320d13c5-7de7-633e-fde2-54b93865aa2a);
DB_FlagReactionAfterDialog((FLAG)UND_PetrifiedDrow_Event_MemoryCrystalRejected_e29001a0-8c2b-3067-008d-06983f1ef83b, (DIALOGRESOURCE)UND_PetrifiedDrow_MemoryCrystal_320d13c5-7de7-633e-fde2-54b93865aa2a);

DB_GLO_DefeatCounter(S_UND_PetrifiedDrow_Wizard_1f86d2de-db96-4662-a360-6ba5ad902fd7, "UND_PetrifiedDrow_AllDead");
DB_GLO_DefeatCounter(S_UND_PetrifiedDrow_002_34722f55-c90a-4458-8740-7e3620cb9600, "UND_PetrifiedDrow_AllDead");
DB_GLO_DefeatCounter(S_UND_PetrifiedDrow_003_7d816c9b-2274-423f-bc81-7e1e4a889e12, "UND_PetrifiedDrow_AllDead");
DB_GLO_DefeatCounter(S_UND_PetrifiedDrow_004_28830520-e1e4-4fa8-be35-3e9feee219c9, "UND_PetrifiedDrow_AllDead");
DB_GLO_DefeatCounter(S_UND_PetrifiedDrow_005_7f57e4f0-125a-4a09-85df-81465b40cde4, "UND_PetrifiedDrow_AllDead");
DB_GLO_DefeatCounter(S_UND_PetrifiedDrow_006_0d5f2f17-9827-45c1-aa6e-67ff0aa4f2f1, "UND_PetrifiedDrow_AllDead");
DB_GLO_DefeatCounter(S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0, "UND_PetrifiedDrow_AllDead");
DB_GLO_DefeatCounter_AllPermaDefeatedGlobalFlag("UND_PetrifiedDrow_AllDead", (FLAG)UND_PetrifiedDrow_State_AllDead_8525d155-11d0-40bc-8f40-87ce6c6e1256);

//REGION Memory Crystal
DB_Dialogs((ITEM)S_UND_PetrifiedDrow_MemoryCrystal_d0791906-a696-4bd0-9b2d-398ea5829769, (DIALOGRESOURCE)UND_PetrifiedDrow_MemoryCrystal_320d13c5-7de7-633e-fde2-54b93865aa2a);

DB_HasItemEvent((ITEM)S_UND_PetrifiedDrow_MemoryCrystal_d0791906-a696-4bd0-9b2d-398ea5829769, (FLAG)UND_PetrifiedDrow_State_HasMap_0731d61a-f17a-434a-90b0-6933b2390491);
DB_GiveItemToEvent((ITEM)S_UND_PetrifiedDrow_MemoryCrystal_d0791906-a696-4bd0-9b2d-398ea5829769, (FLAG)UND_PetrifiedDrow_Event_GiveMap_9f25ea6f-fd19-42bb-9149-58bbdc5cae75);
//END_REGION Memory Crystal
KBSECTION
//REGION Init
IF
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Creature, _PetrifiedStatus, _, _, _, _, _, _)
THEN
ApplyStatus(_Creature, _PetrifiedStatus, -1.0);
PROC_CharacterDisableAllCrimes(_Creature, 1);
SetCanJoinCombat(_Creature, 0);
SetAmbushing(_Creature, 1);

IF
DB_UND_PetrifiedDrow_AmbushAreas(_AmbushArea)
THEN
PROC_TriggerRegisterForPlayers(_AmbushArea);
//END_REGION Init

// Ambush is triggerd if the player enters one of the Ambush Triggers without being hidden 
// OR unpetrifies a petrified creature 
// OR attacks a petrified creature
//REGION Ambush
IF
DB_InRegion(_Player, _AmbushArea)
AND
DB_UND_PetrifiedDrow_AmbushAreas(_AmbushArea)
AND
DB_Players(_Player)
AND
NOT DB_HiddenCharacters(_Player, _)
THEN
PROC_UND_PetrifiedDrow_RevealSpectator(_Player, (CHARACTER)S_UND_PetrifiedDrow_Wizard_1f86d2de-db96-4662-a360-6ba5ad902fd7); // default creature

IF
StatusRemoved((CHARACTER)_Creature, _PetrifiedStatus, (CHARACTER)_Player, _)
AND
DB_Players(_Player)
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Creature, _PetrifiedStatus, _, _, _, _, _, _)
AND
DB_OffStage(S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0)
THEN
PROC_UND_PetrifiedDrow_RevealSpectator(_Player, _Creature);

IF
AttackedBy((CHARACTER)_Creature, _, (CHARACTER)_Player, _, _, _, _)
AND
DB_Players(_Player)
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Creature, _PetrifiedStatus, _, _, _, _, _, _)
AND
DB_OffStage(S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0)
THEN
PROC_UND_PetrifiedDrow_RevealSpectator(_Player, _Creature);

PROC
PROC_UND_PetrifiedDrow_RevealSpectator((CHARACTER)_Player, (CHARACTER)_Creature)
THEN
DB_UND_PetrifiedDrow_Player((CHARACTER)_Player);
PROC_Ambush_SetPlayersSurprised(_Player);

PROC
PROC_UND_PetrifiedDrow_RevealSpectator(_, _)
AND
DB_UND_PetrifiedDrow_AmbushAreas(_AmbushArea)
THEN
PROC_TriggerUnregisterForPlayers(_AmbushArea);

PROC
PROC_UND_PetrifiedDrow_RevealSpectator((CHARACTER)_Player, (CHARACTER)_Creature)
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Creature, _, _, _, _, _AppearArea, _, _)
AND
TriggerGetRandomPosition(_AppearArea, _X, _Y, _Z)
AND
FindValidPosition(_X, _Y, _Z, 2.0, S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0, 0, _ValidX, _ValidY, _ValidZ)
AND
QRY_OnlyOnce("UND_PetrifiedDrow_Reveal")
THEN
TeleportToPosition(S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0, _ValidX, _ValidY, _ValidZ, "UND_PetrifiedDrow_SpectatorTeleported", 0, 0, 0);

PROC
PROC_UND_PetrifiedDrow_RevealSpectator((CHARACTER)_Player, (CHARACTER)_Creature)
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Creature, _, _, _, _, _AppearArea, _, _)
AND
TriggerGetRandomPosition(_AppearArea, _X, _Y, _Z)
AND
QRY_OnlyOnce("UND_PetrifiedDrow_Reveal")
THEN
TeleportToPosition(S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0, _X, _Y, _Z, "UND_PetrifiedDrow_SpectatorTeleported", 0, 0, 0);

IF
EntityEvent(S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0, "UND_PetrifiedDrow_SpectatorTeleported")
THEN
PROC_Foop((CHARACTER)S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0);

IF
WentOnStage(S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0, 1)
AND
DB_UND_PetrifiedDrow_Player(_Player)
THEN
NOT DB_UND_PetrifiedDrow_Player(_Player);
PROC_UND_PetrifiedDrow_LaunchCinematic(_Player);

PROC
PROC_UND_PetrifiedDrow_LaunchCinematic((CHARACTER)_Player)
AND
NOT QRY_StartDialog_Fixed(UND_PetrifiedDrow_CINE_SpectatorReveal_2d104570-1a14-53b1-b98e-97e6fc6f933a, S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0, _Player)
THEN
PROC_UND_PetrifiedDrow_MakeSpectatorHostile();

PROC
PROC_StartDialog_AddExtraSpeakers((DIALOGRESOURCE)UND_PetrifiedDrow_CINE_SpectatorReveal_2d104570-1a14-53b1-b98e-97e6fc6f933a, _ID)
THEN
PROC_DialogAddSpeakingActor(_ID, S_UND_PetrifiedDrow_Wizard_1f86d2de-db96-4662-a360-6ba5ad902fd7);
PROC_DialogAddSpeakingActor(_ID, S_UND_PetrifiedDrow_006_0d5f2f17-9827-45c1-aa6e-67ff0aa4f2f1);
PROC_DialogAddSpeakingActor(_ID, S_UND_PetrifiedDrow_003_7d816c9b-2274-423f-bc81-7e1e4a889e12);
PROC_DialogAddSpeakingActor(_ID, S_UND_PetrifiedDrow_002_34722f55-c90a-4458-8740-7e3620cb9600);
PROC_DialogAddSpeakingActor(_ID, S_UND_PetrifiedDrow_005_7f57e4f0-125a-4a09-85df-81465b40cde4);

IF
DialogEnded(UND_PetrifiedDrow_CINE_SpectatorReveal_2d104570-1a14-53b1-b98e-97e6fc6f933a, _)
THEN
PROC_UND_PetrifiedDrow_MakeSpectatorHostile();

PROC
PROC_UND_PetrifiedDrow_MakeSpectatorHostile()
THEN
SetVisible(S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0, 1);
//END_REGION Ambush

//REGION Combat
IF
StatusRemoved((CHARACTER)_Creature, _PetrifiedStatus, _, _)
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Creature, _PetrifiedStatus, _Dialog, _, _CharmedFaction, _, _DisplayName, _Title)
THEN
SetAmbushing(_Creature, 0);
SetCanJoinCombat(_Creature, 1);
PROC_UND_PetrifiedDrow_SetDialog(_Creature, _Dialog);
PROC_UND_PetrifiedDrow_SetDisplayName(_Creature, _DisplayName);
PROC_UND_PetrifiedDrow_SetTitle(_Creature, _Title);

PROC
PROC_UND_PetrifiedDrow_SetDialog((CHARACTER)_Creature, (DIALOGRESOURCE)_Dialog)
AND
_Dialog != NULL_00000000-0000-0000-0000-000000000000
THEN
DB_Dialogs(_Creature, _Dialog);

PROC
PROC_UND_PetrifiedDrow_SetDisplayName((CHARACTER)_Creature, (STRING)_DisplayName)
AND
_DisplayName != ""
THEN
SetStoryDisplayName(_Creature, _DisplayName);

PROC
PROC_UND_PetrifiedDrow_SetTitle((CHARACTER)_Creature, (STRING)_Title)
AND
_Title != ""
THEN
ObjectSetTitle(_Creature, _Title);

IF
StatusApplied((CHARACTER)_Creature, "UND_SPECTATOR_CHARMED", _, _)
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Creature, _, _, _, _CharmedFaction, _, _, _)
THEN
SetFaction(_Creature, _CharmedFaction);

IF
DB_PermaDefeated((CHARACTER)S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0)
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Creature, _, _, _, _, _, _, _)
AND
HasActiveStatus(_Creature, "UND_SPECTATOR_CHARMED", 1)
THEN
RemoveStatus(_Creature, "UND_SPECTATOR_CHARMED", NULL_00000000-0000-0000-0000-000000000000);

IF
StatusRemoved((CHARACTER)_Creature, "UND_SPECTATOR_CHARMED", _, _)
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Creature, _, _, _DefaultFaction, _, _, _, _)
THEN
PROC_CharacterEnableAllCrimes(_Creature);
SetFaction(_Creature, _DefaultFaction);

// killing / knocking out the leader of the drow group makes them hostile
IF
KilledBy(S_UND_PetrifiedDrow_Wizard_1f86d2de-db96-4662-a360-6ba5ad902fd7, _, (CHARACTER)_Attacker, _)
AND
DB_PartyMembers(_Attacker)
THEN
PROC_SetRelationToPlayers((FACTION)ACT1_UND_PetrifiedDrow_Drow_6a05bdb0-d46a-4ecc-9740-f32e39928448, 0);

IF
StatusApplied(S_UND_PetrifiedDrow_Wizard_1f86d2de-db96-4662-a360-6ba5ad902fd7, "KNOCKED_OUT", (CHARACTER)_Attacker, _) 
AND
DB_PartyMembers(_Attacker)
THEN
PROC_SetRelationToPlayers((FACTION)ACT1_UND_PetrifiedDrow_Drow_6a05bdb0-d46a-4ecc-9740-f32e39928448, 0);

IF
StatusRemoved(S_UND_PetrifiedDrow_Wizard_1f86d2de-db96-4662-a360-6ba5ad902fd7, _PetrifiedStatus, _, _)
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Drow, _PetrifiedStatus, _, _, _, _, _, _)
AND
NOT DB_PermaDefeated((CHARACTER)S_UND_PetrifiedDrow_Wizard_1f86d2de-db96-4662-a360-6ba5ad902fd7)
THEN
SetFlag(UND_PetrifiedDrow_State_WizardUnpetrified_9dfc3375-b93e-43de-880f-4244aad43bd8, NULL_00000000-0000-0000-0000-000000000000);
//END_REGION Combat

//REGION Spectator Arcana check after combat
// if after combat we close enough to the Spectator and succeed on the Arcana check then we comment on the Petrification Ray
// otherwise we make a generic comment
IF
CombatEnded(_CombatID)
AND
DB_Was_InCombat(S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0, _CombatID)
AND
DB_PermaDefeated(S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0)
AND
NOT DB_GlobalFlag((FLAG)UND_PetrifiedDrow_State_WizardUnpetrified_9dfc3375-b93e-43de-880f-4244aad43bd8) // if the wizard is unpetrified (and alive) he will tell us about the spectator
AND
DB_Players(_Player)
AND
DB_Was_InCombat(_Player, _CombatID)
AND
QRY_GLO_SelectFavouredSpeaker_SingleTag(_Player, S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0, (TAG)AVATAR_306b9b05-1057-4770-aa17-01af21acd650, 1)
AND
DB_QRYRTN_GLO_SelectFavouredSpeaker(_Speaker)
AND
NOT DB_CantTalk(_Speaker)
AND
QRY_OnlyOnce("UND_PetrifiedDrow_AfterCombatReaction")
THEN
PROC_UND_PetrifiedDrow_CheckProximity(_Speaker);
PROC_TrySkillCheck(_Player, S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0, "Arcana", (DIFFICULTYCLASS)DC_Legacy_10_625be976-7a67-4394-97c8-14c69715ae4b, "UND_PetrifiedDrow_ArcanaCheckAboutSpectator");

PROC
PROC_UND_PetrifiedDrow_CheckProximity((GUIDSTRING)_Speaker)
AND
GetDistanceTo(_Speaker, S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0, _Distance)
AND
_Distance < 15.0
THEN
DB_UND_PetrifiedDrow_ProximityCheckSuccess(1);

PROC
PROC_RollResult("UND_PetrifiedDrow_ArcanaCheckAboutSpectator", _Player, _, 1)
THEN
DB_UND_PetrifiedDrow_ArcanaCheckSuccess(1);

PROC
PROC_RollResult("UND_PetrifiedDrow_ArcanaCheckAboutSpectator", _Player, _, _)
AND
DB_UND_PetrifiedDrow_ProximityCheckSuccess(1)
AND
DB_UND_PetrifiedDrow_ArcanaCheckSuccess(1)
THEN
SetFlag(UND_PetrifiedDrow_State_CanCommentOnPetrificationRay_e939c222-dcf3-4984-9d72-3c4137d646c4, NULL_00000000-0000-0000-0000-000000000000);

PROC
PROC_RollResult("UND_PetrifiedDrow_ArcanaCheckAboutSpectator", _Player, _, _)
THEN
PROC_TryStartAD((DIALOGRESOURCE)UND_PetrifiedDrow_PAD_AfterCombat_5b03b515-ceab-6b51-c026-b11a3f016d3d, _Player);

PROC
PROC_OneShotTriggerEntered(_Player, S_UND_PetrifiedDrow_ReactionArea_b01f5b4a-40b5-4beb-84f0-c5fb8c000ba1)
THEN
PROC_TryStartAD((DIALOGRESOURCE)UND_PetrifiedDrow_PAD_EnteringArea_2036145b-996b-1560-e360-412996d8699e, _Player);
//END_REGION Spectator Arcana check after combat

//REGION Dialog Setup for Drow
IF
StatusRemoved(S_UND_PetrifiedDrow_Wizard_1f86d2de-db96-4662-a360-6ba5ad902fd7, "UND_SPECTATOR_CHARMED", _, _)
AND
NOT DB_PermaDefeated((CHARACTER)S_UND_PetrifiedDrow_Wizard_1f86d2de-db96-4662-a360-6ba5ad902fd7)
THEN
SetFlag(UND_PetrifiedDrow_State_WizardNormal_9dfc3375-b93e-43de-880f-4244aad43bd8);
DB_SpotPlayers((CHARACTER)S_UND_PetrifiedDrow_Wizard_1f86d2de-db96-4662-a360-6ba5ad902fd7, "UND_PetrifiedDrow_Wizard", NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);

PROC
PROC_SpotPlayers_Spotted((CHARACTER)S_UND_PetrifiedDrow_Wizard_1f86d2de-db96-4662-a360-6ba5ad902fd7, "UND_PetrifiedDrow_Wizard", _Player)
AND
QRY_StartDialog((DIALOGRESOURCE)UND_PetrifiedDrow_Wizard_7a4fc9ca-c472-ab4a-669a-549fdf1b5758, (CHARACTER)S_UND_PetrifiedDrow_Wizard_1f86d2de-db96-4662-a360-6ba5ad902fd7, _Player)
THEN
DB_NOOP(1);

// Each survived drow (including the wizard) in their dialog acknowledges whether there are other survived drow (petrified or alive (alive includes being unpetrified!))
PROC
PROC_DialogFlagSetup(_Dialog, (CHARACTER)_Drow, (CHARACTER)_Player)
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Drow, _, _Dialog, _, _, _, _, _)
THEN
PROC_UND_PetrifiedDrow_SetSomeOtherDrowPetrifiedFlag(_Drow);
PROC_UND_PetrifiedDrow_SetSomeOtherDrowAliveFlag(_Drow);

PROC
PROC_UND_PetrifiedDrow_SetSomeOtherDrowPetrifiedFlag((CHARACTER)_Drow)
AND
QRY_UND_PetrifiedDrow_SomeOtherDrowPetrified(_Drow)
THEN
SetFlag(UND_PetrifiedDrow_State_SomeOtherDrowPetrified_acae52a0-e4a9-4263-8e9c-f615d0ba95e5, _Drow);

PROC
PROC_UND_PetrifiedDrow_SetSomeOtherDrowPetrifiedFlag((CHARACTER)_Drow)
AND
NOT QRY_UND_PetrifiedDrow_SomeOtherDrowPetrified(_Drow)
THEN
ClearFlag(UND_PetrifiedDrow_State_SomeOtherDrowPetrified_acae52a0-e4a9-4263-8e9c-f615d0ba95e5, _Drow);

QRY
QRY_UND_PetrifiedDrow_SomeOtherDrowPetrified((CHARACTER)_Drow)
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_OtherDrow, _PetrifiedStatus, _, _, _, _, _, _)
AND
_OtherDrow != _Drow
AND
IsTagged(_OtherDrow, DROWELF_a672ac1d-d088-451a-9537-3da4bf74466c, 1)
AND
HasActiveStatus(_OtherDrow, _PetrifiedStatus, 1)
THEN
DB_NOOP(1);

PROC
PROC_UND_PetrifiedDrow_SetSomeOtherDrowAliveFlag((CHARACTER)_Drow)
AND
QRY_UND_PetrifiedDrow_SomeOtherDrowAlive(_Drow)
THEN
SetFlag(UND_PetrifiedDrow_State_SomeOtherDrowAlive_0b3149e1-b315-4585-aceb-559d0c67c91e, _Drow);

PROC
PROC_UND_PetrifiedDrow_SetSomeOtherDrowAliveFlag((CHARACTER)_Drow)
AND
NOT QRY_UND_PetrifiedDrow_SomeOtherDrowAlive(_Drow)
THEN
ClearFlag(UND_PetrifiedDrow_State_SomeOtherDrowAlive_0b3149e1-b315-4585-aceb-559d0c67c91e, _Drow);

QRY
QRY_UND_PetrifiedDrow_SomeOtherDrowAlive((CHARACTER)_Drow)
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_OtherDrow, _PetrifiedStatus, _, _, _, _, _, _)
AND
_OtherDrow != _Drow
AND
IsTagged(_OtherDrow, DROWELF_a672ac1d-d088-451a-9537-3da4bf74466c, 1)
AND
HasActiveStatus(_OtherDrow, _PetrifiedStatus, 0)
AND
NOT DB_Defeated(_OtherDrow)
THEN
DB_NOOP(1);

PROC
PROC_GlobalFlagReactionAfterDialog((FLAG)UND_PetrifiedDrow_State_HostileResolution_0e62f611-fe1b-47f1-8bd7-e89e4068a1bd)
THEN
PROC_SetRelationToPlayers((FACTION)ACT1_UND_PetrifiedDrow_Drow_6a05bdb0-d46a-4ecc-9740-f32e39928448, 0); 

PROC
PROC_FlagReactionAfterDialog(_Player, UND_PetrifiedDrow_Event_MemoryCrystalRejected_e29001a0-8c2b-3067-008d-06983f1ef83b)
AND
DB_Players((CHARACTER)_Player)
THEN
ApplyStatus(_Player, "UND_SOB_DAZED", 12.0, 1, S_UND_PetrifiedDrow_MemoryCrystal_d0791906-a696-4bd0-9b2d-398ea5829769);
PROC_ApplyLimitedDamagePercent(_Player, 20.0, "Psychic", _Player);
//END_REGION Dialog Setup for Drow

//REGION Removing the drow on a long rest
// If at least one drow is unpetrified and can act, he unpetrifies the rest of them and they all leave after the next long rest
PROC
PROC_LongRest()
AND
NOT DB_GlobalFlag(UND_PetrifiedDrow_State_DrowLeft_1815af62-0ad7-461e-a61b-2986445bec36)
AND
DB_PermaDefeated(S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0)
THEN
DB_ExecuteInLevel("UND_PetrifiedDrow_CheckRemoveDrow","WLD_Main_A");

PROC
PROC_ExecuteInLevel("UND_PetrifiedDrow_CheckRemoveDrow","WLD_Main_A")
AND
QRY_UND_PetrifiedDrow_AtLeastOneDrowCanAct()
THEN
PROC_UND_PetrifiedDrow_RemoveAllAliveDrow();
PROC_GlobalSetFlagAndCache(UND_PetrifiedDrow_State_DrowLeft_1815af62-0ad7-461e-a61b-2986445bec36);

QRY
QRY_UND_PetrifiedDrow_AtLeastOneDrowCanAct()
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Drow, _, _, _, _, _, _, _)
AND
IsTagged(_Drow, DROWELF_a672ac1d-d088-451a-9537-3da4bf74466c, 1)
AND
NOT DB_CantAct(_Drow)
THEN
DB_NOOP(1);

PROC
PROC_UND_PetrifiedDrow_RemoveAllAliveDrow()
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Drow, _, _, _, _, _, _, _)
AND
IsTagged(_Drow, DROWELF_a672ac1d-d088-451a-9537-3da4bf74466c, 1)
AND
NOT DB_PermaDefeated(_Drow)
AND
NOT DB_IronFlasked(_Drow)
THEN
PROC_SetOnStage(_Drow, 0);
//END_REGION Removing the drow on a long rest

//REGION Anubis Helper
IF
TurnEnded(S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0)
AND
QRY_UND_PetrifiedDrow_HasCreaturesToUnpetrify()
THEN
SetFlag(UND_PetrifiedDrow_State_CanShootUnpetrificationRay_880a4a7f-d2cf-4eae-b31d-52318c279672, S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0);

QRY
QRY_UND_PetrifiedDrow_HasCreaturesToUnpetrify()
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Creature, _PetrifiedStatus, _, _, _, _, _, _)
AND
HasAppliedStatus(_Creature, _PetrifiedStatus, 1)
THEN
DB_NOOP(1);
//END_REGION Anubis Helper

//REGION Banter
IF
DB_GlobalFlag((FLAG)UND_PetrifiedDrow_State_AllDead_8525d155-11d0-40bc-8f40-87ce6c6e1256)
AND
DB_PermaDefeated((CHARACTER)S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3)
AND
NOT DB_UND_PetrifiedDrow_SafeForBanter(1)
THEN
DB_UND_PetrifiedDrow_SafeForBanter(1);

IF
DB_GlobalFlag((FLAG)UND_PetrifiedDrow_State_DrowLeft_1815af62-0ad7-461e-a61b-2986445bec36)
AND
DB_PermaDefeated((CHARACTER)S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3)
AND
NOT DB_UND_PetrifiedDrow_SafeForBanter(1)
THEN
DB_UND_PetrifiedDrow_SafeForBanter(1);

PROC
PROC_LongRest()
AND
DB_UND_PetrifiedDrow_SafeForBanter(1)
AND
NOT DB_WorldGossipTrigger(S_PartyBanterTrigger_UND_PetrifiedCleared_47fe2b9c-128f-4830-ba1a-4384f7443043,_,_)
THEN
PROC_RegisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_UND_PetrifiedCleared_47fe2b9c-128f-4830-ba1a-4384f7443043);
//END_REGION Banter

//REGION Debug
IF
TextEvent("und_petrifieddrow_killdrow")
AND
DB_GLO_DefeatCounter(_Drow, _)
THEN
Die((CHARACTER)_Drow, DEATHTYPE.DoT, NULL_00000000-0000-0000-0000-000000000000, 0, 0);

IF
TextEvent("und_pd_freedrow")
THEN
DB_OnlyOnce("UND_PetrifiedDrow_Reveal");
Die(S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0, DEATHTYPE.DoT, NULL_00000000-0000-0000-0000-000000000000, 0, 0);

IF
TextEvent("und_pd_freedrow")
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Creature, _PetrifiedStatus, _, _DefaultFaction, _, _, _, _)
THEN
RemoveStatus(_Creature, _PetrifiedStatus, NULL_00000000-0000-0000-0000-000000000000);
PROC_CharacterEnableAllCrimes(_Creature);
SetFaction(_Creature, _DefaultFaction);

IF
TextEvent("und_pd_freedrow")
AND
GetHostCharacter(_Player)
THEN
TeleportTo(_Player, S_UND_PetrifiedDrow_SpectatorAppearTrigger_01_7fc44a28-a2df-4646-98cb-7e0a274ea0e6, "", 1, 1, 1, 1, 1);

IF
TextEvent("und_pd_freewizard")
AND
GetHostCharacter(_Player)
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(S_UND_PetrifiedDrow_Wizard_1f86d2de-db96-4662-a360-6ba5ad902fd7, _Status, _, _, _, _, _, _)
THEN
RemoveStatus(S_UND_PetrifiedDrow_Wizard_1f86d2de-db96-4662-a360-6ba5ad902fd7, _Status, _Player);

IF
TextEvent("und_pd_crystal")
AND
GetHostCharacter(_Player)
THEN
ToInventory(S_UND_PetrifiedDrow_MemoryCrystal_d0791906-a696-4bd0-9b2d-398ea5829769, _Player, 1, 1, 1);
//END_REGION

//REGION Peaceful Resolution XP
// If we avoid the fight with the unpetrified drow, then we get XP for all the drow
PROC
PROC_GlobalFlagReactionAfterDialog((FLAG)UND_PetrifiedDrow_State_PeacefulResolution_e5d3cf4d-3dd4-4b8d-a04e-147f24fec1d4)
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Drow, _, _, _, _, _, _, _)
THEN
PROC_PeacefulResolve(_Drow);

// if we somehow manage to kill the spectator before it unpetrifies anyone
// then we also get XP for every drow
IF
DB_PermaDefeated(S_UND_PetrifiedDrow_Spectator_39ff8241-fadd-4fbe-ab89-fc5a8b7638a0)
AND
NOT QRY_UND_PetrifiedDrow_AtLeastOneDrowUnpetrified()
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Drow, _, _, _, _, _, _, _)
THEN
PROC_PeacefulResolve(_Drow);

QRY
QRY_UND_PetrifiedDrow_AtLeastOneDrowUnpetrified()
AND
DB_UND_PetrifiedDrow_PetrifiedCreatures(_Drow, _Status, _, _, _, _, _, _)
AND
HasActiveStatus(_Drow, _Status, 0)
THEN
DB_NOOP(1);
//END_REGION

//REGION Goal Completion
PROC
PROC_LevelLoadedOnce("BGO_Main_A")
THEN
GoalCompleted;
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
