Version 1
SubGoalCombiner SGC_AND
INITSECTION
PROC_UND_SussurTree_Setup();

DB_GLO_Sussur_UnderdarkRegion((TRIGGER)S_UND_SussurZone_000_4067ab6a-2dbd-4525-8fe9-ddb82669d885);
DB_GLO_Sussur_UnderdarkRegion(S_UND_SussurZone_001_0aa8fe7b-2673-4e5b-b779-eaec7ed07419);
DB_GLO_Sussur_UnderdarkRegion(S_UND_SussurZone_002_ba8a0850-4073-4c4c-9c2b-95be60f1c357);
DB_GLO_Sussur_UnderdarkRegion(S_UND_SussurZone_003_bcdadfe3-14f6-469a-9073-47f9a5f157c2);
DB_GLO_Sussur_UnderdarkRegion(S_UND_SussurZone_004_e9d8d445-6551-4720-b08a-fb98b4f7fc87);
DB_GLO_Sussur_UnderdarkRegion(S_UND_SussurZone_005_6aacda00-6e3f-45e1-9846-c434160d94f3);
DB_GLO_Sussur_UnderdarkRegion(S_UND_SussurZone_006_fadac617-d688-42cf-8ccb-747b027337eb);

DB_GLO_FavouredSpeaker_WithTagExpression("UND_SussurTree_Aura_IVB_Companion","SORCERER_18266c0b-efbc-4c80-8784-ada4a37218d7 & AVATAR_306b9b05-1057-4770-aa17-01af21acd650 & UND_SUSSURTREEANTIMAGIC_2c17c8a0-2aa4-49f3-8df2-9955762e51fc");
DB_GLO_FavouredSpeaker_WithTagExpression("UND_SussurTree_Aura_IVB_Companion","SORCERER_18266c0b-efbc-4c80-8784-ada4a37218d7 & UND_SUSSURTREEANTIMAGIC_2c17c8a0-2aa4-49f3-8df2-9955762e51fc");
KBSECTION
//REGION DEBUG
IF
TextEvent("und_sussur")
THEN
PROC_UND_SussurTree_Clean();
PROC_UND_SussurTree_Setup();

IF
TextEvent("und_sussur_backup")
THEN
PROC_Combat_CallingForHelp(S_UND_HookHorror_Hunter_001_3d5a8ed5-771a-491b-93eb-9a65eb2dcf30,"UND_SUSSURTREE");

IF
TextEvent("und_sussur_backup")
AND
GetHostCharacter(_Player)
THEN
StartVoiceBark(UND_HookHorrors_VB_Backup_4e279ef4-0617-114e-6fd5-1a6183952d1f,_Player);

IF
TextEvent("und_sussur_peace")
THEN
PROC_SetRelation(ACT1_UND_HookHorrors_927ec2fe-e960-4558-bc10-a19f22e2ad69,Hero_a1542c81-6895-929e-4522-10ce218bb360,100);

IF
TextEvent("und_sussur_war")
THEN
PROC_SetRelation(ACT1_UND_HookHorrors_927ec2fe-e960-4558-bc10-a19f22e2ad69,Hero_a1542c81-6895-929e-4522-10ce218bb360,0);
//END_REGION DEBUG

//REGION INIT
PROC
PROC_UND_SussurTree_Clean()
AND
QRY_OnlyOnce_Reset("UND_SussurTree_VB")
AND
QRY_OnlyOnce_Reset("UND_HookHorrors_VB")
AND
QRY_OncePerUserAndNearbyPlayers_Reset("UND_SussurTree_Aura")
AND
QRY_OncePerUserAndNearbyPlayers_Reset("UND_SussurTree_Aura_Sorcerer")
THEN
SysClear("DB_UND_HookHorrors",1);
PROC_RemoveOneShotVoiceBarkTrigger(S_UND_SussurTree_VB_f7723a0a-72f0-4519-8b20-8c35ddb0a990,UND_SussurTree_VB_Approach_73f84915-8dd1-4e68-d1e0-3a2fd78d09f5);
PROC_TriggerUnregisterForPlayers(S_UND_HookHorrors_VB_1dc24960-e9f4-4b64-b39c-79ad935ee2d7);
NOT DB_TriggerEvents_AllPlayersLeft((TRIGGER)S_UND_SussurTree_Area_9ce09423-2d62-42e3-b24e-4c8fc9cf8342,"UND_SussurTree_Event_AllPlayerLeftArea");
SetOnStage(S_UND_GnomeEatenCorpse_725e0b57-9573-43c8-a97e-13adabbc1579,1);
SysClear("DB_UND_SussurTree_PlayersUnderAura",1);
SysClear("DB_UND_SussurTree_PlayersUnderAura_Sorcerer",1);

PROC
PROC_UND_SussurTree_Clean()
THEN
PROC_GlobalClearFlagAndCache(UND_SussurTree_Event_PlayersLeft_ddc1f4e5-9161-4e89-a34c-3d7e3667e835);
PROC_GlobalClearFlagAndCache(UND_HookHorrors_Event_CombatEngaged_0f6c7a4f-9ef2-4a88-8b42-5428cacc61d4);
PROC_GlobalClearFlagAndCache(UND_HookHorrors_Event_BackupCalled_02aa8fb1-fdfc-420f-b629-00f83480fe5f);
PROC_GlobalClearFlagAndCache(UND_HookHorrors_Event_BackupAppeared_6c28188e-8420-47be-8875-7163a1860c50);
PROC_GlobalClearFlagAndCache(UND_HookHorrors_Event_Retreated_056016f9-17bd-47a8-a689-dbb12ded0e4e);

PROC
PROC_UND_SussurTree_Setup()
THEN
DB_OneShot_VoiceBarkTrigger(S_UND_SussurTree_VB_f7723a0a-72f0-4519-8b20-8c35ddb0a990,UND_SussurTree_VB_Approach_73f84915-8dd1-4e68-d1e0-3a2fd78d09f5,"Global");
PROC_TriggerRegisterForPlayers(S_UND_HookHorrors_VB_1dc24960-e9f4-4b64-b39c-79ad935ee2d7);
DB_TriggerEvents_AllPlayersLeft(S_UND_SussurTree_Area_9ce09423-2d62-42e3-b24e-4c8fc9cf8342,"UND_SussurTree_Event_AllPlayerLeftArea");


PROC
PROC_UND_SussurTree_Setup()
THEN
DB_Combat_CallingForHelp((CHARACTER)S_UND_HookHorror_Hunter_000_70866004-4b9d-4dde-a9c7-9891229c47c7,"UND_SUSSURTREE","Shout_CallForHelp_HookHorror");
DB_Combat_CallingForHelp(S_UND_HookHorror_Hunter_001_3d5a8ed5-771a-491b-93eb-9a65eb2dcf30,"UND_SUSSURTREE","Shout_CallForHelp_HookHorror");
DB_Combat_CallingForHelp(S_UND_HookHorror_Backup_001_d62704dc-d1ee-4584-b27f-b7c834daebf0,"UND_SUSSURTREE","Shout_CallForHelp_HookHorror");
DB_Combat_CallingForHelp(S_UND_HookHorror_Backup_000_acf10865-b8a9-48cc-be8b-43945357b26d,"UND_SUSSURTREE","Shout_CallForHelp_HookHorror");
DB_GLO_Sussur_Flowers((ITEM)S_UND_SussurFlower_000_bed936c2-370c-461b-aa97-d8f1e8a6492f);
DB_GLO_Sussur_Flowers((ITEM)S_UND_SussurFlower_001_bc63faa4-22d4-4103-a37f-302a95ce563c);
DB_GLO_Sussur_Flowers((ITEM)S_UND_SussurFlower_002_c3a2e5c7-3468-4a9d-a1f1-890e34e33ee6);
DB_GLO_Sussur_Flowers((ITEM)S_UND_SussurFlower_003_b4db33ec-cc2b-4d16-91af-78a884b83e5a);
DB_GLO_Sussur_Flowers((ITEM)S_UND_SussurFlower_004_6bc6f49f-8c1c-47ef-8563-d6149068a50f);
DB_GLO_Sussur_Flowers((ITEM)S_UND_SussurFlower_005_6599d878-8486-4610-9106-aec025395688);

IF
DB_Combat_CallingForHelp(_HookHorror,"UND_SUSSURTREE",_)
THEN
DB_UND_HookHorrors_OnStage(_HookHorror);
AddPreferredAiTargetTag(_HookHorror,BULETTE_06c1ef36-dac6-4224-8320-d4d38f84897f);

//END_REGION INIT


IF
Died(_HH)
AND
DB_UND_HookHorrors_OnStage(_HH)
THEN
NOT DB_UND_HookHorrors_OnStage(_HH);
PROC_UND_HookHorrors_CheckLastAlive();

//REGION Voice Barks
IF
EnteredTrigger(_Player,S_UND_HookHorrors_VB_1dc24960-e9f4-4b64-b39c-79ad935ee2d7)
AND
DB_Players(_Player)
AND
QRY_OnlyOnce("UND_HookHorrors_VB")
THEN
StartVoiceBark(UND_HookHorrors_VB_82543364-aaff-f05f-5f88-fb53bde9641f,_Player);

IF
StatusApplied((CHARACTER)_HH,"CALLING_FOR_HELP_HOOKHORROR",_,_)
AND
DB_UND_HookHorrors_OnStage(_HH)
AND
DB_Is_InCombat(_HH,_Id)
AND
GetClosestAlivePlayer(_HH,_Player,_Dist)
AND // sanity check
_Dist < 30.0
AND
QRY_OnlyOnce("UND_HookHorrors_BackupCalled_QueueVB_OnlyOnce")
THEN
DB_UND_HookHorrors_BackupCalled_QueueVB(_HH,_Id);

//Queue the VB at the start of the next player's turn to ensure the players can see the VB
IF
TurnStarted((CHARACTER)_Player)
AND
DB_UND_HookHorrors_BackupCalled_QueueVB(_HH,_Id)
AND
DB_Players(_Player)
AND
DB_Is_InCombat(_Player,_Id)
THEN
StartVoiceBark(UND_HookHorrors_VB_Backup_4e279ef4-0617-114e-6fd5-1a6183952d1f,_Player);
NOT DB_UND_HookHorrors_BackupCalled_QueueVB(_HH,_Id);

//Clear dbs if HH dies before players can respond with the VB
IF
Died(_HH)
AND
DB_UND_HookHorrors_BackupCalled_QueueVB(_HH,_Id)
THEN
NOT DB_UND_HookHorrors_BackupCalled_QueueVB(_HH,_Id);
NOT DB_OnlyOnce("UND_HookHorrors_BackupCalled_QueueVB_OnlyOnce");


// Magic Drain aura has PAD for usual characters and IVB for sorcerers
// sorcerers are prioritized
// gather anyone with status for a short delay, then check who still has status and choose the dialog

// fill DBs when status is applied, also start short delay timer
IF
StatusApplied(_Player,"UND_SUSSURTREEANTIMAGIC",_,_)
AND
DB_Players((CHARACTER)_Player)
THEN
DB_UND_SussurTree_PlayersUnderAura(_Player);
TimerLaunch("Timer_UND_SussurTree_Aura",500);

IF
DB_UND_SussurTree_PlayersUnderAura((CHARACTER)_Player)
AND
IsTagged(_Player,SORCERER_18266c0b-efbc-4c80-8784-ada4a37218d7,1)
THEN
DB_UND_SussurTree_PlayersUnderAura_Sorcerer(_Player);

// clear DBs if status removed
IF
StatusRemoved(_Player,"UND_SUSSURTREEANTIMAGIC",_,_)
AND
DB_UND_SussurTree_PlayersUnderAura((CHARACTER)_Player)
THEN
NOT DB_UND_SussurTree_PlayersUnderAura(_Player);

IF
DB_UND_SussurTree_PlayersUnderAura_Sorcerer(_Player)
AND
NOT DB_UND_SussurTree_PlayersUnderAura(_Player)
THEN
NOT DB_UND_SussurTree_PlayersUnderAura_Sorcerer(_Player);

// when timer expires, choose best dialog
// IVB will prioritize avatar sorcerers
IF
TimerFinished("Timer_UND_SussurTree_Aura")
AND
DB_UND_SussurTree_PlayersUnderAura_Sorcerer(_Player)
AND
QRY_OncePerUserAndNearbyPlayers(_Player,"UND_SussurTree_Aura_Sorcerer")
AND
QRY_GLO_SelectFavouredSpeaker(_Player,_Player,"UND_SussurTree_Aura_IVB_Companion")
AND
DB_QRYRTN_GLO_SelectFavouredSpeaker(_FavouredSpeaker)
AND
QRY_StartDialog(UND_SussurTree_IVB_Aura_Sorcerer_e1fe9944-d058-f040-aed8-4358931e182c,_FavouredSpeaker)
THEN
DB_NOOP(1);

IF
TimerFinished("Timer_UND_SussurTree_Aura")
AND
NOT DB_UND_SussurTree_PlayersUnderAura_Sorcerer(_)
AND
DB_UND_SussurTree_PlayersUnderAura(_Player)
AND
NOT QRY_OncePerUserAndNearbyPlayers_IsSet(_Player,"UND_SussurTree_Aura_Sorcerer")
AND
QRY_OncePerUserAndNearbyPlayers(_Player,"UND_SussurTree_Aura")
THEN
PROC_TryStartAD(UND_SussurTree_PAD_Aura_7f815ebe-b33f-52b7-2259-a962ebf18bcc,_Player);

//END_REGION Voice Barks


//REGION State switches
IF
EntityEvent(NULL_00000000-0000-0000-0000-000000000000,"UND_SussurTree_Event_AllPlayerLeftArea")
THEN
PROC_GlobalSetFlagAndCache(UND_SussurTree_Event_PlayersLeft_ddc1f4e5-9161-4e89-a34c-3d7e3667e835);

IF
EnteredCombat(_HH,_CombatID)
AND
DB_UND_HookHorrors_OnStage((CHARACTER)_HH)
THEN
PROC_GlobalSetFlagAndCache(UND_HookHorrors_Event_CombatEngaged_0f6c7a4f-9ef2-4a88-8b42-5428cacc61d4);

IF
DB_GlobalFlag(UND_SussurTree_Event_PlayersLeft_ddc1f4e5-9161-4e89-a34c-3d7e3667e835)
THEN
PROC_TriggerUnregisterForPlayers(S_UND_HookHorrors_VB_1dc24960-e9f4-4b64-b39c-79ad935ee2d7);

IF
DB_GlobalFlag(UND_HookHorrors_Event_CombatEngaged_0f6c7a4f-9ef2-4a88-8b42-5428cacc61d4)
THEN
PROC_TriggerUnregisterForPlayers(S_UND_HookHorrors_VB_1dc24960-e9f4-4b64-b39c-79ad935ee2d7);

IF
EnteredCombat(_HH,_CombatID)
AND
DB_UND_HookHorrors_OnStage((CHARACTER)_HH)
AND
DB_Players((CHARACTER)_Player)
AND
DB_Is_InCombat(_Player,_CombatID)
AND
GetFlag(UND_HookHorrors_Knows_SeenHookHorrors_44022bbd-b1bb-4ab4-8764-1e67ed20af24,_Player,0)
THEN
SetFlag(UND_HookHorrors_Knows_SeenHookHorrors_44022bbd-b1bb-4ab4-8764-1e67ed20af24,_Player);

PROC
PROC_Combat_CallingForHelp((CHARACTER)_Char,"UND_SUSSURTREE")
THEN
PROC_GlobalSetFlagAndCache(UND_HookHorrors_Event_BackupAppeared_6c28188e-8420-47be-8875-7163a1860c50);
//END_REGION State switches

//REGION Call for Help
//last hook horror wont call for help if others are dead
PROC
PROC_UND_HookHorrors_CheckLastAlive()
AND
SysCount("DB_UND_HookHorrors_OnStage",1,_Int)
AND
_Int < 2
THEN
PROC_Combat_CallingForHelp_CleanUp("UND_SUSSURTREE");

//Play Sound when successfull
PROC
PROC_Combat_CallingForHelp(_HookHorror,"UND_SUSSURTREE")
THEN
PlaySound(_HookHorror,"UND_Hook_Horror_Summon");
//END_REGION

//REGION Situation progression
IF
FlagSet(UND_SussurTree_Event_PlayersLeft_ddc1f4e5-9161-4e89-a34c-3d7e3667e835,NULL_00000000-0000-0000-0000-000000000000,_)
THEN
PROC_UND_SussurTree_PlayersLeft();

PROC
PROC_UND_SussurTree_PlayersLeft()
AND
DB_UND_HookHorrors_OnStage(_HH)
AND
IsDead(_HH,0)
THEN
SetOnStage(S_UND_GnomeEatenCorpse_725e0b57-9573-43c8-a97e-13adabbc1579,0);

PROC
PROC_UND_SussurTree_PlayersLeft()
THEN
PROC_TriggerUnregisterForPlayers(S_UND_HookHorrors_VB_1dc24960-e9f4-4b64-b39c-79ad935ee2d7);
//END_REGION Situation progression


//REGION Pet Grave

PROC
PROC_UND_SussurTree_Setup()
THEN
DB_OneShot_VoiceBarkTrigger(S_UND_SussurTree_VB_f7723a0a-72f0-4519-8b20-8c35ddb0a990,UND_SussurTree_VB_Approach_73f84915-8dd1-4e68-d1e0-3a2fd78d09f5,"Global");
PROC_TriggerRegisterForPlayers(S_UND_HookHorrors_VB_1dc24960-e9f4-4b64-b39c-79ad935ee2d7);
DB_TriggerEvents_AllPlayersLeft(S_UND_SussurTree_Area_9ce09423-2d62-42e3-b24e-4c8fc9cf8342,"UND_SussurTree_Event_AllPlayerLeftArea");
DB_Dialogs(S_UND_ClericPetGrave_28e00450-53d5-4df7-8025-5edb674f7fec,UND_ClericPetGrave_0e65587a-639c-fb03-d700-daa6075e1f3b);
DB_HasItemTemplateScriptFlag(2, UND_ClericPetGrave_0e65587a-639c-fb03-d700-daa6075e1f3b, CONS_Herbs_Autumncrocus_Plant_A_691da3f8-0fca-4aa9-b5cd-89bc96e3cc74,2,1);
DB_HasItemTemplateScriptFlag(3, UND_ClericPetGrave_0e65587a-639c-fb03-d700-daa6075e1f3b, CONS_Herbs_Autumncrocus_Bundle_A_06f3a0c4-af50-4a79-8c1a-1ace6d1282f2,2,1);
SetOnStage(S_UND_ClericPetCollar_4d93e3eb-c420-4c03-aadf-2c09b14726fe,0);
DB_UND_ClericPetGrave_Flowers((FLAG)UND_ClericPetGrave_PutAutumnCrocus_26f5a9a4-9bad-4e4a-b52e-d8cd03a3e687,(ITEMROOT)CONS_Herbs_Autumncrocus_Plant_A_691da3f8-0fca-4aa9-b5cd-89bc96e3cc74);
DB_UND_ClericPetGrave_Flowers(UND_ClericPetGrave_PutAutumnCrocusBundle_39737622-c9b0-4fb7-900c-27f323fe70ab,CONS_Herbs_Autumncrocus_Bundle_A_06f3a0c4-af50-4a79-8c1a-1ace6d1282f2);

IF
DialogEnded(UND_ClericPetGrave_0e65587a-639c-fb03-d700-daa6075e1f3b,_DlgID)
AND
GetFlag(UND_ClericPetGrave_Event_Dug_f1d5554a-2f01-899b-b7b7-84e45aefd76a,NULL_00000000-0000-0000-0000-000000000000,1)
AND
QRY_OnlyOnce("UND_ClericPetGrave_Event_Dug")
AND
DB_DialogPlayers(_DlgID,_Player,_)
AND
GetPosition(S_UND_ClericPetCollar_DropPos_b54ab8fe-cc3f-4ebc-aabb-42508729430f, _X, _Y, _Z)
THEN
SetOnStage(S_UND_ClericPetCollar_4d93e3eb-c420-4c03-aadf-2c09b14726fe,1);
ScatterAt(S_UND_ClericPetCollar_4d93e3eb-c420-4c03-aadf-2c09b14726fe, _X, _Y, _Z);

IF
Moved(S_UND_ClericPetCollar_4d93e3eb-c420-4c03-aadf-2c09b14726fe)
AND
DB_Players(_Player)
AND
GetFlag(UND_ClericPetGrave_Digger_796ce2d0-fda2-c921-1edf-ab00bd0b4fab,_Player,1)
AND
QRY_OnlyOnce("UND_ClericPetCollar_AD")
THEN
LookAtEntity((CHARACTER)_Player,S_UND_ClericPetCollar_4d93e3eb-c420-4c03-aadf-2c09b14726fe);
PROC_PlayPerceptionRevealEffect(S_UND_ClericPetCollar_4d93e3eb-c420-4c03-aadf-2c09b14726fe, "UND_ClericPetCollar");
PROC_TryStartAD(UND_ClericPetCollar_PAD_Notice_d133b02f-9c01-d708-9a52-49005a1ec609,_Player);

IF
FlagSet(_Flag,_Player,_)
AND
DB_UND_ClericPetGrave_Flowers(_Flag,_FlowerTemplate)
THEN
DB_UND_ClericPetGrave_FlowerPlayer(_Player, _FlowerTemplate);

IF
DialogEnded(UND_ClericPetGrave_0e65587a-639c-fb03-d700-daa6075e1f3b, _)
AND
DB_UND_ClericPetGrave_FlowerPlayer(_Player, _FlowerTemplate)
AND
NOT DB_UND_ClericPetGrave_FlowersPlaced(_)
AND
DB_Players((CHARACTER)_Player)
AND
QRY_RemoveLocalItemsByTemplateFromMagicPockets(_Player,_FlowerTemplate,1)
AND
GetPosition(S_UND_ClericPetCollar_DropPos_b54ab8fe-cc3f-4ebc-aabb-42508729430f,_X,_Y,_Z)
AND
CreateAt(_FlowerTemplate,_X,_Y,_Z,0,0,"",_NewFlower)
THEN
NOT DB_UND_ClericPetGrave_FlowerPlayer(_Player, _FlowerTemplate);
DB_UND_ClericPetGrave_FlowersPlaced(_Player);

IF
FlagSet(_Flag,_Player,_)
AND
DB_UND_ClericPetGrave_Flowers(_Flag,_FlowerTemplate)
THEN
ClearFlag(_Flag,_Player);

IF
DB_UND_ClericPetGrave_FlowersPlaced(_Player)
AND
QRY_OnlyOnce("HIDDEN_WLD_Boosters_UND_ArcaneTower_PetGrave")
THEN
QuestUpdate(_Player, "HIDDEN_WLD_Boosters", "UND_ArcaneTower_PetGrave");
//END_REGION Pet Grave


//REGION Banter 
IF
DB_GlobalFlag(UND_GPHookHorrors_State_BothDied_d343352a-89c8-4ea5-aee8-551c86da8a37)
AND
DB_PermaDefeated((CHARACTER)S_UND_Bulette_307934b5-6fb5-4fdc-a7ff-433a7ba175b3)
AND
DB_PermaDefeated((CHARACTER)S_UND_HookHorror_Backup_000_acf10865-b8a9-48cc-be8b-43945357b26d)
AND
DB_PermaDefeated((CHARACTER)S_UND_HookHorror_Backup_001_d62704dc-d1ee-4584-b27f-b7c834daebf0)
AND
NOT DB_UND_SussurTree_Cleared(1)
THEN
DB_UND_SussurTree_Cleared(1);

PROC
PROC_LongRest()
AND
DB_UND_SussurTree_Cleared(1)
AND
NOT DB_WorldGossipTrigger(S_PartyBanterTrigger_UND_SussurCleared_cf90ac2c-02cf-46fd-b47d-7e604d2e4fc9,_,_)
THEN
PROC_RegisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_UND_SussurCleared_cf90ac2c-02cf-46fd-b47d-7e604d2e4fc9);
//END_REGION Banter
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
