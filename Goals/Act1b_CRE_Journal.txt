Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION Blood of Lathander
PROC_TriggerRegisterForPlayers((TRIGGER)S_CRE_BloodOfLathander_SecretPassageArea_87abb58c-ed26-4d4b-8f06-9160a142c4c2);
PROC_TriggerRegisterForPlayers((TRIGGER)S_CRE_BloodOfLathander_EnterTrapsArea_51b9e371-0b49-4eec-90ae-f539b5cbce38);
PROC_TriggerRegisterForPlayers((TRIGGER)S_CRE_BloodOfLathander_ExitTrapsArea_6754c8a5-88ff-4503-b8a7-d51f40f7e43e);
DB_InRegionFlag((TRIGGER)S_CRE_BloodOfLathander_SecretPassageArea_87abb58c-ed26-4d4b-8f06-9160a142c4c2, (FLAG)CRE_BloodOfLathander_Event_DiscoveredSecretPassage_4843682f-8d9f-428d-8049-d0d0a91a6216);
DB_InRegionFlag((TRIGGER)S_CRE_BloodOfLathander_EnterTrapsArea_51b9e371-0b49-4eec-90ae-f539b5cbce38, (FLAG)CRE_BloodOfLathander_Event_DiscoveredTrapsArea_4fbe4e86-249b-440a-a422-cf82eef0865b);
DB_InRegionFlag((TRIGGER)S_CRE_BloodOfLathander_ExitTrapsArea_6754c8a5-88ff-4503-b8a7-d51f40f7e43e, (FLAG)CRE_BloodOfLathander_Event_ExitTrapsArea_1803b51d-2cb9-4000-813b-be2f51d4211a);
DB_HasItemEvent((ITEM)S_CRE_DawnmasterCrest_b0b3322f-c707-4f96-a3c9-ab4265a7452a, (FLAG)CRE_BloodOfLathander_Event_DiscoveredCrest_ee0a2118-032a-43e7-bd5d-e4b98552a081);

DB_QuestDef_State((FLAG)CRE_BloodOfLathander_Event_DiscoveredSecretPassage_4843682f-8d9f-428d-8049-d0d0a91a6216, "CRE_BloodOfLathander", "DiscoveredSecretPassage");
DB_QuestDef_State((FLAG)CRE_BloodOfLathander_Event_DiscoveredTrapsArea_4fbe4e86-249b-440a-a422-cf82eef0865b, "CRE_BloodOfLathander", "ArrivedAtTraps");
DB_QuestDef_State((FLAG)CRE_BloodOfLathander_Event_ExitTrapsArea_1803b51d-2cb9-4000-813b-be2f51d4211a, "CRE_BloodOfLathander", "GotThroughTraps");
DB_QuestDef_State((FLAG)CRE_BloodOfLathander_Event_TakenBlood_baf03563-0160-4e9d-833b-92b42c1a7cb8, "CRE_BloodOfLathander", "RetrievedLathander");
DB_QuestDef_State((FLAG)CRE_Lance_State_DisarmedByDestroy_2cf709bf-3276-4710-97ec-346dd28f45a9, "CRE_BloodOfLathander", "DisarmedLathanderTrap");
//END_REGION


//REGION Hungry Bird
PROC_TriggerRegisterForPlayers(S_CRE_RoofTopEagles_SawEaglesArea_c818dca8-b452-495d-a9c3-3f508e3afc09);
DB_InRegionFlag(S_CRE_RoofTopEagles_SawEaglesArea_c818dca8-b452-495d-a9c3-3f508e3afc09, CRE_Eagles_State_PlayerFoundEagles_7b9951c2-7523-4798-a795-bf9080e94fc6);
DB_DeadOnceFlag(S_CRE_MonasteryApproach_Bird_c85a9bab-49d6-476b-a07b-15e920c3cc24, CRE_HungryBird_State_Dead_14418497-d09c-4db5-8e4b-83ffd14941bd);

DB_QuestDef_State(CRE_HungryBird_State_QuestAccepted_788e3e27-ca09-4e2d-ba24-edf768905b0b, "CRE_ReclaimTheNest", "AcceptedNestQuest");
DB_QuestDef_State(CRE_Eagles_State_PlayerFoundEagles_7b9951c2-7523-4798-a795-bf9080e94fc6, "CRE_ReclaimTheNest", "FoundEagleNest");
DB_QuestDef_State(CRE_Eagles_State_AllDefeated_7028c3aa-86be-43e6-a7ea-86c129c75188, "CRE_ReclaimTheNest", "DefeatedEagles");
DB_QuestDef_State(CRE_HungryBird_State_RewardGiven_e155685d-9e99-4eff-874a-1f2754a29ac9, "CRE_ReclaimTheNest", "ReportedBack");
DB_QuestDef_State(CRE_HungryBird_State_Dead_14418497-d09c-4db5-8e4b-83ffd14941bd, "CRE_ReclaimTheNest", "BlueJayDead");
DB_QuestDef_State(CRE_Lance_State_ExplosionCountdownActive_8b52ae9e-0cf3-46e5-924d-64e11aefa0e3, "CRE_ReclaimTheNest", "BirdsScaredOff");
//END_REGION Hungry Bird
KBSECTION
//REGION Blood of Lathander
IF
AutomatedDialogEnded((DIALOGRESOURCE)CRE_Monastery_AD_FakeBloodSign_e2c320de-c43f-8ed2-7693-0d41f2e8f894, _)
THEN
QuestUpdate("CRE_BloodOfLathander", "ReadLathanderPlaque");

IF
DialogEnded((DIALOGRESOURCE)CRE_Pilgrim_Dead_44139537-8686-5be5-c68b-468f58cdbc0b, _)
AND
DB_GlobalFlag(CRE_BloodOfLathander_Event_PilgrimMentionedWeapon_0bebe65f-57a7-40b9-a440-e0327fe63df8)
THEN
QuestUpdate("CRE_BloodOfLathander", "CorpseMentionedLathander");

IF
UseFinished(_Player, S_CRE_WeaponsPuzzle_Note_4c5bbc63-61f4-43f1-b2a1-de29071fd87c, 1)
AND
DB_Players(_Player)
THEN
QuestUpdate("CRE_BloodOfLathander", "DiscoveredLathanderNote");

IF
VoiceBarkEnded((VOICEBARKRESOURCE)CRE_BloodOfLathander_VB_FakeBloodPickedUp_b6f1295a-3231-dcea-e3af-b51050a02471, _)
THEN
QuestUpdate("CRE_BloodOfLathander", "DiscoveredFakeLathander");

IF
VoiceBarkFailed((VOICEBARKRESOURCE)CRE_BloodOfLathander_VB_FakeBloodPickedUp_b6f1295a-3231-dcea-e3af-b51050a02471)
THEN
QuestUpdate("CRE_BloodOfLathander", "DiscoveredFakeLathander");

IF
Combined((ITEM)S_CRE_LanceRooftopControlPanel_6f85585b-0c85-4247-b13f-2ce8de6ce7cc, (ITEM)S_CRE_DawnmasterCrest_b0b3322f-c707-4f96-a3c9-ab4265a7452a, _, _, _, _Player, _)
AND
DB_Players(_Player)
THEN
QuestUpdate("CRE_BloodOfLathander", "AvoidedLathanderTrap");

IF
DialogEnded((DIALOGRESOURCE)CRE_Lance_Escape_3ed9f536-77d6-ec62-785a-b8ee10ec814d, _)
THEN
QuestUpdate("CRE_BloodOfLathander", "EscapedLathanderTrap");

IF
FlagSet(Act2_PointOfNoReturnReached_a3155f30-b8f3-4db5-ac21-d3036f4426e3, _, _)
AND
DB_QuestIsOpened("CRE_BloodOfLathander")
THEN
QuestUpdate("CRE_BloodOfLathander", "LeftCrecheWithoutLathander");

//The Crest - unlock only the highest prio journal update it matches
IF
FlagSet(CRE_BloodOfLathander_Event_DiscoveredCrest_ee0a2118-032a-43e7-bd5d-e4b98552a081, _, _)
AND
NOT DB_CRE_BloodOfLathander_Journal_CrestStepUnlocked(1)
AND
QRY_QuestUpdateIsUnlockedForAny("CRE_BloodOfLathander", "GotThroughTraps")
THEN
QuestUpdate("CRE_BloodOfLathander", "DiscoveredGithCrest_RetrieveRelic");
DB_CRE_BloodOfLathander_Journal_CrestStepUnlocked(1);

IF
FlagSet(CRE_BloodOfLathander_Event_DiscoveredCrest_ee0a2118-032a-43e7-bd5d-e4b98552a081, _, _)
AND
NOT DB_CRE_BloodOfLathander_Journal_CrestStepUnlocked(1)
AND
QRY_QuestUpdateIsUnlockedForAny("CRE_BloodOfLathander", "DiscoveredSecretPassage")
THEN
QuestUpdate("CRE_BloodOfLathander", "DiscoveredGithCrest_Passage");
DB_CRE_BloodOfLathander_Journal_CrestStepUnlocked(1);

IF
FlagSet(CRE_BloodOfLathander_Event_DiscoveredCrest_ee0a2118-032a-43e7-bd5d-e4b98552a081, _, _)
AND
NOT DB_CRE_BloodOfLathander_Journal_CrestStepUnlocked(1)
AND
QRY_QuestUpdateIsUnlockedForAny("CRE_BloodOfLathander", "DiscoveredFakeLathander")
THEN
QuestUpdate("CRE_BloodOfLathander", "DiscoveredGithCrest_TrueRelic");
DB_CRE_BloodOfLathander_Journal_CrestStepUnlocked(1);

IF
FlagSet(CRE_BloodOfLathander_Event_DiscoveredCrest_ee0a2118-032a-43e7-bd5d-e4b98552a081, _, _)
AND
NOT DB_CRE_BloodOfLathander_Journal_CrestStepUnlocked(1)
THEN
QuestUpdate("CRE_BloodOfLathander", "DiscoveredGithCrest");
DB_CRE_BloodOfLathander_Journal_CrestStepUnlocked(1);
//END_REGION


//REGION Hungry Bird
//If the creche region becomes inaccessible, the quest ends
IF
DB_LevelUnreachable("CRE_Main_A")
AND
DB_QuestIsOpened("CRE_ReclaimTheNest")
THEN
QuestUpdate("CRE_ReclaimTheNest", "CrecheInaccessible");

//If the Bluejay is knocked out, and despawns over a long rest, or he is scared off by the player the quest ends
IF
WentOnStage(S_CRE_MonasteryApproach_Bird_c85a9bab-49d6-476b-a07b-15e920c3cc24, 0)
AND
DB_QuestIsOpened("CRE_ReclaimTheNest")
THEN
QuestUpdate("CRE_ReclaimTheNest", "BlueJayGone");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1b"
