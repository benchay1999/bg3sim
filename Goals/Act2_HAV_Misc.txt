Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_ItemOwnerShipTriggers("SCL_Main_A", S_HAV_Isobel_RoomOwnershipTrigger_69fd3acf-9daf-445e-bafd-e775428783e5, S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211);

//REGION Blackboard
DB_ItemDialog_NarratorAD((ITEM)S_HAV_BlackBoard_be957a49-cf90-44a9-bdab-cc5f31fbda49, (DIALOGRESOURCE)HAV_Misc_AD_Blackboard_e08b30bf-a073-7a73-f805-907bb0ff00cd);
DB_ItemDialog_NarratorAD((ITEM)S_HAV_Inn_Blackboard5_28f69cd9-1364-48d5-8493-166a16a5c09e, (DIALOGRESOURCE)HAV_LastLightInn_AD_NoWeaponsAllowed_9aaf2ad9-d305-3868-095f-867d270bbf83);
//END_REGION

//REGION Signpost
DB_ItemDialog_NarratorAD((ITEM)S_HAV_Signpost_eedf9f01-695c-4755-9cf7-175dabcf11e3, (DIALOGRESOURCE)HAV_General_AD_OutsideSignPost_284975fe-902e-18cc-718b-a8a94df515ae);
//END_REGION

//REGION Flaming Fists
//Flaming Fists
DB_Dialogs((CHARACTER)S_HAV_Manip_0b8fdd17-7ce6-4038-9d9d-49f5ddbc20be, (DIALOGRESOURCE)HAV_SavingPrisoners_Manip_10793efe-17d6-498f-b910-c22a921726ed);
DB_Dialogs((CHARACTER)S_HAV_FlamingFist_001_47058367-9c0c-4756-a467-07b87b28e0d6, (DIALOGRESOURCE)HAV_SavingPrisoners_FlamingFist_001_365597f1-69b5-c8c9-919f-4402debdb439);
DB_Dialogs((CHARACTER)S_HAV_FlamingFist_002_39ace2b4-699d-468c-a664-8a06492d997a, (DIALOGRESOURCE)HAV_SavingPrisoners_FlamingFist_002_612e02d0-b00b-f10a-52b5-b7e0ef3c5ef1);
DB_Dialogs((CHARACTER)S_HAV_FlamingFist_003_128c0f54-fea0-49df-89ca-c2ca8d996a31, (DIALOGRESOURCE)HAV_SavingPrisoners_FlamingFist_003_603bdf66-2992-0965-2556-241897ce9ea6);
DB_Dialogs((CHARACTER)S_HAV_FlamingFist_005_0e691c0a-562b-43bb-8554-cde1194decd5, (DIALOGRESOURCE)HAV_SavingPrisoners_FlamingFist_005_a33b2a75-f847-12a4-d9c6-1659701b712d);
DB_Dialogs((CHARACTER)S_HAV_FlamingFist_006_9629c27e-8e50-4258-8bda-6639af768ba6, (DIALOGRESOURCE)HAV_SavingPrisoners_FlamingFist_006_19f7ee37-b79f-cd25-e330-295a6cfe0086);

DB_HAV_SavingPrisoners_FlamingFistLeads((CHARACTER)S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9);
DB_HAV_SavingPrisoners_FlamingFistLeads((CHARACTER)S_HAV_Manip_0b8fdd17-7ce6-4038-9d9d-49f5ddbc20be);


DB_HAV_SavingPrisoners_FlamingFist((CHARACTER)S_HAV_Manip_0b8fdd17-7ce6-4038-9d9d-49f5ddbc20be);
DB_HAV_SavingPrisoners_FlamingFist((CHARACTER)S_HAV_FlamingFist_001_47058367-9c0c-4756-a467-07b87b28e0d6);
DB_HAV_SavingPrisoners_FlamingFist((CHARACTER)S_HAV_FlamingFist_002_39ace2b4-699d-468c-a664-8a06492d997a);
DB_HAV_SavingPrisoners_FlamingFist((CHARACTER)S_HAV_FlamingFist_003_128c0f54-fea0-49df-89ca-c2ca8d996a31);
DB_HAV_SavingPrisoners_FlamingFist((CHARACTER)S_HAV_FlamingFist_004_eb083eb6-066c-4693-8fb8-575bc4c71373);
DB_HAV_SavingPrisoners_FlamingFist((CHARACTER)S_HAV_FlamingFist_005_0e691c0a-562b-43bb-8554-cde1194decd5);
DB_HAV_SavingPrisoners_FlamingFist((CHARACTER)S_HAV_FlamingFist_006_9629c27e-8e50-4258-8bda-6639af768ba6);
DB_HAV_SavingPrisoners_FlamingFist((CHARACTER)S_HAV_HavenOutcasts_FountainGuards_Melee_1_3e2db417-c434-4f08-ab6a-dd60c8e9f3b5);

SetOnStage(S_HAV_Florrick_Note_43785054-670c-42c5-bc43-38ab269ed40f,0);

PROC_HAV_FlamingFists_Init();

//END_REGION

//REGION Meenlock
DB_HAV_Misc_Meenlock((CHARACTER)S_HAV_Meenlock_01_e988cfda-0890-4488-9778-0d207050abf9);
DB_HAV_Misc_Meenlock((CHARACTER)S_HAV_Meenlock_02_cc6d55bb-f736-47ac-b3c7-65481b5ddef6);
DB_HAV_Misc_Meenlock((CHARACTER)S_HAV_Meenlock_03_d4ab777e-9309-4a63-8599-79c7099ae8ad);
DB_HAV_Misc_Meenlock((CHARACTER)S_HAV_Meenlock_04_4b0cd20d-4370-4792-9ffb-7ab7f8aaa9f1);
DB_HAV_Misc_Meenlock((CHARACTER)S_HAV_Meenlock_05_ea62447e-0e36-4dc8-8942-52fc872de144);
DB_HAV_Misc_Meenlock((CHARACTER)S_HAV_Meenlock_06_2ff33018-260a-4e06-8799-95449e005ffc);
DB_HAV_Misc_Meenlock((CHARACTER)S_HAV_Meenlock_07_46dce4d3-50bd-4d45-893b-da628bbf19fd);

DB_OneShot_VoiceBarkTrigger((TRIGGER)S_HAV_SeluneShrineBounds_acefbcff-295d-493f-bc32-64de526067f7, (VOICEBARKRESOURCE)HAV_Misc_VB_SeluneShrine_3402136a-0a08-812d-f761-770065f9a998);
//END_REGION

DB_TopicalGreeting((FLAG)TG_HAV_Misc_IsInHaven_f6e3f5a0-d553-41f2-9d6e-78cea503c992);
DB_TopicalGreetingEndCondition_FlagSet((FLAG)TG_HAV_Misc_IsInHaven_f6e3f5a0-d553-41f2-9d6e-78cea503c992, (FLAG)HAV_Mood_State_Siege_b509af8b-a331-47ac-8230-3b8c5ef9956b);
DB_TopicalGreetingEndCondition_FlagSet((FLAG)TG_HAV_Misc_IsInHaven_f6e3f5a0-d553-41f2-9d6e-78cea503c992, (FLAG)HAV_Mood_State_Unprotected_7ac5d939-2c81-4871-81a5-fd5c6a0adba1);
DB_TopicalGreetingEndCondition_LeaveTrigger((FLAG)TG_HAV_Misc_IsInHaven_f6e3f5a0-d553-41f2-9d6e-78cea503c992, (TRIGGER)S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab);

DB_OneShotPlayerTrigger((TRIGGER)S_HAV_DaisyAD_Haven_cfd53d07-fd41-41e1-9313-20387cb9b1c2);

DB_ItemDialog_NarratorAD((ITEM)S_HAV_Isobel_KethericBust_a0c16d51-8aa6-4b04-ab0b-617c428e6262,HAV_Isobel_AD_KethericBust_17d554e9-f90e-47bd-eca0-54524428d5fd);

DB_GLO_DefeatCounter_AllPermaDefeatedGlobalFlag("HAV_Meelock_DefeatCounter",HAV_Misc_State_MeenlocksPermaDefeated_056f0eff-93f2-468a-94e6-fd8aacb54ce7);
KBSECTION
//REGION Flaming Fists

IF
DB_HAV_SavingPrisoners_FlamingFist(_NPC)
THEN
SetOnStage(_NPC, 0);

PROC
PROC_HAV_FlamingFists_Init()
AND
NOT DB_PermaDefeated((CHARACTER)S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9)
AND
DB_GlobalFlag((FLAG)PLA_Desire_State_Freed2_46dbc8ef-61fa-42c4-bc89-4a8f53d53e5b)
THEN
DB_HAV_FlorrickInHAV(1);
RemoveStatus(S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9, "DIRT_COVERED_FULL", NULL_00000000-0000-0000-0000-000000000000);
RemoveStatus(S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9, "SWEATY_LIGHT", NULL_00000000-0000-0000-0000-000000000000);
TeleportTo(S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9, S_HAV_FlorrickPos_ab39d643-3ba1-41db-aafe-aca064be0e0c, "HAV_SavingPrisoners_FlorrickToHaven");
PROC_GlobalSetFlagAndCache(HAV_HavenOutcasts_State_FlamingFistsInHaven_f8e5cd60-867a-4584-8c50-1ff917357acc);
TeleportTo(S_HAV_HavenOutcasts_EntranceGuards_Caster_HighElf_825714dd-7df9-4cd1-aecb-edf577baa487, S_HAV_HavenOutcasts_Pos_EntranceGuards_Caster_HighElf_9570b9bb-43bd-4852-86aa-3b5c31fcd8dd, "");	//Move backup FF guard away from FF room.
SetOnStage(S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9, 1);

PROC
PROC_HAV_FlamingFists_Init()
AND
NOT DB_PermaDefeated((CHARACTER)S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9)
AND
DB_GlobalFlag((FLAG)PLA_Desire_State_Freed2_46dbc8ef-61fa-42c4-bc89-4a8f53d53e5b)
AND
DB_HAV_SavingPrisoners_FlamingFist(_FlamingFist)
THEN
SetOnStage(_FlamingFist, 1);
PROC_GlobalSetFlagAndCache((FLAG)HAV_CursedFist_State_CursedFistNotAlone_4d268b7e-76cd-4a54-9878-f86135b24ab8);

IF
EntityEvent(S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9, "HAV_SavingPrisoners_FlorrickToHaven")
THEN
ToInventory(S_HAV_DukeSupplies_edc4b3a4-f5a1-45ef-b254-c559548452af, S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9, 1, 0, 1);
DB_Dialogs((CHARACTER)S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9, (DIALOGRESOURCE)HAV_Florrick_97692dad-7127-170b-00ce-9ee3a08b1df4);
DB_HAV_SavingPrisoners_FlorrickAndManipScene(1);
LookFromTrigger(S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9, S_HAV_FlorrickPos_ab39d643-3ba1-41db-aafe-aca064be0e0c, 1);

PROC
PROC_StateSet_PermaDefeated(S_HAV_AncientFist_MartCullagh_378938e2-2193-49a3-a06e-8e26cc128055)
THEN
NOT DB_HAV_SavingPrisoners_FlorrickAndManipScene(1);

QRY
QRY_SelectCustomDialog_AfterGenerics(_NPC, _Player)
AND
DB_HAV_SavingPrisoners_FlorrickAndManipScene(1)
AND
DB_HAV_SavingPrisoners_FlamingFistLeads((CHARACTER)_NPC)
AND
NOT DB_GlobalFlag(HAV_Mood_State_Siege_b509af8b-a331-47ac-8230-3b8c5ef9956b)
AND
DB_Dialogs((CHARACTER)S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9, HAV_Florrick_97692dad-7127-170b-00ce-9ee3a08b1df4)
THEN
DB_SelectedDialog((DIALOGRESOURCE)HAV_Florrick_97692dad-7127-170b-00ce-9ee3a08b1df4, (CHARACTER)S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9, _Player, (CHARACTER)S_HAV_Manip_0b8fdd17-7ce6-4038-9d9d-49f5ddbc20be);
NOT DB_HAV_SavingPrisoners_FlorrickAndManipScene(1);

IF
DialogEnded((DIALOGRESOURCE)HAV_Florrick_97692dad-7127-170b-00ce-9ee3a08b1df4,_)
THEN
SetFlag(SCL_AncientFist_InitialDialogComplete_fac9d20d-2f55-425d-bdfb-2ad9e23a9b06,NULL_00000000-0000-0000-0000-000000000000);

IF
FlagSet(HAV_SavingPrisoners_Event_ManipLeavesFlorrick_fb8f1924-d622-f0b8-1467-b1e879165141, _, _ID)
AND
DB_DialogNPCs(_ID, S_HAV_Manip_0b8fdd17-7ce6-4038-9d9d-49f5ddbc20be, _)
AND
DialogRemoveActorFromDialog(_ID, S_HAV_Manip_0b8fdd17-7ce6-4038-9d9d-49f5ddbc20be, _)
THEN
DB_NOOP(1);

IF
DB_GlobalFlag(HAV_SavingPrisoners_Event_ManipLeavesFlorrick_fb8f1924-d622-f0b8-1467-b1e879165141)
AND
NOT DB_DialogNPCs(_, S_HAV_Manip_0b8fdd17-7ce6-4038-9d9d-49f5ddbc20be, _)
THEN
NOT DB_HAV_SavingPrisoners_FlorrickAndManipScene(1);
PROC_CharacterMoveTo(S_HAV_Manip_0b8fdd17-7ce6-4038-9d9d-49f5ddbc20be, S_HAV_VidorBarPos_738c4466-9e91-4b16-93f0-c22eac526c1c, "Stroll", "HAV_SavingPrisoners_VidorAtBar");

//REGION Florrick leaves HAV to act 3

IF
FlagSet((FLAG)HAV_SavingPrisoners_HasMet_Florrick_890d4f79-e94f-a90a-c656-5c9ed49d0ec6,_,_)
AND
DB_HAV_FlorrickInHAV(1)
AND
NOT DB_GlobalFlag(HAV_Florrick_State_LeaveForCity_28f9ed3b-83e0-da1c-595d-2dcb8ffdafc4)
THEN
PROC_GlobalSetFlagAndCache(HAV_Florrick_State_LeaveForCity_28f9ed3b-83e0-da1c-595d-2dcb8ffdafc4);

IF
DB_GlobalFlag((FLAG)GLO_MoonriseTower_EverEnteredBefore_26aeabb0-7644-4897-9bbd-7a95e3e8930f)
AND
DB_HAV_FlorrickInHAV(1)
AND
NOT DB_GlobalFlag(HAV_Florrick_State_LeaveForCity_28f9ed3b-83e0-da1c-595d-2dcb8ffdafc4)
THEN
PROC_GlobalSetFlagAndCache(HAV_Florrick_State_LeaveForCity_28f9ed3b-83e0-da1c-595d-2dcb8ffdafc4);

IF
DB_GlobalFlag((FLAG)Act2_PointOfNoReturnReached_a3155f30-b8f3-4db5-ac21-d3036f4426e3)
AND
DB_HAV_FlorrickInHAV(1)
AND
NOT DB_GlobalFlag(HAV_Florrick_State_LeaveForCity_28f9ed3b-83e0-da1c-595d-2dcb8ffdafc4)
THEN
PROC_GlobalSetFlagAndCache(HAV_Florrick_State_LeaveForCity_28f9ed3b-83e0-da1c-595d-2dcb8ffdafc4);

IF
FlagSet(HAV_HavenOutcasts_State_FlamingFistsInHaven_f8e5cd60-867a-4584-8c50-1ff917357acc,_,_)
AND
IsActive(S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9,0)
THEN
DB_HAV_FlorrickDeactivated(1);

IF
Activated(S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9)
AND
DB_HAV_FlorrickDeactivated(1)
THEN
NOT DB_HAV_FlorrickDeactivated(1);

IF
Deactivated(S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9)
AND
DB_GlobalFlag(HAV_HavenOutcasts_State_FlamingFistsInHaven_f8e5cd60-867a-4584-8c50-1ff917357acc)
THEN
DB_HAV_FlorrickDeactivated(1);

IF
DB_GlobalFlag(HAV_Florrick_State_LeaveForCity_28f9ed3b-83e0-da1c-595d-2dcb8ffdafc4)
AND
DB_HAV_FlorrickDeactivated(1)
AND
DB_HAV_FlorrickInHAV(1)
AND
NOT DB_HAV_AnyPartyMemberInHaven(1)
AND
NOT DB_Defeated(S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9)
THEN
PROC_HAV_FlamingFistLeave();

PROC
PROC_HAV_FlamingFistLeave()
THEN
NOT DB_HAV_FlorrickInHAV(1); //needs to happen first or infinite loops will happen
NOT DB_HAV_FlorrickDeactivated(1);
DB_HAV_FlamingFistLeaveHaven((CHARACTER)S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9);
DB_HAV_FlamingFistLeaveHaven((CHARACTER)S_HAV_FlamingFist_006_9629c27e-8e50-4258-8bda-6639af768ba6);
DB_HAV_FlamingFistLeaveHaven((CHARACTER)S_HAV_Manip_0b8fdd17-7ce6-4038-9d9d-49f5ddbc20be);
DB_HAV_FlamingFistLeaveHaven((CHARACTER)S_HAV_FlamingFist_001_47058367-9c0c-4756-a467-07b87b28e0d6);

IF
DB_HAV_FlamingFistLeaveHaven((CHARACTER)_Character)
THEN
PROC_Poof((CHARACTER)_Character);

IF
WentOnStage(_Character,0)
AND
DB_HAV_FlamingFistLeaveHaven((CHARACTER)_Character)
THEN
SetEntityEvent(_Character,"HAV_FlamingFist_LeftHaven",1);
NOT DB_HAV_FlamingFistLeaveHaven((CHARACTER)_Character);

IF
EntityEvent((CHARACTER)_Character,"HAV_FlamingFist_LeftHaven")
AND
DB_HAV_Siege_NPCs(_Character)
THEN
NOT DB_HAV_Siege_NPCs(_Character);

IF
EntityEvent((CHARACTER)S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9,"HAV_FlamingFist_LeftHaven")
THEN
SetOnStage(S_HAV_Florrick_Note_43785054-670c-42c5-bc43-38ab269ed40f,1);

IF
EntityEvent((CHARACTER)S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9,"HAV_FlamingFist_LeftHaven")
AND
GetHostCharacter(_Player)
THEN
DebugText(_Player,"Florrick left HAV");

IF
LeftLevel(S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9,"SCL_Main_A")
AND
DB_HAV_FlorrickInHAV(1)
THEN
NOT DB_HAV_FlorrickInHAV(1);

//END_REGION

//END_REGION

//REGION Meenlock
IF
DB_HAV_Misc_Meenlock(_Meenlock)
THEN
DB_GLO_DefeatCounter(_Meenlock, "HAV_Meelock_DefeatCounter");

IF
TurnStarted(_Player)
AND
DB_Players((CHARACTER)_Player)
AND
QRY_SpeakerIsAvailable(_Player, 1)
AND
DB_HAV_Misc_Meenlock(_Meenlock)
AND
DB_Is_InCombat(_Meenlock, _ID)
AND
DB_Is_InCombat(_Player, _ID)
AND
QRY_OnlyOnce("HAV_Misc_MeenlockAD")
THEN
PROC_TrySkillCheck(_Player, _Meenlock, "Arcana", (DIFFICULTYCLASS)Legacy_10_625be976-7a67-4394-97c8-14c69715ae4b, "HAV_Misc_Arcana_Meenlock");

PROC
PROC_RollResult("HAV_Misc_Arcana_Meenlock", _Player, _, 1)
AND
NOT DB_GlobalFlag((FLAG)HAV_Misc_State_PassedMeenlockArcanaCheck_e5cd5885-bd08-4ebd-b068-1ae70cae3222)
THEN
PROC_GlobalSetFlagAndCache((FLAG)HAV_Misc_State_PassedMeenlockArcanaCheck_e5cd5885-bd08-4ebd-b068-1ae70cae3222);

PROC
PROC_RollResult("HAV_Misc_Arcana_Meenlock", _Player, _, _)
THEN
StartVoiceBark((VOICEBARKRESOURCE)HAV_Misc_VB_MeenlockReaction_cd668afd-c45c-d58c-871b-e830adb7fdfc, _Player);

PROC
PROC_LongRest()
AND
DB_GlobalFlag(HAV_Misc_State_MeenlocksPermaDefeated_056f0eff-93f2-468a-94e6-fd8aacb54ce7)
AND
QRY_OnlyOnce("HAV_Misc_MeenlockBasementPartyBanter")
AND
NOT DB_WorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_HAV_CellarCleared_c736c59f-d015-49b7-a680-9220524d4498, _, _)
THEN
PROC_RegisterWorldGossipTrigger(S_PartyBanterTrigger_HAV_CellarCleared_c736c59f-d015-49b7-a680-9220524d4498);

//END_REGION

//REGION Reading Jaheira's Map
IF
TemplateUseStarted(_Player, PUZ_GEN_Map_SCL_Region_A_e8b3a769-151d-4224-829e-36748bb18890, _)
AND
DB_Players(_Player)
AND
QRY_OnlyOnce("SCL_UsedJaheirasMap")
THEN
ShowMapMarker(_Player, "TWN_Distillery_SUB", 1);
ShowMapMarker(_Player, "SCL_MoonriseTowerMarker", 1);
ShowMapMarker(_Player, "SCL_OliversHouse", 1);
ShowMapMarker(_Player, "TWN_Tollhouse_SUB", 1);
//END_REGION

//REGION Debug

IF
TextEvent("havenflorrick")
THEN
SetFlag(Debug_Act2Setup_State_DesireSaved_9ab00499-75ee-2b2f-74f9-fed1957e3f26, NULL_00000000-0000-0000-0000-000000000000, 0);

IF
FlagSet(Debug_Act2Setup_State_DesireSaved_9ab00499-75ee-2b2f-74f9-fed1957e3f26, _, _)
THEN
PROC_GlobalSetFlagAndCache(PLA_Desire_State_Freed2_46dbc8ef-61fa-42c4-bc89-4a8f53d53e5b);
PROC_HAV_FlamingFists_Init();
ClearFlag(Debug_Act2Setup_State_DesireSaved_9ab00499-75ee-2b2f-74f9-fed1957e3f26, NULL_00000000-0000-0000-0000-000000000000, 0);


//END_REGION

//TG - 35 - Is in Haven
IF
DB_InRegion(_Player, (TRIGGER)S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab)
AND
DB_Players(_Player)
AND
QRY_OnlyOnce("TG_HAV_MISC_InInHaven")
AND
DB_Avatars(_Avatar)
THEN
SetFlag((FLAG)TG_HAV_Misc_IsInHaven_f6e3f5a0-d553-41f2-9d6e-78cea503c992, _Avatar);

PROC
PROC_State_Changed(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Area", "HAV_Area_State_BarrierIsOff")
THEN
PROC_RemoveOneShotTrigger(S_HAV_DaisyAD_Haven_cfd53d07-fd41-41e1-9313-20387cb9b1c2);

PROC
PROC_OneShotTriggerEntered(_Player,S_HAV_DaisyAD_Haven_cfd53d07-fd41-41e1-9313-20387cb9b1c2)
THEN
PROC_DaisyAD(_Player,HAV_DaisyAD_DiscoveredHaven_1089c14b-9f22-be0c-6a4c-bf6aef1df583);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
