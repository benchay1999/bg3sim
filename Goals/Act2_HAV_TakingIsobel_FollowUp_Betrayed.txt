Version 1
SubGoalCombiner SGC_AND
INITSECTION
PROC_HAV_TakingIsobel_Followup_Betrayed_Setup();

KBSECTION
//REGION Init

PROC
PROC_HAV_TakingIsobel_Followup_Betrayed_Setup()
AND
DB_GlobalFlag(SCE_Debrief_State_Setup_0ebc6b35-0f07-4a73-b6b8-9782ecbcaccf)
THEN
GoalCompleted;

PROC
PROC_HAV_TakingIsobel_Followup_Betrayed_Setup()
AND
DB_GlobalFlag(MOO_Assault_State_InProgress_0f3a8f5d-7402-4220-bebb-d4b21d3db08d)
THEN
GoalCompleted;




IF
DialogEnded(HAV_Siege_Intro_5ec5abc1-a0d5-5c4e-05bf-b65766c05b99,_)
AND
NOT DB_GlobalFlag(SCE_Debrief_State_Setup_0ebc6b35-0f07-4a73-b6b8-9782ecbcaccf)
AND
NOT DB_GlobalFlag(MOO_Assault_State_InProgress_0f3a8f5d-7402-4220-bebb-d4b21d3db08d)
AND
DB_State_Current(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_FollowUp_DefeatedInHaven")
THEN
PROC_HAV_TakingIsobel_Followup_Betrayed_Setup(1);

IF
DialogEnded(HAV_Siege_Intro_5ec5abc1-a0d5-5c4e-05bf-b65766c05b99,_)
AND
NOT DB_GlobalFlag(SCE_Debrief_State_Setup_0ebc6b35-0f07-4a73-b6b8-9782ecbcaccf)
AND
NOT DB_GlobalFlag(MOO_Assault_State_InProgress_0f3a8f5d-7402-4220-bebb-d4b21d3db08d)
AND
DB_State_Current(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_DefeatedInHaven")
THEN
PROC_HAV_TakingIsobel_Followup_Betrayed_Setup(1);

PROC
PROC_HAV_TakingIsobel_Followup_Betrayed_Setup(1)
AND
GetPosition(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, _X, _Y, _Z)
AND
IsInInventoryOf(S_GLO_DriderMoonlantern_4591d212-8f1b-4b85-880c-dc94f76702f4, S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, 1)
THEN
TeleportToPosition(S_GLO_DriderMoonlantern_4591d212-8f1b-4b85-880c-dc94f76702f4, _X, _Y, _Z, "HAV_Takingisobel_IsobelDropsMoonlantern", 0, 0, 0, 0, 1);

PROC
PROC_HAV_TakingIsobel_Followup_Betrayed_Setup(1)
AND
DB_HAV_TakingIsobel_FlyingGhouls(_Ghoul)
AND
NOT DB_Defeated(_Ghoul)
THEN
PROC_Poof(_Ghoul);

PROC
PROC_HAV_TakingIsobel_Followup_Betrayed_Setup(1)
AND
DB_State_Current(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_DefeatedInHaven")
AND
DB_HAV_TakingIsobel_Kidnapper(_Kidnapper)
THEN
SetOnStage(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211,0);
SetOnStage(_Kidnapper,0);
SetFlag(HAV_TakingIsobel_State_IsobelTakenAway_b1c15a65-8060-4b9e-a73f-bde4cc143f7d, NULL_00000000-0000-0000-0000-000000000000, 0);
RemovePreferredAiTargetTag(_Kidnapper, (TAG)ACT2_HAV_AI_HINT_ISOBELFLEE_3f261d03-8132-4f0d-a954-689a040f9f58);
ClearTag(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, (TAG)ACT2_HAV_AI_HINT_ISOBELFLEE_3f261d03-8132-4f0d-a954-689a040f9f58);
PROC_State_Progress(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_AtMoonrise");


IF
DB_DefeatedCauses(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, _, _)
AND
DB_Is_InCombat(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, _CombatID)
AND
DB_HAV_TakingIsobel_MainRoomCombat(_CombatID)
THEN
EndCombat(_CombatID);

//END_REGION

//REGION Isobel Taken
//--- Jaheira talks to player after the fight

IF
TimerFinished("HAV_TakingIsobel_FollowUpDelay")
AND
DB_HAV_TakingIsobel_FollowUpDialog(_)
AND
DB_HAV_SpyCapture_PrioPlayer(_Player)
AND
IsInTrigger(_Player, S_HAV_InnInteriorFollowUpBox_c1d2d3f4-5461-4d30-a774-86e1867326cc, _IsInInn)
AND
IsInTrigger(_Player, S_HAV_IsobelRoomFollowUpBox_1ddc9945-3187-4eee-a2f3-8e2c06e5c06f, _IsInRoom)
AND
QRY_HAV_TakingIsobel_TryFollowUpSamePlayer(_Player, _IsInInn, _IsInRoom)
THEN
PROC_TriggerRegisterForPlayers(S_HAV_InnInteriorFollowUpBox_c1d2d3f4-5461-4d30-a774-86e1867326cc);
PROC_TriggerRegisterForPlayers(S_HAV_IsobelRoomFollowUpBox_1ddc9945-3187-4eee-a2f3-8e2c06e5c06f);

//if the same player as before is in the room or the inn, try following up with them first
QRY
QRY_HAV_TakingIsobel_TryFollowUpSamePlayer((CHARACTER)_Player, (INTEGER)_IsInInn, (INTEGER)_IsInRoom)
AND
DB_Players(_Player)
AND
NOT DB_CantTalk(_Player)
AND
DB_LogicOr(_IsInInn, _IsInRoom, 1)
THEN
DB_HAV_TakingIsobel_FollowUpPlayer(_Player);

QRY
QRY_HAV_TakingIsobel_TryFollowUpSamePlayer((CHARACTER)_Player, (INTEGER)_IsInInn, (INTEGER)_IsInRoom)
THEN
DB_NOOP(1);

//check any other PC
IF
DB_InRegion(_Player, S_HAV_IsobelRoomFollowUpBox_1ddc9945-3187-4eee-a2f3-8e2c06e5c06f)
AND
DB_Players(_Player)
AND
NOT DB_HAV_TakingIsobel_FollowUpPlayer(_Player)
THEN
DB_HAV_TakingIsobel_FollowUpPlayer(_Player);

IF
DB_InRegion(_Player, S_HAV_InnInteriorFollowUpBox_c1d2d3f4-5461-4d30-a774-86e1867326cc)
AND
NOT DB_HAV_TakingIsobel_FollowUpPlayer(_Player)
THEN
DB_HAV_TakingIsobel_FollowUpPlayer(_Player);

IF
DB_HAV_TakingIsobel_FollowUpPlayer(_Player)
AND
NOT DB_InRegion(_Player, S_HAV_IsobelRoomFollowUpBox_1ddc9945-3187-4eee-a2f3-8e2c06e5c06f)
AND
NOT DB_InRegion(_Player, S_HAV_InnInteriorFollowUpBox_c1d2d3f4-5461-4d30-a774-86e1867326cc)
THEN
NOT DB_HAV_TakingIsobel_FollowUpPlayer(_Player);


// Run up and chat
IF
DB_HAV_TakingIsobel_FollowUpPlayer(_Player)
AND
DB_Players(_Player)
AND
NOT DB_CantTalk(_Player)
AND
NOT DB_CantMove(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a)
AND
NOT DB_CantTalk(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a)
AND
DB_HAV_TakingIsobel_FollowUpDialog(_Dialog)
AND
NOT QRY_OnlyOnce("HAV_TakingIsobel_FollowUpCapture")
THEN
PROC_TryStartAD(HAV_TakingIsobel_AD_FollowUp_cccf85eb-7788-c2b1-3dd1-eb7e59a08183, S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, _Player);

//END_REGION

PROC
PROC_SCE_SetupDebrief()
THEN
GoalCompleted;


PROC
PROC_State_Changed(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", _, "HAV_Isobel_State_AtMoonrise")
THEN
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2_HAV_TakingIsobel_AfterAbduction"
