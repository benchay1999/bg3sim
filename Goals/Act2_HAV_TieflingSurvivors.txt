Version 1
SubGoalCombiner SGC_AND
INITSECTION
//_Category, _Character, _DefaultPosition, _DefaultDialog, _IsAChild
//To add a character to Haven as part of Tiefling Survivors, fill this DB. The _Category string is used to sort which tieflings get setup following Act 1 decisions.

DB_HAV_TieflingSurvivors("Base", (CHARACTER)S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c, (TRIGGER)S_HAV_EnteringHaven_AlfiraPoint_86e4a9c8-e2a0-4464-9921-480bdb0400d7, (DIALOGRESOURCE)HAV_AlfiraTale_Bard_741330b4-77ee-f113-3974-4d4d61763d29, 0);
DB_HAV_TieflingSurvivors("Base", S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79,S_HAV_TieflingSurvivors_BlacksmithSpot_1_cfe93bf1-55c4-423d-8d85-f650bfb2afef,HAV_TieflingSurvivors_Weaponsmith_c2312421-411c-757e-551b-3e7ddbb9d7e7,0);
DB_HAV_TieflingSurvivors("Base", S_DEN_BackupLeader_b304e07d-019c-4ec6-b5ea-c4ccb29ed892,S_HAV_BackupLeaderPos_c5fbf71a-440c-4ad6-9731-400fb20dff68, HAV_BackupLeader_304e0466-0537-8efe-f4c3-181ad5806faa, 0);
DB_HAV_TieflingSurvivors("Prodigy", (CHARACTER)S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, (TRIGGER)S_HAV_ProdigyPos_e458d512-a43b-45ab-861c-6f18b02d60c4, (DIALOGRESOURCE)HAV_ProdigyLament_Rolan_ce19b6a1-2055-90e7-cc50-a141f424054d, 0);
DB_HAV_TieflingSurvivors("Mol", (CHARACTER)S_DEN_ThiefHideoutLeader_c8ab1ca6-96bb-467e-91c9-af87bc4d3925, (TRIGGER)S_HAV_EnteringHaven_MolPoint_9052d5fa-fd15-4fae-9faf-3f25544f99da, (DIALOGRESOURCE)HAV_MolsDeal_Mol_d47c649a-06cd-e554-8705-2d38a3214110, 1);
DB_HAV_TieflingSurvivors("Extra", (CHARACTER)S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f , (TRIGGER)S_HAV_TieflingSurvivors_UmiDefaultPosition_6e4a22d2-779f-4a25-800d-161a7e7e3657, (DIALOGRESOURCE)HAV_TieflingSurvivors_Umi_dc04aad9-6c53-0d5d-ab84-54ca945ba90b, 1);
DB_HAV_TieflingSurvivors("Extra", S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387, (TRIGGER)S_HAV_TieflingSurvivors_IdeDefaultPosition_f3609dc6-470d-45a0-90a3-a2579c947c96, (DIALOGRESOURCE)HAV_TieflingSurvivors_Ide_8f9e4dc1-452c-014c-11cf-8d240cbb5d19, 1);
DB_HAV_TieflingSurvivors("Extra", S_DEN_PickpocketTrader_e1fda08b-45d4-41db-84b5-20e9931ba5af, (TRIGGER)S_HAV_TieflingSurvivors_MattisDefaultPosition_e53bc7f2-64ce-46bb-b889-1209d36afe5c, (DIALOGRESOURCE)HAV_TieflingSurvivors_Mattis_4e74776f-1483-ee03-ed6a-3baae7fe5979, 1);
DB_HAV_TieflingSurvivors("Extra", S_DEN_CharmedKid_3b92c689-6024-4446-a6c9-584e9e8d77ca, (TRIGGER)S_HAV_TieflingSurvivors_MirkonDefaultPosition_e4a48b5a-d111-478d-b674-29382b8e3e76, (DIALOGRESOURCE)HAV_TieflingSurvivors_Mirkon_e613c25c-07af-b0e7-70d6-a480d5604e1e, 1);
DB_HAV_TieflingSurvivors("Extra", S_GLO_YoungLover_02_dcf597c0-36c5-4c81-957a-6fe9fca50a68,S_HAV_LoverPos_002_c51d63e6-ddc8-4838-baf3-0ace270537f4, HAV_LoneLover_Bex_4dafa77e-af5b-589c-c53d-7bb07700110c,0);

PROC_CharacterRemoveEquipmentPermanently((CHARACTER)S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f);
PROC_CharacterRemoveEquipmentPermanently((CHARACTER)S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387);

//REGION Rolan initial dialog
DB_HAV_TieflingSurvivors_RolanDialogParticipants((CHARACTER)S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b);
DB_HAV_TieflingSurvivors_RolanDialogParticipants((CHARACTER)S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f);
DB_HAV_TieflingSurvivors_RolanDialogParticipants((CHARACTER)S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387);

DB_HAV_TieflingSurvivors_KidIntroInterruptions("Attacked");
DB_HAV_TieflingSurvivors_KidIntroInterruptions("Died");
DB_HAV_TieflingSurvivors_KidIntroInterruptions("EnteredCombat");
DB_HAV_TieflingSurvivors_KidIntroInterruptions("LeftTrigger");

//END_REGION Rolan initial dialog

//REGION Mol Discussion
DB_HAV_TieflingSurvivors_MolDiscussionParticipants((CHARACTER)S_DEN_PickpocketTrader_e1fda08b-45d4-41db-84b5-20e9931ba5af);
DB_HAV_TieflingSurvivors_MolDiscussionParticipants((CHARACTER)S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f);
DB_HAV_TieflingSurvivors_MolDiscussionParticipants((CHARACTER)S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387);
DB_HAV_TieflingSurvivors_MolDiscussionParticipants((CHARACTER)S_DEN_CharmedKid_3b92c689-6024-4446-a6c9-584e9e8d77ca);
DB_GlobalFlagReactionAfterDialog((FLAG)HAV_TieflingSurvivors_Event_SendKidsToDeath_b489efd3-c243-4574-806a-42262bfb4f28, (DIALOGRESOURCE)HAV_TieflingSurvivors_KidsAfterGhoulAttack_0bf9f421-dc0b-0df6-60a4-f4035ce75123);
//END_REGION

SetOnStage(S_HAV_TieflingSurvivors_SuperOldMagicBook_2def7912-e561-4d60-a073-ddf64b7d3981, 0);

DB_HasItemEvent((ITEM)S_HAV_TieflingSurvivors_SuperOldMagicBook_2def7912-e561-4d60-a073-ddf64b7d3981, (FLAG)HAV_ProdigyLament_State_HasBook_da0fc561-0de8-4f14-b569-d249acc603ef);
DB_GiveItemToEvent((ITEM)S_HAV_TieflingSurvivors_SuperOldMagicBook_2def7912-e561-4d60-a073-ddf64b7d3981, (FLAG)HAV_ProdigyLament_Event_GiveBook_4c6ad7eb-6392-4e11-9b80-3ef41adcd561);

DB_GiveNewItemFromTemplateEvent((ITEMROOT)QUEST_HAV_InfernalReward_001_d9863839-e955-47a2-8a06-070f5c3c9541, (FLAG)HAV_TieflingSurvivors_Event_GiveReward_001_c1c4d2eb-ab94-42d0-812c-8985cf0d08e9, 1);
DB_GiveNewItemFromTemplateEvent((ITEMROOT)QUEST_HAV_InfernalReward_002_6d60fcbb-e7a5-47ad-9cca-44565cbc0856, (FLAG)HAV_TieflingSurvivors_Event_GiveReward_002_731a6948-354b-4fef-9688-c01d392549d9, 1);
DB_GiveNewItemFromTemplateEvent((ITEMROOT)QUEST_HAV_InfernalReward_003_b673f1dc-d925-4811-a78e-53adcce847e8, (FLAG)HAV_TieflingSurvivors_Event_GiveReward_003_75282b94-d0f5-44df-8b50-724fd09390b0, 1);

DB_HasItemTemplateScriptFlag(1, (DIALOGRESOURCE)HAV_TieflingSurvivors_Weaponsmith_c2312421-411c-757e-551b-3e7ddbb9d7e7, (ITEMROOT)S_Placeholder_MerregonMask_b2409a86-dc0d-4193-9985-4f11419c64be, 2);
DB_HasItemTemplateScriptFlag(2, (DIALOGRESOURCE)HAV_TieflingSurvivors_Weaponsmith_c2312421-411c-757e-551b-3e7ddbb9d7e7, (ITEMROOT)S_Placeholder_MerregonMask_b2409a86-dc0d-4193-9985-4f11419c64be, 2, 2);
DB_HasItemTemplateScriptFlag(3, (DIALOGRESOURCE)HAV_TieflingSurvivors_Weaponsmith_c2312421-411c-757e-551b-3e7ddbb9d7e7, (ITEMROOT)S_Placeholder_MerregonMask_b2409a86-dc0d-4193-9985-4f11419c64be, 2, 3);
DB_HasItemTemplateScriptFlag(4, (DIALOGRESOURCE)HAV_TieflingSurvivors_Weaponsmith_c2312421-411c-757e-551b-3e7ddbb9d7e7, (ITEMROOT)S_Placeholder_MerregonMask_b2409a86-dc0d-4193-9985-4f11419c64be, 2, 4);

DB_RemoveItemFromTemplateEvent((ITEMROOT)S_Placeholder_MerregonMask_b2409a86-dc0d-4193-9985-4f11419c64be, (FLAG)HAV_TieflingSurvivors_Event_GiveMask1_39a5ab65-dff3-464f-9c74-b090958421f0, (FLAG)HAV_TieflingSurvivors_State_GivenMask1_e1b6805e-269a-4895-a2c4-ad2be18e3901, 1);
DB_RemoveItemFromTemplateEvent((ITEMROOT)S_Placeholder_MerregonMask_b2409a86-dc0d-4193-9985-4f11419c64be, (FLAG)HAV_TieflingSurvivors_Event_GiveMask2_172bdf05-b858-4dea-a9c9-adf8b0ffd5d8, (FLAG)HAV_TieflingSurvivors_State_GivenMask2_4dc04557-f184-4899-83e6-728b76bacd69, 2);
DB_RemoveItemFromTemplateEvent((ITEMROOT)S_Placeholder_MerregonMask_b2409a86-dc0d-4193-9985-4f11419c64be, (FLAG)HAV_TieflingSurvivors_Event_GiveMask3_f9802e6a-9d98-43b5-b5d0-cd4feb850fc9, (FLAG)HAV_TieflingSurvivors_State_GivenMask3_b5921c5e-cc40-414e-8e9c-d1d479293168, 3);
DB_RemoveItemFromTemplateEvent((ITEMROOT)S_Placeholder_MerregonMask_b2409a86-dc0d-4193-9985-4f11419c64be, (FLAG)HAV_TieflingSurvivors_Event_GiveMask4_cc5a4f56-b514-4a03-a3ce-8d9bd8c94d53, (FLAG)HAV_TieflingSurvivors_State_GivenMask4_e1cb8928-a5be-47af-a365-7f4ce5fdfea1, 4);

DB_GiveNewItemFromTemplateEvent((ITEMROOT)QUEST_HAV_InfernalReward_004_b9eeaebb-9b00-4552-940f-5d829a66e8cb, (FLAG)HAV_TieflingSurvivors_Event_GiveMaskReward1_16dd9e3d-2efe-4427-aa99-420d9877e8ea, 1);
DB_GiveNewItemFromTemplateEvent((ITEMROOT)QUEST_HAV_InfernalReward_004_b9eeaebb-9b00-4552-940f-5d829a66e8cb, (FLAG)HAV_TieflingSurvivors_Event_GiveMaskReward2_bea1ab7f-302d-4d19-9a97-12f0b58193f5, 2);
DB_GiveNewItemFromTemplateEvent((ITEMROOT)QUEST_HAV_InfernalReward_004_b9eeaebb-9b00-4552-940f-5d829a66e8cb, (FLAG)HAV_TieflingSurvivors_Event_GiveMaskReward3_3b8c7ee0-b6b6-4779-805a-1fb5a0080201, 3);
DB_GiveNewItemFromTemplateEvent((ITEMROOT)QUEST_HAV_InfernalReward_004_b9eeaebb-9b00-4552-940f-5d829a66e8cb, (FLAG)HAV_TieflingSurvivors_Event_GiveMaskReward4_37da1809-1f10-4fcd-b738-792e16608917, 4);

DB_HAV_TieflingSurvivors_TreasureHunters((CHARACTER)S_DEN_CharmedKid_3b92c689-6024-4446-a6c9-584e9e8d77ca, (DIALOGRESOURCE)HAV_TieflingSurvivors_AD_PlayerFoundTreasure_MirkonsComment_de4e981d-3df6-7a94-ff56-6a20f4239238);
DB_HAV_TieflingSurvivors_TreasureHunters((CHARACTER)S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f, (DIALOGRESOURCE)HAV_TieflingSurvivors_AD_PlayerFoundTreasure_UmisComment_51820c86-0567-fd13-5d5e-63c046685754);

DB_DialogMoneyTransfer(1, (DIALOGRESOURCE)HAV_TieflingSurvivors_Mattis_4e74776f-1483-ee03-ed6a-3baae7fe5979, 1000);
DB_DialogMoneyTransfer(2, (DIALOGRESOURCE)HAV_TieflingSurvivors_Mattis_4e74776f-1483-ee03-ed6a-3baae7fe5979, 2000);

PROC_HAV_TieflingSurvivors_PreInit();

KBSECTION
//REGION Debug
IF
TextEvent("hav_tif")
THEN
PROC_HAV_TieflingSurvivors_DebugResurrect();
PROC_HAV_TieflingSurvivors_Init();
PROC_MOO_Jailbreak_Init();

IF
FlagSet(GLO_Debug_KillAlfira_0b367f4e-943b-02c9-f881-b88b2da86a54, _, _)
THEN
Die(S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c, DEATHTYPE.DoT, 1);
SetOnStage(S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c, 0);

IF
FlagSet(GLO_Debug_KillZevlor_595aa159-f1da-fbba-91e1-3e760692f358, _, _)
THEN
Die(S_DEN_TieflingLeader_475200ee-cc3c-4dbe-84b1-1820c02ea26a, DEATHTYPE.DoT, 1);
SetOnStage(S_DEN_TieflingLeader_475200ee-cc3c-4dbe-84b1-1820c02ea26a, 0);

IF
FlagSet(HAV_TieflingSurvivors_Debug_Setup_ee73faa7-3b8e-907b-84fe-8ad4aaf6f364, _, _)
AND
NOT DB_GlobalFlag((FLAG)GLO_Halsin_State_PermaDefeated_86bc3df1-08b4-fbc4-b542-6241bcd03df1)
AND
NOT DB_GlobalFlag((FLAG)DEN_AttackOnDen_State_RaiderVictory_abe1bce8-c234-4afe-a490-76210d98a078)
THEN
SetEntityEvent(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, "DebugTeleportedToCamp");
PROC_GlobalSetFlagAndCache((FLAG)GLO_Halsin_Knows_IsFound_a0d83476-e2d0-4972-a76b-b23c39078cd7);

PROC
PROC_HAV_TieflingSurvivors_DebugResurrect()
AND
DB_HAV_TieflingSurvivors(_, _Tiefling, _, _, _)
AND
DB_Defeated(_Tiefling)
THEN
NOT DB_Defeated(_Tiefling);
NOT DB_Dead(_Tiefling);
NOT DB_PermaDefeated(_Tiefling);
Resurrect(_Tiefling);

PROC
PROC_HAV_TieflingSurvivors_DebugResurrect()
AND
DB_HAV_TieflingSurvivors(_, _Tiefling, _, _, _)
AND
DB_Dead(_Tiefling)
THEN
NOT DB_Defeated(_Tiefling);
NOT DB_Dead(_Tiefling);
NOT DB_PermaDefeated(_Tiefling);
Resurrect(_Tiefling);

PROC
PROC_HAV_TieflingSurvivors_DebugResurrect()
AND
DB_MOO_Jailbreak_PotentialTiefling(_Tiefling, _, _, _)
AND
DB_Dead(_Tiefling)
THEN
NOT DB_Defeated(_Tiefling);
NOT DB_Dead(_Tiefling);
NOT DB_PermaDefeated(_Tiefling);
Resurrect(_Tiefling);

//END_REGION Debug

//REGION Init setup
PROC
PROC_HAV_TieflingSurvivors_PreInit()
AND
QRY_OnlyOnce("HAV_TieflingSurvivors_InitialSetup")
THEN
PROC_HAV_TieflingSurvivors_Init();

//Case 1: Helped tieflings, Rolan with them. Usual setup.
PROC
PROC_HAV_TieflingSurvivors_Init()
AND
DB_GlobalFlag(DEN_TieflingRefugees_State_LeftDen_003b304b-f058-4f8a-b9fa-a097e1b6b395)
AND
NOT DB_OnlyOnce("DEN_AttackOnDen_BetrayTieflings")
AND
DB_GlobalFlag(DEN_Prodigy_State_Staying_479db0b1-cc1b-78bf-3e7b-9840747fbd4f)
AND
NOT DB_PermaDefeated(S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b)
THEN
PROC_HAV_TieflingSurvivors_SavedAndRolanSetup();

//Case 2: Helped tieflings, no Rolan or Rolan dead. Kill most Haven characters.
PROC
PROC_HAV_TieflingSurvivors_Init()
AND
DB_GlobalFlag(DEN_TieflingRefugees_State_LeftDen_003b304b-f058-4f8a-b9fa-a097e1b6b395)
AND
NOT DB_OnlyOnce("DEN_AttackOnDen_BetrayTieflings")
AND
DB_GlobalFlag(DEN_Prodigy_State_Staying_479db0b1-cc1b-78bf-3e7b-9840747fbd4f)
AND
DB_PermaDefeated(S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b)
THEN
PROC_HAV_TieflingSurvivors_SavedNoRolanSetup();

PROC
PROC_HAV_TieflingSurvivors_Init()
AND
DB_GlobalFlag(DEN_TieflingRefugees_State_LeftDen_003b304b-f058-4f8a-b9fa-a097e1b6b395)
AND
NOT DB_OnlyOnce("DEN_AttackOnDen_BetrayTieflings")
AND
NOT DB_GlobalFlag(DEN_Prodigy_State_Staying_479db0b1-cc1b-78bf-3e7b-9840747fbd4f)
THEN
PROC_HAV_TieflingSurvivors_SavedNoRolanSetup();

//Case 3: Didn`t help tieflings. Mol in Haven alone.
PROC
PROC_HAV_TieflingSurvivors_Init()
AND
NOT DB_GlobalFlag(DEN_TieflingRefugees_State_LeftDen_003b304b-f058-4f8a-b9fa-a097e1b6b395)
THEN
PROC_HAV_TieflingSurvivors_NotSavedSetup();

PROC
PROC_HAV_TieflingSurvivors_Init()
THEN
PROC_HAV_TieflingSurvivors_HandleRemainingTieflings();

//TODO: Handle deaths of tieflings
//Set all tieflings remaining in the Den off stage. This is for the case where they haven't left the grove and players go to Act 2.
PROC
PROC_HAV_TieflingSurvivors_HandleRemainingTieflings()
AND
DB_DEN_NPC(_Tiefling, _, _)
AND
IsTagged(_Tiefling, TIEFLING_aaef5d43-c6f3-434d-b11e-c763290dbe0c, 1)
THEN
SetOnStage(_Tiefling, 0);
PROC_DEN_RemoveFromDenNPCs(_Tiefling);

PROC
PROC_HAV_TieflingSurvivors_HandleRemainingTieflings()
AND
DB_LevelLoadedOnce("WLD_Main_A")
AND
DB_HAV_TieflingSurvivors(_Category, _Tiefling, _, _, _)
AND
NOT DB_HAV_TieflingSurvivors_ReachedHaven(_Category)
AND
QRY_HAV_TieflingSurvivors_CheckProdigyGroup(_Tiefling)
THEN
PROC_Children_RemoveNPC(_Tiefling);
TeleportTo(_Tiefling, S_HAV_TieflingsAsylumPos_9d1f8c06-608f-404c-b3bd-761f02aad29b, "");
Die(_Tiefling, DEATHTYPE.DoT, 1);
PROC_HAV_TieflingSurvivors_DidNotSurvive(_Tiefling);

QRY
QRY_HAV_TieflingSurvivors_CheckProdigyGroup((CHARACTER)_Tiefling)
AND
NOT DB_GLO_Prodigy_NPCs(_Tiefling)
THEN
DB_NOOP(1);

QRY
QRY_HAV_TieflingSurvivors_CheckProdigyGroup((CHARACTER)_Tiefling)
AND
DB_GLO_Prodigy_NPCs(_Tiefling)
AND
NOT DB_GlobalFlag((FLAG)DEN_Prodigy_State_Leaving_5f02e553-2798-7837-0a7c-6996c6352bbc)
THEN
DB_NOOP(1);

PROC
PROC_HAV_TieflingSurvivors_DidNotSurvive((CHARACTER)_Tiefling)
THEN
DB_NOOP(1);

//TODO: Potentially remove this or place in a global goal as DB_Children should get filled when Act 1 gets loaded.
PROC
PROC_HAV_TieflingSurvivors_HandleRemainingTieflings()
AND
DB_HAV_TieflingSurvivors(_Category, _Tiefling, _, _, 1)
AND
DB_HAV_TieflingSurvivors_ReachedHaven(_Category)
AND
NOT DB_Children(_Tiefling)
THEN
DB_Children(_Tiefling);

PROC
PROC_HAV_TieflingSurvivors_Init()
AND
DB_DEN_CurrentDefenderLeader((CHARACTER)S_DEN_TieflingLeader_475200ee-cc3c-4dbe-84b1-1820c02ea26a)
THEN
SetFlag(HAV_TieflingSurvivors_State_ZevlorWasLeader_0d1c95e7-994d-cc63-26ea-5d2ee41d0177);

PROC
PROC_HAV_TieflingSurvivors_Init()
AND
DB_DEN_CurrentDefenderLeader((CHARACTER)S_DEN_Trainer_02025646-347a-4235-aef7-e46b7c94b435)
THEN
SetFlag(HAV_TieflingSurvivors_State_AsharakWasLeader_85e88176-1f22-6a2f-cbeb-9f6dca9840ab);

PROC
PROC_HAV_TieflingSurvivors_SavedAndRolanSetup()
THEN
PROC_GlobalSetFlagAndCache((FLAG)HAV_TieflingSurvivors_TreasureHunt_State_WaitingForRolan_e96086cf-65cc-474d-8490-653bdbd584db);
PROC_GlobalSetFlagAndCache((FLAG)HAV_TieflingSurvivors_KidsAtHaven_bb09c46f-1973-4e2a-a36d-4fba65736ee0);
TriggerRegisterForCharacter(S_HAV_TieflingSurvivors_BookTriggerNearStairs_27dc7202-3f97-4bb8-b926-a1f5dfdf4f3b, S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387);
TriggerRegisterForCharacter(S_HAV_TieflingSurvivors_BookTriggerAtBarstand_bc53ac25-89c4-4534-89a2-3d8f57a9f258, S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387);
PROC_HAV_TieflingSurvivors_Setup("Base");
PROC_HAV_TieflingSurvivors_Setup("Extra");
PROC_HAV_TieflingSurvivors_Setup("Prodigy");
PROC_HAV_TieflingSurvivors_Setup("Mol");
PROC_HAV_TieflingSurvivors_MoveItemsFromLockboxToTieflings();
DB_MOO_Jailbreak_SetupTieflings(1);

PROC
PROC_HAV_TieflingSurvivors_SavedNoRolanSetup()
THEN
PROC_HAV_TieflingSurvivors_Setup("Base");
PROC_HAV_TieflingSurvivors_Setup("Mol");
DB_MOO_Jailbreak_SetupTieflings(1);

PROC
PROC_HAV_TieflingSurvivors_NotSavedSetup()
THEN
DB_NOOP(1);

PROC
PROC_HAV_TieflingSurvivors_Setup((STRING)_Category)
AND
DB_HAV_TieflingSurvivors(_Category, _Tiefling, _Trigger, _Dialog, _)
AND
NOT DB_PermaDefeated(_Tiefling)
THEN
PROC_DEN_RemoveFromDenNPCs(_Tiefling);	//For debug purposes.
SetFaction(_Tiefling, Act2_HAV_TieflingSurvivors_f4ff1850-c9fc-4ee5-80e7-fa6b2a958d39);
PROC_RemoveAllDialogEntriesForSpeaker(_Tiefling);
DB_Dialogs(_Tiefling, _Dialog);
TeleportTo(_Tiefling, _Trigger, "HAV_TieflingSurvivors_AtHaven");
SetOnStage(_Tiefling, 1);
SetLevel(_Tiefling, 4);
DB_HAV_SavingPrisoners_InHaven((CHARACTER)_Tiefling);

PROC
PROC_HAV_TieflingSurvivors_Setup("Base")
AND
NOT DB_PermaDefeated(S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79)
THEN
PROC_SetCustomTradeTreasure(S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79, "HAV_Weaponsmith_Trade");

PROC
PROC_HAV_TieflingSurvivors_Setup("Extra")
AND
NOT DB_PermaDefeated(S_DEN_PickpocketTrader_e1fda08b-45d4-41db-84b5-20e9931ba5af)
THEN
PROC_GlobalSetFlagAndCache((FLAG)HAV_TieflingSurvivors_State_ChildrenSurvived_a288111a-9567-4f25-9fbb-57f416017895);
PROC_SetCustomTradeTreasure(S_DEN_PickpocketTrader_e1fda08b-45d4-41db-84b5-20e9931ba5af, "HAV_Thiefling_Trade");

PROC
PROC_HAV_TieflingSurvivors_Setup("Extra")
AND
NOT DB_PermaDefeated(S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387)
THEN
PROC_SetCustomTradeTreasure(S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387, "HAV_Thiefling_Ide_Trade");

PROC
PROC_HAV_TieflingSurvivors_Setup((STRING)_Category)
THEN
DB_HAV_TieflingSurvivors_ReachedHaven(_Category);

PROC
PROC_HAV_TieflingSurvivors_MoveItemsFromLockboxToTieflings()
THEN
ToInventory(S_TWN_MasonsGuild_SilverKey_a5ea7cd3-6599-40e5-b609-50056b65a361, S_DEN_PickpocketTrader_e1fda08b-45d4-41db-84b5-20e9931ba5af, 1, 0, 1);
DB_HasItemEvent((ITEM)S_TWN_MasonsGuild_SilverKey_a5ea7cd3-6599-40e5-b609-50056b65a361, (FLAG)HAV_TieflingSurvivors_State_HasSilverKey_cb3808e2-2e8f-4b05-9582-03fd99556300);
DB_GiveItemToEvent((ITEM)S_TWN_MasonsGuild_SilverKey_a5ea7cd3-6599-40e5-b609-50056b65a361, (FLAG)HAV_TieflingSurvivors_Event_GiveSilverKey_a6681e1e-1832-4972-88c2-4fad80c5035b);
SetEntityEvent(S_HAV_TieflingSurvivors_Container_5fbf6ff3-a6f0-477a-b1c5-b07284fd4d2c, "RevealFromStory");

IF
DialogEnded(DebugBook_3af7dec4-4c08-9e44-9064-ec038ebad0ea, _)
AND
DB_GlobalFlag((FLAG)HAV_TieflingSurvivors_Debug_Setup_ee73faa7-3b8e-907b-84fe-8ad4aaf6f364)
THEN
ClearFlag(HAV_TieflingSurvivors_Debug_Setup_ee73faa7-3b8e-907b-84fe-8ad4aaf6f364, NULL_00000000-0000-0000-0000-000000000000, 0);
PROC_HAV_TieflingSurvivors_DebugResurrect();
PROC_HAV_TieflingSurvivors_Init();
PROC_MOO_Jailbreak_Init();

PROC
PROC_HAV_TieflingSurvivors_Setup(_Category)
THEN
PROC_HAV_TieflingSurvivors_OnSetupDone(_Category);

// Override in the other goals to react when setup is done.
PROC
PROC_HAV_TieflingSurvivors_OnSetupDone((STRING)_Category)
THEN
DB_NOOP(1);
//END_REGION


// REGION Alert & Unprotected state
// the scene between Roland and the kids is cancelled
// the kids instead gather near the bar stand to discuss things
QRY
QRY_SelectCustomDialog_AfterGenerics((CHARACTER)_NPC, _Player)
AND
DB_HAV_TieflingSurvivors_MolDiscussionParticipants(_NPC)
AND
QRY_HAV_TieflingSurvivors_KidsAreDiscussingMol()
AND
NOT DB_GlobalFlag((FLAG)HAV_TieflingSurvivors_Event_SendKidsToDeath_b489efd3-c243-4574-806a-42262bfb4f28)
AND
NOT DB_GlobalFlag((FLAG)HAV_TieflingSurvivors_State_PromisedToSaveMol_9b927edd-a5e7-4462-bea6-ff1969c8c00f)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_DEN_PickpocketTrader_e1fda08b-45d4-41db-84b5-20e9931ba5af, _Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f, _Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387, _Player)
AND
QRY_OnlyOnce("HAV_ThieflingsHaveDiscussedSavingMol")
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)HAV_TieflingSurvivors_KidsAfterGhoulAttack_0bf9f421-dc0b-0df6-60a4-f4035ce75123, 
	(CHARACTER)S_DEN_PickpocketTrader_e1fda08b-45d4-41db-84b5-20e9931ba5af, 
	(CHARACTER)S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f,
	(CHARACTER)S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387, _Player)
THEN
DB_NOOP(1);

PROC
PROC_StartDialog_AddExtraSpeakers((DIALOGRESOURCE)HAV_TieflingSurvivors_KidsAfterGhoulAttack_0bf9f421-dc0b-0df6-60a4-f4035ce75123, (INTEGER)_ID)
AND
NOT DB_Defeated((CHARACTER)S_DEN_CharmedKid_3b92c689-6024-4446-a6c9-584e9e8d77ca)
THEN
PROC_DialogAddSpeakingActor(_ID, S_DEN_CharmedKid_3b92c689-6024-4446-a6c9-584e9e8d77ca);

QRY
QRY_HAV_TieflingSurvivors_KidsAreDiscussingMol()
AND
DB_GlobalFlag((FLAG)HAV_Mood_State_Alert_b073eeb9-1d29-449f-baf5-74307e63d085)
THEN
DB_NOOP(1);

QRY
QRY_HAV_TieflingSurvivors_KidsAreDiscussingMol()
AND
DB_GlobalFlag((FLAG)HAV_Mood_State_Unprotected_7ac5d939-2c81-4871-81a5-fd5c6a0adba1)
THEN
DB_NOOP(1);

PROC
PROC_GlobalFlagReactionAfterDialog(HAV_TieflingSurvivors_Event_SendKidsToDeath_b489efd3-c243-4574-806a-42262bfb4f28)
AND
DB_HAV_TieflingSurvivors_MolDiscussionParticipants(_NPC)
THEN
PROC_DisappearOutOfSightTo(_NPC, S_HAV_TieflingSurvivors_KidsDisappearPosition_cf097a39-529e-4844-bdc3-81e40ac25b09, "Run", 0, "");

IF
EntityEvent(S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387, "HAV_TieflingSurvivors_AtHaven")
THEN
PROC_ToInventoryAndOnStage(S_HAV_TieflingSurvivors_SuperOldMagicBook_2def7912-e561-4d60-a073-ddf64b7d3981, S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387, 1, 0, 0);

IF
DialogEnded((DIALOGRESOURCE)HAV_TieflingSurvivors_KidsAfterGhoulAttack_0bf9f421-dc0b-0df6-60a4-f4035ce75123, _ID)
THEN
TeleportTo(S_HAV_TieflingSurvivors_MattisPositionAfterGhoulAttack_79a97dc8-7ec3-4cbd-a728-d6f2bd0f369c, S_HAV_TieflingSurvivors_MattisDefaultPosition_e53bc7f2-64ce-46bb-b889-1209d36afe5c, "MattisIdlePointTeleported", 0, 0, 0, 0, 1);
TeleportTo(S_HAV_TieflingSurvivors_IdePositionAfterGhoulAttack_a7d36238-040d-4d72-b128-bc1f4e2064a0, S_HAV_BarKidPos_002_f3609dc6-470d-45a0-90a3-a2579c947c96, "IdeIdlePointTeleported", 0, 0, 0, 0, 1);
TeleportTo(S_HAV_TieflingSurvivors_MirkonPositionAfterGhoulAttack_a1f9e9f4-9272-4005-baf6-0c0081f6cfc3, S_HAV_TieflingSurvivors_MirkonDefaultPosition_e4a48b5a-d111-478d-b674-29382b8e3e76, "MirkonIdlePointTeleport", 0, 0, 0, 0, 1);


// END_REGION Alert & Unprotected state - the scene between Roland and the kids is cancelled

//REGION Treasure Hunt
//TODO: ADs from the kids need to be in Anubis
IF
UseFinished(_Player, S_HAV_TieflingSurvivors_TreasureHunters_LoosePlank_ae2fca2e-7318-49e2-ba8d-dec73c605c17, 1)
AND
NOT QRY_CharacterIsHidden(_Player)
AND
DB_HAV_TieflingSurvivors_TreasureHunters(_TreasureHunter, _TreasureHunterComment)
AND
IsInTrigger(_TreasureHunter, (TRIGGER)S_HAV_TieflingSurvivors_TreasureHunters_MainRoom_7939197e-b625-485f-98be-e53efd83c841, 1)
AND
QRY_SpeakerIsAvailable(_TreasureHunter)
AND
QRY_OnlyOnce("HAV_TieflingSurvivors_PlayerFoundTreasureComment")
THEN
PROC_GlobalSetFlagAndCache((FLAG)HAV_TieflingSurvivors_State_FinishedTreasureHunt_fd6a1c5f-c674-4c1d-92e7-be7aec75efcc);
PROC_TryStartAD(_TreasureHunterComment, _TreasureHunter);
LookAtEntity(_TreasureHunter, _Player);
//END_REGION Treasure Hunt

//REGION Blacksmith Booster

//REGION Debug

IF
FlagSet((FLAG)HAV_TieflingSurvivors_Debug_GiveMetals_ccb37034-c831-40b6-a9cd-72d85ed9bf84, _Player, _)
THEN
TemplateAddTo((ITEMROOT)S_Placeholder_MerregonMask_b2409a86-dc0d-4193-9985-4f11419c64be, _Player, 9, 1);
SetFlag((FLAG)UND_InfernalPlate_HasPlate_defed9c5-46a4-4216-8f23-0821ab4c1ee0, _Player);
SetFlag((FLAG)SHA_Misc_State_HasInfernalMetal_1e9fcebf-20bf-431a-b805-ecf2a220075e, _Player);
SetFlag((FLAG)TUT_UpperDeck_State_HasInfernalMetal_f0a5546c-ff03-40fd-8150-dac63dc226c7, _Player);

//END_REGION

IF
FlagSet(_Flag, S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79, _)
AND
DB_GiveItemToEvent(_Item, _Flag)
AND
GetTemplate(_Item, _Template)
AND
DB_ORI_Karlach_InfernalMetalTemplates((ITEMROOT)_Template)
AND
DB_HasItemEvent(_Item, _HasFlag)
THEN
NOT DB_HasItemEvent(_Item, _HasFlag);
NOT DB_GiveItemToEvent(_Item, _Flag);
RequestDelete(_Item);

//END_REGION


EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
