Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION Dialogs
//TODO: Set default dialogs for Bazaar (rather than setting in editor)
DB_Dialogs(S_MOO_InfernalVendor_511c9413-25fe-449c-a81d-4b09bc20745a,(DIALOGRESOURCE)MOO_InfernalVendor_ed295125-2ed2-6b75-67e5-3ebd3539ccde);
DB_Dialogs(S_MOO_BazEntranceGuard_002_1a7719bc-9f42-4405-a07d-5d54af11ef0b, (DIALOGRESOURCE)MOO_BazEntranceGuard_002_5aabdf32-2ebb-ce6c-60d8-6f2154dacf66);
DB_Dialogs(S_MOO_BazaarStander_004_a6211285-d412-42c5-8c1c-ffe7feca2e09, (DIALOGRESOURCE)MOO_Bazaar_Bystander04_2d8613a9-c6b7-71b0-db04-f17609408d89);
DB_Dialogs(S_MOO_BazaarStander_007_f152d3c1-fe32-403f-9d7f-2f1049912e0f, (DIALOGRESOURCE)MOO_Bazaar_BazaarStander_007_1a4400df-fb9d-c991-711c-cdd4b2050612);
DB_Dialogs(S_MOO_BazaarStander_005_c31c6739-c991-490e-9857-2a3367042727, (DIALOGRESOURCE)MOO_Bazaar_BazaarStander_005_786cc41a-4d39-250d-0d47-029ca0881325);
DB_Dialogs(S_MOO_BazaarStander_006_5e4c22f4-1122-4822-bc16-ba49d3cb701e, (DIALOGRESOURCE)MOO_Bazaar_BazaarStander_006_7d545e07-3567-b57c-2950-dc1ed0e32b92);

//END_REGION
//REGION Entrance
PROC_TriggerRegisterForParty(S_MOO_BazaarRegion_3c1b8ef9-bb21-4c3e-9e0f-200fd4658a98);
PROC_TriggerRegisterForParty(S_MOO_GuardGuideADBox_185cfe8a-7996-42d6-96e1-dc9fe24d00ac);

//END_REGION
//REGION Bug Bear Vendor
DB_HasItemEvent(S_MOO_SulphurousNote_a7f04dc0-17c4-4a81-9762-4060b27b3a07, MOO_MoonriseBazaarSoulCoins_State_HasFloNote_9639de02-97f6-8efa-3d75-abdc9fb13229);
DB_GiveItemToEvent(S_MOO_SulphurousNote_a7f04dc0-17c4-4a81-9762-4060b27b3a07, MOO_MoonriseBazaarSoulCoins_Event_FloNoteHandoff_3ecdf2fb-b353-14f0-417c-7d8f99ae5c1e);
//END_REGION
//REGION Infernal Vendor
DB_PermaDefeatedFlag(S_MOO_InfernalVendor_511c9413-25fe-449c-a81d-4b09bc20745a, (FLAG)MOO_BloodMerchant_State_PermaDefeated_d5c9fc7e-05da-4e30-bb49-56c4a4792346);

SetOwner(S_MOO_BazaarPotion_319f9bc1-49ed-4eca-b3c5-3348ca2a3934, S_MOO_InfernalVendor_511c9413-25fe-449c-a81d-4b09bc20745a);
SetOwner(S_MOO_BazaarFlask_2de78691-766e-442a-8eee-93a2eaa8f6d8, S_MOO_InfernalVendor_511c9413-25fe-449c-a81d-4b09bc20745a);
SetOwner(S_MOO_BazaarVial_b8839cd6-0116-47f9-8f08-7790fbb6a82b, S_MOO_InfernalVendor_511c9413-25fe-449c-a81d-4b09bc20745a);
//END_REGION
//REGION Pilgrims
//DB_MOO_Bazaar_Pilgrims(_Char, _TownPoint, _ColonyPoint)
DB_MOO_Bazaar_Pilgrims((CHARACTER)S_MOO_BazaarStander_004_a6211285-d412-42c5-8c1c-ffe7feca2e09,(TRIGGER)S_MOO_PilgrimTownPoint_001_d0a8a1b1-5082-4eac-bf3d-dbe4c83e65ca,(TRIGGER)S_COL_AmbassadorRoom_PilgrimPoint_001_c523e2ac-c4f9-4ce7-8b99-f48cb05992b7);
DB_MOO_Bazaar_Pilgrims((CHARACTER)S_MOO_BazaarStander_007_f152d3c1-fe32-403f-9d7f-2f1049912e0f,(TRIGGER)S_MOO_PilgrimTownPoint_002_0c79dbe9-b1f2-4aaa-80c6-0511db34a137,(TRIGGER)S_COL_AmbassadorRoom_PilgrimPoint_002_aa6ed359-d00f-4d8a-a262-e56aa67d83c6);
DB_MOO_Bazaar_Pilgrims((CHARACTER)S_MOO_BazaarStander_005_c31c6739-c991-490e-9857-2a3367042727,(TRIGGER)S_MOO_PilgrimTownPoint_003_67d32791-281a-4006-8b92-228f6e113497,(TRIGGER)S_COL_AmbassadorRoom_PilgrimPoint_003_afe2014a-4306-4184-9741-7b40036ce554);
DB_MOO_Bazaar_Pilgrims((CHARACTER)S_MOO_BazaarStander_006_5e4c22f4-1122-4822-bc16-ba49d3cb701e,(TRIGGER)S_MOO_PilgrimTownPoint_004_16397845-2ac1-4626-be65-107f3018fba8,(TRIGGER)S_COL_AmbassadorRoom_PilgrimPoint_004_4e2538c2-872c-4749-8540-1d4a8200966f);

TriggerRegisterForCharacter((TRIGGER)S_MOO_BazaarRegion_3c1b8ef9-bb21-4c3e-9e0f-200fd4658a98,(CHARACTER)S_MOO_BazaarStander_004_a6211285-d412-42c5-8c1c-ffe7feca2e09);
TriggerRegisterForCharacter((TRIGGER)S_MOO_BazaarRegion_3c1b8ef9-bb21-4c3e-9e0f-200fd4658a98,(CHARACTER)S_MOO_BazaarStander_007_f152d3c1-fe32-403f-9d7f-2f1049912e0f);
TriggerRegisterForCharacter((TRIGGER)S_MOO_BazaarRegion_3c1b8ef9-bb21-4c3e-9e0f-200fd4658a98,(CHARACTER)S_MOO_BazaarStander_005_c31c6739-c991-490e-9857-2a3367042727);
TriggerRegisterForCharacter((TRIGGER)S_MOO_BazaarRegion_3c1b8ef9-bb21-4c3e-9e0f-200fd4658a98,(CHARACTER)S_MOO_BazaarStander_006_5e4c22f4-1122-4822-bc16-ba49d3cb701e);

DB_TriggerEvents_AllPlayersLeft((TRIGGER)S_MOO_MoonriseTower_SUB_14187ad9-cf83-44f9-81bf-bb46cb4cd8e6, "MOO_AllPlayersLeft");

//END_REGION
KBSECTION
//REGION DEBUG
IF
TextEvent("infvndr_addbook")
AND
GetHostCharacter(_Player)
THEN
SetFlag(FOR_DangerousBook_State_HasTome_4a6e2b36-8bcc-a067-6d1c-51c5cba542d3, _Player);

IF
FlagSet(Debug_Teleport_MOO_Bazaar_a36bdc7e-6e90-4ae0-b1f8-8d6834d49b79, _Player, _)
THEN
ClearFlag(Debug_Teleport_MOO_Bazaar_a36bdc7e-6e90-4ae0-b1f8-8d6834d49b79, _Player);
TeleportTo(_Player, S_MOO_BazaarDebugPoint_f464e163-4f6b-4e1a-be02-08dd8fa297ab);

IF
TextEvent("moobugbear_setflags")
AND
GetHostCharacter(_Player)
THEN
DebugText(_Player, "Set flags for Bugbear Vendor");
SetFlag(GLO_Volo_OperationFailed_c4d9a534-3cba-41ed-8176-937eaa0dfc1a);
SetFlag(DEN_AttackOnDen_State_RaiderVictory_abe1bce8-c234-4afe-a490-76210d98a078);
SetFlag(GOB_State_LeadersAreDead_a1c5b01f-4b7f-47ab-82b0-d24d9c6d8bc6);
SetTag(_Player, HAGEYED_c1f7de2b-7cc5-41bc-a723-011172bbc4ba);
//END_REGION
//REGION Entrance
IF
EnteredTrigger(_Player, S_MOO_BazaarRegion_3c1b8ef9-bb21-4c3e-9e0f-200fd4658a98)
AND
NOT DB_GlobalFlag(MOO_Bazaar_State_HasEnteredBazaar_1a0d2dbd-436f-48e7-afaf-9c25e77ebfcd)
THEN
SetFlag(MOO_Bazaar_State_HasEnteredBazaar_1a0d2dbd-436f-48e7-afaf-9c25e77ebfcd);

IF
EnteredTrigger(_Player,(TRIGGER)S_MOO_GuardGuideADBox_185cfe8a-7996-42d6-96e1-dc9fe24d00ac)
AND
NOT DB_HiddenCharacters(_Player, _)
AND
GetFaction(S_MOO_BazEntranceGuard_002_1a7719bc-9f42-4405-a07d-5d54af11ef0b, _Faction)
AND
GetRelation((FACTION)_Faction, (FACTION)Hero_a1542c81-6895-929e-4522-10ce218bb360, _Relation)
AND
_Relation > 0
AND
QRY_SpeakerIsAvailable(S_MOO_BazEntranceGuard_002_1a7719bc-9f42-4405-a07d-5d54af11ef0b)
THEN
PROC_TryStartAD((DIALOGRESOURCE)MOO_Bazaar_AD_BazEntranceGuard_002_15e1428a-49c5-b705-5b44-a5d99636be2c,(CHARACTER)S_MOO_BazEntranceGuard_002_1a7719bc-9f42-4405-a07d-5d54af11ef0b);
PROC_FaceCharacter(MOO_Bazaar_AD_BazEntranceGuard_002_15e1428a-49c5-b705-5b44-a5d99636be2c,_Player);

IF
FlagSet(MOO_GroundFloor_State_ExecutionOver_1560c0ae-5fec-4e7c-a6e7-35e9c33bbbeb, _, _)
THEN
PROC_TriggerUnregisterForParty(S_MOO_GuardGuideADBox_185cfe8a-7996-42d6-96e1-dc9fe24d00ac);

IF
AutomatedDialogStarted(MOO_Bazaar_AD_BazEntranceGuard_002_15e1428a-49c5-b705-5b44-a5d99636be2c, _)
THEN
PROC_TriggerUnregisterForParty(S_MOO_GuardGuideADBox_185cfe8a-7996-42d6-96e1-dc9fe24d00ac);
//END_REGION
//REGION Infernal Vendor
//Blood Trade
IF
FlagSet(MOO_InfernalVendor_Event_SellingBlood_1b431f87-8e14-4c0d-9ca7-b816d31898a0, _Player, _)
AND
GetHitPoints(_Player, _HP)
AND
_HP > 4
THEN
SetFlag(MOO_InfernalVendor_State_HasEnoughHealth_0a5fd694-37a4-4673-9126-9c3b4fbfe830, _Player);
ClearFlag(MOO_InfernalVendor_Event_SellingBlood_1b431f87-8e14-4c0d-9ca7-b816d31898a0, _Player);

IF
FlagSet(MOO_InfernalVendor_Event_SellingBlood_1b431f87-8e14-4c0d-9ca7-b816d31898a0, _Player, _)
AND
GetHitPoints(_Player, _HP)
AND
_HP <= 4
THEN
ClearFlag(MOO_InfernalVendor_State_HasEnoughHealth_0a5fd694-37a4-4673-9126-9c3b4fbfe830, _Player);
ClearFlag(MOO_InfernalVendor_Event_SellingBlood_1b431f87-8e14-4c0d-9ca7-b816d31898a0, _Player);

IF
FlagSet(MOO_InfernalVendor_State_SoldBlood_ae50cf43-a0ef-4ce6-bdb1-0ccf628b9a63, _Player, _)
AND
DB_GLO_BloodElixirs_RacialElixirTemplates(_Tag, _ItemTemplate, _)
AND
IsTagged(_Player, _Tag, 1)
AND
_Tag != REALLY_DRAGONBORN_39783f17-8484-46a6-aa3b-f3d51122e5f3
THEN
ApplyDamage(_Player, 3, "Piercing", _Player);
DB_MOO_InfernalVendor_AddingTemplateToPlayer((ITEMROOT)_ItemTemplate, (CHARACTER)_Player);
TemplateAddTo(_ItemTemplate, _Player, 1, 1);
AddAttitudeTowardsPlayer(S_MOO_InfernalVendor_511c9413-25fe-449c-a81d-4b09bc20745a, (CHARACTER)_Player, 15);

IF
FlagSet(MOO_InfernalVendor_State_SoldBlood_ae50cf43-a0ef-4ce6-bdb1-0ccf628b9a63, _Player, _)
AND
DB_GLO_BloodElixirs_RacialElixirTemplates(REALLY_DRAGONBORN_39783f17-8484-46a6-aa3b-f3d51122e5f3, _ItemTemplate, _DragonbornSubtype)
AND
IsTagged(_Player, REALLY_DRAGONBORN_39783f17-8484-46a6-aa3b-f3d51122e5f3, 1)
AND
HasPassive(_Player, _DragonbornSubtype, 1)
THEN
ApplyDamage(_Player, 3, "Piercing", _Player);
DB_MOO_InfernalVendor_AddingTemplateToPlayer((ITEMROOT)_ItemTemplate, (CHARACTER)_Player);
TemplateAddTo(_ItemTemplate, _Player, 1, 1);
AddAttitudeTowardsPlayer(S_MOO_InfernalVendor_511c9413-25fe-449c-a81d-4b09bc20745a, (CHARACTER)_Player, 15);

IF
TemplateAddedTo((ITEMROOT)_ItemTemplate, _Item, (CHARACTER)_Player, _)
AND
DB_MOO_InfernalVendor_AddingTemplateToPlayer(_ItemTemplate, _Player)
AND
NOT DB_GLO_BloodElixirs_BloodDrawTarget(_Item, _, _) // ignore transfer from other players if happens at the same time as getting the potion from the merchant
THEN
NOT DB_MOO_InfernalVendor_AddingTemplateToPlayer(_ItemTemplate, _Player);
DB_GLO_BloodElixirs_BloodDrawTarget(_Item, _Player, 0);
DB_MOO_InfernalVendor_CharacterSellBlood(_Player);

IF
FlagSet(MOO_InfernalVendor_Event_ReceiveGold_9692a5fc-18a1-4a53-9907-9a42fea203d6, _Player, _)
THEN
AddGold(_Player, 200);
ClearFlag(MOO_InfernalVendor_Event_ReceiveGold_9692a5fc-18a1-4a53-9907-9a42fea203d6, _Player);

//Escape if player threatened to expose them
IF
DialogEnded(MOO_InfernalVendor_ed295125-2ed2-6b75-67e5-3ebd3539ccde,_ID)
AND
DB_GlobalFlag(MOO_InfernalVendor_State_Rejected_cc5f3cf2-8e06-4c9b-91a5-47b35cea6243)
THEN
DB_SceneManager(S_MOO_InfernalVendor_511c9413-25fe-449c-a81d-4b09bc20745a, "MOO_InfernalVendor_Escaping");
PROC_MOO_InfernalVendor_Escape();

//If she does not have potion, flee anyways
PROC
PROC_MOO_InfernalVendor_Escape()
AND
NOT GetItemByTemplateInInventory((ITEMROOT)CONS_Potion_Invisible_A_809d5026-4896-4b3a-986e-95da58da77e2,(CHARACTER)S_MOO_InfernalVendor_511c9413-25fe-449c-a81d-4b09bc20745a,_)
THEN
PROC_DisappearOutOfSight(S_MOO_InfernalVendor_511c9413-25fe-449c-a81d-4b09bc20745a, "Run", 1, "MOO_InfernalVendor_Fled");

PROC
PROC_MOO_InfernalVendor_Escape()
AND
GetItemByTemplateInInventory((ITEMROOT)CONS_Potion_Invisible_A_809d5026-4896-4b3a-986e-95da58da77e2,(CHARACTER)S_MOO_InfernalVendor_511c9413-25fe-449c-a81d-4b09bc20745a,_InvisPotion)
THEN
Use(S_MOO_InfernalVendor_511c9413-25fe-449c-a81d-4b09bc20745a,_InvisPotion,"MOO_InfernalVendor_UsedInvisPotion");

IF
TemplateUseStarted(S_MOO_InfernalVendor_511c9413-25fe-449c-a81d-4b09bc20745a,CONS_Potion_Invisible_A_809d5026-4896-4b3a-986e-95da58da77e2,_)
THEN
ObjectTimerLaunch(S_MOO_InfernalVendor_511c9413-25fe-449c-a81d-4b09bc20745a,"MOO_InfernalVendor_TimeToLeave",3000,0);

IF
ObjectTimerFinished((CHARACTER)_InfernalVendor,"MOO_InfernalVendor_TimeToLeave")
THEN
PROC_DisappearOutOfSight(_InfernalVendor, "Run", 1, "MOO_InfernalVendor_Fled");

IF
EntityEvent(_, "MOO_InfernalVendor_Fled")
THEN
PROC_SceneOver("MOO_InfernalVendor_Escaping");

IF
DB_PermaDefeated(S_MOO_InfernalVendor_511c9413-25fe-449c-a81d-4b09bc20745a)
AND
DB_SceneManager(S_MOO_InfernalVendor_511c9413-25fe-449c-a81d-4b09bc20745a, "MOO_InfernalVendor_Escaping")
THEN
PROC_SceneOver("MOO_InfernalVendor_Escaping");
//END_REGION

//REGION Bug Bear Vendor
IF
FlagSet(MOO_BugBearVendor_State_EarnedRespect_f364408c-b1a0-469b-bf89-975cb8a197c7, _Player, _)
THEN
AddAttitudeTowardsPlayer(S_MOO_BugBearVendor_8cb1cace-ef98-4387-8b76-2e8efc5cfe89, (CHARACTER)_Player, 100);

IF
FlagSet(MOO_BugBearVendor_State_PrivateStash_b628c3e0-468c-4bdd-a4bd-635ba2265705, _, _)
THEN
PROC_SetCustomTradeTreasure(S_MOO_BugBearVendor_8cb1cace-ef98-4387-8b76-2e8efc5cfe89, "MOO_BugBearVendor_PrivateStash");

//Play combat AD on Bugbear turn. Prioritize if at least one player has impressed him. If not, then play AD if he was unimpressed. If neither, play fallback combat AD
//This is likely to play during the Assault, but should play at any point the player enters combat with the BugBearVendor
IF
EnteredCombat(S_MOO_BugBearVendor_8cb1cace-ef98-4387-8b76-2e8efc5cfe89, _ID)
AND
DB_Players(_Player)
AND
CombatGetGuidFor(_Player, _ID)
THEN
PROC_MOO_BugBearVendor_CombatAD(_ID);

IF
SwitchedCombat(S_MOO_BugBearVendor_8cb1cace-ef98-4387-8b76-2e8efc5cfe89, _OldID, _NewID)
AND
DB_Players(_Player)
AND
CombatGetGuidFor(_Player, _NewID)
THEN
PROC_MOO_BugBearVendor_CombatAD(_NewID);

IF
EnteredCombat(_Player, _ID)
AND
DB_Players((CHARACTER)_Player)
AND
CombatGetGuidFor(S_MOO_BugBearVendor_8cb1cace-ef98-4387-8b76-2e8efc5cfe89, _ID)
THEN
PROC_MOO_BugBearVendor_CombatAD(_ID);

IF
SwitchedCombat(_Player, _OldID, _NewID)
AND
DB_Players((CHARACTER)_Player)
AND
CombatGetGuidFor(S_MOO_BugBearVendor_8cb1cace-ef98-4387-8b76-2e8efc5cfe89, _NewID)
THEN
PROC_MOO_BugBearVendor_CombatAD(_NewID);

//Impressed
PROC
PROC_MOO_BugBearVendor_CombatAD((GUIDSTRING)_ID)
AND
NOT DB_OnlyOnce("MOO_BugBearVendor_CombatAD")
AND
DB_Players(_Player) //Checking all players in the combat group, not just the one to first enter
AND
CombatGetGuidFor(_Player, _ID)
AND
GetFaction(_Player, _PlayerFaction)
AND
GetFaction(S_MOO_BugBearVendor_8cb1cace-ef98-4387-8b76-2e8efc5cfe89, _BugBearFaction)
AND
GetRelation(_BugBearFaction, _PlayerFaction, 0)
AND
GetFlag(MOO_BugBearVendor_State_EarnedRespect_f364408c-b1a0-469b-bf89-975cb8a197c7, _Player, 1)
THEN
DB_OnlyOnce("MOO_BugBearVendor_CombatAD");
SetFlag(MOO_BugBearVendor_State_CombatADImpressed_0917908a-2cd3-45a3-9d8e-5a5c48996a33);
DB_CombatReact_AD_OnTurn(S_MOO_BugBearVendor_8cb1cace-ef98-4387-8b76-2e8efc5cfe89, MOO_BugBearVendor_AD_Combat_7a94a72e-7c30-41f2-45aa-c1f4bd7fce26, 1);

//Unimpressed
PROC
PROC_MOO_BugBearVendor_CombatAD(_ID)
AND
NOT DB_OnlyOnce("MOO_BugBearVendor_CombatAD")
AND
DB_Players(_Player)
AND
CombatGetGuidFor(_Player, _ID)
AND
GetFaction(_Player, _PlayerFaction)
AND
GetFaction(S_MOO_BugBearVendor_8cb1cace-ef98-4387-8b76-2e8efc5cfe89, _BugBearFaction)
AND
GetRelation(_BugBearFaction, _PlayerFaction, 0)
AND
GetFlag(MOO_BugBearVendor_State_TriedToPersuade_9d2a8789-01f4-481b-bb40-f721a650e31e, _Player, 1)
THEN
DB_OnlyOnce("MOO_BugBearVendor_CombatAD");
SetFlag(MOO_BugBearVendor_State_CombatADUnimpressed_49c570d7-9ab3-4e46-a0e2-5824d7b89f45);
DB_CombatReact_AD_OnTurn(S_MOO_BugBearVendor_8cb1cace-ef98-4387-8b76-2e8efc5cfe89, MOO_BugBearVendor_AD_Combat_7a94a72e-7c30-41f2-45aa-c1f4bd7fce26, 1);

//Neither
PROC
PROC_MOO_BugBearVendor_CombatAD(_ID)
AND
NOT DB_OnlyOnce("MOO_BugBearVendor_CombatAD")
AND
DB_Players(_Player)
AND
CombatGetGuidFor(_Player, _ID)
THEN
DB_OnlyOnce("MOO_BugBearVendor_CombatAD");
DB_CombatReact_AD_OnTurn(S_MOO_BugBearVendor_8cb1cace-ef98-4387-8b76-2e8efc5cfe89, MOO_BugBearVendor_AD_Combat_7a94a72e-7c30-41f2-45aa-c1f4bd7fce26, 1);

//END_REGION
//REGION Pilgrims
IF
AutomatedDialogEnded(MOO_Bazaar_AD_GuardRejectPilgrims_71257ec2-66a0-9896-9cda-eacbec35f2c8,_)
AND
NOT DB_GlobalFlag(MOO_Bazaar_Event_PilgrimsLeaveToTown_6d5f99a3-d858-4bf2-a76c-7a7d5b9560e1)
THEN
SetFlag(MOO_Bazaar_Event_PilgrimsLeaveToTown_6d5f99a3-d858-4bf2-a76c-7a7d5b9560e1);

IF
DB_GlobalFlag(MOO_Bazaar_Event_PilgrimsLeaveToTown_6d5f99a3-d858-4bf2-a76c-7a7d5b9560e1)
AND
DB_MOO_Bazaar_Pilgrims(_Char, _, _)
THEN
PROC_NotifyWhenReadyToMoveOn(_Char, "MOO_Bazaar_PlgrimLeaveMoonrise");

PROC
PROC_ReadyToMoveOn(_Char, "MOO_Bazaar_PlgrimLeaveMoonrise")
AND
DB_MOO_Bazaar_Pilgrims(_Char, _, _)
THEN
PROC_CharacterMoveTo(_Char, S_MOO_DisappearToTownPoint_fde8de9f-81a5-4b6b-bdb6-2e11a27a1e59, "Walk", "MOO_Bazaar_OutOfMoonrise");

IF
EntityEvent((CHARACTER)_Char, "MOO_Bazaar_OutOfMoonrise")
AND
DB_MOO_Bazaar_Pilgrims(_Char, _, _)
THEN
PROC_DisappearOutOfSight(_Char,"Walk",0,"MOO_Bazaar_PilgrimToTown");

IF
EntityEvent((CHARACTER)_Char, "MOO_Bazaar_PilgrimToTown")
AND
DB_MOO_Bazaar_Pilgrims(_Char, _Point, _)
THEN
ApplyStatus(_Char, "SCL_SHADOW_CURSE_UNDEAD_NEW", -1.0, 1);
PROC_AppearOutOfSightTo(_Char, _Point, _Point, "MOO_Bazaar_PilgrimArrivedInTown");

IF
EntityEvent((CHARACTER)_Char, "MOO_Bazaar_PilgrimArrivedInTown")
AND
NOT DB_GlobalFlag(MOO_Bazaar_State_PilgrimsInTown_460b9434-7f86-4c46-b797-5995479520b2)
THEN
SetFlag(MOO_Bazaar_State_PilgrimsInTown_460b9434-7f86-4c46-b797-5995479520b2);
//Send Pilgrims to colony when all the players have left Moonrise
IF
EntityEvent(_Player, "MOO_AllPlayersLeft")
AND
DB_GlobalFlag(MOO_Bazaar_State_PilgrimsToColony_a1ea9352-3f7b-4380-a580-e41c0a0ef504)
AND
NOT DB_GlobalFlag(MOO_Bazaar_State_PilgrimsInColony_3404254c-fb13-480a-a2d0-710d352ad6a5)
THEN
PROC_MOO_Bazaar_TryToSetPilgrimsToColony();

//If Pilgrims have not been sent to Colony yet when the player arrives
IF
DB_GlobalFlag(COL_KethericShowdown_State_Started_33489af3-23b7-4340-bbf6-19033c9dae03)
AND
DB_GlobalFlag(MOO_Bazaar_State_PilgrimsToColony_a1ea9352-3f7b-4380-a580-e41c0a0ef504)
AND
NOT DB_GlobalFlag(MOO_Bazaar_State_PilgrimsInColony_3404254c-fb13-480a-a2d0-710d352ad6a5)
THEN
PROC_MOO_Bazaar_TryToSetPilgrimsToColony();

PROC
PROC_MOO_Bazaar_TryToSetPilgrimsToColony()
AND
DB_MOO_Bazaar_Pilgrims(_Char, _, _Point)
AND
NOT DB_PermaDefeated(_Char)
THEN
PROC_MOO_Bazaar_UnregisterPilgrimTrigger(_Char);
SetOnStage(_Char, 1);
PROC_SetAnubisConfig(_Char, "DefaultCharacter");
PROC_RemoveDialog(_Char);
TeleportTo(_Char, _Point, "COL_PilgrimsArrived");

IF
EntityEvent((CHARACTER)_Char, "COL_PilgrimsArrived")
AND
NOT DB_GlobalFlag(MOO_Bazaar_State_PilgrimsInColony_3404254c-fb13-480a-a2d0-710d352ad6a5)
AND
DB_MOO_Bazaar_Pilgrims(_Char, _, _)
THEN
SetFlag(MOO_Bazaar_State_PilgrimsInColony_3404254c-fb13-480a-a2d0-710d352ad6a5);

PROC
PROC_MOO_Bazaar_UnregisterPilgrimTrigger((CHARACTER)_Char)
AND
DB_MOO_Bazaar_Pilgrims(_Char, _, _)
THEN
TriggerUnregisterForCharacter((TRIGGER)S_MOO_BazaarRegion_3c1b8ef9-bb21-4c3e-9e0f-200fd4658a98,_Char);

IF
DB_PermaDefeated(_Char)
AND
DB_MOO_Bazaar_Pilgrims((CHARACTER)_Char, _, _)
THEN
PROC_MOO_Bazaar_UnregisterPilgrimTrigger(_Char);
PROC_MOO_Bazaar_CheckPilgrimAvailability();

IF
LeftTrigger(_Char, S_MOO_BazaarRegion_3c1b8ef9-bb21-4c3e-9e0f-200fd4658a98)
AND
DB_MOO_Bazaar_Pilgrims(_Char, _, _)
THEN
PROC_MOO_Bazaar_CheckPilgrimAvailability();

IF
DB_InRegion(_Char, S_MOO_BazaarRegion_3c1b8ef9-bb21-4c3e-9e0f-200fd4658a98)
AND
DB_MOO_Bazaar_Pilgrims(_Char, _, _)
THEN
SetFlag(MOO_Bazaar_State_PilgrimsInBazaar_cde71533-78da-4df4-bec9-332fd5225dbd);

PROC
PROC_MOO_Bazaar_CheckPilgrimAvailability()
AND
NOT QRY_MOO_Bazaar_AnyPilgrimInBazaar()
THEN
ClearFlag(MOO_Bazaar_State_PilgrimsInBazaar_cde71533-78da-4df4-bec9-332fd5225dbd);

PROC
PROC_MOO_Bazaar_CheckPilgrimAvailability()
AND
QRY_MOO_Bazaar_AnyPilgrimInBazaar()
THEN
SetFlag(MOO_Bazaar_State_PilgrimsInBazaar_cde71533-78da-4df4-bec9-332fd5225dbd);

QRY
QRY_MOO_Bazaar_AnyPilgrimInBazaar()
AND
DB_MOO_Bazaar_Pilgrims(_Char, _, _)
AND
DB_InRegion(_Char,S_MOO_BazaarRegion_3c1b8ef9-bb21-4c3e-9e0f-200fd4658a98)
THEN
DB_NOOP(1);

//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
