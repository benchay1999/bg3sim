Version 1
SubGoalCombiner SGC_AND
INITSECTION
PROC_TriggerRegisterForPlayers(S_MOO_HiddenTreasureBox_1ec4fdb0-6913-453a-a87f-ec271ead1ea0);
KBSECTION
//REGION Debug SetHasMarker from Recon
IF
TextEvent("MOO_HiddenTreasure_HasMarker")
AND
GetHostCharacter(_Player)
THEN
ShowMapMarker(_Player, "MOO_HiddenTreasure", 1);
DB_MOO_HiddenTreasure_HasMarker(1);
//END_REGION

//REGION Prison Map Showing Hidden Treasure Booster
IF
GameBookInterfaceClosed(S_TWN_MasonsGuild_TreasureMap_cbd3d1da-eae7-4714-8fd8-3e32b53e6134, _Player)
AND
QRY_OnlyOnce("MOO_HiddenTreasure_ShowMarker")
THEN
StartVoiceBark(MOO_HiddenTreasure_VB_MapMarker_3269d5e9-6338-40b7-58d9-fe24ab4bcf73, _Player);
ShowMapMarker(_Player, "MOO_HiddenTreasure", 1);
DB_MOO_HiddenTreasure_HasMarker(1);

IF
EnteredTrigger(_Player, S_MOO_HiddenTreasureBox_1ec4fdb0-6913-453a-a87f-ec271ead1ea0)
AND
DB_Players(_Player)
AND
DB_MOO_HiddenTreasure_HasMarker(1)
AND
NOT DB_MOO_HiddenTreasure_SkillCheckMade(_Player)
THEN
PROC_TrySkillCheck(_Player, S_MOO_HiddenTreasureWall_46216bdb-1fee-491c-9405-2679f0e44099, "Perception", (DIFFICULTYCLASS)DC_Legacy_Unsaved_1_5f965da8-76c5-a1d4-32bd-0982100f8e10, "MOO_HiddenTreasure_Reveal");
DB_MOO_HiddenTreasure_SkillCheckMade(_Player);

PROC
PROC_RollResult("MOO_HiddenTreasure_Reveal", _Player, _Wall, 2)
THEN
NOT DB_MOO_HiddenTreasure_SkillCheckMade(_Player);

PROC
PROC_RollResult("MOO_HiddenTreasure_Reveal", _Player, _Wall, 1)
AND
NOT DB_MOO_HiddenTreasure_IsRevealed(1)
THEN
StartVoiceBark(MOO_HiddenTreasure_VB_TreasureDiscovered_c71fe693-cdbf-2c32-c1d9-b72e1d9c2af3, _Player);
DB_MOO_HiddenTreasure_IsRevealed(1);
SetCanInteract((ITEM)_Wall, 1);
PROC_PlayPerceptionRevealEffect(_Wall, "MOO_HiddenTreasure_Reveal");
PROC_TriggerUnregisterForPlayers(S_MOO_HiddenTreasureBox_1ec4fdb0-6913-453a-a87f-ec271ead1ea0);

IF
DestroyedBy(S_MOO_HiddenTreasureWall_46216bdb-1fee-491c-9405-2679f0e44099, _Player, _, _)
THEN
ShowMapMarker(_Player, "MOO_HiddenTreasure", 0);
PROC_MOO_HiddenTreasure_PurgeDBs();
GoalCompleted;

PROC
PROC_MOO_HiddenTreasure_PurgeDBs()
AND
DB_MOO_HiddenTreasure_SkillCheckMade(_Player)
THEN
NOT DB_MOO_HiddenTreasure_SkillCheckMade(_Player);
NOT DB_MOO_HiddenTreasure_HasMarker(1);
NOT DB_MOO_HiddenTreasure_IsRevealed(1);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
