Version 1
SubGoalCombiner SGC_AND
INITSECTION
//Character Initialization
//Alfira and kids DB

DB_SCE_TieflingFollowUp_Tieflings((CHARACTER)S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c, 0); //Alfira
DB_SCE_TieflingFollowUp_Tieflings((CHARACTER)S_DEN_BackupLeader_b304e07d-019c-4ec6-b5ea-c4ccb29ed892, 0); //Cerys
DB_SCE_TieflingFollowUp_Tieflings((CHARACTER)S_DEN_PickpocketTrader_e1fda08b-45d4-41db-84b5-20e9931ba5af, 1);//Mattis
DB_SCE_TieflingFollowUp_Tieflings((CHARACTER)S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f, 1);//Umi
DB_SCE_TieflingFollowUp_Tieflings((CHARACTER)S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387, 1);//Ide

DB_SCE_Debrief_Participant((CHARACTER)S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c, S_SCE_AlfiraPoint_c19efaf9-20c8-428a-bace-9055f00e1a35, (DIALOGRESOURCE)SCE_Alfira_9c2a1239-fba7-30c1-df67-d95c45ef3b82); //Alfira
DB_SCE_Debrief_Participant((CHARACTER)S_DEN_PickpocketTrader_e1fda08b-45d4-41db-84b5-20e9931ba5af, S_SCE_MattisPoint_f3df83ee-b69a-4d3c-b4a5-cb992c077b66, (DIALOGRESOURCE)SCE_Mattis_bfe7e6e4-a3e6-46cb-1711-15b4c3ae33e5);//Mattis
DB_SCE_Debrief_Participant((CHARACTER)S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f, S_SCE_UmePoint_df0300cd-5fd3-4ec4-a859-1717ef676599, (DIALOGRESOURCE)SCE_Umi_9e7af292-a8e0-a6e4-9993-e8ef760b7173);//Umi
DB_SCE_Debrief_Participant((CHARACTER)S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387, S_SCE_IdePoint_15b5da8b-37ae-43b9-95b7-a551969f29bb, (DIALOGRESOURCE)SCE_Ide_b1394056-b940-55dc-764c-5e359aa45085);//Ide
DB_SCE_Debrief_Participant((CHARACTER)S_DEN_BackupLeader_b304e07d-019c-4ec6-b5ea-c4ccb29ed892, S_SCE_CerysPoint_c0600837-52c0-4e65-ad01-a8af7f15e346, (DIALOGRESOURCE)SCE_Cerys_5643a446-9daa-2b29-ea65-4a58d9a77df3); //Cerys

DB_SCE_Debrief_Participant_Config((CHARACTER)S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c, "SCE_Alfira");
DB_SCE_Debrief_Participant_Config((CHARACTER)S_DEN_PickpocketTrader_e1fda08b-45d4-41db-84b5-20e9931ba5af, "SCE_Mattis2");
DB_SCE_Debrief_Participant_Config((CHARACTER)S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f, "SCE_Umi2");
DB_SCE_Debrief_Participant_Config((CHARACTER)S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387, "SCE_Ide2");
DB_SCE_Debrief_Participant_Config((CHARACTER)S_DEN_BackupLeader_b304e07d-019c-4ec6-b5ea-c4ccb29ed892, "SCE_Cerys2");

DB_SCE_TieflingFollowUp_ADSequence("SCE_AD_Mattis_01",(DIALOGRESOURCE)SCE_AD_Mattis_fc8f3756-0792-afda-290f-8f00426131c6,(CHARACTER)S_DEN_PickpocketTrader_e1fda08b-45d4-41db-84b5-20e9931ba5af);
DB_SCE_TieflingFollowUp_ADSequence("SCE_AD_Ide_01",(DIALOGRESOURCE)SCE_AD_Ide_d75c6dbd-f09a-16fa-9e56-69cb1668fbc0,(CHARACTER)S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387);
DB_SCE_TieflingFollowUp_ADSequence("SCE_AD_Umi_01",(DIALOGRESOURCE)SCE_AD_Umi_0eedb5f3-3adf-4e45-73b7-1f221684e68d,(CHARACTER)S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f);
DB_SCE_TieflingFollowUp_ADSequence("SCE_AD_Alfira",(DIALOGRESOURCE)SCE_AD_Alfira_28fd8c53-c44f-a366-1c16-9872390427dc,(CHARACTER)S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c);
DB_SCE_TieflingFollowUp_ADSequence("SCE_AD_Mattis_02",(DIALOGRESOURCE)SCE_AD_Mattis_fc8f3756-0792-afda-290f-8f00426131c6,(CHARACTER)S_DEN_PickpocketTrader_e1fda08b-45d4-41db-84b5-20e9931ba5af);
DB_SCE_TieflingFollowUp_ADSequence("SCE_AD_Umi_02",(DIALOGRESOURCE)SCE_AD_Umi_0eedb5f3-3adf-4e45-73b7-1f221684e68d,(CHARACTER)S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f);
DB_SCE_TieflingFollowUp_ADSequence("SCE_AD_Ide_02",(DIALOGRESOURCE)SCE_AD_Ide_d75c6dbd-f09a-16fa-9e56-69cb1668fbc0,(CHARACTER)S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387);
DB_SCE_TieflingFollowUp_ADSequence("SCE_AD_Cerys",(DIALOGRESOURCE)SCE_AD_Cerys_57435d04-77d5-6b2d-192d-d3f04cfea5e8,(CHARACTER)S_DEN_BackupLeader_b304e07d-019c-4ec6-b5ea-c4ccb29ed892);

DB_FlagReactionAfterDialog((FLAG)SCE_TieflingFollowUp_Event_GiveShiningDawn_74e71488-5bee-4428-a0f7-60dc8414c23b, (DIALOGRESOURCE)SCE_Alfira_9c2a1239-fba7-30c1-df67-d95c45ef3b82); //initalize reaction to flag getting set and wait till after dialogue

PROC_TriggerRegisterForParty(S_SCE_TieflingFollowUp_PartyNearby_Box_f8c547b7-64d1-4a54-bdc7-63f1abdfcb5a);
KBSECTION
//REGION Debug
//Gurantee the tiefling survive Debrief setup
IF
TextEvent("SCE_TieflingFollowUp_SetFlags")
AND
DB_Players((CHARACTER)_Player)
THEN
PROC_GlobalSetFlagAndCache((FLAG)HAV_Nightsong_State_AtInn_a54c06a7-2b9f-47fa-9821-3d346f378237);
SetFlag((FLAG)DEN_TieflingBard_HasMet_beeb3fe8-7c9f-82ea-3904-a65a43c1953b, _Player);

//Main Flow: All Tieflings
IF
TextEvent("SCE_TieflingFollowUp_AllTieflings")
THEN
PROC_GlobalClearFlagAndCache(DEN_AttackOnDen_State_RaiderVictory_abe1bce8-c234-4afe-a490-76210d98a078);

//Main Flow: No Cerys
IF
TextEvent("SCE_TieflingFollowUp_NoCerys")
THEN
Die(S_DEN_BackupLeader_b304e07d-019c-4ec6-b5ea-c4ccb29ed892);

//Main Flow: No Alfira
IF
TextEvent("SCE_TieflingFollowUp_NoAlfira")
THEN
Die(S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c);

IF
TextEvent("SCE_TieflingFollowUp_NoTeiflings")
THEN
PROC_GlobalSetFlagAndCache(DEN_AttackOnDen_State_RaiderVictory_abe1bce8-c234-4afe-a490-76210d98a078);

IF
TextEvent("SCE_TieflingFollowUp_AddEyepatch")
AND
GetHostCharacter((CHARACTER)_Player)
THEN
ToInventory((ITEM)S_COL_NecromancerLab_MolEyepatch_fa9baba1-f855-449c-8327-9728a493b193, _Player, 1, 1);

//Remove the flags set by dialogue for faster testing
IF
TextEvent("SCE_ClearTieflingFlags")
AND
DB_Players((CHARACTER)_Player)
THEN
ClearFlag((FLAG)SCE_Alfira_State_RequestedBard_94abae6c-78a4-f8be-eddb-5e45c953f264);
ClearFlag((FLAG)SCE_Alfira_Event_GaveShiningDawn_9d80e79d-62e2-7bdb-c647-3e3023c56a97);
ClearFlag((FLAG)SCE_Alfira_Event_RefusedShiningDawn_7cb23f63-0f5b-9188-6616-59d07b8e5d57, (CHARACTER)S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c, 0);
RemoveSpell(_Player, "Target_SCE_TieflingFollowup_BardicInspiration", 0);

//END_REGION

//REGION Who's Present
//Isobel leaves, no tielfings in epilogue
QRY
QRY_SCE_Debrief_BlockCharacter((CHARACTER)_Char)
AND
DB_SCE_TieflingFollowUp_Tieflings(_Char, _)
AND
DB_GlobalFlag(HAV_Siege_State_NoProtection_2da0dbf1-88ca-4cab-9c8f-ee8921822958)
THEN
DB_NOOP(1);

IF
FlagSet((FLAG)SCE_State_AtEpilogue_3541abb5-6aaf-48c7-bf30-5a13b3abc718, (CHARACTER)S_DEN_BackupLeader_b304e07d-019c-4ec6-b5ea-c4ccb29ed892, 0)
THEN
SetFlag(SCE_BackupLeader_State_AtEpilogue_2cf81795-9696-1cf8-48d2-4610a1e13a77);

IF
DB_SCE_Debrief_CharactersInEpilogue((CHARACTER)S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387)
THEN
DB_DoNotFace(S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387); //he is sitting during his behaviour animation

IF
DB_LevelLoadedOnce("INT_Main_A")
AND
DB_DoNotFace(S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387)
THEN
NOT DB_DoNotFace(S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387); //he is sitting during his behaviour animation

//END_REGION

//REGION Add the kids as Speakers
PROC
PROC_StartDialog_AddExtraSpeakers((DIALOGRESOURCE)SCE_Alfira_9c2a1239-fba7-30c1-df67-d95c45ef3b82, (INTEGER)_ID)
AND
DB_SCE_TieflingFollowUp_Tieflings(_Kid, 1)
AND
QRY_SpeakerIsAvailableAndInDialogRange(_Kid, S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c)
THEN
PROC_DialogAddSpeakingActor(_ID, (CHARACTER)_Kid);
//END_REGION

//REGION Give the bardic inspiration post dialogue

IF
FlagSet((FLAG)SCE_TieflingFollowUp_Event_GiveShiningDawn_74e71488-5bee-4428-a0f7-60dc8414c23b,_Player,_)
AND
IsTagged(_Player,(TAG)BARD_d93434bd-6b71-4789-b128-ee24156057cc,1)
THEN
AddSpell((CHARACTER)_Player, "Target_SCE_TieflingFollowup_BardicInspiration", 1, 0);

//Alfira becomes the master bard and/or stops performing
IF
StatusApplied((CHARACTER)S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c, "PERFORM_POSITIVE_THEPOWER", _, _)
AND
DB_SCE_Debrief_CharactersInEpilogue((CHARACTER)S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c)
AND
QRY_SCE_TieflingFollowUp_AlfiraRequestedBard()
THEN
DB_SCE_TieflingFollowUp_DoingPerformance((CHARACTER)S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c);

IF
DB_SCE_TieflingFollowUp_DoingPerformance((CHARACTER)_Character)
AND
_Character != S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c
AND
NOT DB_SCE_TieflingFollowUp_ParticipatedInPerformance((CHARACTER)_Character)
THEN
DB_SCE_TieflingFollowUp_ParticipatedInPerformance((CHARACTER)_Character);

QRY
QRY_SCE_TieflingFollowUp_AlfiraRequestedBard()
AND
DB_GlobalFlag((FLAG)SCE_Alfira_Event_GaveShiningDawn_9d80e79d-62e2-7bdb-c647-3e3023c56a97)
THEN
DB_NOOP(1);

QRY
QRY_SCE_TieflingFollowUp_AlfiraRequestedBard()
AND
DB_GlobalFlag((FLAG)SCE_Alfira_State_RequestedBard_94abae6c-78a4-f8be-eddb-5e45c953f264)
THEN
DB_NOOP(1);

//Check already performing players if Alfira starts playing
IF
DB_SCE_TieflingFollowUp_DoingPerformance(S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c)
AND
DB_Players(_Player)
AND
NOT DB_SCE_TieflingFollowUp_DoingPerformance((CHARACTER)_Player)
AND
HasActiveStatus(_Player,"PERFORM_POSITIVE_THEPOWER",1)
AND
GetPerformGroupJoinRadius(_Radius)
AND
QRY_IsInRange(_Player,S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c,_Radius)
THEN
DB_SCE_TieflingFollowUp_DoingPerformance((CHARACTER)_Player);

//Check joining players
IF
StatusApplied((CHARACTER)_Character, "PERFORM_POSITIVE_THEPOWER", _, _)
AND
DB_SCE_TieflingFollowUp_DoingPerformance((CHARACTER)S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c)
AND
_Character != S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c
AND
GetPerformGroupJoinRadius(_Radius)
AND
QRY_IsInRange(_Character,S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c,_Radius)
THEN
DB_SCE_TieflingFollowUp_DoingPerformance((CHARACTER)_Character);

IF
StatusRemoved((CHARACTER)S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c, "PERFORM_POSITIVE_THEPOWER", _, _)
AND
DB_SCE_TieflingFollowUp_DoingPerformance(S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c)
AND
DB_SCE_TieflingFollowUp_DoingPerformance(_Performer)
THEN
RemoveStatus(_Performer, "PERFORM_POSITIVE", NULL_00000000-0000-0000-0000-000000000000);
RemoveStatus(_Performer, "PERFORM_POSITIVE_THEPOWER", NULL_00000000-0000-0000-0000-000000000000);

//When a player joins the performance, start the timer
IF
DB_SCE_TieflingFollowUp_DoingPerformance((CHARACTER)_Character)
AND
_Character != S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c
AND
NOT DB_SCE_TieflingFollowUp_TimerActive(1)
AND
GetFlag(SCE_TieflingFollowUp_Event_GiveShiningDawn_74e71488-5bee-4428-a0f7-60dc8414c23b,_Character,0) //didn't already receive the reward
THEN
TimerLaunch("SCE_Alfira_RewardDelay",5000);

IF
StatusRemoved((CHARACTER)_Performer, "PERFORM_POSITIVE_THEPOWER", _, _)
AND
DB_SCE_TieflingFollowUp_DoingPerformance((CHARACTER)_Performer)
THEN
NOT DB_SCE_TieflingFollowUp_DoingPerformance((CHARACTER)_Performer);

//When a player stops performing (after removal from DB_SCE_TieflingFollowUp_DoingPerformance), check the timer
IF
StatusRemoved((CHARACTER)_Character, "PERFORM_POSITIVE_THEPOWER", _, _)
AND
DB_SCE_TieflingFollowUp_TimerActive(1)
AND
SysCount("DB_SCE_TieflingFollowUp_DoingPerformance",1,_Count)
AND
_Count < 2 //no players performing (only Alfira)
THEN
TimerCancel("SCE_Alfira_RewardDelay");
NOT DB_SCE_TieflingFollowUp_TimerActive(1);

//Give the player the reward when the timer finishes and they are a bard
//It is intended to just hand it to all bards instead of needing to go back and forth
//or not give them the reward. It wouldn't feel great to not get it and it would be needlessly
//obtuse for the players to need to re-do the timer and playing for each bard.
IF
TimerFinished("SCE_Alfira_RewardDelay")
THEN
PROC_SCE_TieflingFollowUp_CompletePerformance();
NOT DB_SCE_TieflingFollowUp_TimerActive(1);

//Any bard in the party will also get the reward
PROC
PROC_SCE_TieflingFollowUp_CompletePerformance()
AND
DB_Players((CHARACTER)_Player)
AND
NOT DB_SCE_TieflingFollowUp_ParticipatedInPerformance(_Player)
AND
IsTagged(_Player, (TAG)BARD_d93434bd-6b71-4789-b128-ee24156057cc, 1)
THEN
DB_SCE_TieflingFollowUp_ParticipatedInPerformance(_Player);

//The reward is given to both bards AND players who did a good performance (Performer feat allows for non-bards to do this)
PROC
PROC_SCE_TieflingFollowUp_CompletePerformance()
AND
DB_SCE_TieflingFollowUp_ParticipatedInPerformance(_Player)
THEN
SetFlag(SCE_TieflingFollowUp_Event_GiveShiningDawn_74e71488-5bee-4428-a0f7-60dc8414c23b,_Player);

PROC
PROC_SCE_TieflingFollowUp_CompletePerformance()
AND
NOT DB_GlobalFlag((FLAG)SCE_Alfira_Event_GaveShiningDawn_9d80e79d-62e2-7bdb-c647-3e3023c56a97)
THEN
SetFlag((FLAG)SCE_Alfira_Event_GaveShiningDawn_9d80e79d-62e2-7bdb-c647-3e3023c56a97);

//END_REGION

//REGION Behaviours/AD queue

//For debug purposes - so they get the right anubis configs when level traveling

PROC
PROC_SCE_SetupDebrief()
THEN
NOT DB_GLO_LevelTraveler((CHARACTER)S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c, "SCL_Main_A", "HAV_Bard");
NOT DB_GLO_LevelTraveler((CHARACTER)S_DEN_PickpocketTrader_e1fda08b-45d4-41db-84b5-20e9931ba5af,"SCL_Main_A","HAV_PickpocketTrader");
NOT DB_GLO_LevelTraveler((CHARACTER)S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f,"SCL_Main_A","HAV_Trainee_001");
NOT DB_GLO_LevelTraveler((CHARACTER)S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387,"SCL_Main_A","HAV_Trainee_002");
NOT DB_GLO_LevelTraveler((CHARACTER)S_DEN_BackupLeader_b304e07d-019c-4ec6-b5ea-c4ccb29ed892, "SCL_Main_A", "HAV_BackupLeader");
DB_GLO_LevelTraveler((CHARACTER)S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c, "SCL_Main_A", "SCE_Alfira");
DB_GLO_LevelTraveler((CHARACTER)S_DEN_PickpocketTrader_e1fda08b-45d4-41db-84b5-20e9931ba5af,"SCL_Main_A","SCE_Mattis2");
DB_GLO_LevelTraveler((CHARACTER)S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f,"SCL_Main_A","SCE_Umi2");
DB_GLO_LevelTraveler((CHARACTER)S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387,"SCL_Main_A","SCE_Ide2");
DB_GLO_LevelTraveler((CHARACTER)S_DEN_BackupLeader_b304e07d-019c-4ec6-b5ea-c4ccb29ed892, "SCL_Main_A", "SCE_Cerys2");

//This should happen after the Tieflings are added to DB_SCE_Debrief_CharactersInEpilogue
PROC
PROC_SCE_UpdateBehaviours()
AND
NOT QRY_SCE_TieflingFollowUp_AnyADInQueue()
THEN
PROC_SCE_TieflingFollowUp_DetermineADQueue();

PROC
PROC_SCE_TieflingFollowUp_DetermineADQueue()
AND
DB_SCE_TieflingFollowUp_ADQueue((STRING)_Identifier,(DIALOGRESOURCE)_AD,(CHARACTER)_Speaker)
THEN
NOT DB_SCE_TieflingFollowUp_ADQueue(_Identifier,_AD,_Speaker);

//Add ADs from sequence into queue
PROC
PROC_SCE_TieflingFollowUp_DetermineADQueue()
AND
DB_SCE_TieflingFollowUp_ADSequence((STRING)_Identifier,(DIALOGRESOURCE)_AD,(CHARACTER)_Speaker)
AND
DB_SCE_Debrief_CharactersInEpilogue((CHARACTER)_Speaker)
THEN
DB_SCE_TieflingFollowUp_ADQueue((STRING)_Identifier,(DIALOGRESOURCE)_AD,(CHARACTER)_Speaker);
//String identifier is so we can add multiple database facts with the same dialog + speaker to go in sequence
//Sequence is Mattis -> Ide -> Umi -> Alfira -> Mattis -> Umi -> Ide -> Cerys

//Launch timer
PROC
PROC_SCE_TieflingFollowUp_DetermineADQueue()
AND
QRY_SCE_TieflingFollowUp_AnyADInQueue()
THEN
TimerCancel("SCE_TieflingFollowUp_ADQueue");
TimerLaunch("SCE_TieflingFollowUp_ADQueue",8000);

//AD queue
IF
TimerFinished("SCE_TieflingFollowUp_ADQueue")
AND
QRY_AnyCharacterInTrigger(S_SCE_TieflingFollowUp_PartyNearby_Box_f8c547b7-64d1-4a54-bdc7-63f1abdfcb5a)
AND
DB_SCE_TieflingFollowUp_ADQueue((STRING)_Identifier,(DIALOGRESOURCE)_AD,(CHARACTER)_Speaker)
AND
QRY_OnlyOnce("SCE_TieflingFollowUp_SelectedAD")
AND
QRY_SCE_AvailableForBehaviorAD(_Speaker)
AND
NOT QRY_SCE_TieflingFollowUp_StartDialog(_Identifier,_AD,_Speaker)
THEN
NOT DB_SCE_TieflingFollowUp_ADQueue((STRING)_Identifier,(DIALOGRESOURCE)_AD,(CHARACTER)_Speaker);
PROC_SCE_TieflingFollowUp_AdvanceADQueue(3000);

//Could not select AD
IF
TimerFinished("SCE_TieflingFollowUp_ADQueue")
AND
NOT DB_OnlyOnce("SCE_TieflingFollowUp_SelectedAD")
THEN
PROC_SCE_TieflingFollowUp_AdvanceADQueue(3000);

//Selected AD but failed to play
IF
TimerFinished("SCE_TieflingFollowUp_ADQueue")
AND
DB_OnlyOnce("SCE_TieflingFollowUp_SelectedAD")
AND
NOT DB_SCE_TieflingFollowUp_PlayingDialog((STRING)_,(DIALOGRESOURCE)_)
AND
QRY_OnlyOnce_Reset("SCE_TieflingFollowUp_SelectedAD")
AND
DB_SCE_TieflingFollowUp_ADQueue((STRING)_Identifier,(DIALOGRESOURCE)_AD,(CHARACTER)_Speaker)
AND
QRY_OnlyOnce("SCE_TieflingFollowUp_SelectedAD")
THEN
NOT DB_SCE_TieflingFollowUp_ADQueue((STRING)_Identifier,(DIALOGRESOURCE)_AD,(CHARACTER)_Speaker); //AD failed, remove from queue
PROC_SCE_TieflingFollowUp_AdvanceADQueue(3000);

IF
TimerFinished("SCE_TieflingFollowUp_ADQueue")
AND
DB_OnlyOnce("SCE_TieflingFollowUp_SelectedAD")
THEN
NOT DB_OnlyOnce("SCE_TieflingFollowUp_SelectedAD");

QRY
QRY_SCE_TieflingFollowUp_StartDialog((STRING)_Identifier,(DIALOGRESOURCE)_AD,(CHARACTER)_Speaker)
AND
QRY_StartDialog(_AD,_Speaker)
THEN
DB_SCE_TieflingFollowUp_PlayingDialog(_Identifier,_AD);

IF
AutomatedDialogEnded(_AD,_)
AND
DB_SCE_TieflingFollowUp_PlayingDialog((STRING)_Identifier,(DIALOGRESOURCE)_AD)
AND
DB_SCE_TieflingFollowUp_ADQueue((STRING)_Identifier,(DIALOGRESOURCE)_AD,(CHARACTER)_Speaker)
THEN
NOT DB_SCE_TieflingFollowUp_ADQueue((STRING)_Identifier,(DIALOGRESOURCE)_AD,(CHARACTER)_Speaker);
NOT DB_SCE_TieflingFollowUp_PlayingDialog((STRING)_Identifier,(DIALOGRESOURCE)_AD);
PROC_SCE_TieflingFollowUp_AdvanceADQueue(9000);

//Advance AD queue
PROC
PROC_SCE_TieflingFollowUp_AdvanceADQueue((INTEGER)_Duration)
AND
QRY_SCE_TieflingFollowUp_AnyADInQueue()
THEN
TimerCancel("SCE_TieflingFollowUp_ADQueue");
TimerLaunch("SCE_TieflingFollowUp_ADQueue",_Duration);

PROC
PROC_SCE_TieflingFollowUp_AdvanceADQueue((INTEGER)_)
AND
NOT DB_SCE_TieflingFollowUp_ADQueue((STRING)_,(DIALOGRESOURCE)_,(CHARACTER)_)
THEN
PROC_SCE_TieflingFollowUp_DetermineADQueue();

QRY
QRY_SCE_TieflingFollowUp_AnyADInQueue()
AND
DB_SCE_TieflingFollowUp_ADQueue((STRING)_,(DIALOGRESOURCE)_,(CHARACTER)_)
THEN
DB_NOOP(1);

IF
LevelLoaded("INT_Main_A")
THEN
TimerCancel("SCE_TieflingFollowUp_ADQueue");

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
