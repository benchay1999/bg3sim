Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_SCL_EntryPoint((TRIGGER)S_SCL_EntryBox_Underdark_063e296f-515f-4309-b823-5b7d9d16b8f6, (DIALOGRESOURCE)SCL_EntryPoint_Underdark_14bd35b1-5ed6-02d5-072f-4e3779d9335b, "");
DB_SCL_EntryPoint((TRIGGER)S_SCL_EntryBox_Mountain_4d4ccb57-e568-4536-b71b-6993ed640265, (DIALOGRESOURCE)SCL_EntryPoint_Mountain_ef1ab63c-3954-ba76-2ae1-b68d269917fc, "SCL_EntryPoint_Mountain");

DB_SCL_EntryPoint_GoblinEntries((TRIGGER)S_SCL_EntryPointGoblin_MountainPos_37ff7f66-16eb-4031-9ecd-0d272ffe8466, (TRIGGER)S_SCL_EntryPointsGoblin_MountainArea_707859ee-f9b8-4366-bc3e-baf55bc574f9, "SCL_EntryPoint_Mountain");

DB_SCL_EntryPoint_DaisyAD_Triggers((TRIGGER)S_SCL_EntryPoints_DaisyAD_Trigger_000_32d38926-9630-4521-90d5-6784031d28c9);
DB_SCL_EntryPoint_DaisyAD_Triggers((TRIGGER)S_SCL_EntryPoints_DaisyAD_Trigger_001_e3ec7266-34ed-4d32-ba31-a8082d2c22e7);

PROC_TriggerRegisterForPlayers(S_SCL_EntryPoints_DaisyAD_Trigger_000_32d38926-9630-4521-90d5-6784031d28c9);
PROC_TriggerRegisterForPlayers(S_SCL_EntryPoints_DaisyAD_Trigger_001_e3ec7266-34ed-4d32-ba31-a8082d2c22e7);


//REGION Mountain Pass
DB_CheckPoint(	(CHARACTER)S_SCL_DriderCaravan_GoblinMelee_001_0648b227-b64b-471f-a6ac-d71af926dfb4, 
				(CHARACTER)S_SCL_DriderCaravan_HalfOrcCaster_f9522205-623f-4a82-904a-128b26182707, 
				(TRIGGER)S_SCL_CaravanCheckpoint_Warning_84aa51b8-77e0-4f73-833d-bcab1cb8eb83, 
				(TRIGGER)S_SCL_CaravanCheckpoint_SecondWarning_ced3f240-ab40-4a29-8e38-a5cc37b0b7f5, 
				//(TRIGGER)S_SCL_CaravanCheckpoint_Trespass_5a965dd3-4565-4b54-8c48-8fcb723cd487, "SCL_CaravanCheckpoint", 
				(TRIGGER)NULL_00000000-0000-0000-0000-000000000000, "SCL_CaravanCheckpoint", 
				(FLAG)SCL_EntryPoint_Event_GainedAccessToCaravanCamp_8f7b8388-5f8d-4d56-b224-84abb6311068, "Trespassing",
				(TRIGGER)S_SCL_CaravanCheckpoint_OutTrigger_8934854d-765a-44ff-958a-9ca2df52cad8);
DB_GlobalFlagReactionAfterDialog((FLAG)SCL_EntryPoint_Event_GainedAccessToCaravanCampFromDialog_5ddb61db-b950-4c2d-99eb-6e3324b81f48, (DIALOGRESOURCE)SCL_DriderCaravan_GoblinMelee_001_Checkpoint_0d725356-f264-73f4-d83d-e045da28d9fa);
DB_GlobalFlagReactionAfterDialog((FLAG)SCL_EntryPoint_Event_GainedAccessToCaravanCampFromDialog_5ddb61db-b950-4c2d-99eb-6e3324b81f48, (DIALOGRESOURCE)SCL_DriderCaravan_HalfOrcCaster_Checkpoint_079d46a4-1aa1-a86f-f4cb-26789432bbc8);
DB_GlobalFlagReactionAfterDialog((FLAG)SCL_Hyena_Event_Flees_4f789d83-e657-4dc3-ac87-11e1240c2c0b, (DIALOGRESOURCE)SCL_Hyena_b92d34b1-0d35-8e6a-1214-e9001bbae67a);
DB_Dialogs((CHARACTER)S_SCL_DriderCaravan_GoblinMelee_000_c176d94a-eaa6-4869-a344-88e44b31e17d, (DIALOGRESOURCE)SCL_Drider_Caravan_GoblinMelee_000_a0401902-f62e-dc0c-8262-e02a4ea488e5);
DB_Dialogs((CHARACTER)S_SCL_Hyena_dc46404e-23a7-4a97-820c-159e75816661, (DIALOGRESOURCE)SCL_Hyena_b92d34b1-0d35-8e6a-1214-e9001bbae67a);
DB_Inclusion_Object(S_SCL_Hyena_dc46404e-23a7-4a97-820c-159e75816661, (FLAG)SCL_Hyena_StartInclusion_8b08c5a1-c496-4b0f-b3f7-afe25c460385, (FLAG)SCL_Hyena_EndInclusion_82ed5faa-bb2a-458c-baca-30575925f8de);
DB_Inclusion_NPCDialog((DIALOGRESOURCE)SCL_DriderCaravan_GoblinMelee_001_Checkpoint_0d725356-f264-73f4-d83d-e045da28d9fa, (CHARACTER)S_SCL_Hyena_dc46404e-23a7-4a97-820c-159e75816661);
DB_GlobalFlagReactionAfterDialog((FLAG)SCL_EntryPoint_Event_GoblinDiesInShadowcurse_e5c11971-1b58-0fd9-044c-cb81fa8b2018, (DIALOGRESOURCE)SCL_DriderCaravan_GoblinMelee_001_Checkpoint_0d725356-f264-73f4-d83d-e045da28d9fa);
DB_GlobalFlagReactionAfterDialog((FLAG)SCL_EntryPoint_Event_TossHyena_8920943f-fa0b-e88d-68b2-ea4b481627bc, (DIALOGRESOURCE)SCL_DriderCaravan_GoblinMelee_001_Checkpoint_0d725356-f264-73f4-d83d-e045da28d9fa);
DB_DefeatedStateFlag(S_SCL_Hyena_dc46404e-23a7-4a97-820c-159e75816661, (FLAG)SCL_Hyena_State_Defeated_5bb95e7b-28eb-6ab0-3772-0f7f0a91c2fc);
//END_REGION

DB_ItemDialog_NarratorAD(S_SCL_EntrySign_7741a624-a01e-4de7-90d8-795cf44a65c5,SCL_EntryPoint_AD_Sign_28d8f623-7bc3-3dbe-365a-08c1eaee8b57);
DB_ItemDialog_NarratorAD((ITEM)S_SCL_Entrance_Plaque1_96a1bc10-f6ed-4c3f-9208-ce2e4732f629,(DIALOGRESOURCE)SCL_AD_Entrance_Plaque1_f4f56cb8-893a-684e-7b4b-f86b321dcc8f);
DB_ItemDialog_NarratorAD((ITEM)S_SCL_Entrance_Plaque2_7c8ea0a7-2c5a-425c-870b-ca9742b071a2,(DIALOGRESOURCE)SCL_AD_Entrance_Plaque2_a232dd43-c6d8-ec27-c263-d01e727643d7);
DB_ItemDialog_NarratorAD((ITEM)S_SCL_Entrance_Plaque3_5eece5a1-2a1b-4d1d-aabf-74f22dc9124d,(DIALOGRESOURCE)SCL_AD_Entrance_Plaque3_850787a0-9df4-06f2-a9ad-cfaaa2f385b4);

//REGION Topical Greetings
DB_TopicalGreeting((FLAG)TG_SCL_EnteredShadowLands_63623663-ebe4-4591-8f4c-9aa01c716c80);
//END_REGION Topical Greetings
KBSECTION
//REGION Through Mountain Pass
PROC
PROC_CheckPointDialogue((CHARACTER)_Player, (CHARACTER)S_SCL_DriderCaravan_GoblinMelee_001_0648b227-b64b-471f-a6ac-d71af926dfb4)
AND
QRY_StartCheckpointWarningDialog("SCL_CaravanCheckpoint", (DIALOGRESOURCE)SCL_DriderCaravan_GoblinMelee_001_Checkpoint_0d725356-f264-73f4-d83d-e045da28d9fa, (CHARACTER)S_SCL_DriderCaravan_GoblinMelee_001_0648b227-b64b-471f-a6ac-d71af926dfb4, _Player)
THEN
DB_NOOP(1);

PROC
PROC_CheckPointDialogue((CHARACTER)_Player, (CHARACTER)S_SCL_DriderCaravan_HalfOrcCaster_f9522205-623f-4a82-904a-128b26182707)
AND
QRY_StartCheckpointWarningDialog("SCL_CaravanCheckpoint", (DIALOGRESOURCE)SCL_DriderCaravan_HalfOrcCaster_Checkpoint_079d46a4-1aa1-a86f-f4cb-26789432bbc8, (CHARACTER)S_SCL_DriderCaravan_HalfOrcCaster_f9522205-623f-4a82-904a-128b26182707, _Player)
THEN
DB_NOOP(1);

PROC
PROC_StartDialog_AddExtraSpeakers((DIALOGRESOURCE)SCL_DriderCaravan_GoblinMelee_001_Checkpoint_0d725356-f264-73f4-d83d-e045da28d9fa, (INTEGER)_ID)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_SCL_Hyena_dc46404e-23a7-4a97-820c-159e75816661, S_SCL_DriderCaravan_GoblinMelee_001_0648b227-b64b-471f-a6ac-d71af926dfb4)
THEN
PROC_DialogAddSpeakingActor(_ID, S_SCL_Hyena_dc46404e-23a7-4a97-820c-159e75816661);

PROC
PROC_GlobalFlagReactionAfterDialog(SCL_EntryPoint_Event_GainedAccessToCaravanCampFromDialog_5ddb61db-b950-4c2d-99eb-6e3324b81f48)
THEN
SetFlag(SCL_EntryPoint_Event_GainedAccessToCaravanCamp_8f7b8388-5f8d-4d56-b224-84abb6311068, NULL_00000000-0000-0000-0000-000000000000);

IF
FlagSet(SCL_EntryPoint_Event_GainedAccessToCaravanCamp_8f7b8388-5f8d-4d56-b224-84abb6311068, _, _)
AND
NOT DB_State_Current(S_SCL_Drider_aa59ce01-3d2e-42e8-a539-c07e0ea292ab, "SCL_TheDrider", "EscortingCaravan")
THEN
PROC_RemoveDialog(S_SCL_DriderCaravan_GoblinMelee_001_0648b227-b64b-471f-a6ac-d71af926dfb4);
DB_Dialogs((CHARACTER)S_SCL_DriderCaravan_GoblinMelee_001_0648b227-b64b-471f-a6ac-d71af926dfb4, (DIALOGRESOURCE)SCL_Drider_Caravan_GoblinMelee_001_e9568bb0-00ba-5e19-35cd-612a2117ff8a);

PROC
PROC_GlobalFlagReactionAfterDialog(SCL_EntryPoint_Event_GoblinDiesInShadowcurse_e5c11971-1b58-0fd9-044c-cb81fa8b2018)
THEN
PROC_SCL_Drider_RemoveFromCaravanGroup((CHARACTER)S_SCL_DriderCaravan_GoblinMelee_001_0648b227-b64b-471f-a6ac-d71af926dfb4);
TeleportTo(S_SCL_DriderCaravan_GoblinMelee_001_0648b227-b64b-471f-a6ac-d71af926dfb4, S_SCL_EntryThrowPos_524ef681-bccc-4ddf-8b7c-46f8b0628d5e, "SCL_EntryPoint_DieInShadowcurse");

PROC
PROC_GlobalFlagReactionAfterDialog(SCL_EntryPoint_Event_TossHyena_8920943f-fa0b-e88d-68b2-ea4b481627bc)
THEN
TeleportTo(S_SCL_Hyena_dc46404e-23a7-4a97-820c-159e75816661, S_SCL_EntryThrowPos_524ef681-bccc-4ddf-8b7c-46f8b0628d5e, "SCL_EntryPoint_DieInShadowcurse");

IF
EntityEvent(_Char, "SCL_EntryPoint_DieInShadowcurse")
THEN
Die(_Char, DEATHTYPE.DoT, 1);
ApplyStatus(_Char, "SCL_SHADOW_CURSE_RISING", 6.0, 1);

IF
FlagSet(SCL_Drider_Event_StartCaravanEscort_26062990-9018-4845-a520-d737837fa207,_,_)
THEN
DB_OnlyOnce("SCL_EntryPoint_VB");

PROC
PROC_SCL_Drider_StartsMovingTowardsTower()
AND
NOT DB_Defeated(S_SCL_Hyena_dc46404e-23a7-4a97-820c-159e75816661)
AND
NOT DB_GlobalFlag(SCL_EntryPoint_State_HyenaStaysAtCheckpoint_12abbcb8-a894-4b39-8e4b-4763f8ad0363)
THEN
PROC_SCL_EntryPoint_HyenaFlees();

PROC
PROC_GlobalFlagReactionAfterDialog(SCL_Hyena_Event_Flees_4f789d83-e657-4dc3-ac87-11e1240c2c0b)
THEN
PROC_SCL_EntryPoint_HyenaFlees();

PROC
PROC_SCL_EntryPoint_HyenaFlees()
AND
NOT DB_OnlyOnce("SCL_EntryPoint_HyenaFlees")
AND
NOT DB_Defeated(S_SCL_DriderCaravan_GoblinMelee_001_0648b227-b64b-471f-a6ac-d71af926dfb4)
THEN
PROC_TryStartAD(SCL_Hyena_AD_CaravanLeaves_ea05382b-a66c-50f6-16c6-62f868e58140, S_SCL_Hyena_dc46404e-23a7-4a97-820c-159e75816661, S_SCL_DriderCaravan_GoblinMelee_001_0648b227-b64b-471f-a6ac-d71af926dfb4);

PROC
PROC_SCL_EntryPoint_HyenaFlees()
AND
QRY_OnlyOnce("SCL_EntryPoint_HyenaFlees")
THEN
PROC_RemoveDialog(S_SCL_Hyena_dc46404e-23a7-4a97-820c-159e75816661);
PROC_CharacterMoveTo(S_SCL_Hyena_dc46404e-23a7-4a97-820c-159e75816661, S_SCL_Hyena_DisappearTo_14efd3ac-b430-4009-bd81-f3cb6e83419c, "Sprint", "SCL_EntryPoint_HyenaDisappears");


// Hyena disappears leaving the map at one of the entrance points
IF
EntityEvent(_Hyena, "SCL_EntryPoint_HyenaDisappears")
THEN
SetOnStage(_Hyena, 0);
//END_REGION

//REGION Daisy ADs
IF
EnteredTrigger(_Player, _Trigger)
AND
DB_Players(_Player)
AND
DB_SCL_EntryPoint_DaisyAD_Triggers(_Trigger)
AND
QRY_OnlyOnce("SCL_EntryPoint_DaisyAD_Played")
THEN
PROC_DaisyAD(_Player, SCL_EntryPoint_DaisyAD_ReachTowers_2b38847b-049c-7b22-6ad6-2851a66cf58d);
//END_REGION

//REGION Start Dialogues
IF
DB_SCL_EntryPoint(_Trigger, _Dialog, _)
THEN
PROC_TriggerRegisterForPlayers(_Trigger);

IF
DB_InRegion(_Char,_Trigger)
AND
DB_SCL_EntryPoint(_Trigger,_Dialog, _ID)
AND
NOT DB_SCL_EntryPoint_GoblinEntries(_, _, _ID)
AND
QRY_OnlyOnce("SCL_EntryPoint")
AND
NOT QRY_StartDialog(_Dialog,_Char)
THEN
NOT DB_OnlyOnce("SCL_EntryPoint");

IF
DB_InRegion(_Char,_Trigger)
AND
DB_SCL_EntryPoint(_Trigger, _Dialog, _ID)
AND
DB_SCL_EntryPoint_GoblinEntries(_Pos, _Area, _ID)
AND
QRY_OnlyOnce("SCL_EntryPoint")
THEN
PROC_TriggerRegisterForParty(_Area);
DB_SpotPlayers(S_SCL_EntryPointGoblin_0e978645-03c3-4fa7-aea4-3b3b04676bfb, _ID, NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);
TeleportTo(S_SCL_EntryPointGoblin_0e978645-03c3-4fa7-aea4-3b3b04676bfb, _Pos);

IF
DialogEnded(_Dialog, _)
AND
DB_SCL_EntryPoint(_Trigger, _Dialog, _)
THEN
PROC_SCL_EntryPoints_UnregisterEntryTriggers();
TimerLaunch("SCL_EntryPoint_AfterCutscene_BanterTimer", 2000);

IF
TimerFinished("SCL_EntryPoint_AfterCutscene_BanterTimer")
AND
DB_SCL_EntryPoint(_Trigger, _Dialog, _ID)
AND
GetClosestAlivePlayer(_Trigger, _Player, _Dist)
AND
_Dist < 30.0
AND
QRY_OnlyOnce("SCL_EntryPoint_AfterCutscene_Banter")
THEN
StartVoiceBark(SCL_EntryPoint_VB_AfterCutScene_05f005bb-0f6a-4bb9-42da-a2f7ba244eec, _Player);


IF
DB_InRegion(_Char, _Trigger)
AND
DB_SCL_EntryPoint(_Trigger, _Dialog, _)
AND
DB_DialogName(_Dialog, _ID)
AND
QRY_SpeakerIsAvailable(_Char)
THEN
PROC_DialogAddSpeakingActor(_ID, _Char);


PROC
PROC_SCL_EntryPoints_UnregisterEntryTriggers()
AND
DB_SCL_EntryPoint(_Trigger, _Dialog, _)
THEN
PROC_TriggerUnregisterForPlayers(_Trigger);

IF
ObscuredStateChanged(_Player, "Clear")
AND
DB_SCL_ShadowCurse_InCursedArea(_Player)
AND
NOT DB_SCL_ShadowCurse_InBlightedArea(_Player)
AND
DB_Players((CHARACTER)_Player)
AND
HasActiveStatus(_Player, "SCL_AREA", 1)
AND
NOT DB_CantTalk_IgnoreCombat(_Player)
AND
QRY_OncePerUserAndNearbyPlayers_IsSet(_Player, "SCL_EntryPoint_VB")
AND
QRY_OncePerUserAndNearbyPlayers(_Player, "SCL_EntryPoint_RemovedCurseByLight")
THEN
PROC_TryStartAD((DIALOGRESOURCE)SCL_EntryPoint_PAD_CurseRemovedByLight_ed459198-8795-a0c7-5eb9-f35b48ad37cd, _Player);
//END_REGION

//REGION Goblin encounter
IF
EnteredTrigger(_, S_SCL_EntryBox_Underdark_063e296f-515f-4309-b823-5b7d9d16b8f6)
THEN
SetOnStage(S_SCL_EntryPointGoblin_0e978645-03c3-4fa7-aea4-3b3b04676bfb, 0);

PROC
PROC_SpotPlayers_Spotted(S_SCL_EntryPointGoblin_0e978645-03c3-4fa7-aea4-3b3b04676bfb, _SpotID, _Player)
AND
DB_SCL_EntryPoint(_, _Dialog, _SpotID)
THEN
DB_Dialogs(S_SCL_EntryPointGoblin_0e978645-03c3-4fa7-aea4-3b3b04676bfb, _Dialog);

//Set goblin dialog and start it when spotted
PROC
PROC_SpotPlayers_Spotted(S_SCL_EntryPointGoblin_0e978645-03c3-4fa7-aea4-3b3b04676bfb, _SpotID, _Player)
AND
DB_SCL_EntryPoint(_, _Dialog, _SpotID)
AND
QRY_StartDialogWithAvailableSpeakerInRange(_Dialog, S_SCL_EntryPointGoblin_0e978645-03c3-4fa7-aea4-3b3b04676bfb, _Player, 25.0)
THEN
DB_NOOP(1);

IF
DialogEnded((DIALOGRESOURCE)SCL_EntryPoint_Mountain_ef1ab63c-3954-ba76-2ae1-b68d269917fc,_)
AND
QRY_OnlyOnce("SLC_EntryGoblin_UseTorch")
THEN
SetWeaponUnsheathed((CHARACTER)S_SCL_EntryPointGoblin_0e978645-03c3-4fa7-aea4-3b3b04676bfb,1,1);

IF
LeftTrigger(_, _Trigger)
AND
DB_SCL_EntryPoint_GoblinEntries(_, _Trigger, _)
AND
NOT QRY_TriggerEvents_AnyPartyMemberInTrigger(_Trigger)
THEN
PROC_TriggerUnregisterForParty(_Trigger);
SetTag(S_SCL_EntryPointGoblin_0e978645-03c3-4fa7-aea4-3b3b04676bfb, (TAG)ACT2_SHADOW_CURSE_IMMUNE_b47643e0-583c-4808-b108-f6d3b605b0a9); // Avoid her from resurrecting just in case.
TeleportTo(S_SCL_EntryPointGoblin_0e978645-03c3-4fa7-aea4-3b3b04676bfb, S_SCL_EntryPointGoblin_MountainDeathPos_cabf1c8a-8306-4a3d-b8d0-d6555ddf38f8);
LookFromTrigger(S_SCL_EntryPointGoblin_0e978645-03c3-4fa7-aea4-3b3b04676bfb, S_SCL_EntryPointGoblin_MountainDeathPos_cabf1c8a-8306-4a3d-b8d0-d6555ddf38f8, 1);
Die(S_SCL_EntryPointGoblin_0e978645-03c3-4fa7-aea4-3b3b04676bfb, DEATHTYPE.DoT, NULL_00000000-0000-0000-0000-000000000000, 1, 1, 0.0);
DB_GLO_CharacterCorpseDialog((CHARACTER)S_SCL_EntryPointGoblin_0e978645-03c3-4fa7-aea4-3b3b04676bfb, (DIALOGRESOURCE)SCL_EntryPointGoblin_Dead_eafb8333-c393-c561-2031-058a65bae664);

//END_REGION

//REGION Debug
IF
TextEvent("undep")
THEN
PROC_SCL_EntryPoint_DEBUG_Reset(S_SCL_EntryBox_Underdark_063e296f-515f-4309-b823-5b7d9d16b8f6);

IF
TextEvent("mountep")
THEN
PROC_SCL_EntryPoint_DEBUG_Reset(S_Debug_SCL_Mountain_c0dbeb78-3b91-4200-ab84-4b6c87d6bad8);

IF
TextEvent("driderep")
THEN
SetFlag(CAMP_GoblinHuntCelebration_Knows_CreaturePath_758098e2-ab9c-e667-9210-d3765c021541,NULL_00000000-0000-0000-0000-000000000000);
PROC_SCL_EntryPoint_DEBUG_Reset(S_SCL_EntryBox_Mountain_4d4ccb57-e568-4536-b71b-6993ed640265);

PROC
PROC_SCL_EntryPoint_DEBUG_Reset(S_Debug_SCL_Mountain_c0dbeb78-3b91-4200-ab84-4b6c87d6bad8)
AND
DB_Dead(S_SCL_Hyena_dc46404e-23a7-4a97-820c-159e75816661)
THEN
TeleportTo(S_SCL_Hyena_dc46404e-23a7-4a97-820c-159e75816661,S_SCL_DriderCaravan_GoblinMelee_001_StandTrigger_ef24ce86-83a2-4645-9f23-89cd1cfc9e9a);
Resurrect(S_SCL_Hyena_dc46404e-23a7-4a97-820c-159e75816661);
ClearFlag(SCL_EntryPoint_Event_TossHyena_8920943f-fa0b-e88d-68b2-ea4b481627bc,NULL_00000000-0000-0000-0000-000000000000);
ClearFlag(SCL_Hyena_State_Defeated_5bb95e7b-28eb-6ab0-3772-0f7f0a91c2fc,NULL_00000000-0000-0000-0000-000000000000);

PROC
PROC_SCL_EntryPoint_DEBUG_Reset((TRIGGER)_Entry)
THEN
NOT DB_OnlyOnce("SCL_EntryPoint");
NOT DB_OnlyOnce("SCL_EntryPoint_VB");
PROC_TeleportPartiesTo(_Entry,"");
//END_REGION

//REGION Topical Greetings
IF
EnteredTrigger(_, _Trigger)
AND
DB_SCL_EntryPoint(_Trigger, _Dialog, _ID)
AND
QRY_OnlyOnce("SCL_EntryPointsTG")
THEN
PROC_TopicalGreeting_UnlockTopic((FLAG)TG_SCL_EnteredShadowLands_63623663-ebe4-4591-8f4c-9aa01c716c80);
//END_REGION Topical Greetings
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
