Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_AmbushTrigger((TRIGGER)S_SCL_FishermansHut_AmbushTrigger_debbd443-f3d9-4e76-a6be-db59b466f8e7,(TRIGGER)S_SCL_FishermansHut_AmbushDetectTrigger_f40a6f98-230d-48c8-aa42-e0f8d82505ab,(DIFFICULTYCLASS)Act2_NearlyImpossible_52918812-bc1c-43b5-881a-58443902f5fa);
DB_AmbushTrigger_Ambusher(S_SCL_FishermansHut_AmbushTrigger_debbd443-f3d9-4e76-a6be-db59b466f8e7,S_SCL_FishermansHut_CursedKuoToa_01_506a0ac8-f9f5-4679-a94d-7d1ad6abd173,1);
DB_AmbushTrigger_Ambusher(S_SCL_FishermansHut_AmbushTrigger_debbd443-f3d9-4e76-a6be-db59b466f8e7,S_SCL_FishermansHut_CursedKuoToa_02_81cead3c-d55e-4a1c-bfdd-7758a532c9fd,1);
DB_AmbushTrigger_Ambusher(S_SCL_FishermansHut_AmbushTrigger_debbd443-f3d9-4e76-a6be-db59b466f8e7,S_SCL_FishermansHut_CursedKuoToa_03_61122493-13f4-49f8-8441-76260c8d57fa,1);
DB_AmbushTrigger_Ambusher(S_SCL_FishermansHut_AmbushTrigger_debbd443-f3d9-4e76-a6be-db59b466f8e7,S_SCL_FishermansHut_CursedKuoToa_04_ff24c261-fe97-4451-a79a-04cdf16bc016,1);
DB_AmbushTrigger_Ambusher(S_SCL_FishermansHut_AmbushTrigger_debbd443-f3d9-4e76-a6be-db59b466f8e7,S_SCL_FishermansHut_CursedKuoToa_05_767879dc-4e7a-4d1a-98f3-029ce28752e5,1);
DB_AmbushTrigger_Ambusher(S_SCL_FishermansHut_AmbushTrigger_debbd443-f3d9-4e76-a6be-db59b466f8e7,S_SCL_FishermansHut_CursedKuoToa_06_4a99ee81-35bd-4a20-bcd7-36dfb2bbbf80,1);
DB_AmbushTrigger_Ambusher(S_SCL_FishermansHut_AmbushTrigger_debbd443-f3d9-4e76-a6be-db59b466f8e7,S_SCL_FishermansHut_CursedKuoToa_07_80d5c307-054d-4240-8e08-f52ef24a799b,1);
DB_AmbushTrigger_Ambusher(S_SCL_FishermansHut_AmbushTrigger_debbd443-f3d9-4e76-a6be-db59b466f8e7,S_SCL_FishermansHut_CursedKuoToa_08_1f8eb891-48eb-4804-8ae2-e495c19ed8f8,1);
DB_AmbushTrigger_Ambusher(S_SCL_FishermansHut_AmbushTrigger_debbd443-f3d9-4e76-a6be-db59b466f8e7,S_SCL_FishermansHut_CursedKuoToa_09_83a1863c-2188-4c98-93be-c6437a532332,1);
DB_AmbushTrigger_Ambusher(S_SCL_FishermansHut_AmbushTrigger_debbd443-f3d9-4e76-a6be-db59b466f8e7,S_SCL_FishermansHut_CursedKuoToa_10_78978ec1-6e8b-436a-9df6-81e4fdf53f5c,1);
DB_AmbushTrigger_Ambusher(S_SCL_FishermansHut_AmbushTrigger_debbd443-f3d9-4e76-a6be-db59b466f8e7,S_SCL_FishermansHut_CursedKuoToa_11_1544060f-0538-4e22-8dfc-18147a067eae,1);
DB_AmbushTrigger_Ambusher(S_SCL_FishermansHut_AmbushTrigger_debbd443-f3d9-4e76-a6be-db59b466f8e7,S_SCL_FishermansHut_CursedKuoToa_12_cceac3bb-08ba-4226-ad6b-6bff72c96b36,1);
DB_AmbushTrigger_Ambusher(S_SCL_FishermansHut_AmbushTrigger_debbd443-f3d9-4e76-a6be-db59b466f8e7,S_SCL_FishermansHut_CursedKuoToa_Champion_03524330-c6ca-4078-8fbf-12ec4ffa389a,1);

PROC_TriggerRegisterForParty((TRIGGER)S_SCL_FishermansHut_HutArea_d4f07bca-5e25-4cc6-bd56-1f0e82d3a3b6);

DB_SCL_FishermansHut_Barricades((ITEM)S_SCL_FishermansHut_Barricade01_b4e61fb6-b614-4408-a9de-d61c8ac871a6);
DB_SCL_FishermansHut_Barricades((ITEM)S_SCL_FishermansHut_Barricade02_884a96f3-de3c-49fe-8a69-0f64583a38f0);

DB_TriggerEvents_AllPartyMembersLeft((TRIGGER)S_SCL_FishermansHut_HutArea_d4f07bca-5e25-4cc6-bd56-1f0e82d3a3b6, "SCL_FishermansHut_LeaveHutArea"); 
KBSECTION
IF
DB_AmbushTrigger_Ambusher(S_SCL_FishermansHut_AmbushTrigger_debbd443-f3d9-4e76-a6be-db59b466f8e7, _KuoToa, _)
THEN
DB_SCL_FishermansHut_KuoToa((CHARACTER)_KuoToa);

//KuoToas always prioritize targets with this tag. Script controls when the tag is set and cleared
IF
DB_SCL_FishermansHut_KuoToa((CHARACTER)_KuoToa)
THEN
AddPreferredAiTargetTag((CHARACTER)_KuoToa, (TAG)ACT2_SCL_FISHERMANSHUT_PRIORITYTARGET_8112eae1-d98d-4d47-a6b6-88589aefb455);

//If any party member enters the hut, set a DB to track this state
IF
DB_InRegion(_PartyMember, S_SCL_FishermansHut_HutArea_d4f07bca-5e25-4cc6-bd56-1f0e82d3a3b6)
AND
NOT DB_SCL_FishermansHut_PartyMemberInHut(1)
THEN
DB_SCL_FishermansHut_PartyMemberInHut(1);

//If there are no party members left in the hut, clear the DB state
IF
EntityEvent(_, "SCL_FishermansHut_LeaveHutArea")
THEN
NOT DB_SCL_FishermansHut_PartyMemberInHut(1);

//If there is at least one party member in the hut, make the Kuo Toa unable to cast nets on All party members until the barricades are broken down
//This is added in order for the Kuo Toa's primary swarm spell (Net) to not have an available target, so it will fail and move on to the secondary spell, which would be an attack that can target items and break barricades
IF
DB_PartyMembers(_PartyMember)
AND
DB_SCL_FishermansHut_PartyMemberInHut(1)
THEN
SetTag(_PartyMember, (TAG)ACT2_SCL_FISHERMANSHUT_NETIGNORETARGET_de82eeb4-9595-4ed3-9f1d-9d08b8d17930);

//Make barricades interesting and priority targets while there's someone in the hut
IF
DB_SCL_FishermansHut_Barricades((ITEM)_Barricade)
AND
DB_SCL_FishermansHut_KuoToa((CHARACTER)_KuoToa)
AND
DB_SCL_FishermansHut_PartyMemberInHut(1)
AND
NOT DB_SCL_FishermansHut_BarricadesBroken(1)
THEN
AiAddInterestingItem((CHARACTER)_KuoToa, (ITEM)_Barricade);

//If there are no party members left in the hut, enable casting nets on them
IF
DB_PartyMembers(_PartyMember)
AND
NOT DB_SCL_FishermansHut_PartyMemberInHut(1)
THEN
ClearTag(_PartyMember, (TAG)ACT2_SCL_FISHERMANSHUT_NETIGNORETARGET_de82eeb4-9595-4ed3-9f1d-9d08b8d17930);

//If there are no party members left in the hut, clear interesting item status from barricades
IF
DB_SCL_FishermansHut_Barricades((ITEM)_Barricade)
AND
DB_SCL_FishermansHut_KuoToa((CHARACTER)_KuoToa)
AND
NOT DB_SCL_FishermansHut_PartyMemberInHut(1)
AND
NOT DB_SCL_FishermansHut_BarricadesBroken(1)
THEN
AiRemoveInterestingItem((CHARACTER)_KuoToa, (ITEM)_Barricade);

//The barricades start both damaged at 16 HP
IF
DB_SCL_FishermansHut_Barricades(_Barricade)
THEN
SetHitpoints(_Barricade, 16);

//Once both barricades are destroyed, we track this state in a DB
IF
DB_Destroyed(S_SCL_FishermansHut_Barricade01_b4e61fb6-b614-4408-a9de-d61c8ac871a6)
AND
DB_Destroyed(S_SCL_FishermansHut_Barricade02_884a96f3-de3c-49fe-8a69-0f64583a38f0)
THEN
DB_SCL_FishermansHut_BarricadesBroken(1);

//Once both barricades are broken, enable the net spells being cast on players
IF
DB_SCL_FishermansHut_BarricadesBroken(1)
AND
DB_PartyMembers((CHARACTER)_PartyMember)
THEN
ClearTag(_PartyMember, (TAG)ACT2_SCL_FISHERMANSHUT_NETIGNORETARGET_de82eeb4-9595-4ed3-9f1d-9d08b8d17930);

//Once barricades are broken, make players inside the hut be priority targets
//The ones inside might have stolen their loot, so the Kuo Toa want to get it back.
IF
DB_SCL_FishermansHut_BarricadesBroken(1)
AND
DB_InRegion(_PartyMember, (TRIGGER)S_SCL_FishermansHut_HutArea_d4f07bca-5e25-4cc6-bd56-1f0e82d3a3b6)
THEN
SetTag(_PartyMember, (TAG)ACT2_SCL_FISHERMANSHUT_PRIORITYTARGET_8112eae1-d98d-4d47-a6b6-88589aefb455);

//Once barricades are broken, make players who leave the hut no longer priority targets
IF
DB_SCL_FishermansHut_BarricadesBroken(1)
AND
DB_PartyMembers(_PartyMember)
AND
NOT DB_InRegion(_PartyMember, S_SCL_FishermansHut_HutArea_d4f07bca-5e25-4cc6-bd56-1f0e82d3a3b6)
THEN
ClearTag(_PartyMember, (TAG)ACT2_SCL_FISHERMANSHUT_PRIORITYTARGET_8112eae1-d98d-4d47-a6b6-88589aefb455);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
