Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_SHA_TadpoledSkeletons_Scouts((CHARACTER)S_SHA_TadpoledSkeletons_Skelly_000_bbbb5ea6-d637-466b-8191-bbf461a254e2);
DB_SHA_TadpoledSkeletons_Scouts((CHARACTER)S_SHA_TadpoledSkeletons_Skelly_001_fe0609ee-27cd-46c5-a1c7-3fd2465948b5);
DB_SHA_TadpoledSkeletons_Scouts((CHARACTER)S_SHA_TadpoledSkeletons_Skelly_002_7ae4bbc1-64de-4a8e-94aa-e2b4d1603685);

// Don't do fallback interrupt handling the scene is interrupted by spotting the player or starting a dialog
DB_SHA_TadpoledSkeletons_SceneInterruptWhitelist("Spotted");
DB_SHA_TadpoledSkeletons_SceneInterruptWhitelist("StartedDialog");

DB_SHA_TadpoledSkeletons_Justiciars(S_SHA_TadpoledSkeletons_ShadowQuake_0_68513e92-7d3b-44e9-8454-f088c4f47679);
DB_SHA_TadpoledSkeletons_Justiciars(S_SHA_TadpoledSkeletons_ShadowQuake_1_185be763-f4f1-474c-b810-5efecaff3485);
DB_SHA_TadpoledSkeletons_Justiciars(S_SHA_TadpoledSkeletons_ShadowQuake_2_55844999-b524-42c4-a6ad-b91b9e958085);
DB_SHA_TadpoledSkeletons_Justiciars(S_SHA_TadpoledSkeletons_Justiciar_Melee_01_77e2db6d-cdf7-4490-b444-37d49d3fb6e0);
DB_SHA_TadpoledSkeletons_Justiciars(S_SHA_TadpoledSkeletons_Justiciar_Caster_01_027755e5-86ff-4d55-8704-6ae3b46cb974);
DB_SHA_TadpoledSkeletons_Justiciars(S_SHA_TadpoledSkeletons_Justiciar_Ranged_01_997535c6-4c93-45cb-89d1-83d2f5bb78d1);
DB_SHA_TadpoledSkeletons_Justiciars(S_SHA_TadpoledSkeletons_Justiciar_Melee_02_1987ecb9-9565-4fab-8b1f-0390ca250a01);
DB_SHA_TadpoledSkeletons_Justiciars(S_SHA_TadpoledSkeletons_Justiciar_Melee_03_b7660aea-e4c8-4140-8198-3f9d516d4518);

DB_IgnoreMutingStatussesDialog((DIALOGRESOURCE)SHA_TadpoledSkeletons_Scouts_8c88348b-6302-76dc-54ec-fd3eb430f27b);
KBSECTION
//REGION Introduction
IF
DB_SHA_TadpoledSkeletons_Scouts(_Skelly)
THEN
DB_SHA_Necromancer_Undead(_Skelly);
PROC_SceneDialogOnly("SHA_TadpoledSkeletons_Scouting", _Skelly, (TRIGGER)S_SHA_TadpoledSkeletons_SceneSpottingTrigger_2f269fb5-3870-4e8a-ad16-7c2eb06faf9d);

IF
DB_SceneManager(_Skelly, "SHA_TadpoledSkeletons_Scouting")
THEN
DB_Dialogs(_Skelly, (DIALOGRESOURCE)SHA_TadpoledSkeletons_Scouts_8c88348b-6302-76dc-54ec-fd3eb430f27b);
DB_SpotPlayers(_Skelly, "SHA_TadpoledSkeletons_Scouting", NULL_00000000-0000-0000-0000-000000000000, (FLAG)SHA_TadpoledSkeletons_Event_StopSpotting_16ac7e00-43ec-47c0-8f72-872e08f90a54);
DB_SpotPlayers_TargetIgnoreCantTalk(_Skelly, "SHA_TadpoledSkeletons_Scouting");
DB_SpotPlayers_SpotterIgnoreCantTalk(_Skelly, "SHA_TadpoledSkeletons_Scouting");
DB_SpotPlayers_IncludeWildshapedPlayers(_Skelly, "SHA_TadpoledSkeletons_Scouting");

IF
DB_SHA_TadpoledSkeletons_Justiciars(_Shadow)
THEN
SetCanJoinCombat(_Shadow, 0);
SetCanFight(_Shadow, 0);
SetOnStage(_Shadow, 0);

IF
DB_GlobalFlag((FLAG)SHA_TadpoledSkeletons_Event_ShadowsAttack_c9d43981-22ca-2884-9204-6a6049243ae6)
AND
DB_SHA_TadpoledSkeletons_Justiciars(_Shadow)
AND
IsCharacter(_Shadow,_IsChar)
THEN
PROC_SHA_TadpoledSkeletons_JusticiarAppears(_Shadow,_IsChar);

PROC
PROC_SHA_TadpoledSkeletons_JusticiarAppears((GUIDSTRING)_Obj, 1)
THEN
AppearAt((CHARACTER)_Obj, _Obj, 1, NULL_00000000-0000-0000-0000-000000000000, "SHA_TadpoledSkeletons_ShadowSpawned");

PROC
PROC_SHA_TadpoledSkeletons_JusticiarAppears((GUIDSTRING)_Obj, 0)
THEN
SetOnStage(_Obj,1);

IF
DialogEnded(SHA_TadpoledSkeletons_Scouts_8c88348b-6302-76dc-54ec-fd3eb430f27b, _)
AND
QRY_OnlyOnce("SHA_TadpoledSkeletons_Attacked")
AND
DB_SHA_TadpoledSkeletons_Justiciars(_Shadow)
THEN
SetCanJoinCombat(_Shadow, 1);
SetCanFight(_Shadow, 1);

IF
DialogRequestFailed(SHA_TadpoledSkeletons_Scouts_8c88348b-6302-76dc-54ec-fd3eb430f27b, _)
AND
QRY_OnlyOnce("SHA_TadpoledSkeletons_Attacked")
AND
DB_SHA_TadpoledSkeletons_Justiciars(_Shadow)
THEN
SetCanJoinCombat(_Shadow, 1);
SetCanFight(_Shadow, 1);

IF
AttackedBy(_Shadow, _, _, _, _, _, _)
AND
DB_SHA_TadpoledSkeletons_Justiciars(_Shadow)
AND
QRY_OnlyOnce("SHA_TadpoledSkeletons_Attacked")
AND
DB_SHA_TadpoledSkeletons_Justiciars(_Justiciar)
AND
CanFight(_Justiciar, 0)
AND
CanJoinCombat(_Justiciar, 0)
THEN
SetCanJoinCombat(_Justiciar, 1);
SetCanFight(_Justiciar, 1);

IF
DialogEnded(SHA_TadpoledSkeletons_Scouts_8c88348b-6302-76dc-54ec-fd3eb430f27b, _)
AND
NOT DB_DialogRequestFailed(SHA_TadpoledSkeletons_Scouts_8c88348b-6302-76dc-54ec-fd3eb430f27b, _)
THEN
PROC_SceneOver("SHA_TadpoledSkeletons_Scouting");
PROC_SHA_StartShadowquakeShakes();

IF
AutomatedDialogEnded((DIALOGRESOURCE)SHA_TadpoledSkeletons_AD_ScoutsRetreat_6d1e6e31-dd1a-7333-58af-278aa15ad39e, _)
THEN
PROC_SceneOver("SHA_TadpoledSkeletons_Scouting");
PROC_SHA_StartShadowquakeShakes();

IF
AutomatedDialogRequestFailed((DIALOGRESOURCE)SHA_TadpoledSkeletons_AD_ScoutsRetreat_6d1e6e31-dd1a-7333-58af-278aa15ad39e, _)
THEN
PROC_SceneOver("SHA_TadpoledSkeletons_Scouting");
PROC_SHA_StartShadowquakeShakes();

// Add available speakers to the dialog - dialog has fallbacks if any of the speakers are missing
QRY
QRY_SelectCustomDialog(_Skelly, _Player)
AND
DB_SceneManager((CHARACTER)_Skelly, "SHA_TadpoledSkeletons_Scouting")
AND
DB_SHA_TadpoledSkeletons_Scouts(_AnySkelly)
AND
QRY_OnlyOnce_Reset("SHA_TadpoledSkeletons_AddSkeletonToDialog")
AND
QRY_SpeakerIsAvailable(_AnySkelly)
AND
QRY_OnlyOnce("SHA_TadpoledSkeletons_AddSkeletonToDialog")
THEN
DB_SelectedDialog((DIALOGRESOURCE)SHA_TadpoledSkeletons_Scouts_8c88348b-6302-76dc-54ec-fd3eb430f27b, 
	NULL_00000000-0000-0000-0000-000000000000,
	NULL_00000000-0000-0000-0000-000000000000,
	NULL_00000000-0000-0000-0000-000000000000,
	_Player);

// Ignore muting statuses as long as the player isn't polymorphed
QRY
QRY_DialogShouldIgnoreMutingStatusses((DIALOGRESOURCE)SHA_TadpoledSkeletons_Scouts_8c88348b-6302-76dc-54ec-fd3eb430f27b, _, _Player)
AND
HasActiveStatusWithGroup(_Player, "SG_Polymorph", 0)
THEN
DB_NOOP(1);

PROC
PROC_StartDialog_AddExtraSpeakers((DIALOGRESOURCE)SHA_TadpoledSkeletons_Scouts_8c88348b-6302-76dc-54ec-fd3eb430f27b, _Inst)
AND
DB_SceneManager(_Skelly, "SHA_TadpoledSkeletons_Scouting")
AND
QRY_SpeakerIsAvailable(_Skelly)
THEN
PROC_DialogAddSpeakingActor(_Inst, _Skelly);

PROC
PROC_SceneInterrupted(_Scout, _Player, "SHA_TadpoledSkeletons_Scouting", "Spotted")
AND
QRY_OnlyOnce("SHA_TadpoledSkeletons_SpotPlayerDialog")
AND
NOT QRY_StartDialog((DIALOGRESOURCE)SHA_TadpoledSkeletons_Scouts_8c88348b-6302-76dc-54ec-fd3eb430f27b, 
					NULL_00000000-0000-0000-0000-000000000000, 
					NULL_00000000-0000-0000-0000-000000000000, 
					NULL_00000000-0000-0000-0000-000000000000, _Player)
THEN
SetFlag((FLAG)SHA_TadpoledSkeletons_Event_ShadowsAttack_c9d43981-22ca-2884-9204-6a6049243ae6);
PROC_SHA_TadpoledSkeletons_StartBackupAD((CHARACTER)_Scout);

PROC
PROC_SceneInterrupted(_Scout, _Player, "SHA_TadpoledSkeletons_Scouting", _Interrupt)
AND
NOT DB_SHA_TadpoledSkeletons_SceneInterruptWhitelist(_Interrupt)
THEN
SetFlag((FLAG)SHA_TadpoledSkeletons_Event_ShadowsAttack_c9d43981-22ca-2884-9204-6a6049243ae6);
PROC_SceneOver("SHA_TadpoledSkeletons_Scouting");

PROC
PROC_SceneOver("SHA_TadpoledSkeletons_Scouting")
AND
DB_SHA_TadpoledSkeletons_Scouts(_Skelly)
THEN
PROC_RemoveAllDialogEntriesForSpeaker (_Skelly);
SetFlag(SHA_TadpoledSkeletons_Event_StopSpotting_16ac7e00-43ec-47c0-8f72-872e08f90a54, NULL_00000000-0000-0000-0000-000000000000, 0);
SetEntityEvent(_Skelly, "SHA_TadpoledSkeletons_FinishScouting");

PROC
PROC_SHA_TadpoledSkeletons_StartBackupAD((CHARACTER)_Skelly)
AND
NOT QRY_SpeakerIsAvailable(_Skelly)
THEN
PROC_SceneOver("SHA_TadpoledSkeletons_Scouting");
PROC_SHA_StartShadowquakeShakes();

PROC
PROC_SHA_TadpoledSkeletons_StartBackupAD((CHARACTER)_Skelly)
AND
QRY_SpeakerIsAvailable(_Skelly)
THEN
PROC_TryStartAD((DIALOGRESOURCE)SHA_TadpoledSkeletons_AD_ScoutsRetreat_6d1e6e31-dd1a-7333-58af-278aa15ad39e, _Skelly);
//END_REGION

//REGION Return to Necromancer
// Scouts will be force updating if they finish scouting as a result of the Shadowquake combat starting
// Disable their force update when they reach the Necromancer's library
// Scouts have a separate faction from the rest of Balthazar's minions - allows them to be attacked without all the undead going hostile
// Set the scouts to use the same faction as the rest of minions when they are reuinted
IF
DB_InRegion(_Scout, S_SHA_Necromancer_ShadowquakeArea_a41b16b6-d2ae-425c-a0bf-80183a66e7e6)
AND
DB_SHA_TadpoledSkeletons_Scouts(_Scout)
THEN
DB_Dialogs(_Scout, (DIALOGRESOURCE)SHA_Necromancer_Skeletons_9a891e71-530b-f11a-9450-6759e4fd51b8);
SetFaction(_Scout, ACT2a_SHA_Necromancer_6a8d2366-831d-48ad-8d58-1f99a5cc7ca1);

IF
DB_InRegion(_Scout, S_SHA_Necromancer_ShadowquakeArea_a41b16b6-d2ae-425c-a0bf-80183a66e7e6)
AND
DB_SHA_TadpoledSkeletons_Scouts(_Scout)
AND
DB_SHA_TadpoledSkeletons_ReturnForceUpdate(_Scout)
THEN
SetForceUpdate(_Scout, 0);
NOT DB_SHA_TadpoledSkeletons_ReturnForceUpdate(_Scout);

IF
DB_Defeated(_Scout)
AND
DB_SHA_TadpoledSkeletons_Scouts((CHARACTER)_Scout)
AND
DB_SHA_TadpoledSkeletons_ReturnForceUpdate(_Scout)
THEN
SetForceUpdate(_Scout, 0);
NOT DB_SHA_TadpoledSkeletons_ReturnForceUpdate(_Scout);

IF
RelationChanged(ACT2a_SHA_Scouts_d5118536-414e-4d48-b6ca-3c6b003c2dee, _OtherFaction, _NewRelation, _)
AND
_NewRelation < 50
AND
DB_PartyMembers(_Player)
AND
GetFaction(_Player, _PlayerFaction)
AND
_OtherFaction == _PlayerFaction
THEN
SetFlag(SHA_Necromancer_State_PlayerAttacked_d7a76908-fef8-1e43-7db2-12cb364ce67d, NULL_00000000-0000-0000-0000-000000000000, 0);

// Scouting is finished after dialog ends or when the main shadowquake begins
// Force update since they need to always make it back to the Necromamcer even if the player doesn't follow
IF
EntityEvent(_Scout, "SHA_TadpoledSkeletons_FinishScouting")
AND
DB_SHA_TadpoledSkeletons_Scouts((CHARACTER)_Scout)
AND
NOT DB_SHA_TadpoledSkeletons_ReturnForceUpdate(_Scout)
THEN
SetForceUpdate(_Scout, 1);
DB_SHA_TadpoledSkeletons_ReturnForceUpdate(_Scout);
//END_REGION

//REGION Debug
IF
TextEvent("SHA_TadpoledSkeletons_EnableScouts")
AND
DB_SHA_TadpoledSkeletons_Scouts(_Scout)
THEN
SetOnStage(_Scout, 1);

IF
TextEvent("SHA_TadpoledSkeletons_DisableScouts")
AND
DB_SHA_TadpoledSkeletons_Scouts(_Scout)
THEN
SetOnStage(_Scout, 0);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
