Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs((ITEM)S_TWN_Distillery_ResearchNotes_70a0452f-d5e5-4e57-be4a-f27173b343dc, (DIALOGRESOURCE)TWN_Distillery_ResearchNotes_79f36d06-8585-16fa-f505-98ef6a8d37f6);

DB_Dialogs((CHARACTER)S_TWN_Distillery_Brewer_Zombie_01_36fc4cbc-9723-40b2-b06a-d4c704cdc007, (DIALOGRESOURCE)TWN_Distillery_Brewer_Zombie_01_00e9a8c9-5335-6559-7c5d-2e923dd4f4e9);
DB_Dialogs((CHARACTER)S_TWN_Distillery_Brewer_Zombie_02_cf1bda85-2948-4dc2-8a2e-fdadb5ac7ff3, (DIALOGRESOURCE)TWN_Distillery_Brewer_Zombie_02_e489acb0-15fa-9b78-be5d-9296e563702a);
DB_Dialogs((CHARACTER)S_TWN_Distillery_Brewer_Zombie_03_025180c7-4394-4118-8318-c0cf7169d0d6, (DIALOGRESOURCE)TWN_Distillery_Brewer_Zombie_03_c4486196-9b80-7822-0663-47249d7178ad);
DB_Dialogs((CHARACTER)S_TWN_Distillery_Brewer_Zombie_04_d00c35ae-1e20-4006-b667-5fb4f8c8728d, (DIALOGRESOURCE)TWN_Distillery_Brewer_Zombie_04_7e1219b6-925e-9f09-6221-a7ef701d72d1);

DB_ItemDialog_NarratorAD((ITEM)S_TWN_Distillery_HalflingsSign_4ea73020-e558-4c0b-a081-fd207b96991d, (DIALOGRESOURCE)TWN_Distillery_AD_HalflingsSign_d7e32a1f-c958-9388-3677-0eb3978d88fe);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_Distillery_Blackboard1_1_8adcd9bd-48da-4167-93b0-1447b557f175,(DIALOGRESOURCE)TWN_WaningMoon_AD_Distillery_Blackboard1_9e5c64b3-b4b1-5144-2d9e-eafaafb3485d);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_Distillery_Blackboard1_2_f577b0cb-45d0-490b-bbcf-277231563750,(DIALOGRESOURCE)TWN_WaningMoon_AD_Distillery_Blackboard1_9e5c64b3-b4b1-5144-2d9e-eafaafb3485d);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_Distillery_Blackboard1_3_a3bd8060-4c35-4627-a40b-1c15451397d7,(DIALOGRESOURCE)TWN_WaningMoon_AD_Distillery_Blackboard1_9e5c64b3-b4b1-5144-2d9e-eafaafb3485d);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_Distillery_Blackboard1_4_f9586d0c-0544-49d4-91a6-b422cf293268,(DIALOGRESOURCE)TWN_WaningMoon_AD_Distillery_Blackboard1_9e5c64b3-b4b1-5144-2d9e-eafaafb3485d);
DB_ItemDialog_NarratorAD(S_TWN_Distillery_Blackboard2_1_c9269f14-4394-4d6c-83e6-85cceac3dfc9,(DIALOGRESOURCE)TWN_WaningMoon_AD_Distillery_Blackboard2_ab81a269-36ad-625d-03a7-14b1679826fa);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_Distillery_Blackboard4_1_b09d6516-eefe-4d2f-93a4-a6486f1a0913,(DIALOGRESOURCE)TWN_WaningMoon_AD_Distillery_Blackboard4_fb25284c-cba5-2240-28a7-48fe7b0bee19);

DB_BookFlags(S_TWN_Distillery_BrewersDiary_a35dd7a5-f965-4d29-8229-3d282ddeb4d8,TWN_Distillery_State_ReadBrewersDiary_49a614a6-dbc4-4e9f-8130-1bbcd989fcc0);

// Flag, Status, Duration
DB_TWN_Distillery_PoisonFlags((FLAG)TWN_Distillery_Event_DrankOnce_932bbbe3-6daf-4aee-8dad-c62b992c6eec, "TWN_DISTILLERY_DRANK_ONCE", -1.0);
DB_TWN_Distillery_PoisonFlags((FLAG)TWN_Distillery_Event_DrankTwice_813f049c-31e2-4a57-9079-67274a668948, "TWN_DISTILLERY_DRANK_TWICE", -1.0);

//elevator
DB_TWN_Distillery_ElevatorLevers(S_PLT_Distillery_Elevator_Lever_5c4ca812-8a43-8c16-2be1-733ba7bfd233);
DB_TWN_Distillery_ElevatorLevers(S_TWN_Distillery_ElevatorLeverAtTop_340d6fd3-a0e4-4e9d-876d-50a1312822c8);
DB_TWN_Distillery_ElevatorLevers(S_TWN_Distillery_ElevatorLeverAtBottom_dd32d3f7-b73d-4ead-a743-8379370ca4d2);

//1 = _IsDown, so move up
DB_TWN_Distillery_ElevatorPosition(1);
DB_TWN_Distillery_ElevatorPoints(1, S_TWN_Distillery_ElevatorBottomPosition_43b59089-136f-4a9c-bae8-40a95b1b5c0c);
DB_TWN_Distillery_ElevatorPoints(0, S_TWN_Distillery_ElevatorTopPosition_53ffbcfa-5ae4-4d52-8eae-c582c2d5000f);

DB_GLO_DefeatCounter(S_TWN_Distillery_Brewer_4d9e3db3-9a78-4f4b-8101-1dd73c0f3be5, "TWN_Distillery_BrewerAndZombies");
DB_GLO_DefeatCounter(S_TWN_Distillery_Brewer_Zombie_01_36fc4cbc-9723-40b2-b06a-d4c704cdc007, "TWN_Distillery_BrewerAndZombies");
DB_GLO_DefeatCounter(S_TWN_Distillery_Brewer_Zombie_02_cf1bda85-2948-4dc2-8a2e-fdadb5ac7ff3, "TWN_Distillery_BrewerAndZombies");
DB_GLO_DefeatCounter(S_TWN_Distillery_Brewer_Zombie_03_025180c7-4394-4118-8318-c0cf7169d0d6, "TWN_Distillery_BrewerAndZombies");
DB_GLO_DefeatCounter(S_TWN_Distillery_Brewer_Zombie_04_d00c35ae-1e20-4006-b667-5fb4f8c8728d, "TWN_Distillery_BrewerAndZombies");
DB_GLO_DefeatCounter_AllPermaDefeatedGlobalFlag("TWN_Distillery_BrewerAndZombies", (FLAG)TWN_Distillery_State_Safe_ab5934e9-0f92-4aed-bcf2-e092e461396f);

PROC_TriggerRegisterForPlayers((TRIGGER)S_TWN_Distillery_LabCinematicTrigger_a0b667dc-aff3-4c8f-9425-73ce48d01d88);
DB_InRegionFlag((TRIGGER)S_TWN_Distillery_LabCinematicTrigger_a0b667dc-aff3-4c8f-9425-73ce48d01d88, TWN_Distillery_State_InLabCinematicTrigger_637ae36c-ddd3-4b02-bd84-2d702dfbdd60);
DB_FlagReactionAfterDialog(TWN_Distillery_Event_LearnedAlchemyStashLocation_a2a8979f-09d1-4ef9-87b4-9662760ab248, (DIALOGRESOURCE)TWN_Distillery_ResearchNotes_79f36d06-8585-16fa-f505-98ef6a8d37f6);

DB_CombatReact_AD_OnDeathPostCombat(S_TWN_Distillery_Brewer_4d9e3db3-9a78-4f4b-8101-1dd73c0f3be5, (DIALOGRESOURCE)TWN_Distillery_PAD_BrewerDead_4c3a9394-c683-10f2-a25c-5418c29e544e);
KBSECTION
//REGION Questions Limit
IF
FlagSet(TWN_Distillery_Event_SucceededOnRoll_6907caf8-81a6-4ab4-99e5-c2d521db4e81, _Target, _)
THEN
PROC_ObjectCountHelper(_Target, "TWN_Distillery_SuccessesCount");
ClearFlag(TWN_Distillery_Event_SucceededOnRoll_6907caf8-81a6-4ab4-99e5-c2d521db4e81, _Target);

IF
DB_ObjectCountHelper(_Target, "TWN_Distillery_SuccessesCount", 4)
THEN
SetFlag((FLAG)TWN_Distillery_State_SuccessesLimitReached_9f6aa071-02d7-4b0d-9922-90389ce27c80, _Target);
//END_REGION Questions Limit

//REGION Bar Stand Smash
IF
DialogEnded((DIALOGRESOURCE)TWN_Distillery_Brewer_c41d8459-3237-248f-c1bc-51e66892a65f, _)
AND
GetFlag((FLAG)TWN_Distillery_State_HostileResolution_9eb0881e-4818-4d3f-8580-238793666fc8, NULL_00000000-0000-0000-0000-000000000000, 1)
THEN
Die((ITEM)S_TWN_Distillery_BarStand_75855c78-f8af-48a4-8a2c-99045a197ecf);
//END_REGION

//REGION Questions Limit
IF
FlagSet(TWN_Distillery_Event_QuestionAsked_c214a1d8-54d9-432e-a652-028ce15f1f4f, _Target, _)
THEN
PROC_ObjectCountHelper(_Target, "TWN_Distillery_QuestionsCount");
ClearFlag(TWN_Distillery_Event_QuestionAsked_c214a1d8-54d9-432e-a652-028ce15f1f4f, _Target);

IF
DB_ObjectCountHelper(_Target, "TWN_Distillery_QuestionsCount", 4)
THEN
SetFlag((FLAG)TWN_Distillery_State_QuestionsLimitReached_a7e54a82-828e-46f8-82b0-b51c4a9b4dcd, _Target);
//END_REGION Questions Limit

//REGION statuses
IF
FlagSet(_Flag, _Player, _)
AND
DB_TWN_Distillery_PoisonFlags(_Flag, _Status, _Duration)
THEN
ApplyStatus(_Player, _Status, _Duration, 1, S_TWN_Distillery_Brewer_4d9e3db3-9a78-4f4b-8101-1dd73c0f3be5);

IF
DialogEnded((DIALOGRESOURCE)TWN_Distillery_Brewer_c41d8459-3237-248f-c1bc-51e66892a65f, _ID)
AND
DB_DialogPlayers(_ID, _Player, _)
AND
GetFlag((FLAG)TWN_Distillery_Event_DrankThrice_ba7ca9a4-2df6-4ea2-9255-3c923dfece3d, _Player, 1)
THEN
ApplyStatus(_Player, "TWN_DISTILLERY_DRANK_THRICE", 60.0, 1, S_TWN_Distillery_Brewer_4d9e3db3-9a78-4f4b-8101-1dd73c0f3be5);

IF
DialogEnded(TWN_Distillery_Brewer_c41d8459-3237-248f-c1bc-51e66892a65f, _Inst)
AND
DB_DialogPlayers(_Inst, _Player, 1)
AND
GetFlag(TWN_Distillery_Event_DrankThrice_ba7ca9a4-2df6-4ea2-9255-3c923dfece3d, _Player, 1)
THEN
EnterCombat(S_TWN_Distillery_Brewer_4d9e3db3-9a78-4f4b-8101-1dd73c0f3be5, _Player); // if the player is unconcious then combat won't auto start, need to start manually
//END_REGION statuses

//REGION elevator
// blocked because moving
PROC
PROC_BlockUseOfItem(_Player, _ElevatorLever)
AND
DB_TWN_Distillery_ElevatorLevers(_ElevatorLever)
AND
DB_Players(_Player)
AND
DB_TWN_Distillery_ElevatorIsMoving(1)
THEN
DB_CustomUseItemResponse(_Player, _ElevatorLever, 0);
PROC_PlayCantUseItemAD(_Player);

IF
UseStarted(_Player, _ElevatorLever)
AND
DB_TWN_Distillery_ElevatorLevers(_ElevatorLever)
AND
DB_TWN_Distillery_ElevatorPosition(_IsDown)
AND
DB_TWN_Distillery_ElevatorPoints(_IsDown, _Point)
AND
GetPosition(_Point,_X,_Y,_Z)
THEN
DB_TWN_Distillery_ElevatorIsMoving(1);
PlatformMoveTo(S_LT_PLT_TWN_Distillery_Elevator_2aafca43-ecd3-4e23-a15e-d53d138b9219, _X, _Y, _Z, 3.0, "TWN_Distillery_ElevatorMoved", 0);

IF
PlatformMovementFinished(S_LT_PLT_TWN_Distillery_Elevator_2aafca43-ecd3-4e23-a15e-d53d138b9219, "TWN_Distillery_ElevatorMoved")
AND
DB_TWN_Distillery_ElevatorPosition(_OldPosition)
AND
DB_Negate(_OldPosition,_NewPosition)
THEN
NOT DB_TWN_Distillery_ElevatorPosition(_OldPosition);
DB_TWN_Distillery_ElevatorPosition(_NewPosition);
NOT DB_TWN_Distillery_ElevatorIsMoving(1);
//END_REGION elevator

//REGION debug
IF
TextEvent("twn_freeel")
THEN
NOT DB_TWN_Distillery_ElevatorIsMoving(1);

IF
TextEvent("twn_pwp")
AND
GetHostCharacter(_Player)
THEN
TeleportTo(_Player, S_Debug_TWN_Distillery_Lab_a6ec1428-7b8f-41c2-acd4-b33ed2a3421b, "");
//END_REGION debug

//REGION Arm in a barrel
IF
UseStarted(_Player, (ITEM)S_TWN_Distillery_BarrelWithSkeletonArm_4cf1bf5d-c920-41c0-828d-884bc19251ce)
AND
QRY_OncePerUserAndNearbyPlayers(_Player, "TWN_Distillery_BarrelArmReaction")
THEN
PROC_TryStartAD((DIALOGRESOURCE)TWN_Distillery_PAD_ArmInBarrel_b529d126-8f53-633b-ac57-1a4d38d00734, _Player);
//END_REGION Arm in a barrel

//REGION Research Notes
PROC
PROC_FlagReactionAfterDialog(_Player, TWN_Distillery_Event_LearnedAlchemyStashLocation_a2a8979f-09d1-4ef9-87b4-9662760ab248)
THEN
PROC_GlobalSetFlagAndCache(TWN_Distillery_Knows_AlchemyStashLocation_04005a89-e0a5-4539-b1af-930a6e718f00);
UnlockRecipe((CHARACTER)_Player, "ALCH_Poison_UNI_Brewer", 1);

PROC
PROC_FlagReactionAfterDialog(_Player, TWN_Distillery_Event_LearnedAlchemyStashLocation_a2a8979f-09d1-4ef9-87b4-9662760ab248)
AND
NOT DB_GlobalFlag(TWN_Distillery_State_FoundAlchemyStash_3c0bc980-c628-4b4e-8009-429227e2056c)
THEN
PROC_UnlockPartyMarker("TWN_Distillery_AlchemyStash");
PROC_Shovel_RevealRemoteMound(S_TWN_Distillery_AlchemyStash_BuriedMound_4f0b2658-edb7-4d4c-99c9-cf658a670447, (CHARACTER)_Player);

IF
UseFinished(_, (ITEM)S_TWN_Distillery_AlchemyStash_BuriedMound_4f0b2658-edb7-4d4c-99c9-cf658a670447, 1)
AND
QRY_OnlyOnce("TWN_Distillery_RemoveAlchemyStashMaker")
THEN
PROC_HidePartyMarker("TWN_Distillery_AlchemyStash");
PROC_GlobalSetFlagAndCache(TWN_Distillery_State_FoundAlchemyStash_3c0bc980-c628-4b4e-8009-429227e2056c);
//END_REGION

//REGION Peaceful Resolution XP
PROC
PROC_GlobalFlagReactionAfterDialog(TWN_Distillery_State_PeacefulResolution_ef40ccf6-a7d7-4fde-8fcf-82b947c29a44)
AND
DB_GLO_DefeatCounter(_NPC, "TWN_Distillery_BrewerAndZombies")
THEN
PROC_PeacefulResolve((CHARACTER)_NPC);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
