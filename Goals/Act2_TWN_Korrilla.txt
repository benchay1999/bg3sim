Version 1
SubGoalCombiner SGC_AND
INITSECTION
PROC_TriggerRegisterForPlayers(S_GLO_KorrillaAppearCheck_e92bd605-d256-4031-b976-ef65614586c7);

KBSECTION
//REGION debug
IF
TextEvent("TWN_TestKorrilla")
THEN
PROC_TeleportPartiesTo(S_GLO_KorrillaAppearCheck_e92bd605-d256-4031-b976-ef65614586c7, "");
//END_REGION

IF
DB_InRegion(_Player ,S_GLO_KorrillaAppearCheck_e92bd605-d256-4031-b976-ef65614586c7)
AND
DB_Players(_Player)
AND
QRY_OnlyOnce("TWN_Korrilla_Appear")
THEN
PROC_TWN_TeleportKorrillaAndWatch(S_GLO_KorrillaTownPos_552a7659-99de-4bf5-9108-8ab093fb0f56, _Player);

//Korrilla teleports in and watches the palyer
PROC
PROC_TWN_TeleportKorrillaAndWatch((TRIGGER)_Trigger, (CHARACTER)_Player)
THEN
TeleportTo(S_GOB_Warlock_d432aafd-f728-4d9d-9707-732e1cdd8297, _Trigger, "", 0, 0, 0, 0, 1);
CharacterDisableAllCrimes(S_GOB_Warlock_d432aafd-f728-4d9d-9707-732e1cdd8297);
PROC_GLO_Monitor_EntityFoop(S_GOB_Warlock_d432aafd-f728-4d9d-9707-732e1cdd8297);
DB_Dialogs((CHARACTER)S_GOB_Warlock_d432aafd-f728-4d9d-9707-732e1cdd8297, (DIALOGRESOURCE)TWN_KorrillaTheSpy_Korrilla_aab4f3d2-4018-3855-d62d-62dc7cd1fef5);
SetHitpointsPercentage(S_GOB_Warlock_d432aafd-f728-4d9d-9707-732e1cdd8297, 100.0);
LookAtEntity(S_GOB_Warlock_d432aafd-f728-4d9d-9707-732e1cdd8297, _Player, 30.0);
SetVisible(S_GOB_Warlock_d432aafd-f728-4d9d-9707-732e1cdd8297, 1);
PROC_GLO_Warlock_ApplyAllStatuses();
ObjectTimerLaunch(S_GOB_Warlock_d432aafd-f728-4d9d-9707-732e1cdd8297, "TWN_KorrillaPresenceTimer", 30000, 0);

IF
ObjectTimerFinished(S_GOB_Warlock_d432aafd-f728-4d9d-9707-732e1cdd8297, "TWN_KorrillaPresenceTimer")
THEN
PROC_GLO_Warlock_TeleportOut();

//IF Korrilla is approached by the player and she sees it, she teleports away
IF
StatusApplied(_Player, "TWN_KORRILLATHESPY_ISNEAR", _, _)
AND
DB_Players((CHARACTER)_Player)
AND
DB_Sees((CHARACTER)S_GOB_Warlock_d432aafd-f728-4d9d-9707-732e1cdd8297, _Player)
THEN
PROC_GLO_Warlock_TeleportOut();
PROC_TryStartAD(GLO_KorrillaTheSpy_PAD_KorrillaVanishesReaction_d67b6532-f773-6e07-3a4f-615aa3b949dc, _Player);

IF
DB_Sees((CHARACTER)S_GOB_Warlock_d432aafd-f728-4d9d-9707-732e1cdd8297, _Player)
AND
DB_Players((CHARACTER)_Player)
AND
HasActiveStatus(_Player, "TWN_KORRILLATHESPY_ISNEAR", 1)
THEN
PROC_GLO_Warlock_TeleportOut();
PROC_TryStartAD(GLO_KorrillaTheSpy_PAD_KorrillaVanishesReaction_d67b6532-f773-6e07-3a4f-615aa3b949dc, _Player);

IF
DialogEnded(TWN_KorrillaTheSpy_Korrilla_aab4f3d2-4018-3855-d62d-62dc7cd1fef5, _ID)
AND
DB_DialogPlayers(_ID, _Player, _)
THEN
PROC_GLO_Warlock_TeleportOut();
PROC_TryStartAD(GLO_KorrillaTheSpy_PAD_KorrillaVanishesReaction_d67b6532-f773-6e07-3a4f-615aa3b949dc, _Player);

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
