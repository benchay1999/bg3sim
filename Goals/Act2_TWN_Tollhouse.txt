Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_ItemDialog_NarratorAD((ITEM)S_TWN_Tollhouse_ConfiscatedItemsPlaque_4b6bc019-30ba-4c1b-9d16-78f52af5ff77, (DIALOGRESOURCE)TWN_Tollhouse_AD_ConfiscatedItemsPlaque_431ec44c-52d6-a5bb-0eeb-647a927bbed9);
DB_DialogMoneyTransfer(1, (DIALOGRESOURCE)TWN_Tollhouse_TollhouseMaster_1fc993a8-c338-7cd6-aef2-add52d540d28, 1);
DB_OneShotPlayerTrigger(S_TWN_Tollhouse_ConfiscatedItemsRoom_e4c93415-7088-4f10-b5a1-f185f5bf3d17);

DB_Dialogs((CHARACTER)S_TWN_Tollhouse_Face_Cowardice_efea0b12-68eb-4329-8fd9-14078e983607, (DIALOGRESOURCE)TWN_Tollhouse_Face_Cowardice_4917a29f-e4ac-3bbe-86c2-2b7252946955);
DB_Dialogs((CHARACTER)S_TWN_Tollhouse_Face_Greed_46e0f41a-6603-43ee-8e99-5dd91e7e9bb9, (DIALOGRESOURCE)TWN_Tollhouse_Face_Greed_bf0e6f76-3550-8e75-5287-2dd8980e7dad);
DB_Dialogs((CHARACTER)S_TWN_Tollhouse_Face_Guilt_2321affc-6302-4800-87dc-bcd220642920, (DIALOGRESOURCE)TWN_Tollhouse_Face_Guilt_1561f08d-8028-a66a-55ae-272f954c6e18);
DB_Dialogs((CHARACTER)S_TWN_Tollhouse_Face_Heartlessness_f335ce62-1d8b-4f70-969d-ef75f0dd78b1, (DIALOGRESOURCE)TWN_Tollhouse_Face_Heartlessness_95115f05-f286-ac71-d76d-fdc0625a77c8);
DB_Dialogs((CHARACTER)S_TWN_Tollhouse_Face_Obedience_0e35df6f-31f8-4ebb-a44f-e472c2e6a9c9, (DIALOGRESOURCE)TWN_Tollhouse_Face_Obedience_295ecb1c-9092-845d-97d0-86d0528eb3e7);
DB_Dialogs((CHARACTER)S_TWN_Tollhouse_Face_Regret_f5206c52-e481-4c2c-b24b-f186904d768d, (DIALOGRESOURCE)TWN_Tollhouse_Face_Regret_8326a216-cf9a-2adb-e91d-1c5c7d2f28fc);

DB_GLO_ADManager_Category("TWN_Tollhouse", (FLAG)TWN_Tollhouse_State_BlockAD_5aaf9778-e482-46e1-8cd4-606070a27bb7, 4000, 1000);
DB_GLO_ADManager_AD("TWN_Tollhouse", (DIALOGRESOURCE)TWN_Tollhouse_AD_Face_Cowardice_fb9b26a1-c917-61a5-2e16-9f4e57ca61c0, (FLAG)TWN_Tollhouse_State_BlockAD_Face_Cowardice_68000234-924a-49b4-a6fc-60116d27fbfe);
DB_GLO_ADManager_AD("TWN_Tollhouse", (DIALOGRESOURCE)TWN_Tollhouse_AD_Face_Greed_e689dd20-a966-936c-8ca8-638d693692a4, (FLAG)TWN_Tollhouse_State_BlockAD_Face_Greed_b62f32dc-8791-46ed-8e62-a4567f768bae);
DB_GLO_ADManager_AD("TWN_Tollhouse", (DIALOGRESOURCE)TWN_Tollhouse_AD_Face_Guilt_118adcf3-2ab3-f10e-6992-ecc3c947fb49, (FLAG)TWN_Tollhouse_State_BlockAD_Face_Guilt_9e7fa685-3388-4512-bb67-aacda744324c);
DB_GLO_ADManager_AD("TWN_Tollhouse", (DIALOGRESOURCE)TWN_Tollhouse_AD_Face_Heartlessness_32db23cb-81c3-1ced-9d03-cdbeb600cb08, (FLAG)TWN_Tollhouse_State_BlockAD_Face_Heartlessness_c24b426f-c68d-464c-af99-960dfa22ae84);
DB_GLO_ADManager_AD("TWN_Tollhouse", (DIALOGRESOURCE)TWN_Tollhouse_AD_Face_Obedience_a1b4adaa-bb31-bebe-16e3-4f84dd87aefa, (FLAG)TWN_Tollhouse_State_BlockAD_Face_Obedience_d18bf76e-a7fe-4e7b-b702-49d04944b69c);
DB_GLO_ADManager_AD("TWN_Tollhouse", (DIALOGRESOURCE)TWN_Tollhouse_AD_Face_Regret_665caa6c-f171-6ebf-98ac-5d0ef50cdb82, (FLAG)TWN_Tollhouse_State_BlockAD_Face_Regret_01c1eabe-f46a-4a0a-a054-ee57686b9221);

DB_TWN_Tollhouse_FacesCombatPositions((CHARACTER)S_TWN_Tollhouse_Face_Heartlessness_f335ce62-1d8b-4f70-969d-ef75f0dd78b1, (TRIGGER)S_TWN_Tollhouse_Face_Heartlessness_CombatPosition_e430bf7a-c9d8-451c-bb32-c49a85d92894);
DB_TWN_Tollhouse_FacesCombatPositions(S_TWN_Tollhouse_Face_Guilt_2321affc-6302-4800-87dc-bcd220642920, S_TWN_Tollhouse_Face_Guilt_CombatPosition_1e2717e9-63f6-4760-bf64-620098361d57);
DB_TWN_Tollhouse_FacesCombatPositions(S_TWN_Tollhouse_Face_Obedience_0e35df6f-31f8-4ebb-a44f-e472c2e6a9c9, S_TWN_Tollhouse_Face_Obedience_CombatPosition_a842e8e6-0f54-4560-9785-ad6412af5c3f);
DB_TWN_Tollhouse_FacesCombatPositions(S_TWN_Tollhouse_Face_Cowardice_efea0b12-68eb-4329-8fd9-14078e983607, S_TWN_Tollhouse_Face_Cowardice_CombatPosition_06b03415-2e55-48b9-b3f4-5060dc88ab28);
DB_TWN_Tollhouse_FacesCombatPositions(S_TWN_Tollhouse_Face_Greed_46e0f41a-6603-43ee-8e99-5dd91e7e9bb9, S_TWN_Tollhouse_Face_Greed_CombatPosition_1ea0f0c4-cc31-4db1-866d-5150836a0047);
DB_TWN_Tollhouse_FacesCombatPositions(S_TWN_Tollhouse_Face_Regret_f5206c52-e481-4c2c-b24b-f186904d768d, S_TWN_Tollhouse_Face_Regret_CombatPosition_73f38710-090c-4642-b03e-02ef8fc8f9d5);

DB_TWN_Tollhouse_Toll(5000);

DB_GLO_DefeatCounter(S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, "TWN_Tollhouse_MasterAndFaces");
DB_GLO_DefeatCounter(S_TWN_Tollhouse_Face_Greed_46e0f41a-6603-43ee-8e99-5dd91e7e9bb9, "TWN_Tollhouse_MasterAndFaces");
DB_GLO_DefeatCounter(S_TWN_Tollhouse_Face_Obedience_0e35df6f-31f8-4ebb-a44f-e472c2e6a9c9, "TWN_Tollhouse_MasterAndFaces");
DB_GLO_DefeatCounter(S_TWN_Tollhouse_Face_Guilt_2321affc-6302-4800-87dc-bcd220642920, "TWN_Tollhouse_MasterAndFaces");
DB_GLO_DefeatCounter(S_TWN_Tollhouse_Face_Heartlessness_f335ce62-1d8b-4f70-969d-ef75f0dd78b1, "TWN_Tollhouse_MasterAndFaces");
DB_GLO_DefeatCounter(S_TWN_Tollhouse_Face_Regret_f5206c52-e481-4c2c-b24b-f186904d768d, "TWN_Tollhouse_MasterAndFaces");
DB_GLO_DefeatCounter(S_TWN_Tollhouse_Face_Cowardice_efea0b12-68eb-4329-8fd9-14078e983607, "TWN_Tollhouse_MasterAndFaces");
DB_GLO_DefeatCounter_AllPermaDefeatedGlobalFlag("TWN_Tollhouse_MasterAndFaces", (FLAG)TWN_Tollhouse_State_Safe_35ef29da-a7d1-435f-9fd9-7a03f828a903);

//Unofficial Ledger
DB_BookFlags(S_TWN_Tollhouse_UnofficialLedger_d465ddcd-ed89-41e2-be48-87e9256bd74f,TWN_Tollhouse_Knows_CollectorWasStealing_8b80a42f-675f-4fe9-b8c1-94b1b5813286);

PROC_TriggerRegisterForParty(S_TWN_Tollhouse_SecretBasementRoom_001_953e5587-7b8a-4250-80ad-9ec6d6a3db9f);
PROC_TriggerRegisterForParty(S_TWN_Tollhouse_SecretBasementRoom_002_5a1f927d-4006-40a1-9c08-ea994dc48de9);

DB_ItemDialog_NarratorAD((ITEM)S_TWN_Tollhouse_SafeSkeleton_ab98c110-133c-486c-a839-2f41a0bb035e,TWN_Tollhouse_AD_SafeSkeleton_7a92253a-dbb4-df89-fcb8-fae4d811ebef);

DB_CombatReact_AD_OnDeathPostCombat(S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, (DIALOGRESOURCE)TWN_Tollhouse_PAD_TollmasterDead_f0bd01e5-6936-1ab4-a5e6-dedf8a39adcd);
KBSECTION
//REGION Basement
IF
Unlocked((ITEM)S_TWN_Tollhouse_BasementDoor_Inner_dc897fe0-df62-4d66-a431-491c8d0ac706, _, _)
AND
IsLocked((ITEM)S_TWN_Tollhouse_BasementDoor_Outer_2f6686df-117e-40c5-986b-bd550a7ae9be, 1)
THEN
Unlock((ITEM)S_TWN_Tollhouse_BasementDoor_Outer_2f6686df-117e-40c5-986b-bd550a7ae9be);

IF
Unlocked((ITEM)S_TWN_Tollhouse_BasementDoor_Outer_2f6686df-117e-40c5-986b-bd550a7ae9be, _, _)
AND
IsLocked((ITEM)S_TWN_Tollhouse_BasementDoor_Inner_dc897fe0-df62-4d66-a431-491c8d0ac706, 1)
THEN
Unlock((ITEM)S_TWN_Tollhouse_BasementDoor_Inner_dc897fe0-df62-4d66-a431-491c8d0ac706);
//END_REGION

//REGION Faces
IF
DB_TWN_Bosses_Minions((CHARACTER)S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, _Face)
AND
DB_Dialogs(_Face, _Dialog)
THEN
DB_IgnoreAssault(_Face);
PROC_CharacterDisableAllCrimes(_Face);
DB_DoNotFace(_Face);
DB_DoNotFaceDialog(_Face, _Dialog);
ApplyStatus(_Face, "TWN_TOLLHOUSE_SHADOWVISAGE", -1.0, 1, NULL_00000000-0000-0000-0000-000000000000);
SetCanFight(_Face, 0);

// Attacking the Tollhouse Collector teleports all the Faces to her and make them enter fight on her side
// Attacking a Face separately only teleports that Face to the Tollhouse Collector
IF
EnteredCombat(S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, _)
AND
QRY_OnlyOnce("TWN_Tollhouse_InitiateCombat")
THEN
PROC_TWN_Tollhouse_InitiateCombat();

IF
AttackedBy(S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, _, _, _, _, _, _)
AND
QRY_OnlyOnce("TWN_Tollhouse_InitiateCombat")
THEN
PROC_TWN_Tollhouse_InitiateCombat();

PROC
PROC_TWN_Tollhouse_InitiateCombat()
THEN
PROC_SpotPlayers_StopSpotting("TWN_Tollhouse_TollhouseMaster");

PROC
PROC_TWN_Tollhouse_InitiateCombat()
AND
DB_TWN_Bosses_Minions((CHARACTER)S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, _Face)
AND
GetFlag(TWN_Tollhouse_State_WasAttacked_924d925d-5fe2-4e59-8c2f-8c21bd9bab5e, _Face, 0)
THEN
PROC_ForceStopDialog(_Face);
PROC_TWN_Tollhouse_AddSurprisedImmunity(_Face);
SetCanFight(_Face, 1); // we need to allow the Face join the fight immediately, or they will have "late combat join" penalty (will skip the first turn)
DB_TWN_Tollhouse_FastTeleport(_Face); // we also need to teleport them to the Tollhouse Collecter without a regular delay
SetFlag(TWN_Tollhouse_State_WasAttacked_924d925d-5fe2-4e59-8c2f-8c21bd9bab5e, _Face);

PROC
PROC_TWN_Tollhouse_AddSurprisedImmunity((CHARACTER)_Face)
AND
HasActiveStatus(_Face, "SURPRISED", 0)
THEN
AddPassive(_Face, "Surprise_Immunity");

PROC
PROC_TWN_Tollhouse_InitiateCombat()
THEN
PROC_SetRelationToPlayers((FACTION)Act2_TWN_Tollhouse_TollhouseMaster_b8a8f7b8-bf0c-4356-ae0b-e5d6622ccf6c, 0);

IF
AttackedBy((CHARACTER)_Face, _, _, _, _, _, _)
AND
DB_TWN_Bosses_Minions((CHARACTER)S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, _Face)
AND
GetFlag(TWN_Tollhouse_State_WasAttacked_924d925d-5fe2-4e59-8c2f-8c21bd9bab5e, _Face, 0)
THEN
PROC_ForceStopDialog(_Face);
SetFlag(TWN_Tollhouse_State_WasAttacked_924d925d-5fe2-4e59-8c2f-8c21bd9bab5e, _Face);
SetFlag(TWN_Tollhouse_State_FaceWasAttacked_eccaf5ed-fc28-45f1-b70c-e8084cd4d2dd);

IF
FlagSet(TWN_Tollhouse_State_WasAttacked_924d925d-5fe2-4e59-8c2f-8c21bd9bab5e, (CHARACTER)_Face, _)
AND
DB_TWN_Tollhouse_FastTeleport(_Face)
THEN
PlayEffect(_Face, VFX_Enemies_Visage_Teleport_Exit_01_76df2e67-c252-2a39-42ae-e8136be8ea69);
RealtimeObjectTimerLaunch(_Face, "TWN_Tollhouse_FaceVFXExitDelay", 660);

IF
FlagSet(TWN_Tollhouse_State_WasAttacked_924d925d-5fe2-4e59-8c2f-8c21bd9bab5e, (CHARACTER)_Face, _)
AND
NOT DB_TWN_Tollhouse_FastTeleport(_Face)
THEN
RealtimeObjectTimerLaunch(_Face, "TWN_Tollhouse_FaceAttacked_DelayBeforeTeleport", 1000);

IF
ObjectTimerFinished((CHARACTER)_Face, "TWN_Tollhouse_FaceAttacked_DelayBeforeTeleport")
THEN
PlayEffect(_Face, VFX_Enemies_Visage_Teleport_Exit_01_76df2e67-c252-2a39-42ae-e8136be8ea69);
RealtimeObjectTimerLaunch(_Face, "TWN_Tollhouse_FaceVFXExitDelay", 660);

IF
ObjectTimerFinished(_Face, "TWN_Tollhouse_FaceVFXExitDelay")
AND
DB_TWN_Tollhouse_FacesCombatPositions((CHARACTER)_Face, _NewPos)
AND
NOT DB_TWN_Tollhouse_FastTeleport(_Face) // if the Face was attacked we set it off stage before teleporting, if the Tollhouse Collector was attacked - we don't set it off stage, because it's in combat
THEN
PROC_SetOnStage(_Face, 0);
TeleportTo(_Face, _NewPos, "TWN_Tollhouse_FaceTeleported", 0, 0, 0, 0, 1);

IF
ObjectTimerFinished(_Face, "TWN_Tollhouse_FaceVFXExitDelay")
AND
DB_TWN_Tollhouse_FacesCombatPositions((CHARACTER)_Face, _NewPos)
AND
DB_TWN_Tollhouse_FastTeleport(_Face)
THEN
TeleportTo(_Face, _NewPos, "TWN_Tollhouse_FaceTeleported", 0, 0, 0, 0, 1);

IF
EntityEvent((CHARACTER)_Face, "TWN_Tollhouse_FaceTeleported")
THEN
PlayEffect(_Face, VFX_Enemies_Visage_Teleport_Arrival_01_2da9d00b-4996-6df6-217f-186e237babd7);
PROC_SetOnStage(_Face, 1);
NOT DB_IgnoreAssault(_Face);
PROC_CharacterEnableAllCrimes(_Face);
RemoveStatus(_Face, "TWN_TOLLHOUSE_SHADOWVISAGE");
RemovePassive(_Face, "Surprise_Immunity");

IF
EntityEvent((CHARACTER)_Face, "TWN_Tollhouse_FaceTeleported")
AND
CanFight(_Face, 0)
THEN
SetCanFight(_Face, 1);
//END_REGION Faces

//REGION Confiscated Items room
PROC
PROC_OneShotTriggerEntered(_Player, S_TWN_Tollhouse_ConfiscatedItemsRoom_e4c93415-7088-4f10-b5a1-f185f5bf3d17)
THEN
DB_TWN_Tollhouse_EnteredRoomWithConfiscatedItems(1);

IF
AutomatedDialogEnded(TWN_Tollhouse_AD_ConfiscatedItemsPlaque_431ec44c-52d6-a5bb-0eeb-647a927bbed9, _ID)
AND
GetClosestAlivePlayer(S_TWN_Tollhouse_ConfiscatedItemsPlaque_4b6bc019-30ba-4c1b-9d16-78f52af5ff77, _Player, _Dist)
AND
_Dist < 10.0
AND
DB_Players(_Player)
AND
NOT DB_TWN_Tollhouse_EnteredRoomWithConfiscatedItems(1)
AND
IsDestroyed(S_TWN_Tollhouse_CursedVinesWall_33f9c029-37d1-496a-b1d6-8ad2ce1151ca, 0)
AND
QRY_OncePerUserAndNearbyPlayers(_Player, "TWN_Tollhouse_EnteredRoomWithConfiscatedItems")
THEN
PROC_TryStartAD(TWN_Tollhouse_PAD_ConfiscatedItemPlaqueUsed_bfd4a858-e8ff-4f39-1b27-ba836f4982b0, _Player);

IF
DestroyedBy(S_TWN_Tollhouse_CursedVinesWall_33f9c029-37d1-496a-b1d6-8ad2ce1151ca, _, _, _)
THEN
SetFlag(TWN_Tollhouse_State_VinesDestroyed_c5c44e1b-9240-4e12-a02c-ce525d466db9, NULL_00000000-0000-0000-0000-000000000000);
//END_REGION Confiscated Items room

//REGION Give her all your money
IF
FlagSet(TWN_Tollhouse_Event_GiveAllMoney_69c835db-61e1-45f0-8dd2-8bb16d947dfc, _Player, _)
AND
GetGold(_Player, _Amount)
AND
IntegerProduct(_Amount, -1, _NegatedAmount)
THEN
AddGold(_Player, _NegatedAmount);
AddGold(S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, _Amount);
PROC_TWN_Tollhouse_CheckTollPaid(_Amount);

PROC
PROC_TWN_Tollhouse_CheckTollPaid((INTEGER)_Amount)
AND
DB_TWN_Tollhouse_Toll(_Toll)
AND
_Amount >= _Toll
THEN
SetFlag(TWN_Tollhouse_State_TollPaid_6816172f-7ba4-434a-92e6-beee8464200b, NULL_00000000-0000-0000-0000-000000000000);
PROC_TWN_Tollhouse_GiveBuffToTollhouseMaster();

PROC
PROC_TWN_Tollhouse_GiveBuffToTollhouseMaster()
THEN
DB_NOOP(1);

IF
DialogEnded(TWN_Tollhouse_TollhouseMaster_1fc993a8-c338-7cd6-aef2-add52d540d28, _Inst)
AND
DB_GlobalFlag((FLAG)TWN_Tollhouse_State_TollPaid_6816172f-7ba4-434a-92e6-beee8464200b)
AND
DB_DialogPlayers(_Inst, _Player, _)
AND
IsInInventoryOf(S_TWN_Tollhouse_Pass_Unsigned_b4693f34-66b0-4040-84b8-a1940ae5b9fc, S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, 1)
AND
QRY_OnlyOnce("TWN_Tollhouse_TollPaid")
THEN
RequestDelete(S_TWN_Tollhouse_Pass_Unsigned_b4693f34-66b0-4040-84b8-a1940ae5b9fc);
TemplateAddTo(UNI_TWN_TollhousePass_Signed_f2c940ba-3049-49d2-bb90-af9f4830057c, _Player, 1, 1);

//END_REGION Give her all your money

//REGION Destructible Floor in Tollhouse
IF
DestroyingBy(S_TWN_Tollhouse_TollCollectorStatue_c19ff9ef-cafb-49a1-bd18-095671c91080, _, _, _)
THEN
TimerLaunch("TWN_Tollhouse_DestroyFloor", 300); // the statue falls on the floor

IF
TimerFinished("TWN_Tollhouse_DestroyFloor")
AND
NOT DB_TWN_Tollhouse_FloorDestroyed(1)
THEN
DestroyPlatform(S_PLT_TWN_TollhouseDestructibleFloor_c24efcdd-46bb-47a9-bbf3-2db2939b2ee8);

IF
PlatformDestroyed(S_PLT_TWN_TollhouseDestructibleFloor_c24efcdd-46bb-47a9-bbf3-2db2939b2ee8) // the floor can also be destroyed manually
THEN
DB_TWN_Tollhouse_FloorDestroyed(1);

IF
PlatformDestroyed(S_PLT_TWN_TollhouseDestructibleFloor_c24efcdd-46bb-47a9-bbf3-2db2939b2ee8)
AND
GetPortalIsOpen((ITEM)S_TWN_Tollhouse_BlockedDoor_7dd7ccb9-f9af-4891-be7f-1fc531395aa3, 0)
THEN
SetPortalIsOpen((ITEM)S_TWN_Tollhouse_BlockedDoor_7dd7ccb9-f9af-4891-be7f-1fc531395aa3, 1);
//END_REGION Destructible Floor in Tollhouse

//REGION Selune Prayer Book
IF
AddedTo(S_TWN_SelunePrayerBook_f64d6149-a6ad-4279-bdc6-b0399a14dedf, (CHARACTER)_Player, _)
AND
DB_Players(_Player)
AND
_Player == S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679
AND
QRY_OnlyOnce("TWN_SelunePrayerBook")
THEN
SetFlag(TWN_State_PickedUpSelunePrayerBook_8c06e0b8-8b39-4471-8d6f-41c69498a4ab, S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679);
PROC_TryStartAD((DIALOGRESOURCE)TWN_PickedUpSelunePrayerBook_PAD_efd4ae26-2c33-374b-2dcc-a622ba8a9106, S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679);

IF
AddedTo(S_TWN_SelunePrayerBook_f64d6149-a6ad-4279-bdc6-b0399a14dedf, (CHARACTER)_Player, _)
AND
DB_Players(_Player)
AND
_Player != S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679
AND
IsInPartyWith(S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679, _Player, 1)
AND
GetDistanceTo(S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679, _Player, _Dist)
AND
_Dist < 8.0
AND
QRY_OnlyOnce("TWN_SelunePrayerBook")
THEN
PROC_TryStartAD((DIALOGRESOURCE)TWN_PickedUpSelunePrayerBook_PAD_efd4ae26-2c33-374b-2dcc-a622ba8a9106, S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679);

IF
GameBookInterfaceClosed(S_TWN_SelunePrayerBook_f64d6149-a6ad-4279-bdc6-b0399a14dedf, _Player)
AND
_Player == S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679
AND
QRY_OnlyOnce("TWN_SelunePrayerBook")
THEN
SetFlag(TWN_State_ReadSelunePrayerBook_4dc1728c-6186-43bb-b510-a3c3aa937425, S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679);
PROC_TryStartAD((DIALOGRESOURCE)TWN_ReadSelunePrayerBook_PAD_b265d42e-1b8e-d81b-d99b-9f0881cd320d, S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679);

IF
GameBookInterfaceClosed(S_TWN_SelunePrayerBook_f64d6149-a6ad-4279-bdc6-b0399a14dedf, _Player)
AND
_Player != S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679
AND
IsInPartyWith(S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679, _Player, 1)
AND
GetDistanceTo(S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679, _Player, _Dist)
AND
_Dist < 8.0
AND
QRY_OnlyOnce("TWN_SelunePrayerBook")
THEN
PROC_TryStartAD((DIALOGRESOURCE)TWN_ReadSelunePrayerBook_PAD_b265d42e-1b8e-d81b-d99b-9f0881cd320d, S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679);

IF
TextEvent("clearspb")
THEN
NOT DB_OnlyOnce("TWN_SelunePrayerBook");
//END_REGION Selune Prayer Book

//REGION Moving Bookcases
// Bookcases movement
// BlackPlane can't be removed via Room Triggers, so we put put it on stage / off stage manually
IF
DualEntityEvent(_, _, "TWN_Tollhouse_MovingBookcasesActivatorUsed")
AND
NOT DB_TWN_Tollhouse_BookcasesMoving(1)
THEN
DB_TWN_Tollhouse_BookcasesMoving(1);
PROC_TWN_Tollhouse_MoveBookcases();

PROC
PROC_TWN_Tollhouse_MoveBookcases()
AND
IsOpened(S_TWN_Tollhouse_Bookcase_001_c1ac3e9d-ac8e-458e-9b36-e58938faddb0, 0)
THEN
Open(S_TWN_Tollhouse_Bookcase_001_c1ac3e9d-ac8e-458e-9b36-e58938faddb0);
Open(S_TWN_Tollhouse_Bookcase_003_48765b79-6005-4a16-9767-7e7d244968c5);

IF
Opened(S_TWN_Tollhouse_Bookcase_003_48765b79-6005-4a16-9767-7e7d244968c5)
THEN
Open(S_TWN_Tollhouse_Bookcase_002_8e4fe044-f562-4873-93ee-a928b77c51ca);
PROC_SetOnStage((ITEM)S_TWN_Tollhouse_BlackPlane_f493a970-06da-4cdc-813f-fa28d137f1ca, 0);

IF
Opened(S_TWN_Tollhouse_Bookcase_002_8e4fe044-f562-4873-93ee-a928b77c51ca)
THEN
NOT DB_TWN_Tollhouse_BookcasesMoving(1);

PROC
PROC_TWN_Tollhouse_MoveBookcases()
AND
IsOpened(S_TWN_Tollhouse_Bookcase_001_c1ac3e9d-ac8e-458e-9b36-e58938faddb0, 1)
THEN
Close(S_TWN_Tollhouse_Bookcase_002_8e4fe044-f562-4873-93ee-a928b77c51ca);

IF
Closed(S_TWN_Tollhouse_Bookcase_002_8e4fe044-f562-4873-93ee-a928b77c51ca)
THEN
Close(S_TWN_Tollhouse_Bookcase_001_c1ac3e9d-ac8e-458e-9b36-e58938faddb0);
Close(S_TWN_Tollhouse_Bookcase_003_48765b79-6005-4a16-9767-7e7d244968c5);
NOT DB_TWN_Tollhouse_BookcasesMoving(1);

IF
Closed(S_TWN_Tollhouse_Bookcase_002_8e4fe044-f562-4873-93ee-a928b77c51ca)
AND
NOT QRY_TriggerEvents_AnyPartyMemberInTrigger(S_TWN_Tollhouse_SecretBasementRoom_001_953e5587-7b8a-4250-80ad-9ec6d6a3db9f)
AND
NOT QRY_TriggerEvents_AnyPartyMemberInTrigger(S_TWN_Tollhouse_SecretBasementRoom_002_5a1f927d-4006-40a1-9c08-ea994dc48de9)
THEN
PROC_SetOnStage((ITEM)S_TWN_Tollhouse_BlackPlane_f493a970-06da-4cdc-813f-fa28d137f1ca, 1);

// in case the player somehow teleports inside while the door is closed
IF
EnteredTrigger(_PartyMember, S_TWN_Tollhouse_SecretBasementRoom_001_953e5587-7b8a-4250-80ad-9ec6d6a3db9f)
AND
NOT DB_OffStage(S_TWN_Tollhouse_BlackPlane_f493a970-06da-4cdc-813f-fa28d137f1ca)
THEN
PROC_SetOnStage((ITEM)S_TWN_Tollhouse_BlackPlane_f493a970-06da-4cdc-813f-fa28d137f1ca, 0);

IF
EnteredTrigger(_PartyMember, S_TWN_Tollhouse_SecretBasementRoom_002_5a1f927d-4006-40a1-9c08-ea994dc48de9)
AND
NOT DB_OffStage(S_TWN_Tollhouse_BlackPlane_f493a970-06da-4cdc-813f-fa28d137f1ca)
THEN
PROC_SetOnStage((ITEM)S_TWN_Tollhouse_BlackPlane_f493a970-06da-4cdc-813f-fa28d137f1ca, 0);

IF
LeftTrigger(_PartyMember, (TRIGGER)S_TWN_Tollhouse_SecretBasementRoom_001_953e5587-7b8a-4250-80ad-9ec6d6a3db9f)
AND
NOT QRY_TriggerEvents_AnyPartyMemberInTrigger((TRIGGER)S_TWN_Tollhouse_SecretBasementRoom_002_5a1f927d-4006-40a1-9c08-ea994dc48de9)
AND
IsClosed((ITEM)S_TWN_Tollhouse_Bookcase_002_8e4fe044-f562-4873-93ee-a928b77c51ca, 1)
THEN
PROC_SetOnStage((ITEM)S_TWN_Tollhouse_BlackPlane_f493a970-06da-4cdc-813f-fa28d137f1ca, 1);

IF
LeftTrigger(_PartyMember, (TRIGGER)S_TWN_Tollhouse_SecretBasementRoom_002_5a1f927d-4006-40a1-9c08-ea994dc48de9)
AND
NOT QRY_TriggerEvents_AnyPartyMemberInTrigger((TRIGGER)S_TWN_Tollhouse_SecretBasementRoom_001_953e5587-7b8a-4250-80ad-9ec6d6a3db9f)
AND
IsClosed((ITEM)S_TWN_Tollhouse_Bookcase_002_8e4fe044-f562-4873-93ee-a928b77c51ca, 1)
THEN
PROC_SetOnStage((ITEM)S_TWN_Tollhouse_BlackPlane_f493a970-06da-4cdc-813f-fa28d137f1ca, 1);
//END_REGION Moving Bookcases

//REGION Blocked Door
PROC
PROC_BlockUseOfItem(_Player, S_TWN_Tollhouse_BlockedDoor_7dd7ccb9-f9af-4891-be7f-1fc531395aa3)
AND
DB_Players(_Player)
THEN
PROC_TryStartAD(TWN_Tollhouse_PAD_BlockedDoor_41cf6686-b579-4aa8-c97d-981dd219ced5, _Player);

PROC
PROC_BlockUseOfItem(_PartyMember, S_TWN_Tollhouse_BlockedDoor_7dd7ccb9-f9af-4891-be7f-1fc531395aa3)
AND
DB_PartyMembers(_PartyMember)
THEN
DB_CustomUseItemResponse(_PartyMember, (ITEM)S_TWN_Tollhouse_BlockedDoor_7dd7ccb9-f9af-4891-be7f-1fc531395aa3, 0);
//END_REGION Blocked Door

//REGION Debug
IF
TextEvent("twn_bookcases")
AND
GetHostCharacter(_Player)
THEN
SetDualEntityEvent(_Player, S_TWN_Tollhouse_ActivatorForMoveableBookcases_76380614-999f-40ae-868e-fc2ac746fead, "TWN_Tollhouse_MovingBookcasesActivatorUsed");
//END_REGION


//REGION Safes In Tollhouse GUS-260175

IF
TextEvent("tollhousesafeflag")
AND
DB_Players(_Player)
THEN
SetFlag((FLAG)TWN_Tollhouse_LearnedSkeletonSafeCombo_4d5c580a-f9a4-4848-a880-077bf8381cb7,_Player,0);

IF
GameBookInterfaceClosed((ITEM)S_TWN_Note_TollhouseSafeCombination_343df2c7-4735-4aaa-a7e4-e07ee448d564,_Player)
THEN
SetFlag((FLAG)TWN_Tollhouse_LearnedSkeletonSafeCombo_4d5c580a-f9a4-4848-a880-077bf8381cb7,_Player,0);

IF
UseFinished(_Player,(ITEM)S_TWN_Tollhouse_Safe1_f8933735-9e8a-4c3e-a0d7-5529e8a52dff,0)
AND
IsLocked(S_TWN_Tollhouse_Safe1_f8933735-9e8a-4c3e-a0d7-5529e8a52dff,1)
AND
DB_GlobalFlag((FLAG)TWN_Tollhouse_LearnedSkeletonSafeCombo_4d5c580a-f9a4-4848-a880-077bf8381cb7)
AND
QRY_OnlyOnce("TWN_Tollhouse_UnlockedSkeletonSafe")
THEN
PROC_TryStartAD(TWN_Tollhouse_AD_SaveOpenedWithCombination_149626e8-c4e0-f528-c15f-328c8096eb13,S_TWN_Tollhouse_Safe1_f8933735-9e8a-4c3e-a0d7-5529e8a52dff);
PROC_ItemUnlockAndOpen(S_TWN_Tollhouse_Safe1_f8933735-9e8a-4c3e-a0d7-5529e8a52dff);

IF
UseFinished(_Player,(ITEM)S_TWN_Tollhouse_Safe2_fd2c5750-7fec-45de-a683-5c9e6ba64619,_)
THEN
RequestActiveRoll(_Player,S_TWN_Tollhouse_Safe2_fd2c5750-7fec-45de-a683-5c9e6ba64619,"Strength",(DIFFICULTYCLASS)Act2_Challenging_c44bfd7d-84de-4568-9c57-a059b8df5435,"TWN_Tollhouse_StuckSafeDoor");

IF
RollResult("TWN_Tollhouse_StuckSafeDoor",_Player,S_TWN_Tollhouse_Safe2_fd2c5750-7fec-45de-a683-5c9e6ba64619,1,1,_)
THEN
PROC_ItemUnlockAndOpen((ITEM)S_TWN_Tollhouse_Safe2_fd2c5750-7fec-45de-a683-5c9e6ba64619);
SetCanInteract(S_TWN_Tollhouse_Safe2_fd2c5750-7fec-45de-a683-5c9e6ba64619,0);
//END_REGION

//REGION Peaceful Resolution XP
PROC
PROC_GlobalFlagReactionAfterDialog(TWN_Tollhouse_Event_GaveEnoughCoins_a387ad5b-5959-4626-b1b7-d4936eef8c72)
AND
DB_GLO_DefeatCounter(_NPC, "TWN_Tollhouse_MasterAndFaces")
THEN
PROC_PeacefulResolve((CHARACTER)_NPC);

PROC
PROC_GlobalFlagReactionAfterDialog(TWN_Tollhouse_State_PeacefulResolution_2eb42178-c700-4742-9325-8fef38ed46ca)
AND
DB_GLO_DefeatCounter(_NPC, "TWN_Tollhouse_MasterAndFaces")
THEN
PROC_PeacefulResolve((CHARACTER)_NPC);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
