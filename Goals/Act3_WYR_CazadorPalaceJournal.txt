Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION
DB_QuestDef_State(WYR_GurCamp_State_GaveQuestToKillCazador_109ed582-3803-4e0d-9e4a-7440e4fe02d0, "WYR_CazadorPalace", "GotGurQuest");
DB_QuestDef_State(WYR_VampireSpawns_State_InvitedToPalace_34a280bd-f3d1-473f-b0df-e5615dcb7705, "WYR_CazadorPalace", "MetFlophouseSpawns");
DB_QuestDef_State(CAMP_VampireAmbush_State_AmbushFailed_c7ad2cf7-59de-4310-aac1-2635d66474bf, "WYR_CazadorPalace", "MetAmbushSpawns_Failed");
DB_QuestDef_State(CAMP_VampireAmbush_State_AmbushSucceded_6cf85531-c098-475c-9dba-528c039125ad, "WYR_CazadorPalace", "MetAmbushSpawns_Succeeded");
DB_QuestDef_State(LOW_CazadorsPalace_GuardTower_State_Met_bc621a57-c901-40c7-a81a-00d2daf59653, "WYR_CazadorPalace", "MetGuards");

DB_QuestDef_State_ConditionalFlag(LOW_CazadorsPalace_State_ReadBook_c5709b91-cd0f-4a8a-9400-777234670180, "WYR_CazadorPalace", "ReadBook", 0, (FLAG)LOW_CazadorsPalace_Ballroom_MainDoor_State_Met_f271c171-d26a-4c4c-b694-5960c04ea99b);
DB_QuestDef_State_ConditionalFlag(LOW_CazadorsPalace_State_ReadBook_c5709b91-cd0f-4a8a-9400-777234670180, "WYR_CazadorPalace", "ReadBook_MetDoor", 1, (FLAG)LOW_CazadorsPalace_Ballroom_MainDoor_State_Met_f271c171-d26a-4c4c-b694-5960c04ea99b);

DB_QuestDef_State(LOW_CazadorsPalace_Ballroom_State_Visited_9816b1d4-c234-44af-8917-8b9ee9b17061, "WYR_CazadorPalace", "EnteredBallroom");
DB_QuestDef_State(LOW_CazadorsPalace_Ballroom_Event_OpenDoor_5c450f44-1c79-47d1-944a-66cf07003abc, "WYR_CazadorPalace", "EnteredBallroom");
DB_QuestDef_State(LOW_CazadorsPalace_State_EnteredDungeon_740ea5cd-7ada-4610-aadb-f52026782700, "WYR_CazadorPalace", "EnteredDungeon");
DB_QuestDef_State(LOW_CazadorsPalace_Cells_Knows_StaffAdults_af553ab6-58e3-45ad-a758-df9ab417b46f, "WYR_CazadorPalace", "FoundAdultPrisoners");
DB_QuestDef_State_ConditionalFlag(LOW_CazadorsPalace_Cells_Knows_GurChildren_0e263a7e-5d16-3753-c5ba-ae39e4cc16b6, "WYR_CazadorPalace", "FoundChildrenPrisoners_NoGur", 0, WYR_GurCamp_State_GaveQuestToKillCazador_109ed582-3803-4e0d-9e4a-7440e4fe02d0);
DB_QuestDef_State_ConditionalFlag(LOW_CazadorsPalace_Cells_Knows_GurChildren_0e263a7e-5d16-3753-c5ba-ae39e4cc16b6, "WYR_CazadorPalace", "FoundChildrenPrisoners_GurQuest", 1, WYR_GurCamp_State_GaveQuestToKillCazador_109ed582-3803-4e0d-9e4a-7440e4fe02d0);
DB_QuestDef_State(LOW_CazadorsPalace_RitualRoom_State_FoundCazador_7021887c-c303-49dc-bb29-71bf88d7157d, "WYR_CazadorPalace", "FoundCazador");
DB_QuestDef_State((FLAG)LOW_CazadorsPalace_RitualRoom_State_SparedCazador_c18a3ec7-cdd4-4ce5-8421-f2b32ae7ebf6, "WYR_CazadorPalace", "SparedCazador");

DB_QuestDef_State_ConditionalFlag(LOW_CazadorsPalace_RitualRoom_State_ReleasedPrisoners_5784cf70-bff9-467c-b150-39d1f5a78240, "WYR_CazadorPalace", "ReleasedPrisoners_NoGur", 0, WYR_GurCamp_State_GaveQuestToKillCazador_109ed582-3803-4e0d-9e4a-7440e4fe02d0);
DB_QuestDef_State_ConditionalFlag(LOW_CazadorsPalace_RitualRoom_State_ReleasedPrisoners_5784cf70-bff9-467c-b150-39d1f5a78240, "WYR_CazadorPalace", "ReleasedPrisoners_GurQuest", 1, WYR_GurCamp_State_GaveQuestToKillCazador_109ed582-3803-4e0d-9e4a-7440e4fe02d0);

DB_QuestDef_State_ConditionalFlag(LOW_CazadorsPalace_RitualRoom_State_LeftPrisonersRot_52cc991c-1d18-4d84-ab28-61e21ac958b5, "WYR_CazadorPalace", "LeftPrisoners_NoGur", 0, WYR_GurCamp_State_GaveQuestToKillCazador_109ed582-3803-4e0d-9e4a-7440e4fe02d0);
DB_QuestDef_State_ConditionalFlag(LOW_CazadorsPalace_RitualRoom_State_LeftPrisonersRot_52cc991c-1d18-4d84-ab28-61e21ac958b5, "WYR_CazadorPalace", "LeftPrisoners_GurQuest", 1, WYR_GurCamp_State_GaveQuestToKillCazador_109ed582-3803-4e0d-9e4a-7440e4fe02d0);

DB_QuestDef_State_ConditionalFlag(LOW_CazadorsPalace_RitualRoom_State_KilledPrisoners_a138792f-04ae-410b-afce-1bf661e9c868, "WYR_CazadorPalace", "KilledPrisoners_NoGur", 0, WYR_GurCamp_State_GaveQuestToKillCazador_109ed582-3803-4e0d-9e4a-7440e4fe02d0);
DB_QuestDef_State_ConditionalFlag(LOW_CazadorsPalace_RitualRoom_State_KilledPrisoners_a138792f-04ae-410b-afce-1bf661e9c868, "WYR_CazadorPalace", "KilledPrisoners_GurQuest", 1, WYR_GurCamp_State_GaveQuestToKillCazador_109ed582-3803-4e0d-9e4a-7440e4fe02d0);

DB_QuestDef_State_ConditionalFlag((FLAG)ORI_Astarion_State_BecameVampireLord_c446ce94-efd8-45d5-b407-284177b6b57e, "WYR_CazadorPalace", "Ascended_NoGur", 0, WYR_GurCamp_State_GaveQuestToKillCazador_109ed582-3803-4e0d-9e4a-7440e4fe02d0);
// Gur Specific
DB_QuestDef_State_ConditionalFlag(LOW_GurFollowUp_State_BetrayedAstarion_d1683786-f95c-4113-8465-7468f6ef75cf, "WYR_CazadorPalace", "BetrayedAstarion_GurQuest", 1, WYR_GurCamp_State_GaveQuestToKillCazador_109ed582-3803-4e0d-9e4a-7440e4fe02d0);
DB_QuestDef_State_ConditionalFlag(LOW_GurFollowUp_State_KilledAstarion_94ca72ad-9b37-4ce5-baa9-959fcfad14d0, "WYR_CazadorPalace", "KilledAstarion_GurQuest", 1, WYR_GurCamp_State_GaveQuestToKillCazador_109ed582-3803-4e0d-9e4a-7440e4fe02d0);
DB_QuestDef_State_ConditionalFlag(LOW_GurFollowUp_State_PeacefulResolution_260836ee-6f67-4ea4-9e2c-ccae94a75035, "WYR_CazadorPalace", "GurPeaceful_GurQuest", 1, WYR_GurCamp_State_GaveQuestToKillCazador_109ed582-3803-4e0d-9e4a-7440e4fe02d0);

// LeftToEND_CazadorSpared - in scripting
// LeftToEND - in scripting
//END_REGION
KBSECTION
//REGION Found Cazador
IF
DialogStarted((DIALOGRESOURCE)LOW_CazadorsPalace_RitualRoom_PreCombat_NoAstarion_061da20f-7611-e738-999b-ab05089626d6, _)
AND
NOT DB_GlobalFlag(LOW_CazadorsPalace_RitualRoom_State_FoundCazador_7021887c-c303-49dc-bb29-71bf88d7157d)
THEN
PROC_GlobalSetFlagAndCache(LOW_CazadorsPalace_RitualRoom_State_FoundCazador_7021887c-c303-49dc-bb29-71bf88d7157d);

IF
DialogStarted((DIALOGRESOURCE)LOW_CazadorsPalace_RitualRoom_PreCombat_OM_Astarion_AOM_COM_OOM_6f574f0c-848b-522f-b104-1e1488b2e212, _)
AND
NOT DB_GlobalFlag(LOW_CazadorsPalace_RitualRoom_State_FoundCazador_7021887c-c303-49dc-bb29-71bf88d7157d)
THEN
PROC_GlobalSetFlagAndCache(LOW_CazadorsPalace_RitualRoom_State_FoundCazador_7021887c-c303-49dc-bb29-71bf88d7157d);

IF
AttackedBy(S_GLO_Cazador_2f1880e6-1297-4ca3-a79c-9fabc7f179d3, (CHARACTER)_Player, _, _, _, _, _)
AND
DB_Players(_Player)
AND
NOT DB_GlobalFlag(LOW_CazadorsPalace_RitualRoom_State_FoundCazador_7021887c-c303-49dc-bb29-71bf88d7157d)
THEN
PROC_GlobalSetFlagAndCache(LOW_CazadorsPalace_RitualRoom_State_FoundCazador_7021887c-c303-49dc-bb29-71bf88d7157d);
//END_REGION

//REGION Left to end
IF
FlagSet((FLAG)END_General_State_Started_a0fd5f91-e4b3-4d01-84d3-9ff484139e99, _, _)
AND
NOT DB_GlobalFlag((FLAG)ORI_Astarion_State_CazadorPermaDefeated_5258c8d8-09a3-4343-bb6e-13ff5b8e3438)
AND
DB_GlobalFlag((FLAG)LOW_CazadorsPalace_RitualRoom_State_SparedCazador_c18a3ec7-cdd4-4ce5-8421-f2b32ae7ebf6)
AND
DB_Players(_Player)
THEN
QuestUpdate(_Player, "WYR_CazadorPalace", "LeftToEND_CazadorSpared");

IF
FlagSet((FLAG)END_General_State_Started_a0fd5f91-e4b3-4d01-84d3-9ff484139e99, _, _)
AND
NOT DB_GlobalFlag((FLAG)ORI_Astarion_State_CazadorPermaDefeated_5258c8d8-09a3-4343-bb6e-13ff5b8e3438)
AND
NOT DB_GlobalFlag((FLAG)LOW_CazadorsPalace_RitualRoom_State_SparedCazador_c18a3ec7-cdd4-4ce5-8421-f2b32ae7ebf6)
AND
DB_Players(_Player)
THEN
QuestUpdate(_Player, "WYR_CazadorPalace", "LeftToEND");
//END_REGION

//REGION Debug
IF
TextEvent("cj_close_nogur")
AND
DB_Players(_Player)
THEN
QuestUpdate(_Player, "WYR_CazadorPalace", "MetFlophouseSpawns");
QuestUpdate(_Player, "WYR_CazadorPalace", "MetAmbushSpawns_Failed");
QuestUpdate(_Player, "WYR_CazadorPalace", "MetGuards");
QuestUpdate(_Player, "WYR_CazadorPalace", "EnteredPalace");
QuestUpdate(_Player, "WYR_CazadorPalace", "FoundDoor");
QuestUpdate(_Player, "WYR_CazadorPalace", "FoundRing");
QuestUpdate(_Player, "WYR_CazadorPalace", "ReadBook");
QuestUpdate(_Player, "WYR_CazadorPalace", "EnteredBallroom");
QuestUpdate(_Player, "WYR_CazadorPalace", "EnteredDungeon");
QuestUpdate(_Player, "WYR_CazadorPalace", "EnteredPalace");
QuestUpdate(_Player, "WYR_CazadorPalace", "FoundAdultPrisoners");
QuestUpdate(_Player, "WYR_CazadorPalace", "FoundChildrenPrisoners_NoGur");
QuestUpdate(_Player, "WYR_CazadorPalace", "FoundCazador");
QuestUpdate(_Player, "WYR_CazadorPalace", "ReleasedPrisoners_NoGur");

IF
TextEvent("cj_close_gurquest_peace")
AND
DB_Players(_Player)
THEN
PROC_WYR_CazadorPalaceJournal_GurSteps();
QuestUpdate(_Player, "WYR_CazadorPalace", "GurPeaceful_GurQuest");

IF
TextEvent("cj_close_gurquest_hostile")
AND
DB_Players(_Player)
THEN
PROC_WYR_CazadorPalaceJournal_GurSteps();
QuestUpdate(_Player, "WYR_CazadorPalace", "GurHostile_GurQuest");

IF
TextEvent("cj_close_gurquest_betray")
AND
DB_Players(_Player)
THEN
PROC_WYR_CazadorPalaceJournal_GurSteps();
QuestUpdate(_Player, "WYR_CazadorPalace", "BetrayedAstarion_GurQuest");
QuestUpdate(_Player, "WYR_CazadorPalace", "KilledAstarion_GurQuest");

PROC
PROC_WYR_CazadorPalaceJournal_GurSteps()
AND
DB_Players(_Player)
THEN
QuestUpdate(_Player, "WYR_CazadorPalace", "GotGurQuest");
QuestUpdate(_Player, "WYR_CazadorPalace", "MetAmbushSpawns_Failed");
QuestUpdate(_Player, "WYR_CazadorPalace", "MetGuards");
QuestUpdate(_Player, "WYR_CazadorPalace", "EnteredPalace");
QuestUpdate(_Player, "WYR_CazadorPalace", "FoundDoor");
QuestUpdate(_Player, "WYR_CazadorPalace", "FoundRing");
QuestUpdate(_Player, "WYR_CazadorPalace", "ReadBook");
QuestUpdate(_Player, "WYR_CazadorPalace", "EnteredBallroom");
QuestUpdate(_Player, "WYR_CazadorPalace", "EnteredDungeon");
QuestUpdate(_Player, "WYR_CazadorPalace", "EnteredPalace");
QuestUpdate(_Player, "WYR_CazadorPalace", "FoundAdultPrisoners");
QuestUpdate(_Player, "WYR_CazadorPalace", "FoundChildrenPrisoners_GurQuest");
QuestUpdate(_Player, "WYR_CazadorPalace", "FoundCazador");
QuestUpdate(_Player, "WYR_CazadorPalace", "ReleasedPrisoners_GurQuest");
//END_REGION

//REGION Entering Palace
IF
FlagSet((FLAG)LOW_CazadorsPalace_State_EnteredMansion_97df5c27-5740-4277-96e1-5fdd4c5e6699, _, _)
AND
DB_QuestIsAccepted("WYR_CazadorPalace")
THEN
QuestUpdate("WYR_CazadorPalace", "EnteredPalace");

IF
FlagSet((FLAG)LOW_CazadorsPalace_State_EnteredMansion_97df5c27-5740-4277-96e1-5fdd4c5e6699, _, _)
AND
NOT DB_QuestIsAccepted("WYR_CazadorPalace")
AND
DB_PartOfTheTeam((CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255)
THEN
QuestUpdate("WYR_CazadorPalace", "EnteredPalace");

IF
FlagSet((FLAG)LOW_CazadorsPalace_State_EnteredMansion_97df5c27-5740-4277-96e1-5fdd4c5e6699, _, _)
AND
NOT DB_QuestIsAccepted("WYR_CazadorPalace")
AND
NOT DB_PartOfTheTeam((CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255)
THEN
QuestUpdate("WYR_CazadorPalace", "EnteredPalace_NoClue");
//END_REGION

//REGION Ballroom Door related updates
// Global flag to track that we met the door
IF
FlagSet((FLAG)LOW_CazadorsPalace_Ballroom_MainDoor_State_TriedDoor_03974c07-2722-67c7-c102-df8a31f9df08, _, _)
THEN
SetFlag((FLAG)LOW_CazadorsPalace_Ballroom_MainDoor_State_Met_f271c171-d26a-4c4c-b694-5960c04ea99b);

// we can learn from three sources - by interacting with the door, from a servant and from Godey
// the are mutually exclusive

// door
IF
FlagSet((FLAG)LOW_CazadorsPalace_Ballroom_MainDoor_State_Met_f271c171-d26a-4c4c-b694-5960c04ea99b, _, _)
AND 
NOT DB_GlobalFlag((FLAG)LOW_CazadorsPalace_Knows_GodeyHasKey_82a42ad2-0812-1799-9438-6096d13e73e8)
AND
NOT DB_GlobalFlag((FLAG)LOW_CazadorsPalace_Kennel_State_MentionedSeal_27af1bba-5add-4be3-9e89-6b57cfd4e819)
THEN
QuestUpdate("WYR_CazadorPalace", "LearnedDoor_Door");

// servant
IF
FlagSet(LOW_CazadorsPalace_Knows_GodeyHasKey_82a42ad2-0812-1799-9438-6096d13e73e8, _, _)
AND 
NOT DB_GlobalFlag((FLAG)LOW_CazadorsPalace_Ballroom_MainDoor_State_Met_f271c171-d26a-4c4c-b694-5960c04ea99b)
AND
NOT DB_GlobalFlag((FLAG)LOW_CazadorsPalace_Kennel_State_MentionedSeal_27af1bba-5add-4be3-9e89-6b57cfd4e819)
THEN
QuestUpdate("WYR_CazadorPalace", "LearnedDoor_Servant");

// Godey
IF
FlagSet(LOW_CazadorsPalace_Kennel_State_MentionedSeal_27af1bba-5add-4be3-9e89-6b57cfd4e819, _, _)
AND 
NOT DB_GlobalFlag((FLAG)LOW_CazadorsPalace_Ballroom_MainDoor_State_Met_f271c171-d26a-4c4c-b694-5960c04ea99b)
AND 
NOT DB_GlobalFlag((FLAG)LOW_CazadorsPalace_Knows_GodeyHasKey_82a42ad2-0812-1799-9438-6096d13e73e8)
THEN
QuestUpdate("WYR_CazadorPalace", "LearnedDoor_Godey");

// Learned about Godey from the servant
// unlock only if LearnedDoor_Door is unlocked
IF
FlagSet((FLAG)LOW_CazadorsPalace_Knows_GodeyHasKey_82a42ad2-0812-1799-9438-6096d13e73e8, _, _)
AND
QRY_QuestUpdateIsUnlockedForAny("WYR_CazadorPalace", "LearnedDoor_Door")
THEN
QuestUpdate("WYR_CazadorPalace", "LearnedGodey");

// Ring - 4 possible updates, all mutually exclusive
// - Godey gaves it to us, so we know it's purpose
// - servant told us its purpose then we loot/steal it from Godey
// - we tried the ballroom before, then we stole / loot the ring from Godey
// - we stole/loot it from Godey without knowing its purpose

// Godey
IF
FlagSet((FLAG)LOW_CazadorsPalace_Kennel_Event_GiveSzarrRing_0b897260-2193-4ec1-b1b0-397cb11c315c, _, _)
THEN
PROC_GlobalSetFlagAndCache(LOW_CazadorsPalace_Kennel_State_GotRingFromGodey_a0d7ea46-e9ff-4c7e-8ff5-609f209a699a);
QuestUpdate("WYR_CazadorPalace", "FoundRing_Godey");

// other cases
IF
FlagSet((FLAG)LOW_CazadorsPalace_Kennel_State_HasSzarrRing_98b11c1c-e774-4a1e-9704-7e72f7f4a8d8, (CHARACTER)_Player, _)
AND
DB_Players(_Player)
AND
NOT DB_GlobalFlag(LOW_CazadorsPalace_Kennel_State_GotRingFromGodey_a0d7ea46-e9ff-4c7e-8ff5-609f209a699a)
THEN
PROC_LOW_CazadorsPalace_Kennel_GotRingIllegally();

// Interact with Door -> Loot/Steal
PROC
PROC_LOW_CazadorsPalace_Kennel_GotRingIllegally()
AND
QRY_QuestUpdateIsUnlockedForAny("WYR_CazadorPalace", "LearnedDoor_Door")
THEN
QuestUpdate("WYR_CazadorPalace", "FoundRing_MetDoor");

// Learn Purpose -> Loot/Steal
PROC
PROC_LOW_CazadorsPalace_Kennel_GotRingIllegally()
AND
NOT QRY_QuestUpdateIsUnlockedForAny("WYR_CazadorPalace", "LearnedDoor_Door")
AND
DB_GlobalFlag((FLAG)LOW_CazadorsPalace_Ballroom_MainDoor_Knows_RingPurpose_ff23fcb7-b334-49f4-94e9-8d3bf434d6a0)
THEN
QuestUpdate("WYR_CazadorPalace", "FoundRing_KnowsPurpose");

// Loot / Steal
PROC
PROC_LOW_CazadorsPalace_Kennel_GotRingIllegally()
AND
NOT QRY_QuestUpdateIsUnlockedForAny("WYR_CazadorPalace", "LearnedDoor_Door")
AND
NOT DB_GlobalFlag((FLAG)LOW_CazadorsPalace_Ballroom_MainDoor_Knows_RingPurpose_ff23fcb7-b334-49f4-94e9-8d3bf434d6a0)
THEN
QuestUpdate("WYR_CazadorPalace", "FoundRing");

IF
FlagSet((FLAG)LOW_CazadorsPalace_Ballroom_MainDoor_State_TriedRingOnly_cee0610e-0e37-46ea-8e3d-5877df277486, (CHARACTER)_Player, _)
AND
NOT QRY_ItemInMagicPockets(_Player, (ITEM)S_LOW_CazadorsPalace_Bedrooms_AncientLanguageBook_7d0f3b3d-afcf-49a7-af68-b51fbb83a01f)
THEN
QuestUpdate(_Player, "WYR_CazadorPalace", "TriedRingOnly");

IF
FlagSet((FLAG)LOW_CazadorsPalace_Ballroom_MainDoor_State_TriedRingOnly_cee0610e-0e37-46ea-8e3d-5877df277486, (CHARACTER)_Player, _)
AND
QRY_ItemInMagicPockets(_Player, (ITEM)S_LOW_CazadorsPalace_Bedrooms_AncientLanguageBook_7d0f3b3d-afcf-49a7-af68-b51fbb83a01f)
THEN
QuestUpdate(_Player, "WYR_CazadorPalace", "TriedRingOnly_HasBook");
//END_REGION

//REGION Ascendant Path - defeating Ulma and Cazador in any order
IF
DB_GlobalFlag(ORI_Astarion_State_BecameVampireLord_c446ce94-efd8-45d5-b407-284177b6b57e)
AND
DB_GlobalFlag(WYR_GurCamp_State_GaveQuestToKillCazador_109ed582-3803-4e0d-9e4a-7440e4fe02d0)
AND
NOT DB_PermaDefeated(S_WYR_GurCamp_GurLeader_5e52cf9c-b673-45b7-888c-2c105eb4d890)
THEN
QuestUpdate("WYR_CazadorPalace", "Ascended_GurQuest");

// closing the quest
IF
DB_GlobalFlag(ORI_Astarion_State_BecameVampireLord_c446ce94-efd8-45d5-b407-284177b6b57e)
AND
DB_GlobalFlag(WYR_GurCamp_State_GaveQuestToKillCazador_109ed582-3803-4e0d-9e4a-7440e4fe02d0)
AND
DB_PermaDefeated(S_WYR_GurCamp_GurLeader_5e52cf9c-b673-45b7-888c-2c105eb4d890)
THEN
QuestUpdate("WYR_CazadorPalace", "GurHostile_GurQuest");

// no Astarion, no Gur quest -> Peaceful Gur resolution
IF
FlagSet((FLAG)LOW_GurFollowUp_State_PeacefulResolution_260836ee-6f67-4ea4-9e2c-ccae94a75035, _, _)
AND
QRY_WYR_CazadorPalaceJournal_NoGurCazadorKilled()
THEN
QuestUpdate("WYR_CazadorPalace", "GurPeaceful_GurQuest");

// no Astarion, no Gur quest -> Hostile Gur resolution
IF
FlagSet((FLAG)GLO_GurCamp_State_GurLeaderPermaDefeated_2dd63ae4-02c6-4360-8bdd-4d8d938a56e0, _, _)
AND
QRY_WYR_CazadorPalaceJournal_NoGurCazadorKilled()
THEN
QuestUpdate("WYR_CazadorPalace", "GurHostile_GurQuest");

QRY
QRY_WYR_CazadorPalaceJournal_NoGurCazadorKilled()
AND
NOT DB_GlobalFlag((FLAG)WYR_GurCamp_State_GaveQuestToKillCazador_109ed582-3803-4e0d-9e4a-7440e4fe02d0)
AND
DB_GlobalFlag((FLAG)LOW_CazadorsPalace_RitualRoom_State_CazadorAscended_68cf8d54-b6f2-468b-925d-5f4610e93c04)
AND
DB_GlobalFlag((FLAG)ORI_Astarion_State_CazadorPermaDefeated_5258c8d8-09a3-4343-bb6e-13ff5b8e3438)
THEN
DB_NOOP(1);

// closing the quest with hostile resolution in case both Ulma and Cazador are dead and the quest is still not closed
IF
DB_GlobalFlag((FLAG)WYR_GurCamp_State_GaveQuestToKillCazador_109ed582-3803-4e0d-9e4a-7440e4fe02d0)
AND
DB_GlobalFlag((FLAG)ORI_Astarion_State_CazadorPermaDefeated_5258c8d8-09a3-4343-bb6e-13ff5b8e3438)
AND
DB_GlobalFlag((FLAG)GLO_GurCamp_State_GurLeaderPermaDefeated_2dd63ae4-02c6-4360-8bdd-4d8d938a56e0)
AND
DB_QuestIsOpened("WYR_CazadorPalace")
THEN
QuestUpdate("WYR_CazadorPalace", "GurHostile_GurQuest");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act3"
