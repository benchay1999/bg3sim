Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(S_WYR_VampireSpawns_Dalyria_204a74d2-e6ad-4e9b-a275-f03cb5f3d975, (DIALOGRESOURCE)WYR_VampireSpawns_Dalyria_6b2df18f-a498-146e-13c3-c2a5d3c02287);
DB_Dialogs(S_WYR_VampireSpawns_Petra_6fb6e85e-ccd0-42ef-a37d-1e256a433f3b, (DIALOGRESOURCE)WYR_VampireSpawns_Petra_4270d451-e31a-9da8-e0bb-d9ee8aa5a5e4);

DB_SceneManager((CHARACTER)S_WYR_VampireSpawns_Dalyria_204a74d2-e6ad-4e9b-a275-f03cb5f3d975, "WYR_Flophouse_Spawns");
DB_SceneManager((CHARACTER)S_WYR_VampireSpawns_Petra_6fb6e85e-ccd0-42ef-a37d-1e256a433f3b, "WYR_Flophouse_Spawns");
DB_SceneTriggerManager("WYR_Flophouse_Spawns", (TRIGGER)S_WYR_Flophouse_FirstFloor_b4100439-799e-49a8-acc0-1934465f4845);
DB_SceneNoHostileContinuousDisturbance("WYR_Flophouse_Spawns");

PROC_DefineSingleOriginMoment((DIALOGRESOURCE)WYR_VampireSpawns_NoAstarion_88b7cbfa-265d-c4fb-485c-f41c78d948d8, 
	(TAG)ASTARION_23a46e79-e73c-4043-940f-cb0aace9ab2e,
	(DIALOGRESOURCE)WYR_VampireSpawns_OM_Astarion_AOM_OOM_bdd29b6e-7dca-2aab-8558-10260f1f5f88,
	(DIALOGRESOURCE)WYR_VampireSpawns_OM_Astarion_COM_edfd3209-d298-7854-c369-3a5c153a642f,
	(DIALOGRESOURCE)WYR_VampireSpawns_OM_Astarion_AOM_OOM_bdd29b6e-7dca-2aab-8558-10260f1f5f88);
DB_OriginMoment_ExtraNPCs((DIALOGRESOURCE)WYR_VampireSpawns_OM_Astarion_AOM_OOM_bdd29b6e-7dca-2aab-8558-10260f1f5f88, S_WYR_VampireSpawns_Petra_6fb6e85e-ccd0-42ef-a37d-1e256a433f3b);
DB_OriginMoment_ExtraNPCs((DIALOGRESOURCE)WYR_VampireSpawns_OM_Astarion_COM_edfd3209-d298-7854-c369-3a5c153a642f, S_WYR_VampireSpawns_Petra_6fb6e85e-ccd0-42ef-a37d-1e256a433f3b);

DB_WYR_VampireSpawns_Spawns((CHARACTER)S_WYR_VampireSpawns_Dalyria_204a74d2-e6ad-4e9b-a275-f03cb5f3d975);
DB_WYR_VampireSpawns_Spawns((CHARACTER)S_WYR_VampireSpawns_Petra_6fb6e85e-ccd0-42ef-a37d-1e256a433f3b);

DB_WYR_VampireSpawns_OriginMomentDialogs((DIALOGRESOURCE)WYR_VampireSpawns_OM_Astarion_AOM_OOM_bdd29b6e-7dca-2aab-8558-10260f1f5f88);
DB_WYR_VampireSpawns_OriginMomentDialogs((DIALOGRESOURCE)WYR_VampireSpawns_OM_Astarion_COM_edfd3209-d298-7854-c369-3a5c153a642f);
KBSECTION
// Spawns can't be defeated till the ritual in Cazador's Palace takes place
IF
DB_WYR_VampireSpawns_Spawns(_Spawn)
THEN
SetImmortal(_Spawn, 1);

// they only care about you attacking them or trying to steal from them
PROC
PROC_SceneInterrupted(_, _Player, "WYR_Flophouse_Spawns", _Reason)
AND
DB_GLO_GenericSceneManager_ViolenceReason(_Reason)
THEN
PROC_WYR_VampireSpawns_SendToCazador();

PROC
PROC_SceneInterrupted(_, _Char, "WYR_Flophouse_Spawns", "DisturbanceRegistering")
AND
DB_SceneManager_DisturbanceRegistering(_Char, _Reason, _, _, _, _)
AND
QRY_CRIME_IsCrimeFamilyMember(_Reason, "Steal")
THEN
PROC_WYR_VampireSpawns_SendToCazador();

// if the player somehow makes them leave the first floor - send them to Cazador
// we don't want the vampire spawns to stand under the sun like it's nothing
PROC
PROC_SceneInterrupted(_, _Player, "WYR_Flophouse_Spawns", "LeftTrigger")
THEN
PROC_WYR_VampireSpawns_SendToCazador();

QRY
QRY_SelectCustomDialog_AfterGenerics((CHARACTER)_Spawn, _Player)
AND
DB_WYR_VampireSpawns_Spawns(_Spawn)
AND
NOT DB_GlobalFlag(WYR_VampireSpawns_State_MovedToCazador_a03cf538-552c-a672-db83-10e9ceaf99e2)
THEN
DB_SelectedDialog((DIALOGRESOURCE)WYR_VampireSpawns_NoAstarion_88b7cbfa-265d-c4fb-485c-f41c78d948d8, S_WYR_VampireSpawns_Dalyria_204a74d2-e6ad-4e9b-a275-f03cb5f3d975, S_WYR_VampireSpawns_Petra_6fb6e85e-ccd0-42ef-a37d-1e256a433f3b, _Player);

IF
DialogStarted(_Dialog, _)
AND
DB_WYR_VampireSpawns_OriginMomentDialogs(_Dialog)
THEN
SetFlag((FLAG)WYR_VampireSpawns_Knows_AstarionInCity_d8b89f75-b06f-bc60-684c-6010a061b6c8);

IF
FlagSet((FLAG)WYR_VampireSpawns_State_ToldAscension_a48047ea-da16-4451-838d-d129ed8ecbae, _, _)
THEN
SetFlag((FLAG)WYR_VampireSpawns_Event_TurnedToPlayer_8374d4e4-9436-4a6a-b8fb-306fe3ef35c0);

IF
FlagSet(WYR_VampireSpawns_Event_TurnedToPlayer_8374d4e4-9436-4a6a-b8fb-306fe3ef35c0, _, _)
THEN
ClearFlag(WYR_VampireSpawns_Event_TurnedToPlayer_8374d4e4-9436-4a6a-b8fb-306fe3ef35c0);
LookFromTrigger((CHARACTER)S_WYR_VampireSpawns_Dalyria_204a74d2-e6ad-4e9b-a275-f03cb5f3d975, S_WYR_VampireSpawns_LookFromTrigger_3f8ac17e-d9e7-41a1-8081-9d8d54731c2b, 0);
LookFromTrigger((CHARACTER)S_WYR_VampireSpawns_Petra_6fb6e85e-ccd0-42ef-a37d-1e256a433f3b, S_WYR_VampireSpawns_LookFromTrigger_3f8ac17e-d9e7-41a1-8081-9d8d54731c2b, 0);
NOT DB_DoNotFace(S_WYR_VampireSpawns_Dalyria_204a74d2-e6ad-4e9b-a275-f03cb5f3d975);
NOT DB_DoNotFace(S_WYR_VampireSpawns_Petra_6fb6e85e-ccd0-42ef-a37d-1e256a433f3b);

IF
FlagSet((FLAG)WYR_VampireSpawns_Event_RemoveSpawnsFromDialog_59f7c9cb-e064-428d-83a1-eaed1d338d5b, _, _ID)
AND
DB_WYR_VampireSpawns_Spawns(_Spawn)
AND
DialogRemoveActorFromDialog(_ID, _Spawn, _)
THEN
DB_NOOP(1);

IF
FlagSet((FLAG)WYR_VampireSpawns_Event_RemoveSpawnsFromDialog_59f7c9cb-e064-428d-83a1-eaed1d338d5b, _, _ID)
AND
DB_DialogPlayers(_ID, _Player, 1)
THEN
DB_WYR_VampireSpawns_QueuedVB((INTEGER)_ID, (CHARACTER)_Player); // we comment on the spawns' ability to turn into mist after the dialog
PROC_WYR_VampireSpawns_SendToCazador();

IF
DialogEnded(_Dialog, _ID)
AND
DB_WYR_VampireSpawns_QueuedVB(_ID, _Player)
THEN
TimerLaunch("WYR_VampireSpawns_QueuedVB", 500); // starting the VB immediately fails

IF
TimerFinished("WYR_VampireSpawns_QueuedVB")
AND
DB_WYR_VampireSpawns_QueuedVB(_ID, _Player)
THEN
NOT DB_WYR_VampireSpawns_QueuedVB(_ID, _Player);
PROC_Act3_VampireSpawns_CommentOnSpawnEscape(_Player);

// in case one of the OM dialogs gets interrupted before the flag is set we remove them anyway
IF
DialogEnded(_Dialog, _)
AND
DB_WYR_VampireSpawns_OriginMomentDialogs(_Dialog)
THEN
PROC_WYR_VampireSpawns_SendToCazador();

PROC
PROC_LongRest()
AND
DB_GlobalFlag(WYR_VampireSpawns_State_InvitedToPalace_34a280bd-f3d1-473f-b0df-e5615dcb7705)
AND
QRY_OnlyOnce("WYR_VampireSpawns_State_InvitedToPalaceAndWentHome")
THEN
PROC_WYR_VampireSpawns_SendToCazador();

// returning to Wyrm's Crossing
IF
FlagSet((FLAG)CURRENTREGION_BGO_Main_A_11239767-48ad-4879-8312-b9164b6e4978, _, _)
AND
DB_GlobalFlag(WYR_VampireSpawns_State_InvitedToPalace_34a280bd-f3d1-473f-b0df-e5615dcb7705)
AND
QRY_OnlyOnce("WYR_VampireSpawns_State_InvitedToPalaceAndWentHome")
THEN
PROC_WYR_VampireSpawns_SendToCazador();

QRY
QRY_OriginMoment_PreventRelaunchDialog(_Dialog, _, _)
AND
DB_WYR_VampireSpawns_OriginMomentDialogs(_Dialog)
THEN
DB_NOOP(1);

PROC
PROC_WYR_VampireSpawns_SendToCazador()
AND
QRY_OnlyOnce("WYR_VampireSpawns_SendToCazador")
THEN
PROC_WYR_VampireSpawns_SendToCazador_Intern();

PROC
PROC_WYR_VampireSpawns_SendToCazador_Intern()
THEN
PROC_SceneOver("WYR_Flophouse_Spawns");

PROC
PROC_WYR_VampireSpawns_SendToCazador_Intern()
AND
DB_WYR_VampireSpawns_Spawns(_Spawn)
THEN
PROC_Act3_VampireSpawns_SendToPalace(_Spawn);

PROC
PROC_WYR_VampireSpawns_SendToCazador_Intern()
AND
NOT DB_WYR_VampireSpawns_QueuedVB(_, _) // if they disappeared during a dialog, then we don't comment here, only after the dialog is over
AND
DB_WYR_VampireSpawns_Spawns(_Spawn)
AND
NOT DB_OnlyOnce("Act3_VampireSpawns_CommentOnSpawnEscape")
AND
GetClosestAlivePlayer(_Spawn, _Player, _)
THEN
PROC_Act3_VampireSpawns_CommentOnSpawnEscape((CHARACTER)_Player); // sets DB_OnlyOnce

PROC
PROC_WYR_VampireSpawns_SendToCazador_Intern()
THEN
PROC_GlobalSetFlagAndCache(WYR_VampireSpawns_State_MovedToCazador_a03cf538-552c-a672-db83-10e9ceaf99e2);

//REGION Topical Greeting 80
IF
FlagSet((FLAG)WYR_VampireSpawns_State_ToldAscension_a48047ea-da16-4451-838d-d129ed8ecbae, _, _)
AND
NOT DB_GlobalFlag((FLAG)ORI_Astarion_Knows_RitualsPurpose_d1dc0f8a-b77d-4e40-a501-a66e163b8376)
THEN
PROC_TopicalGreeting_UnlockTopic((FLAG)TG_ORI_Astarion_SpawnToldRitual_fdee3e48-b521-49f3-9cc6-29a9442164e5);
//END_REGION

IF
FlagSet((FLAG)WYR_VampireSpawns_State_MovedToCazador_a03cf538-552c-a672-db83-10e9ceaf99e2, _, _)
THEN
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act3"
