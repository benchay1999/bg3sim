Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION Vampire Spawns Night Ambush
DB_LOW_VampireAmbush_Dialogs((FLAG)NIGHT_Astarion_VampireSpawnsAttack_Avatar_fc832e29-5e5f-41bd-8019-90abe16df07c, (DIALOGRESOURCE)CAMP_VampireAmbush_SCO_Avatar_3781f961-3081-18b3-d0d3-fd2b1f832f02, (FLAG)ASTARIONAVATAR_250978c0-7289-4746-aa8b-1dffd323832d);
DB_LOW_VampireAmbush_Dialogs((FLAG)NIGHT_Astarion_VampireSpawnsAttack_Companion_dae7e2c4-50b1-407b-a957-55cc5df9d38a, (DIALOGRESOURCE)CAMP_VampireAmbush_SCO_Companion_34895cc1-7299-44fa-6fef-ace461369aba, (FLAG)ASTARIONCOMPANION_9fa6b609-3ba0-43ed-a95b-82304f7b8dac);

// spawn, camp, position
DB_LOW_VampireAmbush_Spawns((CHARACTER)S_LOW_CazadorsPalace_RitualRoom_Spawn_001_062ff971-d709-4f54-bb03-51c02844eacb, "SLUMS", (TRIGGER)S_CAMP_VampireAmbush_Slums_Spawn_001_Pos_cbc6c7a4-6337-4c12-8182-19d8b31c84f1);
DB_LOW_VampireAmbush_Spawns((CHARACTER)S_LOW_CazadorsPalace_RitualRoom_Spawn_002_a612de01-a750-491a-bacd-e09a67fc0de3, "SLUMS", (TRIGGER)S_CAMP_VampireAmbush_Slums_Spawn_002_Pos_2d8df041-8295-4b4f-97d3-1d2552a0a0fe);
DB_LOW_VampireAmbush_Spawns((CHARACTER)S_LOW_CazadorsPalace_RitualRoom_Spawn_003_52ede1c9-6caf-4f7a-8d97-984c0e90e9cf, "SLUMS", (TRIGGER)S_CAMP_VampireAmbush_Slums_Spawn_003_Pos_b725bc62-1ffc-4640-9702-0bda66b6f13a);
DB_LOW_VampireAmbush_Spawns((CHARACTER)S_LOW_CazadorsPalace_RitualRoom_Spawn_004_a4dc8e21-833c-4469-a72b-1969c688b8a4, "SLUMS", (TRIGGER)S_CAMP_VampireAmbush_Slums_Spawn_004_Pos_7f80ccab-1be6-45f5-86de-00df41afe5db);

DB_LOW_VampireAmbush_Spawns((CHARACTER)S_LOW_CazadorsPalace_RitualRoom_Spawn_001_062ff971-d709-4f54-bb03-51c02844eacb, "ELFSONG", (TRIGGER)S_CAMP_VampireAmbush_Elfsong_Spawn_001_Pos_82813c1a-500d-4b5a-80d8-6164c2ee56c0);
DB_LOW_VampireAmbush_Spawns((CHARACTER)S_LOW_CazadorsPalace_RitualRoom_Spawn_002_a612de01-a750-491a-bacd-e09a67fc0de3, "ELFSONG", (TRIGGER)S_CAMP_VampireAmbush_Elfsong_Spawn_002_Pos_ca0e977b-a93e-424d-9688-651a38f978b1);
DB_LOW_VampireAmbush_Spawns((CHARACTER)S_LOW_CazadorsPalace_RitualRoom_Spawn_003_52ede1c9-6caf-4f7a-8d97-984c0e90e9cf, "ELFSONG", (TRIGGER)S_CAMP_VampireAmbush_Elfsong_Spawn_003_Pos_7d6301c4-02c5-444f-b424-5a924fbbbf99);
DB_LOW_VampireAmbush_Spawns((CHARACTER)S_LOW_CazadorsPalace_RitualRoom_Spawn_004_a4dc8e21-833c-4469-a72b-1969c688b8a4, "ELFSONG", (TRIGGER)S_CAMP_VampireAmbush_Elfsong_Spawn_004_Pos_555e68c2-aebc-4611-8580-adeb77d7f0a3);

PROC_LOW_VampireAmbush_Spawns_Init();

DB_LOW_VampireAmbush_Players("ELFSONG", (TRIGGER)S_CAMP_VampireAmbush_Elfsong_Player_001_Pos_0ea306a2-a1e1-4a15-bfe2-5cc0408c5d35);
DB_LOW_VampireAmbush_Players("ELFSONG", (TRIGGER)S_CAMP_VampireAmbush_Elfsong_Player_002_Pos_3e50df59-edb3-41bc-8a68-2a535b67cd5b);
DB_LOW_VampireAmbush_Players("ELFSONG", (TRIGGER)S_CAMP_VampireAmbush_Elfsong_Player_003_Pos_04031e51-4e3f-4a4e-9559-775ad2119e90);
DB_LOW_VampireAmbush_Players("ELFSONG", (TRIGGER)S_CAMP_VampireAmbush_Elfsong_Player_004_Pos_0a82c626-ee0d-4133-923e-ed919b9edf12);

DB_LOW_VampireAmbush_Players("SLUMS", (TRIGGER)S_CAMP_VampireAmbush_Slums_Player_001_Pos_304a82a7-63f7-4000-8db2-8751fafec8d8);
DB_LOW_VampireAmbush_Players("SLUMS", (TRIGGER)S_CAMP_VampireAmbush_Slums_Player_002_Pos_073dd797-47a3-479f-bf49-0afdb54c5ab4);
DB_LOW_VampireAmbush_Players("SLUMS", (TRIGGER)S_CAMP_VampireAmbush_Slums_Player_003_Pos_258427e4-5163-4f20-a516-795c551476ef);
DB_LOW_VampireAmbush_Players("SLUMS", (TRIGGER)S_CAMP_VampireAmbush_Slums_Player_004_Pos_40a326a9-edbd-4e0c-9fba-3cfa968aa11d);

// > 4 players via mods
DB_LOW_VampireAmbush_Players_FallbackPos("ELFSONG", (TRIGGER)S_CAMP_VampireAmbush_Elfsong_Player_FallbackPos_b945b697-bfa5-4d5e-b058-75e68e8cf7f1); 
DB_LOW_VampireAmbush_Players_FallbackPos("SLUMS", (TRIGGER)S_CAMP_VampireAmbush_Slums_Player_FallbackPos_9a9541e8-cc87-4301-8982-8f8fdd519dab);

DB_GLO_ExclusiveFlag("LOW_VampireAmbush_SpawnsLeft", LOW_VampireAmbush_State_OneSpawnLeft_b1b0be0b-524c-4549-b2fa-0680650af7f4);
DB_GLO_ExclusiveFlag("LOW_VampireAmbush_SpawnsLeft", LOW_VampireAmbush_State_AllSpawnsEscaped_341b49c2-af26-4037-b2f9-bedfab2d6939);
//END_REGION Vampire Spawns Night Ambush
KBSECTION
//REGION Vampire Ambush
IF
DB_LOW_VampireAmbush_Dialogs(_NightFlag, _Dialog, _RequirementFlag)
THEN
DB_CampNight(_NightFlag, 2080 /* 3080 for avatar */, (CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255);
DB_CampNight_Camp(_NightFlag, "SLUMS");
DB_CampNight_Camp(_NightFlag, "ELFSONG");
DB_CampNight_ExclusiveMoment(_NightFlag);
DB_CampNight_SCO(_NightFlag, _Dialog);
DB_CampNight_Requirement(_NightFlag, _RequirementFlag);
DB_CampNight_CancelledBy(_NightFlag, (FLAG)LOW_CazadorsPalace_State_EnteredMansion_97df5c27-5740-4277-96e1-5fdd4c5e6699);
DB_CampNight_CancelledBy(_NightFlag, (FLAG)LOW_CazadorsPalace_State_EnteredDungeon_740ea5cd-7ada-4610-aadb-f52026782700);

PROC
PROC_LOW_VampireAmbush_Spawns_Init()
AND
DB_LOW_VampireAmbush_Spawns(_Spawn, _, _)
AND
NOT DB_LOW_VampireAmbush_SpawnsToDefeat((CHARACTER)_Spawn)
THEN
SetImmortal(_Spawn, 1);
AddPreferredAiTargetTag(_Spawn, (TAG)REALLY_ASTARION_ffd08582-7396-4cac-bcd4-8f9cd0fd8ef3);
DB_LOW_VampireAmbush_SpawnsToDefeat((CHARACTER)_Spawn); // not trully defeated, so using a unique DB

PROC
PROC_CampNight_StartSelected()
AND
DB_Camp_QueuedNight((FLAG)_CampNightFlag)
AND
DB_LOW_VampireAmbush_Dialogs(_CampNightFlag, _, _)
THEN
DB_Camp_PreventEndOfNight(1);

// if he is not in the active party he will be set off stage
// we don't want this, since he participates in the dialog & fight after the dialog
QRY
QRY_Camp_DoNotHideCamper((CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255)
AND
DB_Camp_QueuedNight(_NightFlag)
AND
DB_LOW_VampireAmbush_Dialogs(_NightFlag, _, _)
THEN
DB_NOOP(1);

// Companion
// The order of speakers: Astarion, Avatar, Spawn 001, Spawn 002, Optional Avatars
// Since the total num of speakers can get to 7 we can't use QRY_StartDialog
// Instead we add Optional Avatars after the dialog starts
QRY
QRY_Camp_SleepCutsceneOverride_CustomDialogStart((DIALOGRESOURCE)CAMP_VampireAmbush_SCO_Companion_34895cc1-7299-44fa-6fef-ace461369aba, (CHARACTER)_Player, (CHARACTER)_Player2, (CHARACTER)_Player3, (CHARACTER)_Player4)
AND
QRY_LOW_VampireAmbush_StoreOptionalSpeakers(_Player, _Player2, _Player3, _Player4)
AND
QRY_GetBestAvatarForCompanion((CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255)
AND
DB_QRYRTN_GetBestAvatarForCompanion((CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255, (CHARACTER)_BestAvatar)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)CAMP_VampireAmbush_SCO_Companion_34895cc1-7299-44fa-6fef-ace461369aba, 
	S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255,
	_BestAvatar,
	S_LOW_CazadorsPalace_RitualRoom_Spawn_001_062ff971-d709-4f54-bb03-51c02844eacb, 
	S_LOW_CazadorsPalace_RitualRoom_Spawn_002_a612de01-a750-491a-bacd-e09a67fc0de3)
THEN
DB_NOOP(1);

// Avatar
// The order of speakers: Astarion-Avatar, Spawn 001, Spawn 002, Optional Avatars
QRY
QRY_Camp_SleepCutsceneOverride_CustomDialogStart((DIALOGRESOURCE)CAMP_VampireAmbush_SCO_Avatar_3781f961-3081-18b3-d0d3-fd2b1f832f02, (CHARACTER)_Player, (CHARACTER)_Player2, (CHARACTER)_Player3, (CHARACTER)_Player4)
AND
QRY_LOW_VampireAmbush_StoreOptionalSpeakers(_Player, _Player2, _Player3, _Player4)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)CAMP_VampireAmbush_SCO_Avatar_3781f961-3081-18b3-d0d3-fd2b1f832f02, 
	S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255, 
	S_LOW_CazadorsPalace_RitualRoom_Spawn_001_062ff971-d709-4f54-bb03-51c02844eacb, 
	S_LOW_CazadorsPalace_RitualRoom_Spawn_002_a612de01-a750-491a-bacd-e09a67fc0de3)
THEN
DB_NOOP(1);

QRY
QRY_LOW_VampireAmbush_StoreOptionalSpeakers((CHARACTER)_Player, (CHARACTER)_Player2, (CHARACTER)_Player3, (CHARACTER)_Player4)
AND
QRY_LOW_VampireAmbush_StoreOptionalSpeaker((CHARACTER)_Player)
AND
QRY_LOW_VampireAmbush_StoreOptionalSpeaker((CHARACTER)_Player2)
AND
QRY_LOW_VampireAmbush_StoreOptionalSpeaker((CHARACTER)_Player3)
AND
QRY_LOW_VampireAmbush_StoreOptionalSpeaker((CHARACTER)_Player4)
THEN
DB_NOOP(1);

QRY
QRY_LOW_VampireAmbush_StoreOptionalSpeaker((CHARACTER)_Player)
AND
_Player != S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255
AND
_Player != NULL_00000000-0000-0000-0000-000000000000
THEN
DB_LOW_VampireAmbush_StoredOptionalSpeakers((CHARACTER)_Player);

QRY
QRY_LOW_VampireAmbush_StoreOptionalSpeaker((CHARACTER)_Player)
THEN
DB_NOOP(1);

PROC
PROC_StartDialog_AddExtraSpeakers(_Dialog, (INTEGER)_ID)
AND
DB_LOW_VampireAmbush_Dialogs(_, _Dialog, _)
AND
NOT DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AmbushFailed_c7ad2cf7-59de-4310-aac1-2635d66474bf)
THEN
PROC_GlobalSetFlagAndCache((FLAG)LOW_VampireAmbush_State_AmbushStarted_1d39e1e0-d419-4185-905c-c798511703b6);

PROC
PROC_StartDialog_AddExtraSpeakers(_Dialog, (INTEGER)_ID)
AND
DB_LOW_VampireAmbush_Dialogs(_, _Dialog, _)
AND
NOT DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AmbushFailed_c7ad2cf7-59de-4310-aac1-2635d66474bf)
AND
DB_LOW_VampireAmbush_StoredOptionalSpeakers(_Speaker)
THEN
PROC_DialogAddSpeakingActor(_ID, _Speaker);

IF
DialogStarted(_Dialog, _)
AND
DB_LOW_VampireAmbush_Dialogs(_, _Dialog, _)
AND
NOT DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AmbushFailed_c7ad2cf7-59de-4310-aac1-2635d66474bf)
THEN
SetFlag((FLAG)GLO_Camp_Event_SkipSleepCutscene_1ee0a25e-4115-44ef-b87d-c2a5eee494b6);
PROC_LOW_VampireAmbush_TeleportPlayers();

// Astarion in camp, but not in the party
PROC
PROC_LOW_VampireAmbush_TeleportPlayers()
AND
NOT DB_Players((CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255)
AND
DB_ActiveCamp(_Camp)
AND
DB_LOW_VampireAmbush_Players_FallbackPos(_Camp, _AstarionPos)
AND
GetPosition(_AstarionPos, _X, _Y, _Z)
AND
FindValidPosition(_X, _Y, _Z, 2.5, S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255, 0, _ValidX, _ValidY, _ValidZ)
THEN
DB_LOW_VampireAmbush_AstarionTeleported(1);
TeleportToPosition(S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255, _ValidX, _ValidY, _ValidZ);
SetFlag(LOW_VampireAmbush_State_CannotBeKidnapped_11d1273c-6c66-4621-abad-02b9cd4367cd); // if he is not in the active party, then the spaws just want to defeat the party

// Astarion in camp, but not in the party
//FindValidPosition Fallback
PROC
PROC_LOW_VampireAmbush_TeleportPlayers()
AND
NOT DB_LOW_VampireAmbush_AstarionTeleported(1)
AND
NOT DB_Players((CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255)
AND
DB_ActiveCamp(_Camp)
AND
DB_LOW_VampireAmbush_Players_FallbackPos(_Camp, _AstarionPos)
AND
GetPosition(_AstarionPos, _X, _Y, _Z)
THEN
DB_LOW_VampireAmbush_AstarionTeleported(1);
TeleportToPosition(S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255, _X, _Y, _Z);
SetFlag(LOW_VampireAmbush_State_CannotBeKidnapped_11d1273c-6c66-4621-abad-02b9cd4367cd); // if he is not in the active party, then the spaws just want to defeat the party

PROC
PROC_LOW_VampireAmbush_TeleportPlayers()
AND
DB_ActiveCamp(_Camp)
AND
DB_Players(_Player)
AND
QRY_LOW_VampireAmbush_TakeFreePlayerPos(_Camp)
AND
DB_QRYRTN_LOW_VampireAmbush_TakenPos(_Pos)
AND
GetPosition(_Pos, _X, _Y, _Z)
AND
FindValidPosition(_X, _Y, _Z, 2.5, _Player, 0, _ValidX, _ValidY, _ValidZ)
THEN
DB_LOW_VampireAmbush_TakenPos((CHARACTER)_Player, (TRIGGER)_Pos);
TeleportToPosition(_Player, _ValidX, _ValidY, _ValidZ);

//FindValidPosition Fallback
PROC
PROC_LOW_VampireAmbush_TeleportPlayers()
AND
DB_ActiveCamp(_Camp)
AND
DB_Players(_Player)
AND
QRY_LOW_VampireAmbush_TakeFreePlayerPos(_Camp)
AND
DB_QRYRTN_LOW_VampireAmbush_TakenPos(_Pos)
AND
NOT DB_LOW_VampireAmbush_TakenPos((CHARACTER)_Player, (TRIGGER)_Pos)
AND
GetPosition(_Pos, _X, _Y, _Z)
THEN
DB_LOW_VampireAmbush_TakenPos((CHARACTER)_Player, (TRIGGER)_Pos);
TeleportToPosition(_Player, _X, _Y, _Z);

PROC
PROC_LOW_VampireAmbush_TeleportPlayers()
AND
DB_LOW_VampireAmbush_TakenPos((CHARACTER)_Player, (TRIGGER)_Pos)
THEN
NOT DB_LOW_VampireAmbush_TakenPos(_Player, _Pos);

QRY
QRY_LOW_VampireAmbush_TakeFreePlayerPos((STRING)_Camp)
AND
DB_QRYRTN_LOW_VampireAmbush_TakenPos(_Pos)
THEN
NOT DB_QRYRTN_LOW_VampireAmbush_TakenPos(_Pos);

QRY
QRY_LOW_VampireAmbush_TakeFreePlayerPos((STRING)_Camp)
AND
DB_LOW_VampireAmbush_Players(_Camp, _Pos)
AND
NOT DB_LOW_VampireAmbush_TakenPos(_, _Pos)
AND
NOT DB_QRYRTN_LOW_VampireAmbush_TakenPos(_)
THEN
DB_QRYRTN_LOW_VampireAmbush_TakenPos(_Pos);

QRY
QRY_LOW_VampireAmbush_TakeFreePlayerPos((STRING)_Camp)
AND
NOT DB_QRYRTN_LOW_VampireAmbush_TakenPos(_)
AND
DB_LOW_VampireAmbush_Players_FallbackPos(_Camp, _FallbackPos)
THEN
DB_QRYRTN_LOW_VampireAmbush_TakenPos(_FallbackPos);

IF
DialogEnded(_Dialog, _)
AND
DB_LOW_VampireAmbush_Dialogs(_, _Dialog, _)
AND
NOT DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AmbushFailed_c7ad2cf7-59de-4310-aac1-2635d66474bf)
THEN
PROC_LOW_VampireAmbush_TeleportNPCs();
PROC_SetRelationToPlayers((FACTION)ACT3_LOW_VampireAmbush_465cdc48-52d0-4c07-9c14-c62e22e5bc72, 0);

PROC
PROC_LOW_VampireAmbush_TeleportNPCs()
AND
DB_ActiveCamp(_CampName)
AND
DB_LOW_VampireAmbush_Spawns(_Spawn, _CampName, _Pos)
THEN
TeleportTo(_Spawn, _Pos, "");

IF
DialogEnded((DIALOGRESOURCE)CAMP_VampireAmbush_SCO_Companion_34895cc1-7299-44fa-6fef-ace461369aba, _ID)
AND
NOT DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AmbushFailed_c7ad2cf7-59de-4310-aac1-2635d66474bf)
AND
DB_DialogPlayers(_ID, _Player, 2)
THEN
DB_LOW_VampireAmbush_PreCombatSpeaker(_Player);

IF
EntityEvent((CHARACTER)_Spawn, "LOW_VampireAmbush_EscapeWithAstarion")
THEN
PROC_GlobalSetFlagAndCache((FLAG)LOW_VampireAmbush_State_AstarionKidnapped_b52d681f-4722-4f43-ba95-ac9f49c173d8);
PROC_LOW_CazadorsPalace_Kennel_KidnapAstarion();
PROC_Act3_VampireSpawns_SendToPalace(_Spawn);
PROC_LOW_VampireAmbush_SpawnEscaped(_Spawn);

IF
DB_Defeated(_Spawn)
AND
DB_LOW_VampireAmbush_SpawnsToDefeat((CHARACTER)_Spawn)
AND
DB_GlobalFlag((FLAG)CAMP_VampireAmbush_State_AmbushInProgress_81f2f108-1611-4ef1-b8c7-fec2faf6bfa5)
THEN
PROC_Act3_VampireSpawns_SendToPalace(_Spawn);
PROC_LOW_VampireAmbush_SpawnEscaped(_Spawn);

IF
HitpointsChanged((CHARACTER)_Spawn, _RemainingHPPercentage)
AND
DB_LOW_VampireAmbush_SpawnsToDefeat(_Spawn)
AND
DB_GlobalFlag((FLAG)CAMP_VampireAmbush_State_AmbushInProgress_81f2f108-1611-4ef1-b8c7-fec2faf6bfa5)
AND
_RemainingHPPercentage < 5.0
THEN
PROC_Act3_VampireSpawns_SendToPalace(_Spawn);
PROC_LOW_VampireAmbush_SpawnEscaped(_Spawn);

PROC
PROC_LOW_VampireAmbush_SpawnEscaped((CHARACTER)_Spawn)
THEN
RemovePreferredAiTargetTag(_Spawn, (TAG)REALLY_ASTARION_ffd08582-7396-4cac-bcd4-8f9cd0fd8ef3);
NOT DB_LOW_VampireAmbush_SpawnsToDefeat((CHARACTER)_Spawn);

PROC
PROC_LOW_VampireAmbush_SpawnEscaped((CHARACTER)_Spawn)
AND
SysCount("DB_LOW_VampireAmbush_SpawnsToDefeat", 1, 1)
THEN
PROC_GlobalSetFlagAndCache(LOW_VampireAmbush_State_OneSpawnLeft_b1b0be0b-524c-4549-b2fa-0680650af7f4);

PROC
PROC_LOW_VampireAmbush_SpawnEscaped((CHARACTER)_Spawn)
AND
NOT DB_LOW_VampireAmbush_SpawnsToDefeat(_)
THEN
PROC_GlobalSetFlagAndCache((FLAG)LOW_VampireAmbush_State_AllSpawnsEscaped_341b49c2-af26-4037-b2f9-bedfab2d6939);

IF
DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AllSpawnsEscaped_341b49c2-af26-4037-b2f9-bedfab2d6939)
THEN
PROC_GlobalSetFlagAndCache(LOW_VampireAmbush_State_AmbushEnded_c1ec3905-8c4a-4300-bf20-e1a136065f8a);

IF
FlagSet((FLAG)CAMP_VampireAmbush_State_AmbushEnded_c1ec3905-8c4a-4300-bf20-e1a136065f8a, _, _)
AND
HasActiveStatus((CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255, "KNOCKED_OUT", 1)
THEN
RemoveStatus(S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255, "KNOCKED_OUT", NULL_00000000-0000-0000-0000-000000000000);

IF
DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AmbushEnded_c1ec3905-8c4a-4300-bf20-e1a136065f8a)
AND
NOT DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AstarionKidnapped_b52d681f-4722-4f43-ba95-ac9f49c173d8)
THEN
PROC_GlobalSetFlagAndCache((FLAG)LOW_VampireAmbush_State_AmbushFailed_c7ad2cf7-59de-4310-aac1-2635d66474bf);
DB_LOW_VampireAmbush_AfterCombatDialogQueued(1);

IF
DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AmbushEnded_c1ec3905-8c4a-4300-bf20-e1a136065f8a)
AND
DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AstarionKidnapped_b52d681f-4722-4f43-ba95-ac9f49c173d8)
THEN
PROC_GlobalSetFlagAndCache((FLAG)LOW_VampireAmbush_State_AmbushSucceded_6cf85531-c098-475c-9dba-528c039125ad);

IF
FlagSet(LOW_VampireAmbush_State_AmbushSucceded_6cf85531-c098-475c-9dba-528c039125ad, _, _)
AND
DB_ActiveCamp(_ActiveCamp)
AND
DB_Camp(_ActiveCamp,_,_,_Entrypoint)
AND
GetClosestAlivePlayer(_EntryPoint, _Player, _)
AND
DB_InCamp(_Player)
THEN
StartVoiceBark((VOICEBARKRESOURCE)CAMP_VampireAmbush_VB_AstarionKidnapped_ad430251-5621-a04a-4fa8-6c0cdde4c39c, _Player);

IF
DialogActorJoined((DIALOGRESOURCE)CAMP_VampireAmbush_PAD_AstarionKidnapped_39146622-d93b-aaaa-0be9-ea052d034fa3, _, _Actor, _)
AND
DB_ActiveCamp(_ActiveCamp)
AND
DB_Camp(_ActiveCamp, _Campfire, _, _)
THEN
SteerTo((CHARACTER)_Actor, _Campfire, 1);
SetFlag((FLAG)GLO_CAMP_StandingStill_776de2de-871c-4cf4-bcbb-72d26f244489, _Actor);

IF
DialogActorLeft((DIALOGRESOURCE)CAMP_VampireAmbush_PAD_AstarionKidnapped_39146622-d93b-aaaa-0be9-ea052d034fa3, _, _Actor, _)
THEN
ClearFlag((FLAG)GLO_CAMP_StandingStill_776de2de-871c-4cf4-bcbb-72d26f244489, _Actor);

IF
DB_LOW_VampireAmbush_AfterCombatDialogQueued(1)
AND
DB_LOW_VampireAmbush_PreCombatSpeaker(_Player)
AND
NOT DB_CantTalk(_Player)
AND
NOT DB_CantTalk(S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255)
THEN
PROC_LOW_VampireAmbush_StartPostCombatDialog((CHARACTER)_Player);

PROC
PROC_LOW_VampireAmbush_StartPostCombatDialog((CHARACTER)_Player)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)CAMP_VampireAmbush_SCO_Companion_34895cc1-7299-44fa-6fef-ace461369aba, S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255, _Player)
THEN
NOT DB_LOW_VampireAmbush_AfterCombatDialogQueued(1);

// failed for some reason
PROC
PROC_LOW_VampireAmbush_StartPostCombatDialog((CHARACTER)_Player)
AND
DB_LOW_VampireAmbush_AfterCombatDialogQueued(1)
THEN
NOT DB_LOW_VampireAmbush_AfterCombatDialogQueued(1);
PROC_RelationshipDialog((CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255, (DIALOGRESOURCE)CAMP_VampireAmbush_SCO_Companion_34895cc1-7299-44fa-6fef-ace461369aba, S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255);

PROC
PROC_Camp_EndEvening()
AND
DB_LOW_VampireAmbush_AfterCombatDialogQueued(1)
THEN
NOT DB_LOW_VampireAmbush_AfterCombatDialogQueued(1);

IF
TurnStarted((CHARACTER)_Spawn)
AND
DB_LOW_VampireAmbush_SpawnsToDefeat(_Spawn)
AND
DB_GlobalFlag((FLAG)CAMP_VampireAmbush_State_AmbushInProgress_81f2f108-1611-4ef1-b8c7-fec2faf6bfa5)
AND
NOT DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AstarionKidnapped_b52d681f-4722-4f43-ba95-ac9f49c173d8)
AND
NOT DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_KidnapperChoosen_8237cad2-2563-4850-993c-7437a53b302c)
AND
GetFlag(LOW_CazadorsPalace_State_SentToPalace_df019be0-deac-4302-a3a6-018fb985c705, _Spawn, 0)
AND 
QRY_LOW_VampireAmbush_CanKidnapAstarion()
THEN
PROC_GlobalSetFlagAndCache((FLAG)LOW_VampireAmbush_State_KidnapperChoosen_8237cad2-2563-4850-993c-7437a53b302c);
SetFlag((FLAG)LOW_VampireAmbush_State_Kidnapper_d33af2db-fc2c-4bdc-bf58-3ea2cee4ac6a, _Spawn);

QRY
QRY_LOW_VampireAmbush_CanKidnapAstarion()
AND
DB_Downed((CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255)
THEN
DB_NOOP(1);

QRY
QRY_LOW_VampireAmbush_CanKidnapAstarion()
AND
IsDead((CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255, 1)
THEN
DB_NOOP(1);

PROC
PROC_Act3_VampireSpawns_SendToPalace((CHARACTER)_Spawn)
THEN
ClearFlag((FLAG)LOW_VampireAmbush_State_KidnapperChoosen_8237cad2-2563-4850-993c-7437a53b302c);
ClearFlag((FLAG)LOW_VampireAmbush_State_Kidnapper_d33af2db-fc2c-4bdc-bf58-3ea2cee4ac6a, _Spawn);

// campfire is blocked till the ambush is over
PROC
PROC_BlockUseOfItem(_Char, _Campfire)
AND
DB_Camp(_, _Campfire, _, _)
AND
DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AmbushInProgress_81f2f108-1611-4ef1-b8c7-fec2faf6bfa5)
THEN
DB_CustomUseItemResponse(_Char, _Campfire, 0);
PROC_PlayCantUseItemAD((CHARACTER)_Char);

// camp beds & bedrolls are blocked till the ambush is over
PROC
PROC_BlockUseOfItem(_Char, _Bedroll)
AND
DB_CampBed(_, _, _Bedroll, _, _, _, _)
AND
DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AmbushStarted_1d39e1e0-d419-4185-905c-c798511703b6)
AND
NOT DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AmbushEnded_c1ec3905-8c4a-4300-bf20-e1a136065f8a)
THEN
DB_CustomUseItemResponse(_Char, _Bedroll, 0);
PROC_PlayCantUseItemAD((CHARACTER)_Char);

PROC
PROC_BlockUseOfItem(_Char, _Bed)
AND
DB_CampBed(_, _, _, _Bed, _, _, _)
AND
DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AmbushStarted_1d39e1e0-d419-4185-905c-c798511703b6)
AND
NOT DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AmbushEnded_c1ec3905-8c4a-4300-bf20-e1a136065f8a)
THEN
DB_CustomUseItemResponse(_Char, _Bed, 0);
PROC_PlayCantUseItemAD((CHARACTER)_Char);

IF
FlagSet((FLAG)LOW_VampireAmbush_State_AmbushStarted_1d39e1e0-d419-4185-905c-c798511703b6, _, _)
THEN
PROC_GlobalSetFlagAndCache(LOW_VampireAmbush_State_AmbushInProgress_81f2f108-1611-4ef1-b8c7-fec2faf6bfa5);

IF
FlagSet((FLAG)LOW_VampireAmbush_State_AmbushEnded_c1ec3905-8c4a-4300-bf20-e1a136065f8a, _, _)
THEN
PROC_GlobalClearFlagAndCache(LOW_VampireAmbush_State_AmbushInProgress_81f2f108-1611-4ef1-b8c7-fec2faf6bfa5);
//END_REGION

//REGION Solo Avatar Astarion
// solo Avatar Astarion can be captured (downed / killed), it's not a game over
QRY
QRY_GameOver_BlockGameOver()
AND
DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AmbushInProgress_81f2f108-1611-4ef1-b8c7-fec2faf6bfa5)
AND
QRY_IsSoloAvatar(S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255)
THEN
DB_NOOP(1);

// In general, the fight shouldn't end till either the spawns or the party is defeated
// in case the player somehow ended the fight, but the spawns are still present then:
// a) if astarion is within 20m and is downed, then they auto-kidnap him and escape to Cazador - ambush succeded
// b) otherwise they just leave - ambush failed 
IF
CombatEnded(_)
AND
DB_GlobalFlag(LOW_VampireAmbush_State_AmbushInProgress_81f2f108-1611-4ef1-b8c7-fec2faf6bfa5)
THEN
PROC_LOW_VampireAmbush_AmbushCombatEnded();

// if we are within 20m of Astarion - he can be kidnapped
PROC
PROC_LOW_VampireAmbush_AmbushCombatEnded()
AND
QRY_LOW_VampireAmbush_CanKidnapAstarion()
AND
DB_LOW_VampireAmbush_SpawnsToDefeat(_Spawn)
AND
GetDistanceTo(_Spawn, (CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255, _Dist)
AND
_Dist < 20.0
AND
NOT DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_KidnapperChoosen_8237cad2-2563-4850-993c-7437a53b302c)
THEN
PROC_GlobalSetFlagAndCache((FLAG)LOW_VampireAmbush_State_KidnapperChoosen_8237cad2-2563-4850-993c-7437a53b302c);
DB_LOW_VampireAmbush_ForcedKidnapper((CHARACTER)_Spawn);
SetFlag((FLAG)LOW_VampireAmbush_State_Kidnapper_d33af2db-fc2c-4bdc-bf58-3ea2cee4ac6a, _Spawn);
SetEntityEvent(_Spawn, "LOW_VampireAmbush_EscapeWithAstarion", 1);

// the rest simply teleports to Cazador
PROC
PROC_LOW_VampireAmbush_AmbushCombatEnded()
AND
DB_LOW_VampireAmbush_SpawnsToDefeat(_Spawn)
AND
NOT DB_LOW_VampireAmbush_ForcedKidnapper(_Spawn)
THEN
PROC_Act3_VampireSpawns_SendToPalace(_Spawn);
PROC_LOW_VampireAmbush_SpawnEscaped(_Spawn);
//END_REGION

//REGION Goal Completion
PROC
PROC_LongRest()
AND
DB_GlobalFlag((FLAG)LOW_VampireAmbush_State_AmbushEnded_c1ec3905-8c4a-4300-bf20-e1a136065f8a)
THEN
GoalCompleted;
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act3b"
