Version 1
SubGoalCombiner SGC_AND
INITSECTION
//Help callers
DB_Combat_CallingForHelp(S_END_HelpCaller_001_0071c1ba-d898-42c2-afb4-cf2e7e0138d0, "END_REINFORCEMENTS", "Shout_END_Rooftops_CallForHelp");
DB_Combat_CallingForHelp(S_END_HelpCaller_002_ee47c6db-af55-468f-a3de-3d7458cf4770, "END_REINFORCEMENTS", "Shout_END_Rooftops_CallForHelp");
DB_Combat_CallingForHelp(S_END_HelpCaller_003_0a732d95-6566-4594-a188-1ebb44a0aaa3, "END_REINFORCEMENTS", "Shout_END_Rooftops_CallForHelp");
DB_Combat_CallingForHelp(S_END_HelpCaller_004_5bbdbe71-c3d7-4712-a211-36c8c17a178d, "END_REINFORCEMENTS", "Shout_END_Rooftops_CallForHelp");

DB_Combat_CallingForHelp_CrimeDisabled("END_REINFORCEMENTS");

//This DB won't stay ordered once logic starts using it.
DB_END_Reinforcements_Characters(1, (CHARACTER)S_END_StandingGuard_017_51432576-f8c0-4f81-98a0-b6bd7134a263);
DB_END_Reinforcements_Characters(1, S_END_StandingGuard_015_33d5c39b-cc07-4e94-a10a-0e50a87b9c6c);
DB_END_Reinforcements_Characters(1, S_END_StandingGuard_016_bfe28d31-fbb6-4a43-9c85-e0d81c56a62d);
DB_END_Reinforcements_Characters(2, S_END_StandingGuard_012_561fa553-9b55-47e8-a206-9f2ddcf88035);
DB_END_Reinforcements_Characters(2, S_END_StandingGuard_013_409ac3da-1cf5-444d-932f-48e99e6e7d68);
DB_END_Reinforcements_Characters(2, S_END_StandingGuard_011_35a89031-819d-4957-be25-e3836c95f95a);
DB_END_Reinforcements_Characters(2, S_END_StandingGuard_010_6ecf01b5-d3fa-4bd7-b1bd-e18411eef232);
DB_END_Reinforcements_Characters(3, S_END_MindflayerGuard_007_842a1a06-80a8-43e2-8847-109a5681e2be);
DB_END_Reinforcements_Characters(3, S_END_IntellectDevourerGuard_001_adddfc7d-7ca6-4048-ac02-219b544e2186);
DB_END_Reinforcements_Characters(3, S_END_IntellectDevourerGuard_002_ae1fd81f-1b1c-4714-a061-ae7aa527155f);
DB_END_Reinforcements_Characters(4, S_END_PatrolFollower_005_f52a25dc-8bc4-4bbe-adca-9a38b86ce064);
DB_END_Reinforcements_Characters(4, S_END_MindflayerPatrol_003_fc6fb56f-1ad5-4a94-9c02-ea84a32bb713);
DB_END_Reinforcements_Characters(4, S_END_PatrolFollower_006_b6406c2d-a72b-4764-bd48-4cc775cd3441);
DB_END_Reinforcements_Characters(5, S_END_PatrolFollower_001_4a3cf9ab-1ebf-4f3d-aa7e-7ae0ce02a291);
DB_END_Reinforcements_Characters(5, S_END_MindflayerPatrol_001_890ee459-4366-42c8-81c7-8f9886f76849);
DB_END_Reinforcements_Characters(5, S_END_PatrolFollower_002_ee47c6db-af55-468f-a3de-3d7458cf4770);
DB_END_Reinforcements_Characters(7, S_END_MindflayerPatrol_002_24121be6-f4fd-4199-b7e0-7787cf93c9fe);
DB_END_Reinforcements_Characters(7, S_END_PatrolFollower_003_9e3c2677-3137-4278-a9b8-547714832fff);
DB_END_Reinforcements_Characters(7, S_END_HelpCaller_003_0a732d95-6566-4594-a188-1ebb44a0aaa3);
DB_END_Reinforcements_Characters(8, S_END_HighHall_Goblin_003_a5166cc2-1f92-465b-8c1e-c88e2eec5974);
DB_END_Reinforcements_Characters(8, S_END_HighHall_Goblin_004_6072f578-11a7-444e-bfef-5d60943736cb);
DB_END_Reinforcements_Characters(8, S_END_HighHall_Goblin_002_0f2cb66f-9a87-46f4-a59e-c614f8aeba2f);
DB_END_Reinforcements_Characters(8, S_END_HighHall_Goblin_005_fc1a34d7-986c-4faf-8004-87fc4b2d88ae);
DB_END_Reinforcements_Characters(9, S_END_HighHall_Goblin_000_187d3a31-e42a-4c7c-83cf-71361b813b8b);
DB_END_Reinforcements_Characters(9, S_END_HighHall_Goblin_001_1049d503-dd33-4eef-8c19-21b4b0c557e8);
DB_END_Reinforcements_Characters(9, S_END_HighHall_Goblin_007_dd2c8ac4-9b2a-4d5f-b027-8670b2492858);
DB_END_Reinforcements_Characters(9, S_END_HighHall_Goblin_006_140768dd-903a-4bf0-815f-f17a8498373d);
DB_END_Reinforcements_Characters(10, S_END_HelpCaller_001_0071c1ba-d898-42c2-afb4-cf2e7e0138d0);
DB_END_Reinforcements_Characters(10, S_END_StandingGuard_004_cfa06c22-e9ae-4d67-8cbf-876a96ae5ba7);
DB_END_Reinforcements_Characters(10, S_END_Gatekeeper_dc7085f4-a001-4fb9-9624-a6faeb41b538);
DB_END_Reinforcements_Characters(10, S_END_StandingGuard_007_043e161c-a1d9-4736-a787-3a7ce51e6179);
DB_END_Reinforcements_Characters(11, S_END_StandingGuard_006_d0be03b1-12d3-4fc7-aee7-10142fe257ef);
DB_END_Reinforcements_Characters(11, S_END_MindflayerGuard_002_d4745b04-7fc4-4356-be8e-f333c2560160);
DB_END_Reinforcements_Characters(11, S_END_StandingGuard_005_dfb87024-ac7e-4cd7-a006-da866b30055d);
DB_END_Reinforcements_Characters(13, S_END_StandingGuard_014_e6d8e635-f7e8-4e4b-bcfc-a43bba646864);
DB_END_Reinforcements_Characters(14, S_END_HighHall_Spectator_01_9966c0db-819a-4873-bcd6-34595713d265);
DB_END_Reinforcements_Characters(14, S_END_HelpCaller_004_5bbdbe71-c3d7-4712-a211-36c8c17a178d);

//Areas where reinforcements will go. Will check in order of priority from top to bottom, and stop at first trigger the helper is in.
DB_END_Reinforcements_HelpAreas((TRIGGER)S_END_EntranceCombatArea_31b595a6-4734-426c-aa7e-8005be23cfe2);
DB_END_Reinforcements_HelpAreas((TRIGGER)S_END_BackCombatArea_72e97a7f-bd14-4063-b040-2f891f30fe35);
DB_END_Reinforcements_HelpAreas((TRIGGER)S_END_WestPalisadeCombatArea_ff9fd4e3-f05b-4dbe-b67e-406eb6922640);
DB_END_Reinforcements_HelpAreas((TRIGGER)S_END_EastPalisadeCombatArea_0e0bb637-da9d-44ef-a7bb-922951294ced);

//-- Main entrance combat
DB_END_Reinforcements_Destinations((TRIGGER)S_END_EntranceCombatArea_31b595a6-4734-426c-aa7e-8005be23cfe2, 1, (TRIGGER)S_END_EntranceHelpPos_1_5e0f8cc1-0c39-4afc-a012-6cf6f898a111);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_EntranceCombatArea_31b595a6-4734-426c-aa7e-8005be23cfe2, 2, (TRIGGER)S_END_EntranceHelpPos_2_9f8018c0-1adf-419d-ad61-64b19615e7c1);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_EntranceCombatArea_31b595a6-4734-426c-aa7e-8005be23cfe2, 3, (TRIGGER)S_END_EntranceHelpPos_3_519dcf19-e545-4599-b48b-fdd35b93d860);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_EntranceCombatArea_31b595a6-4734-426c-aa7e-8005be23cfe2, 4, (TRIGGER)S_END_EntranceHelpPos_4_ad847e24-424f-4ba0-bbfa-cf229542e53a);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_EntranceCombatArea_31b595a6-4734-426c-aa7e-8005be23cfe2, 5, (TRIGGER)S_END_EntranceHelpPos_5_4c27b4cb-8ed3-48c3-9144-7e0d8c29d77d);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_EntranceCombatArea_31b595a6-4734-426c-aa7e-8005be23cfe2, 7, (TRIGGER)S_END_EntranceHelpPos_7_fa3b2ce8-e8c5-4322-9997-a5bc76370ef1);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_EntranceCombatArea_31b595a6-4734-426c-aa7e-8005be23cfe2, 12, (TRIGGER)S_END_EntranceHelpPos_12_dec80e26-a0d7-4508-af1a-1dc8ebc60ecf);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_EntranceCombatArea_31b595a6-4734-426c-aa7e-8005be23cfe2, 13, (TRIGGER)S_END_EntranceHelpPos_13_9077d8ba-dead-4d5a-a4ce-ab4e9f0f3609);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_EntranceCombatArea_31b595a6-4734-426c-aa7e-8005be23cfe2, 14, (TRIGGER)S_END_EntranceHelpPos_14_3eb86b33-2b72-4ac4-8411-daee0d66dfcf);

DB_END_Reinforcements_Destinations((TRIGGER)S_END_BackCombatArea_72e97a7f-bd14-4063-b040-2f891f30fe35, 4, (TRIGGER)S_END_HighHelpPos_4_632c18ba-553c-42ec-98c3-7e99ce773d4a);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_BackCombatArea_72e97a7f-bd14-4063-b040-2f891f30fe35, 5, (TRIGGER)S_END_HighHelpPos_5_58ce903c-f465-449f-8115-bed8e4a61db7);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_BackCombatArea_72e97a7f-bd14-4063-b040-2f891f30fe35, 7, (TRIGGER)S_END_HighHelpPos_7_997b719b-0670-4281-af5d-c5d5f346ba73);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_BackCombatArea_72e97a7f-bd14-4063-b040-2f891f30fe35, 8, (TRIGGER)S_END_HighHelpPos_8_826ab1ea-c5f3-444f-89d5-c1aea475b956);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_BackCombatArea_72e97a7f-bd14-4063-b040-2f891f30fe35, 9, (TRIGGER)S_END_HighHelpPos_9_075eed20-ae5b-4f58-8d94-0a0d9fe6afbb);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_BackCombatArea_72e97a7f-bd14-4063-b040-2f891f30fe35, 10, (TRIGGER)S_END_HighHelpPos_10_3e674b90-696d-42fb-a702-621e1d262e79);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_BackCombatArea_72e97a7f-bd14-4063-b040-2f891f30fe35, 11, (TRIGGER)S_END_HighHelpPos_11_79dca8b1-6c5e-4e49-900d-9c81b2d0ac4c);

DB_END_Reinforcements_Destinations((TRIGGER)S_END_WestPalisadeCombatArea_ff9fd4e3-f05b-4dbe-b67e-406eb6922640, 2, (TRIGGER)S_END_WestHelpPos_2_ef2ba990-193f-4103-99ad-2d85090debf1);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_WestPalisadeCombatArea_ff9fd4e3-f05b-4dbe-b67e-406eb6922640, 5, (TRIGGER)S_END_WestHelpPos_5_83a714ec-0a12-4464-905b-fcb0f0e4feca);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_WestPalisadeCombatArea_ff9fd4e3-f05b-4dbe-b67e-406eb6922640, 6, (TRIGGER)S_END_WestHelpPos_9_b3544489-966d-4673-a223-5c56338712dd);

DB_END_Reinforcements_Destinations((TRIGGER)S_END_EastPalisadeCombatArea_0e0bb637-da9d-44ef-a7bb-922951294ced, 3, (TRIGGER)S_END_EastHelpPos_3_05b1d39a-d2a4-4a2d-b8a5-c8c69d8d6d60);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_EastPalisadeCombatArea_0e0bb637-da9d-44ef-a7bb-922951294ced, 7, (TRIGGER)S_END_EastHelpPos_7_ac046f8e-8868-4055-b802-c793b21efa65);
DB_END_Reinforcements_Destinations((TRIGGER)S_END_EastPalisadeCombatArea_0e0bb637-da9d-44ef-a7bb-922951294ced, 12, (TRIGGER)S_END_EastHelpPos_12_d36ce7d5-0502-4870-b96e-1ba37006a3d6);

PROC_TriggerRegisterForParty(S_END_CanEnterCombatArea_7b6c3a83-1f89-4439-86bd-68a4f2854e2d);
PROC_TriggerRegisterForParty(S_END_CanEnterCombatArea_002_484a0b8d-708c-4358-8630-352e9b31c5eb);
KBSECTION
IF
DB_END_Reinforcements_Characters(_, _Character)
THEN
SetCanFight(_Character, 0);
SetCanJoinCombat(_Character, 0);

IF
EnteredTrigger(_PartyMember, S_END_CanEnterCombatArea_7b6c3a83-1f89-4439-86bd-68a4f2854e2d)
AND
DB_PartyMembers(_PartyMember)
THEN
PROC_Reinforcements_CharactersCanFight();

IF
EnteredTrigger(_PartyMember, S_END_CanEnterCombatArea_002_484a0b8d-708c-4358-8630-352e9b31c5eb)
AND
DB_PartyMembers(_PartyMember)
THEN
PROC_Reinforcements_CharactersCanFight();

IF
AttackedBy((CHARACTER)_Character, _, _, _, _, _, _)
AND
NOT DB_OnlyOnce("END_Reinforcements_EnableCombatOnce")
AND
DB_END_Reinforcements_Characters(_, _Character)
THEN
PROC_Reinforcements_CharactersCanFight();

PROC
PROC_Reinforcements_CharactersCanFight()
AND
QRY_OnlyOnce("END_Reinforcements_EnableCombatOnce")
AND
DB_END_Reinforcements_Characters(_, _Character)
AND
NOT DB_KnockedOut(_Character)
THEN
SetCanFight(_Character, 1);
SetCanJoinCombat(_Character, 1);

PROC
PROC_Reinforcements_CharactersCanFight()
THEN
PROC_TriggerUnregisterForParty(S_END_CanEnterCombatArea_7b6c3a83-1f89-4439-86bd-68a4f2854e2d);
PROC_TriggerUnregisterForParty(S_END_CanEnterCombatArea_002_484a0b8d-708c-4358-8630-352e9b31c5eb);

PROC
PROC_Combat_CallingForHelp(_Character, "END_REINFORCEMENTS")
AND
DB_Is_InCombat(_Character, _ID)
AND
DB_END_Reinforcements_HelpAreas(_HelpArea)
AND
IsInTrigger(_Character, _HelpArea, 1)
AND
NOT DB_END_Reinfrocements_Calling(_Character)
THEN
DB_END_Reinfrocements_Calling(_Character);
PROC_END_Reinforcements_CallReinforcementsToCombat(_Character, _ID, _HelpArea);

PROC
PROC_END_Reinforcements_CallReinforcementsToCombat((CHARACTER)_Caller, (GUIDSTRING)_Combat, (TRIGGER)_HelpArea)
AND
DB_END_Reinforcements_Characters(_Squad, _Character)
AND
QRY_END_Reinforcements_Available(_Character)
AND
DB_END_Reinforcements_Destinations(_HelpArea, _Squad, _Destination)
THEN
PROC_END_Reinforcements_SendCharacterToDestination(_Character, _Destination, _Combat);

//REGION Once Call for help happens, make all Flying Ghouls Swarm

IF
DB_Combat_CallingForHelp(_Char, "END_REINFORCEMENTS", _)
THEN
DB_END_HighHallCourtyard_CallForHelperUsers(_Char);

PROC
PROC_Combat_CallingForHelp(_, "END_REINFORCEMENTS")
AND
DB_END_HighHallCourtyard_CallForHelperUsers(_Char)
THEN
RequestSetSwarmGroup(_Char, "END_HighHall_Flying Ghoul");
NOT DB_END_HighHallCourtyard_CallForHelperUsers(_Char);

//END_REGION

QRY
QRY_END_Reinforcements_Available((CHARACTER)_Character)
AND
NOT DB_CantAct(_Character)
AND
NOT DB_END_Reinforcements_Called(_Character, _, _)
THEN
DB_NOOP(1);

PROC
PROC_END_Reinforcements_SendCharacterToDestination((CHARACTER)_Character, (TRIGGER)_DestinationPoint, (GUIDSTRING)_Combat)
AND
NOT DB_Following(_Character, _)
THEN
DB_END_Reinforcements_Called(_Character, _DestinationPoint, _Combat);
SetCombatGroupID(_Character, "");
PROC_CharacterMoveTo(_Character, _DestinationPoint, "Sprint", "END_Reinforcements_Arrived");

PROC
PROC_END_Reinforcements_SendCharacterToDestination((CHARACTER)_Character, (TRIGGER)_DestinationPoint, (GUIDSTRING)_Combat)
AND
DB_Following(_Character, _Target)
THEN
PROC_StopFollow(_Character);
FlushOsirisQueue(_Character);
DB_END_Reinforcements_Called(_Character, _DestinationPoint, _Combat);
SetEntityEvent(_Character, "END_Reinforcements_Called", 1);

IF
EntityEvent((CHARACTER)_Character, "END_Reinforcements_Called")
AND
DB_END_Reinforcements_Called(_Character, _DestinationPoint, _Combat)
THEN
SetCombatGroupID(_Character, "");
PROC_CharacterMoveTo(_Character, _DestinationPoint, "Sprint", "END_Reinforcements_Arrived");

IF
LeftCombat((CHARACTER)_Character, _)
AND
DB_END_Reinfrocements_Calling(_Character)
THEN
NOT DB_END_Reinfrocements_Calling(_Character);

//REGION Reinforcement arrives at destination

//If story reaches the entity event it means the character didn't join combat on the way. Try to force a join combat.
IF
EntityEvent((CHARACTER)_Character, "END_Reinforcements_Arrived")
AND
DB_END_Reinforcements_Called(_Character, _DestinationPoint, _Combat)
THEN
PROC_END_Reinforcements_TryJoinCombat(_Character, _Combat);

PROC
PROC_END_Reinforcements_TryJoinCombat((CHARACTER)_Character, (GUIDSTRING)_Combat)
AND
DB_PartyMembers(_PartyMember)
AND
DB_Is_InCombat(_PartyMember, _Combat)
AND
NOT DB_Is_InCombat(_Character, _)
AND
GUIDToString(_Combat, _CombatString)
THEN
PROC_END_Reinforcements_JoinCombatGroup(_Character, _CombatString);
PROC_EnterCombat(_Character, _PartyMember);

IF
EnteredCombat((CHARACTER)_Character, _)
AND
DB_END_Reinforcements_Called(_Character, _DestinationPoint, _Combat)
THEN
PROC_END_Reinforcements_ClearCalledCharacter(_Character);

//Happens when a character joins combat OR remains idle at a help required location for too long. Character will then return to their original destination using Anubis.
PROC
PROC_END_Reinforcements_ClearCalledCharacter((CHARACTER)_Character)
AND
DB_END_Reinforcements_Called(_Character, _DestinationPoint, _Combat)
THEN
NOT DB_END_Reinforcements_Called(_Character, _DestinationPoint, _Combat);
PROC_ClearStoryMove(_Character);
PROC_END_Reinforcements_RestoreCombatGroup(_Character);

//END_REGION

//REGION Combat Groups

IF
EnteredCombat((CHARACTER)_Character, _Combat)
AND
DB_END_Reinforcements_Characters(_, _Character)
AND
GUIDToString(_Combat, _CombatString)
AND
GetCombatGroupID(_Character, _CurrentGroup)
AND
_CurrentGroup != _CombatString
THEN
PROC_END_Reinforcements_JoinCombatGroup(_Character, _CombatString);

PROC
PROC_END_Reinforcements_JoinCombatGroup((CHARACTER)_Character, (STRING)_CombatString)
AND
NOT DB_END_Reinforcements_InitialCombatGroup(_Character, _)
AND
GetCombatGroupID(_Character, _CurrentGroup)
THEN
DB_END_Reinforcements_InitialCombatGroup(_Character, _CurrentGroup);

PROC
PROC_END_Reinforcements_JoinCombatGroup((CHARACTER)_Character, (STRING)_CombatString)
THEN
SetCombatGroupID(_Character, _CombatString);

PROC
PROC_END_Reinforcements_RestoreCombatGroup((CHARACTER)_Character)
AND
DB_END_Reinforcements_InitialCombatGroup(_Character, _InitialGroup)
THEN
SetCombatGroupID(_Character, _InitialGroup);

//END_REGION

//REGION Defeat tracking

IF
DB_END_Reinforcements_Characters(_Index, _Character)
AND
IntegerToString(_Index, _String)
AND
Concatenate("END_Courtyard_Group_", _String, _DefeatCounter)
THEN
DB_GLO_DefeatCounter(_Character, _DefeatCounter); 
DB_END_Courtyard_DefeatCounter(_DefeatCounter, _Index);

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act3c"
