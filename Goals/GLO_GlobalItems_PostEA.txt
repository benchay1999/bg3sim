Version 1
SubGoalCombiner SGC_AND
INITSECTION
// PADs on clicking on items (instead of behaviours/constellations)
DB_ItemTemplateDialog((ITEMROOT)DEC_GEN_DjinnLamp_A_099ef425-5894-46a7-aa85-729c385dfcb8,(DIALOGRESOURCE)GLO_PAD_DjinnLamp_c808c0d1-35e3-d36b-27a4-55a67581befe, 0);
DB_ItemTemplateDialog((ITEMROOT)DEC_GEN_DjinnLamp_B_066023fe-832e-4619-84cd-fad31edc386b,(DIALOGRESOURCE)GLO_PAD_DjinnLamp_c808c0d1-35e3-d36b-27a4-55a67581befe, 0);
DB_ItemTemplateDialog((ITEMROOT)DEC_GEN_DjinnLamp_A_Activated_9fc2c59a-e53d-44dc-9270-63a0bf5418bf, (DIALOGRESOURCE)GLO_PAD_DjinnLamp_c808c0d1-35e3-d36b-27a4-55a67581befe, 0);
DB_ItemTemplateDialog((ITEMROOT)DEC_GEN_Crate_Wood_Open_A_3815452b-af92-4a4b-a77d-15eb3b5a7263,(DIALOGRESOURCE)GLO_PAD_Crate_Wood_Open_Interact_8f1099dc-16ec-1f96-6959-7666c804aa8f, 0);
DB_ItemTemplateDialog((ITEMROOT)DEC_GEN_Crate_Wood_Open_A_3815452b-af92-4a4b-a77d-15eb3b5a7263,(DIALOGRESOURCE)GLO_PAD_Crate_Wood_Open_Interact_8f1099dc-16ec-1f96-6959-7666c804aa8f, 0);
DB_ItemTemplateDialog((ITEMROOT)DEC_GEN_WorldGlobe_B_94eec216-13d6-46b3-8065-1a21b05600f5,(DIALOGRESOURCE)GLO_WorldGlobe_B_PAD_b225e148-3102-affe-7983-5fafa7b80a51, 0);
DB_ItemTemplateDialog((ITEMROOT)CONT_GEN_Globe_Rich_A_a05b9f7d-950a-4e73-9ce4-74ff71425362,(DIALOGRESOURCE)GLO_Globe_Rich_A_PAD_b0dc959c-2168-925c-8039-5ee8412665e9, 0);
DB_ItemTemplateDialog((ITEMROOT)TOOL_GEN_Telescope_A_7dc63d8d-c4b7-49a1-906d-9dff56e805f8, (DIALOGRESOURCE)GLO_Telescope_PAD_54d193f6-92c6-cc15-637d-2819ac6405f2, 0);
DB_ItemTemplateDialog((ITEMROOT)DEC_HAG_Cauldron_Gruel_A_e806bbdd-5f48-4c2d-a70d-8bb1f6131650, (DIALOGRESOURCE)GLO_PAD_HagGruel_9610f441-7e00-5d3a-1eb7-1459e6da5501, 0);
DB_ItemTemplateDialog((ITEMROOT)DEC_HAG_Cauldron_Gruel_A_Boiling_A_e798fc3b-5856-47eb-bd46-1fa9dc5bebb6, (DIALOGRESOURCE)GLO_PAD_HagGruel_9610f441-7e00-5d3a-1eb7-1459e6da5501, 0);
DB_ItemTemplateDialog((ITEMROOT)DEC_GEN_Circus_Poster_Large_A_11d9b6b9-fa1f-4fb1-8845-b7b880781d05, (DIALOGRESOURCE)WYR_Circus_AD_Poster_830103fe-d684-84dc-b4fe-5b7859aff02d, 1);
DB_ItemTemplateDialog((ITEMROOT)DEC_HAR_Fish_Rack_Big_Fish_A_042be340-dc9c-4d35-9b14-c42118e24a7f, (DIALOGRESOURCE)LOW_DocksFish_Narrator_AD_Fishes_524b14de-c8bc-311b-a2ad-753cc95f0210, 1);
DB_ItemTemplateDialog((ITEMROOT)DEC_HAR_Fish_Rack_Shark_A_f16e0845-19c4-4e8f-b6c6-da934c003d2b, (DIALOGRESOURCE)LOW_DocksFish_Narrator_AD_Shark_517182e5-03f5-b437-f02d-d107a23d3fd9, 1);
DB_ItemTemplateDialog((ITEMROOT)DEC_HAR_Fish_Rack_Shark_B_85734327-aad0-4645-a05c-0215c349559b, (DIALOGRESOURCE)LOW_DocksFish_Narrator_AD_Shark_517182e5-03f5-b437-f02d-d107a23d3fd9, 1);
DB_ItemTemplateDialog((ITEMROOT)BOOK_GEN_Paper_Gortash_Poster_A_9a5a9588-27c4-4e44-b8a2-0eeb0e932967, (DIALOGRESOURCE)GLO_AD_GortashPoster_da403c4a-5532-4d12-464d-ced365e5ab2e, 1);
// mirrors
DB_ItemTemplateDialog((ITEMROOT)FUR_GEN_Mirror_Wood_A_4569b706-fe5f-4453-a62e-99d2f2506508,(DIALOGRESOURCE)GEN_PAD_Mirror_57a87d95-5303-e7d9-2ec2-b40ec8f90f72, 0);
DB_ItemTemplateDialog((ITEMROOT)FUR_GEN_Mirror_Wood_A_Painted_052b7bba-fbbe-4d56-ab5b-b47cdfb82388,(DIALOGRESOURCE)GEN_PAD_Mirror_57a87d95-5303-e7d9-2ec2-b40ec8f90f72, 0);
DB_ItemTemplateDialog((ITEMROOT)FUR_GEN_Mirror_Wood_Standing_A_5a5d9d08-4ad4-4651-b119-57c55252bbf8,(DIALOGRESOURCE)GEN_PAD_Mirror_57a87d95-5303-e7d9-2ec2-b40ec8f90f72, 0);
DB_ItemTemplateDialog((ITEMROOT)FUR_GEN_Mirror_Wood_Standing_A_Painted_f0a8bf41-f21e-4289-8eb9-b034215f5a63,(DIALOGRESOURCE)GEN_PAD_Mirror_57a87d95-5303-e7d9-2ec2-b40ec8f90f72, 0);
DB_ItemTemplateDialog((ITEMROOT)FUR_GEN_Mirror_Wood_Round_A_6b5eabad-12d4-48e1-8a9b-a9ac3edcabf1,(DIALOGRESOURCE)GEN_PAD_Mirror_57a87d95-5303-e7d9-2ec2-b40ec8f90f72, 0);
DB_ItemTemplateDialog((ITEMROOT)FUR_GEN_Mirror_Wood_Round_A_Painted_98d4cb31-b160-41e8-97c3-8a96afb461a3,(DIALOGRESOURCE)GEN_PAD_Mirror_57a87d95-5303-e7d9-2ec2-b40ec8f90f72, 0);
DB_ItemTemplateDialog((ITEMROOT)FUR_GEN_Mirror_Wood_Squared_A_0ea687eb-0fbf-4cde-8531-a99d1e0291f7,(DIALOGRESOURCE)GEN_PAD_Mirror_57a87d95-5303-e7d9-2ec2-b40ec8f90f72, 0);
DB_ItemTemplateDialog((ITEMROOT)FUR_GEN_Mirror_Wood_Squared_A_Painted_1d15c7c5-bbce-4c41-8d13-d5a9a9677b0c,(DIALOGRESOURCE)GEN_PAD_Mirror_57a87d95-5303-e7d9-2ec2-b40ec8f90f72, 0);

//REGION Circus items
DB_GLO_GlobalItems_PitcherEffects(0, "DRINK_ALCOHOL", 10.0);
DB_GLO_GlobalItems_PitcherEffects(1, "CAUSTIC_BRINE", 1.0);

DB_GLO_GlobalItems_TeleportBoots_EquippedItemSlots("Helmet");
DB_GLO_GlobalItems_TeleportBoots_EquippedItemSlots("Breast");
DB_GLO_GlobalItems_TeleportBoots_EquippedItemSlots("Cloak");
DB_GLO_GlobalItems_TeleportBoots_EquippedItemSlots("Gloves");
DB_GLO_GlobalItems_TeleportBoots_EquippedItemSlots("VanityBody");

DB_GLO_GlobalItems_ThiefGloves_Coal(0,(ITEMROOT)LOOT_GEN_Charcoal_A_99cc464d-8d7e-493a-bc63-fdfd365e813d);
DB_GLO_GlobalItems_ThiefGloves_Coal(1,LOOT_GEN_Charcoal_B_ee1edb85-6c14-4f26-9ccc-0443a3f37324);
DB_GLO_GlobalItems_ThiefGloves_Coal(2,LOOT_GEN_Charcoal_C_4c95f360-eda0-4cf8-87f4-12147a5b9d67);
DB_GLO_GlobalItems_ThiefGloves_Coal(3,LOOT_GEN_Charcoal_D_84630063-62f4-4e9a-a578-0b5808e90e3f);
DB_GLO_GlobalItems_ThiefGloves_Coal(4,LOOT_GEN_Charcoal_E_e409b155-a938-46b5-aaf9-5b493f90e6fd);
//END_REGION

//REGION Face paints
DB_GLO_GlobalItems_FacePaints("WYR_FACEPAINT_CLOWN", "84be66ac-53ae-4243-9436-aca6c8d8de32");
DB_GLO_GlobalItems_FacePaints("GLO_PIXIECURSE", "bcd624e2-8347-4f97-a7ec-5fd52a6b49e4");
DB_Splatters_Statuses("WYR_FACEPAINT_CLOWN");
DB_Splatters_Statuses("GLO_PIXIECURSE");

DB_GLO_GlobalItems_FacePaintsOverride("WYR_FACEPAINT_CLOWN", (TAG)DRAGONBORN_02e5e9ed-b6b2-4524-99cd-cb2bc84c754a, "80ba02f5-ce5b-4611-a745-1c9406e1d780");
DB_GLO_GlobalItems_FacePaintsOverride("GLO_PIXIECURSE", (TAG)DRAGONBORN_02e5e9ed-b6b2-4524-99cd-cb2bc84c754a, "67c4eed0-be5b-4e34-a9c1-bbd3fc88eccd");
//END_REGION Face paints
KBSECTION
//REGION Circus facepaint texture change
// Face paint
IF
StatusApplied((CHARACTER)_Char, _Status, _, _)
AND
DB_GLO_GlobalItems_FacePaints(_Status, _)
THEN
PROC_GLO_GlobalItems_ApplyFacePaint(_Char, _Status);

PROC
PROC_GLO_GlobalItems_ApplyFacePaint((CHARACTER)_Char, (STRING)_FacePaint)
AND
NOT DB_GLO_GlobalItems_FacePaintApplied(_Char, _FacePaint, _)
AND
DB_GLO_GlobalItems_FacePaintsOverride(_FacePaint, _Tag, _MaterialOverrideTagged)
AND
IsTagged(_Char, _Tag, 1)
THEN
PROC_GLO_GlobalItems_ApplyMaterialOverride(_Char, _MaterialOverrideTagged);
DB_GLO_GlobalItems_FacePaintApplied(_Char, _FacePaint, _MaterialOverrideTagged);

PROC
PROC_GLO_GlobalItems_ApplyFacePaint((CHARACTER)_Char, (STRING)_FacePaint)
AND
NOT DB_GLO_GlobalItems_FacePaintApplied(_Char, _FacePaint, _)
AND
DB_GLO_GlobalItems_FacePaints(_FacePaint, _MaterialOverride)
THEN
PROC_GLO_GlobalItems_ApplyMaterialOverride(_Char, _MaterialOverride);
DB_GLO_GlobalItems_FacePaintApplied(_Char, _FacePaint, _MaterialOverride);

PROC
PROC_GLO_GlobalItems_ApplyMaterialOverride((CHARACTER)_Char, (STRING)_MaterialOverride)
AND
NOT DB_GLO_GlobalItems_FacePaintApplied(_Char, _, _MaterialOverride)
THEN
AddCustomMaterialOverride(_Char, _MaterialOverride);

IF
StatusRemoved((CHARACTER)_Char, _Status, _, _)
AND
DB_GLO_GlobalItems_FacePaintApplied(_Char, _Status, _MaterialOverride)
AND
HasActiveStatus(_Char, _Status, 0) // check in case we re-applying the same face paint again, since it doesnt stack
THEN
PROC_GLO_GlobalItems_RemoveFacePaint(_Char, _Status, _MaterialOverride);

PROC
PROC_GLO_GlobalItems_RemoveFacePaint((CHARACTER)_Char, (STRING)_FacePaint, (STRING)_MaterialOverride)
THEN
NOT DB_GLO_GlobalItems_FacePaintApplied(_Char, _FacePaint, _MaterialOverride);

PROC
PROC_GLO_GlobalItems_RemoveFacePaint((CHARACTER)_Char, _, (STRING)_MaterialOverride)
AND
NOT DB_GLO_GlobalItems_FacePaintApplied(_Char, _, _MaterialOverride)
THEN
RemoveCustomMaterialOverride(_Char, _MaterialOverride);

IF
TextEvent("testFacePaint")
AND
GetTextEventParamString(1, _Status)
AND
GetHostCharacter(_Player)
THEN
PROC_GLO_GlobalItems_ApplyFacePaint(_Player, _Status);

IF
TextEvent("removeFacePaint")
AND
GetTextEventParamString(1, _Status)
AND
GetHostCharacter(_Player)
AND
DB_GLO_GlobalItems_FacePaintApplied(_Player, _Status, _MaterialOverride)
THEN
PROC_GLO_GlobalItems_RemoveFacePaint(_Player, _Status, _MaterialOverride);
//END_REGION

//REGION Handaxe of returning
IF
OnThrown(_Item, UNI_WYR_Circus_HandaxeReturning_82999790-f80d-47f7-9383-9bb6cf57a10b, _Thrower, _,_,_,_)
AND
_Thrower != NULL_00000000-0000-0000-0000-000000000000
AND
NOT DB_GLO_GlobalItems_ReturningThrower(_Item, _)
AND
IsCharacter(_Thrower, 1)
THEN
DB_GLO_GlobalItems_ReturningThrower(_Item, _Thrower);
UseSpell(NULL_00000000-0000-0000-0000-000000000000, "Throw_WYR_Circus_AxeThrow", _Thrower, _Item);

IF
OnThrown(_Item, UNI_WYR_Circus_HandaxeReturning_82999790-f80d-47f7-9383-9bb6cf57a10b, NULL_00000000-0000-0000-0000-000000000000, _,_,_,_)
AND
DB_GLO_GlobalItems_ReturningThrower(_Item, _OriginalItemThrower)
THEN
NOT DB_GLO_GlobalItems_ReturningThrower(_Item, _OriginalItemThrower);
ToInventory(_Item, _OriginalItemThrower, 1, 1, 1);

IF
CastSpellFailed(_Item, "Throw_WYR_Circus_AxeThrow",_,_,_)
AND
DB_GLO_GlobalItems_ReturningThrower(_Item, _Thrower)
THEN
NOT DB_GLO_GlobalItems_ReturningThrower(_Item, _Thrower);
//END_REGION

//REGION Pitcher
IF
TemplateUseFinished(_Player, UNI_WYR_Circus_Pitcher_9ee5433b-ce71-4de4-871d-04f918c318ae, _Item, _)
AND
NOT DB_GLO_GlobalItems_Pitchers(_Item, _)
AND
DB_GLO_GlobalItems_PitcherEffects(1, _Status, _)
THEN
DB_GLO_GlobalItems_Pitchers(_Item, _Status);

IF
TemplateUseFinished(_Player, UNI_WYR_Circus_Pitcher_9ee5433b-ce71-4de4-871d-04f918c318ae, _Item, _)
AND
DB_GLO_GlobalItems_Pitchers(_Item, _Status)
AND
DB_GLO_GlobalItems_PitcherEffects(_, _Status, _Time)
THEN
ApplyStatus(_Player, _Status, _Time);

PROC
PROC_LongRest()
AND
DB_GLO_GlobalItems_Pitchers(_Item, _Status)
AND
Random(2, _Random)
AND
DB_GLO_GlobalItems_PitcherEffects(_Random, _NewStatus, _)
THEN
NOT DB_GLO_GlobalItems_Pitchers(_Item, _Status);
DB_GLO_GlobalItems_Pitchers(_Item, _NewStatus);
//END_REGION

//REGION Circus magical teleport footwear
IF
UsingSpell(_Caster, "Target_WYR_Circus_TeleportBoots_MistyStep", _,_,_)
AND
DB_GLO_GlobalItems_TeleportBoots_WaitsForTheTimer((CHARACTER)_Caster)
THEN
NOT DB_GLO_GlobalItems_TeleportBoots_WaitsForTheTimer((CHARACTER)_Caster);
RealtimeObjectTimerCancel(_Caster, "GLO_GlobalItems_TeleportBoots_Timer");

IF
UsingSpell(_Caster, "Target_WYR_Circus_TeleportBoots_MistyStep", _,_,_)
AND
DB_GLO_GlobalItems_TeleportBoots_WaitsToBeUnequipped((CHARACTER)_Caster, _Item, _X, _Y, _Z)
THEN
NOT DB_GLO_GlobalItems_TeleportBoots_WaitsToBeUnequipped((CHARACTER)_Caster, _Item, _X, _Y, _Z);

IF
UsingSpell(_Caster, "Target_WYR_Circus_TeleportBoots_MistyStep", _,_,_)
AND
GetPosition(_Caster, _X, _Y, _Z)
AND
DB_GLO_GlobalItems_TeleportBoots_EquippedItemSlots(_Slot)
AND
GetEquippedItem((CHARACTER)_Caster, _Slot, _Item)
THEN
DB_GLO_GlobalItems_TeleportBoots_WaitsToBeUnequipped(_Caster, _Item, _X, _Y, _Z);
DB_GLO_GlobalItems_TeleportBoots_WaitsForTheTimer(_Caster);
RealtimeObjectTimerLaunch(_Caster, "GLO_GlobalItems_TeleportBoots_Timer", 500);

IF
ObjectTimerFinished(_Caster, "GLO_GlobalItems_TeleportBoots_Timer")
AND
DB_GLO_GlobalItems_TeleportBoots_WaitsForTheTimer((CHARACTER)_Caster)
AND
DB_GLO_GlobalItems_TeleportBoots_WaitsToBeUnequipped((CHARACTER)_Caster, _Item, _X, _Y, _Z)
THEN
Unequip((CHARACTER)_Caster, _Item);

IF
ObjectTimerFinished(_Caster, "GLO_GlobalItems_TeleportBoots_Timer")
AND
DB_GLO_GlobalItems_TeleportBoots_WaitsForTheTimer((CHARACTER)_Caster)
THEN
NOT DB_GLO_GlobalItems_TeleportBoots_WaitsForTheTimer((CHARACTER)_Caster);

IF
Unequipped(_Item, _Caster)
AND
DB_GLO_GlobalItems_TeleportBoots_WaitsToBeUnequipped(_Caster, _Item, _X, _Y, _Z)
THEN
NOT DB_GLO_GlobalItems_TeleportBoots_WaitsToBeUnequipped(_Caster, _Item, _X, _Y, _Z);
ScatterAt(_Item, _X, _Y, _Z);
//END_REGION

//REGION Circus ThiefGloves
IF
CharacterStoleItem(_Character, _,_,_,_,_,_,_,_Amount,_)
AND
_Amount > 0
AND
IsTagged(_Character, (TAG)ACT3_WYR_CIRCUS_THIEFGLOVES_3777b973-c1d2-45db-9c3b-f055cfc4dc6f, 1)
THEN
PROC_GLO_GlobalItems_GiveCoal(_Character);

IF
CharacterPickpocketSuccess(_Character, _,_,_,_Amount,_)
AND
_Amount > 0
AND
IsTagged(_Character, (TAG)ACT3_WYR_CIRCUS_THIEFGLOVES_3777b973-c1d2-45db-9c3b-f055cfc4dc6f, 1)
THEN
PROC_GLO_GlobalItems_GiveCoal(_Character);

PROC
PROC_GLO_GlobalItems_GiveCoal((CHARACTER)_Character)
AND
NOT DB_GLO_GlobalItems_ThiefGloves_CoalsTotal(_Character, _)
THEN
DB_GLO_GlobalItems_ThiefGloves_CoalsTotal(_Character, 0);

PROC
PROC_GLO_GlobalItems_GiveCoal((CHARACTER)_Character)
AND
DB_GLO_GlobalItems_ThiefGloves_CoalsTotal(_Character, _CurrentCoals)
AND
_CurrentCoals >= 20
AND
CreateAtObject(LOOT_Bathroom_Bucket_Coals_A_d9aa2e72-03c0-4b77-aca0-638ce2ba948a, _Character, 0, 0, "", 0, _CoalItem)
THEN
NOT DB_GLO_GlobalItems_ThiefGloves_CoalsTotal(_Character, _CurrentCoals);
ToInventory(_CoalItem, _Character, 1, 1, 1);
ApplyStatus(_Character, "BURNING", 6.0, 0, NULL_00000000-0000-0000-0000-000000000000);

PROC
PROC_GLO_GlobalItems_GiveCoal((CHARACTER)_Character)
AND
DB_GLO_GlobalItems_ThiefGloves_CoalsTotal(_Character, _CurrentCoals)
AND
_CurrentCoals < 20
AND
Random(5, _Random)
AND
DB_GLO_GlobalItems_ThiefGloves_Coal(_Random, _CoalTemplate)
AND
CreateAtObject(_CoalTemplate, _Character, 0, 0, "", 0, _CoalItem)
AND
IntegerSum(_CurrentCoals, 1, _CoalsPlusOne)
THEN
ToInventory(_CoalItem, _Character, 1, 1, 1);
NOT DB_GLO_GlobalItems_ThiefGloves_CoalsTotal(_Character, _CurrentCoals);
DB_GLO_GlobalItems_ThiefGloves_CoalsTotal(_Character, _CoalsPlusOne);
//END_REGION

//REGION Broken lanterns
IF
TemplateUseFinished((CHARACTER)_Player, UNI_GLO_Moonlantern_Broken_2c99f1f1-776b-45dd-a9d7-06e6a8e14b3f, _, 1)
AND
DB_Players(_Player)
AND
QRY_StartDialog_Fixed(UND_BrokenMoonlantern_26dfd5df-1d47-67f1-d8a3-8f5268e85d8d, _Player)
THEN
DB_NOOP(1);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ModWrapper_Gustav"
