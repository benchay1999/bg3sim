Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_GLO_HagSpawnDoubleSpellSetup("Shout_HAG_SpawnDoubles_Phase1",2,(DIALOGRESOURCE)HAG_HagLair_AD_SpawnDoublesPhase1_f4ddf18c-82c1-87ab-b763-c8510cc2b2b5,"Phase1","","EASY");
DB_GLO_HagSpawnDoubleSpellSetup("Shout_HAG_SpawnDoubles_Phase2",3,HAG_HagLair_AD_SpawnDoublesPhase2_caf23836-4c04-d4d9-b69a-c9564ec56d6d,"Phase2","","EASY");
DB_GLO_HagSpawnDoubleSpellSetup("Shout_LOW_Hag_SpawnDoubles_Phase1",3,HAG_HagLair_AD_SpawnDoublesPhase1_f4ddf18c-82c1-87ab-b763-c8510cc2b2b5,"","ClearDoublesOnCast","EASY");
DB_GLO_HagSpawnDoubleSpellSetup("Shout_LOW_Hag_SpawnDoubles_Phase2",3,HAG_HagLair_AD_SpawnDoublesPhase1_f4ddf18c-82c1-87ab-b763-c8510cc2b2b5,"","ClearDoublesOnCast","EASY");
DB_GLO_HagSpawnDoubleSpellSetup("Shout_LOW_Hag_SpawnDoubles_Phase3",4,HAG_HagLair_AD_SpawnDoublesPhase1_f4ddf18c-82c1-87ab-b763-c8510cc2b2b5,"","ClearDoublesOnCast","EASY");
DB_GLO_HagSpawnDoubleSpellSetup("Shout_LOW_Hag_SpawnDoubles_Phase4",4,HAG_HagLair_AD_SpawnDoublesPhase2_caf23836-4c04-d4d9-b69a-c9564ec56d6d,"","ClearDoublesOnCast","EASY");

DB_GLO_HagSpawnDoubleSpellSetup("Shout_HAG_SpawnDoubles_Phase1",3,(DIALOGRESOURCE)HAG_HagLair_AD_SpawnDoublesPhase1_f4ddf18c-82c1-87ab-b763-c8510cc2b2b5,"Phase1","","MEDIUM");
DB_GLO_HagSpawnDoubleSpellSetup("Shout_HAG_SpawnDoubles_Phase2",4,HAG_HagLair_AD_SpawnDoublesPhase2_caf23836-4c04-d4d9-b69a-c9564ec56d6d,"Phase2","","MEDIUM");
DB_GLO_HagSpawnDoubleSpellSetup("Shout_LOW_Hag_SpawnDoubles_Phase1",3,HAG_HagLair_AD_SpawnDoublesPhase1_f4ddf18c-82c1-87ab-b763-c8510cc2b2b5,"","ClearDoublesOnCast","MEDIUM");
DB_GLO_HagSpawnDoubleSpellSetup("Shout_LOW_Hag_SpawnDoubles_Phase2",4,HAG_HagLair_AD_SpawnDoublesPhase1_f4ddf18c-82c1-87ab-b763-c8510cc2b2b5,"","ClearDoublesOnCast","MEDIUM");
DB_GLO_HagSpawnDoubleSpellSetup("Shout_LOW_Hag_SpawnDoubles_Phase3",4,HAG_HagLair_AD_SpawnDoublesPhase1_f4ddf18c-82c1-87ab-b763-c8510cc2b2b5,"","ClearDoublesOnCast","MEDIUM");
DB_GLO_HagSpawnDoubleSpellSetup("Shout_LOW_Hag_SpawnDoubles_Phase4",5,HAG_HagLair_AD_SpawnDoublesPhase2_caf23836-4c04-d4d9-b69a-c9564ec56d6d,"","ClearDoublesOnCast","MEDIUM");

DB_GLO_HagSpawnDoubleSpellSetup("Shout_HAG_SpawnDoubles_Phase1",5,(DIALOGRESOURCE)HAG_HagLair_AD_SpawnDoublesPhase1_f4ddf18c-82c1-87ab-b763-c8510cc2b2b5,"Phase1","","HARD");
DB_GLO_HagSpawnDoubleSpellSetup("Shout_HAG_SpawnDoubles_Phase2",6,HAG_HagLair_AD_SpawnDoublesPhase2_caf23836-4c04-d4d9-b69a-c9564ec56d6d,"Phase2","","HARD");
DB_GLO_HagSpawnDoubleSpellSetup("Shout_LOW_Hag_SpawnDoubles_Phase1",3,HAG_HagLair_AD_SpawnDoublesPhase1_f4ddf18c-82c1-87ab-b763-c8510cc2b2b5,"","ClearDoublesOnCast","HARD");
DB_GLO_HagSpawnDoubleSpellSetup("Shout_LOW_Hag_SpawnDoubles_Phase2",4,HAG_HagLair_AD_SpawnDoublesPhase1_f4ddf18c-82c1-87ab-b763-c8510cc2b2b5,"","ClearDoublesOnCast","HARD");
DB_GLO_HagSpawnDoubleSpellSetup("Shout_LOW_Hag_SpawnDoubles_Phase3",5,HAG_HagLair_AD_SpawnDoublesPhase1_f4ddf18c-82c1-87ab-b763-c8510cc2b2b5,"","ClearDoublesOnCast","HARD");
DB_GLO_HagSpawnDoubleSpellSetup("Shout_LOW_Hag_SpawnDoubles_Phase4",6,HAG_HagLair_AD_SpawnDoublesPhase2_caf23836-4c04-d4d9-b69a-c9564ec56d6d,"","ClearDoublesOnCast","HARD");
KBSECTION
//REGION Doubles summoning logic depending on what spell was used by the hag
IF
UsingSpell(S_HAG_Hag_c457d064-83fb-4ec6-b74d-1f30dfafd12d,_SummonDoubleSpell,_,_,_)
AND
DB_GLO_HagSpawnDoubleSpellSetup(_SummonDoubleSpell,_,(DIALOGRESOURCE)HAG_HagLair_AD_SpawnDoublesPhase1_f4ddf18c-82c1-87ab-b763-c8510cc2b2b5,_,_,_)
AND
GetPosition(S_HAG_Hag_c457d064-83fb-4ec6-b74d-1f30dfafd12d,_X,_Y,_Z)
AND
RealSum(_Y,1.5,_NewY)
AND
CreateAt(QUEST_HAG_AdHelper_35f37f05-6942-42dd-98a9-d5c6ebf624eb,_X,_NewY,_Z,0,0,"",_Helper)
THEN
PROC_TryStartAD(HAG_HagLair_AD_SpawnDoublesPhase1_f4ddf18c-82c1-87ab-b763-c8510cc2b2b5,_Helper);
DB_HAG_SpawnDoubles_Phase1_DialogHelper(_Helper);

IF
UsingSpell(S_HAG_Hag_c457d064-83fb-4ec6-b74d-1f30dfafd12d,_SummonDoubleSpell,_,_,_)
AND
DB_GLO_HagSpawnDoubleSpellSetup(_SummonDoubleSpell,_,(DIALOGRESOURCE)HAG_HagLair_AD_SpawnDoublesPhase2_caf23836-4c04-d4d9-b69a-c9564ec56d6d,_,_,_)
THEN
PROC_TryStartAD(HAG_HagLair_AD_SpawnDoublesPhase2_caf23836-4c04-d4d9-b69a-c9564ec56d6d,S_HAG_Hag_c457d064-83fb-4ec6-b74d-1f30dfafd12d);

IF
CastedSpell(S_HAG_Hag_c457d064-83fb-4ec6-b74d-1f30dfafd12d,_SummonDoubleSpell,_,_,_)
AND
DB_GLO_HagSpawnDoubleSpellSetup(_SummonDoubleSpell,_,_,_SoundState,_,_)
AND
_SoundState != ""
THEN
TriggerSetSoundState(Amb_SV_Hag_Lair_FZ_513adaab-3e65-4fee-ab08-762b78fd3df3, "CombatPhase_Hag", _SoundState, 1);
TriggerSetSoundState(Amb_SV_Hag_Lair_Laboratory_FZ_0fc035a5-4cc8-464e-956c-95f92d2a715d, "CombatPhase_Hag", _SoundState, 1);

IF
CastSpell(S_HAG_Hag_c457d064-83fb-4ec6-b74d-1f30dfafd12d,_SummonDoubleSpell,_,_,_)
AND
GetRulesetModifierString(SCRIPTED_COMBAT_MECHANICS_cac2d8bd-c197-4a84-9df1-f86f54ad4521,_Difficulty)
AND
DB_GLO_HagSpawnDoubleSpellSetup(_SummonDoubleSpell,_SummonAmount,_,_,_,_Difficulty)
THEN
PROC_HAG_SpawnDoubles_GetAvailableSpawnPoints(S_HAG_Hag_c457d064-83fb-4ec6-b74d-1f30dfafd12d);
PROC_HAG_SpawnDoubles(S_HAG_Hag_c457d064-83fb-4ec6-b74d-1f30dfafd12d,_SummonAmount);
SetEntityEventReal(S_HAG_Hag_c457d064-83fb-4ec6-b74d-1f30dfafd12d,"GLO_CombatWait",2.6);

IF
CastSpell(S_HAG_Hag_c457d064-83fb-4ec6-b74d-1f30dfafd12d,_SummonDoubleSpell,_,_,_)
AND
GetRulesetModifierString(SCRIPTED_COMBAT_MECHANICS_cac2d8bd-c197-4a84-9df1-f86f54ad4521,_Difficulty)
AND
NOT DB_GLO_HagSpawnDoubleSpellSetup(_SummonDoubleSpell,_,_,_,_,_Difficulty)
THEN
PROC_GLO_NoDifficultyDefinedHagDoublesSpawn(_SummonDoubleSpell);

PROC
PROC_GLO_NoDifficultyDefinedHagDoublesSpawn((STRING)_SummonDoubleSpell)
AND
DB_GLO_HagSpawnDoubleSpellSetup(_SummonDoubleSpell,_SummonAmount,_,_,_,"MEDIUM")
THEN
PROC_HAG_SpawnDoubles_GetAvailableSpawnPoints(S_HAG_Hag_c457d064-83fb-4ec6-b74d-1f30dfafd12d);
PROC_HAG_SpawnDoubles(S_HAG_Hag_c457d064-83fb-4ec6-b74d-1f30dfafd12d,_SummonAmount);
SetEntityEventReal(S_HAG_Hag_c457d064-83fb-4ec6-b74d-1f30dfafd12d,"GLO_CombatWait",2.6);

IF
AutomatedDialogEnded((DIALOGRESOURCE)HAG_HagLair_AD_SpawnDoublesPhase1_f4ddf18c-82c1-87ab-b763-c8510cc2b2b5,_)
AND
DB_HAG_SpawnDoubles_Phase1_DialogHelper(_Helper)
THEN
NOT DB_HAG_SpawnDoubles_Phase1_DialogHelper(_Helper);
Die((ITEM)_Helper);
SetOnStage(_Helper,0);
//END_REGION

//REGION Choosing a spawn point for hag double
//Do not use spawn points that are too close to the caster
PROC
PROC_HAG_SpawnDoubles_GetAvailableSpawnPoints((CHARACTER)_Char)
AND
DB_HAG_HagLair_DoublesSpawnPoints((TRIGGER)_Trigger)
AND
GetDistanceTo(_Char,_Trigger,_Dist)
AND
_Dist > 6.0
AND
_Dist < 22.0
AND
NOT QRY_IsInDangerousSurfaceFor(_Trigger,_Char,2.0)
AND
QRY_HAG_HagLair_SpawnDoubles_NoNearbyPlayers(_Trigger)
AND
DB_HAG_HagLair_SpawnDoubles_NoPlayersNearby(1)
THEN
SysClear("DB_HAG_HagLair_SpawnDoubles_NoPlayersNearby",1);
DB_HAG_HagLair_DoublesSpawnPoints_Available(_Trigger);

// Check spawn points for nearby players
QRY
QRY_HAG_HagLair_SpawnDoubles_NoNearbyPlayers((TRIGGER)_Trigger)
AND
DB_Players(_Player)
AND
GetDistanceTo(_Player,_Trigger,_Dist)
AND
_Dist < 4.0
THEN
DB_HAG_HagLair_SpawnDoubles_PlayerNearSpawn(1);

QRY
QRY_HAG_HagLair_SpawnDoubles_NoNearbyPlayers((TRIGGER)_Trigger)
AND
SysCount("DB_HAG_HagLair_SpawnDoubles_PlayerNearSpawn",1,_Int)
AND
_Int == 0
THEN
DB_HAG_HagLair_SpawnDoubles_NoPlayersNearby(1);

QRY
QRY_HAG_HagLair_SpawnDoubles_NoNearbyPlayers((TRIGGER)_Trigger)
AND
DB_HAG_HagLair_SpawnDoubles_PlayerNearSpawn(1)
THEN
NOT DB_HAG_HagLair_SpawnDoubles_PlayerNearSpawn(1);

//Find Closest Spawn Point
PROC
PROC_HAG_SpawnDoubles((CHARACTER)_Char,(INTEGER)_Int)
AND
IntegerProduct(_Int, 3, _IntProduct)
AND
IntegerSum(_IntProduct, 5, _MinDistance)
AND
DB_HAG_HagLair_DoublesSpawnPoints_Available((TRIGGER)_Trigger)
AND
GetDistanceTo(_Trigger, _Char, _DistanceCasterToTrigger)
AND
_DistanceCasterToTrigger > _MinDistance
AND
QRY_HAG_HagLair_DoublesSpawnPoint_GetClosest(_Char,_Trigger)
THEN
DB_NOOP(1);

//If undefined, then add DB
QRY
QRY_HAG_HagLair_DoublesSpawnPoint_GetClosest((CHARACTER)_Char,(TRIGGER)_Trigger)
AND
NOT DB_HAG_HagLair_DoublesSpawnPoints_DistToCaster(_,_)
AND
GetDistanceTo(_Char,_Trigger,_Dist)
THEN
DB_HAG_HagLair_DoublesSpawnPoints_DistToCaster(_Trigger,_Dist);

//If new distance is shorter, then remove old DB
QRY
QRY_HAG_HagLair_DoublesSpawnPoint_GetClosest((CHARACTER)_Char,(TRIGGER)_Trigger)
AND
GetDistanceTo(_Char,_Trigger,_Dist)
AND
DB_HAG_HagLair_DoublesSpawnPoints_DistToCaster(_OtherTrigger,_OtherDist)
AND
_Trigger != _OtherTrigger
AND
_Dist <= _OtherDist
THEN
DB_HAG_HagLair_DoublesSpawnPoints_DistToCaster(_Trigger,_Dist);
NOT DB_HAG_HagLair_DoublesSpawnPoints_DistToCaster(_OtherTrigger,_OtherDist);
//END_REGION

//REGION Spawning hag doubles
//Find random spots to spawn Hag Doubles
PROC
PROC_HAG_SpawnDoubles((CHARACTER)_Char,(INTEGER)_Int)
AND
_Int != 0
AND
DB_HAG_HagLair_DoublesSpawnPoints_DistToCaster(_Trigger,_Dist)
AND
DB_HAG_HagLair_DoublesSpawnPoints_Available(_Trigger)
AND
QRY_HAG_SpawnDoubles_FindValidPosition(_Char,_Trigger)
AND
DB_HAG_SpawnDoubles_FindValidPosition(_Char,_X,_Y,_Z)
AND
CreateAt(Hag_Green_Act1Hag_Double_8c85932d-12dd-4046-a694-6534071470a2,_X,_Y,_Z,1,1,"",_HagDouble)
AND
GetHitpoints(_Char,_Hp)
AND
DB_HAG_HagLair_DoublesSpawnFxHelper(_Helper)
THEN
NOT DB_HAG_HagLair_DoublesSpawnPoints_Available(_Trigger);
NOT DB_HAG_HagLair_DoublesSpawnPoints_DistToCaster(_Trigger,_Dist);
NOT DB_HAG_SpawnDoubles_FindValidPosition(_Char,_X,_Y,_Z);
DB_HAG_HagLair_Doubles(_HagDouble);
DB_GLO_SetHagDoubleHP(_HagDouble,_Hp);
SetImmortal((CHARACTER)_HagDouble,1);
PROC_CharacterDisableAllCrimes(_HagDouble, 1);
SetCombatGroupID(_HagDouble,"HAG_Hag_State_HagInLair");
PROC_HAG_SpawnDoubles_PlayVFX(_HagDouble,_X,_Y,_Z);
PROC_HAG_ApplyDoublesStatus(_HagDouble,-1.0);
PROC_HAG_SetDoublesAttitudeToPlayers(_HagDouble);
PROC_GLO_HagDoublePostSpawnSetup(_HagDouble);

// Setting hp of hag doubles is deffered this way so "HEALTHBOOST_HARDCORE" doesn't override it
IF
EnteredCombat(_HagDouble,_CombatID)
AND
DB_GLO_SetHagDoubleHP(_HagDouble,_Hp)
THEN
SetHitpoints(_HagDouble,_Hp);
NOT DB_GLO_SetHagDoubleHP(_HagDouble,_Hp);

PROC
PROC_HAG_SpawnDoubles_PlayVFX((CHARACTER)_HagDouble,(REAL)_X,(REAL)_Y,(REAL)_Z)
AND
NOT DB_GLO_HagCounterswapInProgress(1)
THEN
PlayEffectAtPosition(VFX_Script_Hag_SpawnDoubles_Reappear_01_5259b176-0aaf-e1a0-663e-85fdebbaa681,_X,_Y,_Z);
PlayEffect(_HagDouble,VFX_Script_Hag_SpawnDoubles_Reappear_Overlay_01_3a1bf6ef-f124-b468-348b-516ae935e813,"Dummy_BodyFX");

PROC
PROC_GLO_HagDoublePostSpawnSetup((GUIDSTRING)_HagDouble)
AND
DB_CurrentLevel("WLD_Main_A")
THEN
SetCombatGroupID(_HagDouble,"HAG_Hag_State_HagInLair");
SetTag(_HagDouble,(TAG)AI_USESPELL_A_33aa698c-aea0-43e2-bd16-582aa718fd3e);

//Find Position
QRY
QRY_HAG_SpawnDoubles_FindValidPosition((CHARACTER)_Char,(TRIGGER)_Trigger)
AND
GetPosition(_Trigger,_X,_Y,_Z)
AND
FindValidPosition(_X,_Y,_Z,10.0,_Char,1,_NewX,_NewY,_NewZ)
THEN
DB_HAG_SpawnDoubles_FindValidPosition(_Char,_NewX,_NewY,_NewZ);

//Fallback
QRY
QRY_HAG_SpawnDoubles_FindValidPosition((CHARACTER)_Char,(TRIGGER)_Trigger)
AND
NOT DB_HAG_SpawnDoubles_FindValidPosition(_Char,_,_,_)
AND
GetPosition(_Trigger,_X,_Y,_Z)
THEN
DB_HAG_SpawnDoubles_FindValidPosition(_Char,_X,_Y,_Z);

//Teleport Caster
PROC
PROC_HAG_SpawnDoubles((CHARACTER)_Char,(INTEGER)_Int)
AND
_Int == 0
AND
DB_HAG_HagLair_DoublesSpawnPoints_DistToCaster(_Trigger,_Dist)
AND
DB_HAG_HagLair_DoublesSpawnFxHelper(_Helper)
AND
QRY_HAG_SpawnDoubles_FindValidPosition(_Char,_Trigger)
AND
DB_HAG_SpawnDoubles_FindValidPosition(_Char,_X,_Y,_Z)
THEN
NOT DB_HAG_SpawnDoubles_FindValidPosition(_Char,_X,_Y,_Z);
PROC_HAG_SpawnDoubles_CasterTeleport(_Char,_X,_Y,_Z);
PROC_HAG_ApplyDoublesStatus(_Char,-1.0);
TimerLaunch("HAG_HagLair_HagSpawnFx",100);
PROC_HAG_HagLair_DoublesSpawnPoints_CleanUp();

PROC
PROC_HAG_SpawnDoubles_CasterTeleport((CHARACTER)_Character,(REAL)_X,(REAL)_Y,(REAL)_Z)
AND
NOT DB_GLO_HagCounterswapInProgress(1)
THEN
TeleportToPosition(_Character,_X,_Y,_Z,"",0,0,0);
PlayEffectAtPosition(VFX_Script_Hag_SpawnDoubles_Reappear_01_5259b176-0aaf-e1a0-663e-85fdebbaa681,_X,_Y,_Z);
PlayEffect(_Character,VFX_Script_Hag_SpawnDoubles_Reappear_Overlay_01_3a1bf6ef-f124-b468-348b-516ae935e813,"Dummy_BodyFX");

PROC
PROC_HAG_HagLair_DoublesSpawnPoints_CleanUp()
THEN
SysClear("DB_HAG_HagLair_DoublesSpawnPoints_Available", 1);
SysClear("DB_HAG_HagLair_DoublesSpawnPoints_DistToCaster", 2);
SysClear("DB_HAG_SpawnDoubles_FindValidPosition", 4);

//Hag Doubles status apply helpers
PROC
PROC_HAG_ApplyDoublesStatus((CHARACTER)_Char,(REAL)_Duration)
AND
HasActiveStatus(_Char,"HAG_DOUBLES",0)
THEN
ApplyStatus(_Char,"HAG_DOUBLES",_Duration,1,NULL_00000000-0000-0000-0000-000000000000);

//CLEAN UP OR RESET TILL COUNT IS 0
PROC
PROC_HAG_SpawnDoubles((CHARACTER)_Char,(INTEGER)_Int)
AND
IntegerSubtract(_Int,1,_NewInt)
AND
QRY_HAG_SpawnDoubles_Reset(_Char,_NewInt)
THEN
DB_NOOP(1);

//Reset if we still have Doubles to make
QRY
QRY_HAG_SpawnDoubles_Reset((CHARACTER)_Char,(INTEGER)_Int)
AND
_Int >= 0
THEN
PROC_HAG_SpawnDoubles(_Char,_Int);

PROC
PROC_HAG_SpawnDoubles((CHARACTER)S_HAG_Hag_c457d064-83fb-4ec6-b74d-1f30dfafd12d,0)
THEN
PROC_GLO_ClearHagDoublesOverflow();
//END_REGION

//REGION FX
//Fx Helper
PROC
PROC_HAG_SpawnDoubles_GetAvailableSpawnPoints((CHARACTER)_Char)
AND
CreateAtObject(Helper_Invisible_Walkthrough_c13a872b-7d9b-4c1d-8c65-f672333b0c11,_Char,0,0,"",0,_Helper)
THEN
DB_HAG_HagLair_DoublesSpawnFxHelper(_Helper);
RealtimeObjectTimerLaunch(_Helper,"HAG_HagLair_DoublesSpawnFx",3000);

//Beam Fx to doubles
IF
TimerFinished("HAG_HagLair_HagSpawnFx")
AND
DB_HAG_HagLair_DoublesSpawnFxHelper(_Helper)
AND
DB_HAG_HagLair_Doubles(_HagDouble)
THEN
PlayBeamEffect(_Helper,_HagDouble,VFX_Script_Hag_SpawnDoubles_Beam_01_1b57f75e-e1ed-c838-3421-0b21422b2a0e,"","");

//Beam Fx to hag
IF
TimerFinished("HAG_HagLair_HagSpawnFx")
AND
DB_HAG_HagLair_DoublesSpawnFxHelper(_Helper)
THEN
PlayBeamEffect(_Helper,S_HAG_Hag_c457d064-83fb-4ec6-b74d-1f30dfafd12d,VFX_Script_Hag_SpawnDoubles_Beam_01_1b57f75e-e1ed-c838-3421-0b21422b2a0e,"","");

//Remove Fx helper after
IF
ObjectTimerFinished((ITEM)_Helper,"HAG_HagLair_DoublesSpawnFx")
AND
DB_HAG_HagLair_DoublesSpawnFxHelper(_Helper)
THEN
Die(_Helper);
SetOnStage(_Helper,0);
NOT DB_HAG_HagLair_DoublesSpawnFxHelper(_Helper);
//END_REGION

//REGION Killing Hag Doubles

IF
StatusRemoved(_Double,"HAG_DOUBLES",_,_)
AND
DB_HAG_HagLair_Doubles(_Double)
THEN
PROC_TryStartAD((DIALOGRESOURCE)HAG_HagLair_AD_DoubleDied_9ee97b03-a862-60c3-1e5e-79de7911f0cd,_Double);
SetEntityEvent(_Double,"HAG_RemoveDouble");

IF
EnteredChasm(_Double,_,_,_,_,_)
AND
DB_HAG_HagLair_Doubles(_Double)
THEN
PROC_TryStartAD((DIALOGRESOURCE)HAG_HagLair_AD_DoubleDied_9ee97b03-a862-60c3-1e5e-79de7911f0cd,_Double);
SetEntityEvent(_Double,"HAG_RemoveDouble");

IF
FlagSet(HAG_Hag_State_HagDefeated_7a1acad0-589f-ae02-f712-f0d24080d720, _, _) // flagType: Global
AND
DB_HAG_HagLair_Doubles(_Double)
THEN
SetEntityEvent(_Double,"HAG_RemoveDouble");

PROC
PROC_LeftCombat(S_HAG_Hag_c457d064-83fb-4ec6-b74d-1f30dfafd12d,_)
AND
DB_HAG_HagLair_Doubles(_Double)
THEN
SetEntityEvent(_Double,"HAG_RemoveDouble");

IF
EntityEvent(_Double,"HAG_RemoveDouble")
AND
NOT DB_HAG_HagLair_DoubleRemoving(_Double)
THEN
DB_HAG_HagLair_DoubleRemoving(_Double);
SetEntityEventReal(_Double,"GLO_CombatWait",5.0);
PROC_GLO_BreakConcentration(_Double,_Double);
PlayEffect(_Double,(EFFECTRESOURCE)VFX_Script_Hag_IllusionHit_01_85f79ea8-36ea-8a43-e21b-48ea94bdf4db,"Dummy_BodyFX");
RealtimeObjectTimerLaunch(_Double,"HAG_RemoveDouble_Timer",1000);

IF
ObjectTimerFinished(_Double,"HAG_RemoveDouble_Timer")
AND
IsOnStage(_Double, 1)
THEN
PROC_Poof(_Double,(EFFECTRESOURCE)VFX_Script_Hag_Poof_01_a5ac4b11-0c02-a7a6-5c41-a95cf56c847f);

IF
WentOnStage(_Double, 0)
AND
DB_HAG_HagLair_DoubleRemoving(_Double)
THEN
NOT DB_HAG_HagLair_DoubleRemoving(_Double);
PROC_HAG_HagLair_Doubles_Removed(_Double);

PROC
PROC_HAG_HagLair_Doubles_Removed((GUIDSTRING)_Double)
THEN
DB_NOOP(1);

IF
ObjectTimerFinished(_Double,"HAG_RemoveDouble_Timer")
AND
DB_HAG_HagLair_Doubles(_Double)
THEN
NOT DB_HAG_HagLair_Doubles(_Double);
PROC_HAG_HagLair_Doubles_CountRemaining();
//END_REGION

//REGION Sync Double attitude to indiv players to Ethel's
PROC
PROC_HAG_SetDoublesAttitudeToPlayers((CHARACTER)_HagDouble)
AND
DB_Players(_Player)
AND
GetAttitudeTowardsPlayer(S_HAG_Hag_c457d064-83fb-4ec6-b74d-1f30dfafd12d,_Player,_Attitude)
THEN
PROC_SetAttitude(_HagDouble,_Player,_Attitude);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "GLO_Hag"
