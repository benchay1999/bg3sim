Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_GLO_PaladinOathbreaker_SubclassOathBrokenFlags((FLAG)GLO_PaladinOathbreaker_Event_CrownBrokeOath_b6344b22-0d02-489f-acc8-04a18671fe5b, (TAG)PALADIN_CROWN_7efcadc0-9e88-4d0a-87b9-64a589e2df91);
DB_GLO_PaladinOathbreaker_SubclassTags((TAG)PALADIN_CROWN_7efcadc0-9e88-4d0a-87b9-64a589e2df91, (TAG)OATHBREAKER_CROWN_dc03261b-0480-4338-80e5-50c2b5ca230b);

// EludeArrest: Used for eluding an arrest or resisitng an inspection (generally attacking the investigator). Not a real crime, only used for validation and in specific moments.
DB_GLO_PaladinOathbreaker_CrimesToReact((TAG)PALADIN_CROWN_7efcadc0-9e88-4d0a-87b9-64a589e2df91, "EludeArrest", (TAG)CIVILIAN_71120d5d-3853-46e4-9762-33f59aa6b4ae);
DB_GLO_PaladinOathbreaker_CrimesToReact((TAG)PALADIN_CROWN_7efcadc0-9e88-4d0a-87b9-64a589e2df91, "EludeArrest", (TAG)CROWDSYS_NPC_1274ffab-2fa2-4d6f-801e-751664ceb771);
DB_GLO_PaladinOathbreaker_CrimesToReact((TAG)PALADIN_CROWN_7efcadc0-9e88-4d0a-87b9-64a589e2df91, "EludeArrest", (TAG)KID_ee978587-6c68-4186-9bfc-3b3cc719a835);
DB_GLO_PaladinOathbreaker_CrimesToReact((TAG)PALADIN_CROWN_7efcadc0-9e88-4d0a-87b9-64a589e2df91, "EludeArrest", (TAG)GUARD_0b52f35e-fb1f-4865-bcd2-5d21ef7343cd);

KBSECTION
//Palading of the crown
IF
DB_GLO_PaladinOathbreaker_GenericCrimes(_Crime)
THEN
DB_GLO_PaladinOathbreaker_CrimesToReact((TAG)PALADIN_CROWN_7efcadc0-9e88-4d0a-87b9-64a589e2df91, _Crime, (TAG)CIVILIAN_71120d5d-3853-46e4-9762-33f59aa6b4ae);
DB_GLO_PaladinOathbreaker_CrimesToReact((TAG)PALADIN_CROWN_7efcadc0-9e88-4d0a-87b9-64a589e2df91, _Crime, (TAG)CROWDSYS_NPC_1274ffab-2fa2-4d6f-801e-751664ceb771);
DB_GLO_PaladinOathbreaker_CrimesToReact((TAG)PALADIN_CROWN_7efcadc0-9e88-4d0a-87b9-64a589e2df91, _Crime, (TAG)KID_ee978587-6c68-4186-9bfc-3b3cc719a835);
DB_GLO_PaladinOathbreaker_CrimesToReact((TAG)PALADIN_CROWN_7efcadc0-9e88-4d0a-87b9-64a589e2df91, _Crime, (TAG)GUARD_0b52f35e-fb1f-4865-bcd2-5d21ef7343cd);

//REGION Crimes
// Eluding committed crimes 
PROC
PROC_CrimeBribes_RecordElude(_, _Victim, _Player, _)
AND
GetHandlingCrimeID((CHARACTER)_Victim, _CrimeID)
THEN
PROC_GLO_PaladinOathbreaker_MightBreakOath(_Player, "EludeArrest", _CrimeID, _Victim);

IF
FlagSet(TemporaryHostilityAfterDialog_ca0cbe39-d121-069e-b408-c02954b32cf7, (CHARACTER)_, _ID)
THEN
PROC_GLO_PaladinOathbreaker_Crown_Resist(_ID);

// Resisting inspection
IF
FlagSet(CRIME_Resist_Search_542c71fa-0019-9464-ba46-00ff8d1856f3, _, _ID)
THEN
PROC_GLO_PaladinOathbreaker_Crown_Resist(_ID);

PROC
PROC_GLO_PaladinOathbreaker_Crown_Resist((INTEGER)_ID)
AND
DialogIsCrimeDialog(_ID, 1)
AND
DB_DialogSpeakers(_ID, _Victim, 1)
AND
DB_DialogPlayers(_ID, _Player, 1)
AND
GetHandlingCrimeID((CHARACTER)_Victim, _CrimeID)
THEN
PROC_GLO_PaladinOathbreaker_MightBreakOath((CHARACTER)_Player, "EludeArrest", _CrimeID, (CHARACTER)_Victim);
//END_REGION Crimes

//REGION Debug
IF
TextEvent("makepldcrown")
AND
DB_Players(_Player)
THEN
SetTag(_Player, (TAG)PALADIN_6d85ab2d-5c23-498c-a61e-98f05a00177a);
SetTag(_Player, (TAG)PALADIN_CROWN_7efcadc0-9e88-4d0a-87b9-64a589e2df91);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "__Shared_Campaign"
