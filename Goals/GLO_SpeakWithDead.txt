Version 1
SubGoalCombiner SGC_AND
INITSECTION
//Documentation on Confluence: [Link Redacted]

NOT DB_GLO_CharacterCorpseDialog((CHARACTER)NULL_00000000-0000-0000-0000-000000000000, (DIALOGRESOURCE)NULL_00000000-0000-0000-0000-000000000000);

// entity (item, corpse), speaker (character that actually speaks, needs to be dead), dialog
NOT DB_GLO_CustomEntityCorpseDialog((GUIDSTRING)NULL_00000000-0000-0000-0000-000000000000, (CHARACTER)NULL_00000000-0000-0000-0000-000000000000, (DIALOGRESOURCE)NULL_00000000-0000-0000-0000-000000000000);

// don't add entity to the SwD dialog, only add the speaker
NOT DB_GLO_CustomEntityCorpseDialog_IgnoreEntity((DIALOGRESOURCE)NULL_00000000-0000-0000-0000-000000000000);

DB_DialogBlockTradeButton((DIALOGRESOURCE)GLO_SpeakWithDead_Generic_14019d29-4bf1-d48c-16ef-c422882268a1);
/*
DB_GLO_SpeakWithDeadSpells("Target_SpeakWithDead");
DB_GLO_SpeakWithDeadSpells("Target_SpeakWithDead_FreeRecast");
DB_GLO_SpeakWithDeadSpells("Target_SpeakWithDead_Amulet_CHA");
DB_GLO_SpeakWithDeadSpells("Target_SpeakWithDead_Test");
*/
KBSECTION
//REGION Start speak with dead dialog
IF
StatusApplied(_Target, "SPEAK_WITH_DEAD", _Player, _)
AND
IsDead((CHARACTER)_Target, 1)
AND
DB_GLO_CharacterCorpseDialog(_Target, _Dialog)
AND
NOT QRY_PlayOriginMoment(_Dialog, _Target, _Player)
AND
QRY_StartDialogCustom_Fixed((DIALOGRESOURCE)_Dialog, _Target, _Player, 0, 0, 1, 0)
THEN
SetTag(_Target, (TAG)CORPSE_SPOKEN_249ba319-6652-47ec-9f31-ef5be55429c3);

IF
DialogEnded(_Dialog,_)
AND
DB_GLO_CharacterCorpseDialog(_Target, _Dialog)
AND
NOT QRY_GLO_CharacterCorpseHasSpoken(_Target)
THEN
ClearTag(_Target, (TAG)CORPSE_SPOKEN_249ba319-6652-47ec-9f31-ef5be55429c3);

QRY
QRY_GLO_CharacterCorpseHasSpoken((CHARACTER)_Target)
AND
DB_ObjectCountHelper(_Target, "GLO_SpeakWithDead_QuestionCount", _Count)
AND
_Count > 0
THEN
DB_NOOP(1);

IF
StatusApplied(_Target, "SPEAK_WITH_DEAD", _Player, _)
AND
IsDead((CHARACTER)_Target, 1)
AND
NOT DB_GLO_CharacterCorpseDialog(_Target, _)
AND
NOT DB_GLO_CustomEntityCorpseDialog(_Target, _, _)
AND
IsSpeakerReserved(_Target, 0)
AND
QRY_StartDialogCustom_Fixed((DIALOGRESOURCE)GLO_SpeakWithDead_Generic_14019d29-4bf1-d48c-16ef-c422882268a1, _Player, 0, 0, 1, 0)
THEN
SetTag(_Target, (TAG)CORPSE_SPOKEN_249ba319-6652-47ec-9f31-ef5be55429c3);

IF
DB_GLO_CharacterCorpseDialog(_, _SpeakWithDeadDialog)
THEN
DB_DialogBlockTradeButton(_SpeakWithDeadDialog);
//END_REGION

//REGION Track your own killers
IF
DB_GLO_CharacterCorpseDialog(_Victim, _)
THEN
DB_WitnessKiller(_Victim, _Victim);

PROC
PROC_WitnessedKiller(_Victim, _Player, _Race, _Gender, _Victim)
AND
DB_GLO_CharacterCorpseDialog(_Victim, _)
THEN
DB_SpeakWithDead_KnowsKillerShape(_Victim, _Player, _Race, _Gender);
//END_REGION

//REGION Set flag on the player in SwD dialogs if they are the killer
PROC
PROC_DialogFlagSetup(_Dialog, _Target, (CHARACTER)_Player)
AND
DB_GLO_CharacterCorpseDialog((CHARACTER)_Target, _Dialog)
AND
GetRace((CHARACTER)_Player, 1, _Race)
AND
GetGender((CHARACTER)_Player, 1, _Gender)
AND
DB_SpeakWithDead_KnowsKillerShape(_Target, _Player, _Race, _Gender)
THEN
SetFlag((FLAG)GLO_SpeakWithDead_State_Killer_7827d93e-1619-402e-b9ec-3ec5d762b6ea, _Player);
DB_SpeakWithDead_KillerFlagSet(_Player);

IF
DialogEnded(_Dialog, _ID)
AND
DB_DialogPlayers(_ID, _Player, _)
AND
DB_SpeakWithDead_KillerFlagSet((CHARACTER)_Player)
THEN
NOT DB_SpeakWithDead_KillerFlagSet(_Player);
ClearFlag((FLAG)GLO_SpeakWithDead_State_Killer_7827d93e-1619-402e-b9ec-3ec5d762b6ea, _Player);
//END_REGION

//REGION Questions Limit
IF
FlagSet(GLO_SpeakWithDead_Event_QuestionAsked_9984cd59-2fad-46cb-bf61-96af6034fbea, _Target, _)
THEN
PROC_ObjectCountHelper(_Target, "GLO_SpeakWithDead_QuestionCount");
ClearFlag(GLO_SpeakWithDead_Event_QuestionAsked_9984cd59-2fad-46cb-bf61-96af6034fbea, _Target);

IF
DB_ObjectCountHelper(_Target, "GLO_SpeakWithDead_QuestionCount", 5)
THEN
SetFlag((FLAG)GLO_SpeakWithDead_State_QuestionsLimitReached_01c40268-4a8d-4787-8566-77e01ff04762, _Target, 0);
ClearTag(_Target, (TAG)HASSWDDIALOG_21be1053-c642-4081-b64b-56678ae11594);

//END_REGION Questions Limit

//REGION Marking what characters are interesting for Speak With dead
IF
DB_GLO_CharacterCorpseDialog(_Character, _)
AND
_Character != NULL_00000000-0000-0000-0000-000000000000
THEN
DB_GLO_Internal_CharacterHasSwDDialog(_Character);

IF
DB_GLO_Internal_CharacterHasSwDDialog(_Character)
AND
GetFlag(GLO_SpeakWithDead_State_QuestionsLimitReached_01c40268-4a8d-4787-8566-77e01ff04762, _Character, 0)
THEN
SetTag(_Character, (TAG)HASSWDDIALOG_21be1053-c642-4081-b64b-56678ae11594);

IF
DB_GLO_Internal_CharacterHasSwDDialog(_Character)
AND
NOT DB_GLO_CharacterCorpseDialog(_Character, _)
THEN
NOT DB_GLO_Internal_CharacterHasSwDDialog(_Character);
ClearTag(_Character, (TAG)HASSWDDIALOG_21be1053-c642-4081-b64b-56678ae11594);

//END_REGION

//REGION Has ever used speak with dead.

IF
StatusApplied(_Target, "SPEAK_WITH_DEAD", _Player, _)
AND
IsDead((CHARACTER)_Target, 1)
AND
DB_GLO_CharacterCorpseDialog(_Target, _Dialog)
AND
NOT DB_GlobalFlag((FLAG)GLO_SpeakWithDead_State_PartySpokeWithDead_b375c49f-b908-4794-b12c-bf2c7122e19e)
THEN
PROC_GlobalSetFlagAndCache((FLAG)GLO_SpeakWithDead_State_PartySpokeWithDead_b375c49f-b908-4794-b12c-bf2c7122e19e);

IF
StatusApplied(_Target, "SPEAK_WITH_DEAD", _Player, _)
AND
IsDead((CHARACTER)_Target, 1)
AND
DB_GLO_CharacterCorpseDialog(_Target, _Dialog)
AND
GetFlag((FLAG)GLO_SpeakWithDead_State_CharacterSpokeWithDead_e1d72992-3fa4-4c5e-bdf2-2357e9a3ffed, _Player, 0)
THEN
SetFlag((FLAG)GLO_SpeakWithDead_State_CharacterSpokeWithDead_e1d72992-3fa4-4c5e-bdf2-2357e9a3ffed, _Player);



//END_REGION

//REGION Custom allowed SwD entities
IF
DB_GLO_CustomEntityCorpseDialog(_, _Corpse, _Dialog)
THEN
DB_GLO_CharacterCorpseDialog(_Corpse, _Dialog);

IF
StatusApplied(_Target, "SPEAK_WITH_DEAD", _Player, _)
AND
DB_GLO_CustomEntityCorpseDialog(_Target, _Corpse, _Dialog)
THEN
ApplyStatus(_Corpse, "SPEAK_WITH_DEAD", -1.0, 1, _Player);

PROC
PROC_StartDialog_AddExtraSpeakers((DIALOGRESOURCE)_Dialog, (INTEGER)_ID)
AND
DB_GLO_CustomEntityCorpseDialog(_Entity, _, _Dialog)
AND
NOT DB_GLO_CustomEntityCorpseDialog_IgnoreEntity(_Dialog)
THEN
PROC_DialogAddSpeakingActor(_ID, _Entity);

IF
TagSet((CHARACTER)_Corpse, HASSWDDIALOG_21be1053-c642-4081-b64b-56678ae11594)
AND
DB_GLO_CustomEntityCorpseDialog(_Entity, _Corpse, _)
THEN
SetTag(_Entity, (TAG)HASSWDDIALOG_21be1053-c642-4081-b64b-56678ae11594);

IF
TagCleared((CHARACTER)_Corpse, HASSWDDIALOG_21be1053-c642-4081-b64b-56678ae11594)
AND
DB_GLO_CustomEntityCorpseDialog(_Entity, _Corpse, _)
THEN
ClearTag(_Entity, (TAG)HASSWDDIALOG_21be1053-c642-4081-b64b-56678ae11594);

IF
TagSet((CHARACTER)_Corpse, CORPSE_SPOKEN_249ba319-6652-47ec-9f31-ef5be55429c3)
AND
DB_GLO_CustomEntityCorpseDialog(_Entity, _Corpse, _)
THEN
SetTag(_Entity, (TAG)CORPSE_SPOKEN_249ba319-6652-47ec-9f31-ef5be55429c3);

IF
TagCleared((CHARACTER)_Corpse, CORPSE_SPOKEN_249ba319-6652-47ec-9f31-ef5be55429c3)
AND
DB_GLO_CustomEntityCorpseDialog(_Entity, _Corpse, _)
THEN
ClearTag(_Entity, (TAG)CORPSE_SPOKEN_249ba319-6652-47ec-9f31-ef5be55429c3);

IF
FlagSet((FLAG)GLO_SpeakWithDead_State_QuestionsLimitReached_01c40268-4a8d-4787-8566-77e01ff04762, (CHARACTER)_Corpse, _)
AND
DB_GLO_CustomEntityCorpseDialog(_Entity, _Corpse, _)
THEN
SetFlag(GLO_SpeakWithDead_State_QuestionsLimitReached_01c40268-4a8d-4787-8566-77e01ff04762, _Entity);
//END_REGION

//REGION Set custom killer
PROC
PROC_SpeakWithDead_SetCustomKiller((CHARACTER)_Victim, (CHARACTER)_Killer)
AND
GetRace(_Killer, 1, _Race)
AND
GetGender(_Killer, 1, _Gender)
THEN
DB_SpeakWithDead_KnowsKillerShape(_Victim, _Killer, _Race, _Gender);

//END_REGION
EXITSECTION

ENDEXITSECTION
