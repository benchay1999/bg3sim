Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_GLO_LevelTraveler((CHARACTER)S_GLO_ThrallOfTheAbsolute_AbsoluteHelper_15d4bd12-855b-4483-a2e5-661bfeb96d1f, "INT_Main_A", "Dummy"); //No Level Traveler goal for Intermezzo currently

DB_DialogBlockTradeButton(GLO_ThrallOfTheAbsolute_GameOver_d736e2e2-5ee8-8b12-4680-3906c1265202);
DB_DropMutingStatussesDialog(GLO_ThrallOfTheAbsolute_GameOver_d736e2e2-5ee8-8b12-4680-3906c1265202);
DB_IgnoreMutingStatussesDialog(GLO_ThrallOfTheAbsolute_GameOver_d736e2e2-5ee8-8b12-4680-3906c1265202);
DB_DialogStarted_IgnoreStopConditions(GLO_ThrallOfTheAbsolute_GameOver_d736e2e2-5ee8-8b12-4680-3906c1265202);

DB_GLO_SetupForAct_Character("INT_Main_A", S_GLO_ThrallOfTheAbsolute_AbsoluteHelper_15d4bd12-855b-4483-a2e5-661bfeb96d1f);
DB_GLO_SetupForAct_Character("BGO_Main_A", S_GLO_ThrallOfTheAbsolute_AbsoluteHelper_15d4bd12-855b-4483-a2e5-661bfeb96d1f);
DB_GLO_SetupForAct_Character("CTY_Main_A", S_GLO_ThrallOfTheAbsolute_AbsoluteHelper_15d4bd12-855b-4483-a2e5-661bfeb96d1f);
DB_GLO_SetupForAct_Character("END_Main", S_GLO_ThrallOfTheAbsolute_AbsoluteHelper_15d4bd12-855b-4483-a2e5-661bfeb96d1f);

DB_GLO_ThrallOfTheAbsolute_LoadedLevelPoints("INT_Main_A",(TRIGGER)S_INT_ThrallOfTheAbsolute_AbsoluteSpeakerPoint_ade7947b-bc32-42d2-9c88-4c3f8fa0bdd0);
DB_GLO_ThrallOfTheAbsolute_LoadedLevelPoints("BGO_Main_A",(TRIGGER)S_BGO_ThrallOfTheAbsolute_AbsoluteSpeakerPoint_dfe3a4dc-51de-46be-8f9e-d66a05a15bfd);
DB_GLO_ThrallOfTheAbsolute_LoadedLevelPoints("CTY_Main_A",(TRIGGER)S_CTY_ThrallOfTheAbsolute_AbsoluteSpeakerPoint_6d20f510-8aa7-4d1c-85ed-e72357a26f13);
DB_GLO_ThrallOfTheAbsolute_LoadedLevelPoints("END_Main",(TRIGGER)S_END_ThrallOfTheAbsolute_AbsoluteSpeakerPoint_1d436b0b-4a85-444a-8bce-f260a412d583);

//Event flags are the character hooks, State flags are only used in the GLO_ThrallOfTheAbsolute_GameOver dialog
DB_GLO_ThrallOfTheAbsolute_ActivationFlags((FLAG)GLO_ThrallOfTheAbsolute_Event_Generic_bc7239fc-216b-4c48-9d07-5e01fc548b7f,(FLAG)NULL_00000000-0000-0000-0000-000000000000, "GLO_ThrallOfTheAbsolute_Generic"); //Is fallback, so no need for state flag
DB_GLO_ThrallOfTheAbsolute_ActivationFlags((FLAG)GLO_ThrallOfTheAbsolute_Event_AstralPrism_4d658c40-92a9-4c2b-9828-acc604789025,(FLAG)GLO_ThrallOfTheAbsolute_State_AstralPrism_3db9e462-7eef-45cc-af7b-51b129361d5e, "GLO_ThrallOfTheAbsolute_AstralPrism");
DB_GLO_ThrallOfTheAbsolute_ActivationFlags((FLAG)GLO_ThrallOfTheAbsolute_Event_AttemptedToEnterUppercity_9b2c04eb-81fb-4a64-8e38-87483610e4c7,(FLAG)GLO_ThrallOfTheAbsolute_State_AttemptedToEnterUppercity_09636c4c-5d7f-4233-9939-f898815e3f84, "GLO_ThrallOfTheAbsolute_Uppercity");
DB_GLO_ThrallOfTheAbsolute_ActivationFlags((FLAG)GLO_ThrallOfTheAbsolute_Event_DestroyedStone_7814f813-2b4f-4acd-8331-8c80d49a5ea1,(FLAG)GLO_ThrallOfTheAbsolute_State_DestroyedStone_5fd5cb4b-2388-418f-8e8d-8d0eebe54f08, "GLO_ThrallOfTheAbsolute_DestroyedStone");
DB_GLO_ThrallOfTheAbsolute_ActivationFlags((FLAG)GLO_ThrallOfTheAbsolute_Event_EmperorsDeath_97c0d6bc-0b6c-4d83-b6f5-ee3308d2919b,(FLAG)GLO_ThrallOfTheAbsolute_State_EmperorsDeath_3a190dfe-41aa-4976-8d66-fee2e64d3886, "GLO_ThrallOfTheAbsolute_EmperorDeath");
DB_GLO_ThrallOfTheAbsolute_ActivationFlags((FLAG)GLO_ThrallOfTheAbsolute_Event_DaisyDistrust_0b23432d-70cb-4edb-959c-f36d0098a2f5,(FLAG)GLO_ThrallOfTheAbsolute_State_DaisyDistrust_ebea58c1-1aaf-4216-8cba-6c4645c290ec, "GLO_ThrallOfTheAbsolute_DaisyDistrust");
DB_GLO_ThrallOfTheAbsolute_ActivationFlags((FLAG)GLO_ThrallOfTheAbsolute_Event_EndingLost_56da89f9-916b-076f-d6c1-035a9ac39cb8,(FLAG)GLO_ThrallOfTheAbsolute_State_EndingLost_989e6114-0912-af38-da9b-b7d5f65b36d2, "GLO_ThrallOfTheAbsolute_EndingLost");


DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("SCL_Main_A",(TRIGGER)S_SCL_ThrallOfTheAbsolutePlayerPoint_001_1cd61899-105d-4ca9-a22d-7dcffb0955b8);
DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("SCL_Main_A",(TRIGGER)S_SCL_ThrallOfTheAbsolutePlayerPoint_002_16f75776-ff49-4a2a-8c03-90cc44b0a0a7);
DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("SCL_Main_A",(TRIGGER)S_SCL_ThrallOfTheAbsolutePlayerPoint_003_0d0a3b9a-e7a3-43c3-987f-3803ca4e6506);
DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("SCL_Main_A",(TRIGGER)S_SCL_ThrallOfTheAbsolutePlayerPoint_004_1af2573d-734d-42ec-896c-8a14032fee32);

DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("INT_Main_A",(TRIGGER)S_INT_ThrallOfTheAbsolutePlayerPoint_001_0a85b286-a1e4-40b1-902c-d061d40227ff);
DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("INT_Main_A",(TRIGGER)S_INT_ThrallOfTheAbsolutePlayerPoint_002_ededdd78-b999-4c1f-9a1b-335860624163);
DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("INT_Main_A",(TRIGGER)S_INT_ThrallOfTheAbsolutePlayerPoint_003_465d91ad-a3e4-4eaf-af75-9262553dcbaf);
DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("INT_Main_A",(TRIGGER)S_INT_ThrallOfTheAbsolutePlayerPoint_004_6ce9025f-2969-402f-86d9-3d1299a4af43);

DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("BGO_Main_A",(TRIGGER)S_BGO_ThrallOfTheAbsolutePlayerPoint_001_495de0ec-e5a5-41b7-afb5-f6d0b514e770);
DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("BGO_Main_A",(TRIGGER)S_BGO_ThrallOfTheAbsolutePlayerPoint_002_e8433bd1-4816-436e-ac16-884bd92a08c6);
DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("BGO_Main_A",(TRIGGER)S_BGO_ThrallOfTheAbsolutePlayerPoint_003_8762ee93-30ca-472b-ab71-06f2f60c4da4);
DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("BGO_Main_A",(TRIGGER)S_BGO_ThrallOfTheAbsolutePlayerPoint_004_06976dcf-73b6-4bb4-bed9-57711f7c9278);

DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("CTY_Main_A",(TRIGGER)S_CTY_ThrallOfTheAbsolutePlayerPoint_001_75b9d0c1-2ee0-4a66-a4c2-033aab35a257);
DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("CTY_Main_A",(TRIGGER)S_CTY_ThrallOfTheAbsolutePlayerPoint_002_cc7e5724-fa34-4824-ba5e-99b487675f81);
DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("CTY_Main_A",(TRIGGER)S_CTY_ThrallOfTheAbsolutePlayerPoint_003_61762511-6920-4fc2-a54b-084b1e72d365);
DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("CTY_Main_A",(TRIGGER)S_CTY_ThrallOfTheAbsolutePlayerPoint_004_b7f2024b-9035-45be-b791-904c45337213);

DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("END_Main",(TRIGGER)S_END_ThrallOfTheAbsolutePlayerPoint_001_38ac402e-01ff-49f0-bc65-9286ff8c9b2c);
DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("END_Main",(TRIGGER)S_END_ThrallOfTheAbsolutePlayerPoint_002_4607a30f-5ae9-47b4-97a4-b0b0921be3e8);
DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("END_Main",(TRIGGER)S_END_ThrallOfTheAbsolutePlayerPoint_003_8293e266-625c-4f5b-a490-80c681a03419);
DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers("END_Main",(TRIGGER)S_END_ThrallOfTheAbsolutePlayerPoint_004_37b103a0-fd14-4731-9f85-9ae8ec91f21c);

//To prevent the player leaving the area in the edge case they somehow find a way to bypass the game over UI
DB_GLO_ThrallOfTheAbsolute_DangerZone("SCL_Main_A",(TRIGGER)S_SCL_ThrallOfTheAbsolute_DangerZone_f62e1c6c-ebdd-49d9-aaea-632f112c9f35, "SCL_ThrallOfTheAbsolute_DangerZone");
DB_GLO_ThrallOfTheAbsolute_DangerZone("INT_Main_A",(TRIGGER)S_INT_ThrallOfTheAbsolute_DangerZone_be4a4604-cdf2-4b8c-b3ad-7458ae39f0f2, "INT_ThrallOfTheAbsolute_DangerZone");
DB_GLO_ThrallOfTheAbsolute_DangerZone("BGO_Main_A",(TRIGGER)S_BGO_ThrallOfTheAbsolute_DangerZone_5d98df20-73a3-4417-88fe-276f6b068337, "BGO_ThrallOfTheAbsolute_DangerZone");
DB_GLO_ThrallOfTheAbsolute_DangerZone("CTY_Main_A",(TRIGGER)S_CTY_ThrallOfTheAbsolute_DangerZone_c663e577-5765-4694-9077-a8ec97d767d0, "CTY_ThrallOfTheAbsolute_DangerZone");
DB_GLO_ThrallOfTheAbsolute_DangerZone("END_Main",(TRIGGER)S_END_ThrallOfTheAbsolute_DangerZone_b2473b6b-4fd0-4527-90f5-ac41fc8c99a5, "END_ThrallOfTheAbsolute_DangerZone");

NOT DB_GLO_ThrallOfTheAbsolute_TeleportedPlayer(NULL_00000000-0000-0000-0000-000000000000);

NOT DB_GLO_ThrallOfTheAbsolute_FailedToStartDialog(0);

DB_FlagReactionAfterDialog((FLAG)GLO_Debug_ThrallOfTheAbsolute_StartDebugDialog_7d238448-ae4c-462c-a310-46bcd394dd66, (DIALOGRESOURCE)DebugBook_3af7dec4-4c08-9e44-9064-ec038ebad0ea);
KBSECTION
//REGION Debug
IF
DB_GLO_ThrallOfTheAbsolute_FailedToStartDialog(_Int)
THEN
DB_NOOP(1);

PROC
PROC_FlagReactionAfterDialog(_Player, GLO_Debug_ThrallOfTheAbsolute_StartDebugDialog_7d238448-ae4c-462c-a310-46bcd394dd66)
THEN
ClearFlag(GLO_Debug_ThrallOfTheAbsolute_StartDebugDialog_7d238448-ae4c-462c-a310-46bcd394dd66, _Player);
PROC_DEBUG_GLO_StartThrallOfTheAbsoluteDebugDialog((CHARACTER)_Player);

IF
TextEvent("thralloftheabsolute")
AND
GetHostCharacter(_Player)
THEN
PROC_DEBUG_GLO_StartThrallOfTheAbsoluteDebugDialog((CHARACTER)_Player);

PROC
PROC_DEBUG_GLO_StartThrallOfTheAbsoluteDebugDialog((CHARACTER)_Player)
AND
QRY_StartDialogCustom_Fixed(DEBUG_ThrallOfTheAbsolute_ce779558-d3f3-7074-c3e8-2bc77ebdd13d, _Player, 0, 0, 0, 0)
THEN
NOT DB_GLO_ThrallOfTheAbsolute_DialogStarted(1);
DB_GLO_DEBUG_ThrallOfTheAbsolute_DebugCalled(1);

IF
TextEvent("thralloftheabsolute_reset")
THEN
PROC_GLO_ThrallOfTheAbsolute_ResetFlags();

IF
DialogEnded(GLO_ThrallOfTheAbsolute_GameOver_d736e2e2-5ee8-8b12-4680-3906c1265202, _)
AND
DB_GLO_DEBUG_ThrallOfTheAbsolute_DebugCalled(1)
THEN
PROC_GLO_ThrallOfTheAbsolute_ResetFlags();

IF
TextEvent("netherstone_ketheric")
AND
GetHostCharacter(_Player)
THEN
ToInventory(S_COL_CrownController_Ketheric_06b8891b-e71c-423b-8482-2680c3c16a4d, _Player, 1, 1, 1);
//END_REGION
//How to Trigger:
//  Set the Event flag corresponding to your situation. If it's set in dialog, it will wait till that dialog is completed before proceeding to the game over dialog
//  If set outside the dialog, it will trigger immediately, stopping all players and forcing them into the dialog.

PROC
PROC_GLO_SetupForAct(_Level, S_GLO_ThrallOfTheAbsolute_AbsoluteHelper_15d4bd12-855b-4483-a2e5-661bfeb96d1f, 1)
AND
DB_GLO_ThrallOfTheAbsolute_LoadedLevelPoints(_Level, _Trigger)
THEN
PROC_GLO_SetupForAct_Place(S_GLO_ThrallOfTheAbsolute_AbsoluteHelper_15d4bd12-855b-4483-a2e5-661bfeb96d1f, _Trigger);

//Protections against users saving their game during this sequence then leaving the mind arena with a party of mind flayers
IF
LevelLoaded(_Level)
AND
DB_GLO_ThrallOfTheAbsolute_DangerZone(_Level, _Trigger, _DangerZoneID)
AND
NOT DB_DangerZone(_Trigger, _DangerZoneID)
THEN
DB_DangerZone(_Trigger, _DangerZoneID);

IF
LevelLoaded(_Level)
AND
DB_GLO_ThrallOfTheAbsolute_DangerZone(_Level, _, _)
AND
DB_GLO_ThrallOfTheAbsolute_GameOverOnLoadSave(1)
THEN
RealtimeObjectTimerLaunch(S_GLO_ThrallOfTheAbsolute_AbsoluteHelper_15d4bd12-855b-4483-a2e5-661bfeb96d1f, "GLO_ThrallOfTheAbsolute_LoadedGameOverTimer", 1000); //Needs delay, otherwise menu will fail to load

IF
ObjectTimerFinished(_, "GLO_ThrallOfTheAbsolute_LoadedGameOverTimer")
THEN
PROC_GLO_ThrallOfTheAbsolute_ShowGameOverMenu();

//REGION Flag Set Handling
//If set in dialog, prepare to trigger after the dialog has finished
IF
FlagSet(_EventFlag, _Player, _DialogID)
AND
DB_GLO_ThrallOfTheAbsolute_ActivationFlags(_EventFlag, _StateFlag, _)
AND
QRY_OnlyOnce("GLO_ThrallOfTheAbsolute_EndStarted")
//In case a flag is set on multiple players, which causes the pipeline to compete against iteself 
THEN
PROC_GLO_ThrallOfTheAbsolute_CheckForValidGameOver((CHARACTER)_Player,(FLAG)_EventFlag,(FLAG)_StateFlag, _DialogID);

PROC
PROC_GLO_ThrallOfTheAbsolute_CheckForValidGameOver((CHARACTER)_Player,(FLAG)_EventFlag,(FLAG)_StateFlag,(INTEGER)_DialogID)
AND
DB_DialogName(_Dialog, _DialogID)
THEN
PROC_GlobalSetFlagAndCache(_StateFlag);
DB_FlagReactionAfterDialog_Prepare(_Player,_EventFlag,_Dialog,_DialogID);

PROC
PROC_FlagReactionAfterDialog(_Player, _EventFlag)
AND
DB_GLO_ThrallOfTheAbsolute_ActivationFlags(_EventFlag, _, _StringKey)
THEN
PROC_GLO_ThrallOfTheAbsolute_PrepareGameOverDialog((CHARACTER)_Player);

//Set outside of dialog
PROC
PROC_GLO_ThrallOfTheAbsolute_CheckForValidGameOver((CHARACTER)_Player,(FLAG)_,(FLAG)_StateFlag,(INTEGER)_DialogID)
AND
NOT DB_DialogName(_, _DialogID)
THEN
PROC_GlobalSetFlagAndCache(_StateFlag);
PROC_GLO_ThrallOfTheAbsolute_PrepareGameOverDialog((CHARACTER)_Player);

//For the Emperor dying in gameplay
PROC
PROC_StateSet_PermaDefeated(S_GLO_Emperor_73d49dc5-8b8b-45dc-a98c-927bb4e3169b)
AND
NOT QRY_GLO_ThrallOfTheAbsolute_IgnoreEmperorGameplayDeath() //If local custom handling is preferred
AND
QRY_GLO_ThrallOfTheAbsolute_GetNearestTadpoledPlayer((CHARACTER)S_GLO_Emperor_73d49dc5-8b8b-45dc-a98c-927bb4e3169b)
AND
DB_QRYRTN_GLO_ThrallOfTheAbsolute_NearestPlayer(_Player, _Dist)
THEN
SetFlag(GLO_ThrallOfTheAbsolute_Event_EmperorsDeathGameplay_97c0d6bc-0b6c-4d83-b6f5-ee3308d2919b, _Player);

QRY
QRY_GLO_ThrallOfTheAbsolute_IgnoreEmperorGameplayDeath()
AND
DB_CurrentLevel("END_Main")
THEN
DB_NOOP(1);

//TODO: As they are story items, this check may not be needed, though we will still need to check for items being left behind
IF
DestroyedBy(_Netherstone, _, _Char, _)
AND
DB_GLO_EmperorPrelude_NetherStoneTrackers(_Netherstone, _, _, _, _, _)
THEN
SetFlag(GLO_ThrallOfTheAbsolute_Event_DestroyedStone_7814f813-2b4f-4acd-8331-8c80d49a5ea1, _Char);

IF
EnteredChasm(_Netherstone, _, _, _, _, _)
AND
DB_GLO_EmperorPrelude_NetherStoneTrackers((ITEM)_Netherstone, _, _, _, _, _)
AND
QRY_GLO_ThrallOfTheAbsolute_GetNearestTadpoledPlayer(_Netherstone)
AND
DB_QRYRTN_GLO_ThrallOfTheAbsolute_NearestPlayer(_Player, _)
THEN
SetFlag(GLO_ThrallOfTheAbsolute_Event_DestroyedStone_7814f813-2b4f-4acd-8331-8c80d49a5ea1, _Player);

QRY
QRY_GLO_ThrallOfTheAbsolute_InProgress()
AND
DB_Players(_Player)
AND
DB_GLO_ThrallOfTheAbsolute_ActivationFlags(_EventFlag, _StateFlag, _)
AND
GetFlag(_EventFlag, _Player, 1)
THEN
DB_NOOP(1);
//END_REGION

//REGION Game Over Dialog
PROC
PROC_GLO_ThrallOfTheAbsolute_StartGenericGameOverDialog((CHARACTER)_Player)
AND
DB_Players(_Player)
THEN
SetFlag(GLO_ThrallOfTheAbsolute_Event_Generic_bc7239fc-216b-4c48-9d07-5e01fc548b7f, _Player);

PROC
PROC_GLO_ThrallOfTheAbsolute_PrepareGameOverDialog((CHARACTER)_)
AND
DB_Players(_Player)
THEN
RemoveHarmfulStatuses(_Player);
PROC_ForceStopDialog(_Player);
PROC_RemoveMutingStatusses(_Player);
PROC_GLO_ThrallOfTheAbsolute_RecoverDownedPlayers();

PROC
PROC_GLO_ThrallOfTheAbsolute_RecoverDownedPlayers()
AND
DB_Downed(_Player)
AND
DB_Players(_Player)
THEN
RemoveStatusesWithType(_Player, "DOWNED", NULL_00000000-0000-0000-0000-000000000000);

PROC
PROC_GLO_ThrallOfTheAbsolute_PrepareGameOverDialog((CHARACTER)_)
THEN
PROC_ForceStopDialog(S_GLO_Emperor_73d49dc5-8b8b-45dc-a98c-927bb4e3169b);

PROC
PROC_GLO_ThrallOfTheAbsolute_PrepareGameOverDialog((CHARACTER)_Player)
THEN
PROC_GLO_ThrallOfTheAbsolute_GameOverDialog(_Player);

//Avatar and not Defeated
PROC
PROC_GLO_ThrallOfTheAbsolute_GameOverDialog((CHARACTER)_Player)
AND
DB_Avatars(_Player)
AND
QRY_GLO_ThrallOfTheAbsolute_ValidPlayer(_Player)
THEN
PROC_GLO_ThrallOfTheAbsolute_TryStartGameOverDialog((CHARACTER)_Player);

//Try all the avatars
PROC
PROC_GLO_ThrallOfTheAbsolute_GameOverDialog((CHARACTER)_)
AND
DB_Avatars(_Avatar)
AND
NOT DB_Defeated(_Avatar)
THEN
PROC_GLO_ThrallOfTheAbsolute_TryStartGameOverDialog((CHARACTER)_Avatar);

//Tadpoled Character and not Defeated
PROC
PROC_GLO_ThrallOfTheAbsolute_GameOverDialog((CHARACTER)_Player)
AND
QRY_GLO_ThrallOfTheAbsolute_ValidPlayer(_Player)
THEN
PROC_GLO_ThrallOfTheAbsolute_TryStartGameOverDialog((CHARACTER)_Player);

//Not Tadpoled, so find nearest Tadpoled Character
PROC
PROC_GLO_ThrallOfTheAbsolute_GameOverDialog((CHARACTER)_Player)
AND
NOT QRY_GLO_ThrallOfTheAbsolute_ValidPlayer(_Player)
AND
QRY_GLO_ThrallOfTheAbsolute_GetNearestTadpoledPlayer(_Player)
AND
DB_QRYRTN_GLO_ThrallOfTheAbsolute_NearestPlayer(_SelectedPlayer, _Dist)
THEN
PROC_GLO_ThrallOfTheAbsolute_TryStartGameOverDialog((CHARACTER)_SelectedPlayer);

//No available other Player, just try to use the Non tadpoled initiator
PROC
PROC_GLO_ThrallOfTheAbsolute_GameOverDialog((CHARACTER)_Player)
AND
NOT DB_Defeated(_Player)
THEN
PROC_GLO_ThrallOfTheAbsolute_TryStartGameOverDialog((CHARACTER)_Player);

//Try all the players
PROC
PROC_GLO_ThrallOfTheAbsolute_GameOverDialog((CHARACTER)_)
AND
DB_Players(_Player)
AND
NOT DB_Avatars(_Player)
AND
NOT DB_Defeated(_Player)
THEN
PROC_GLO_ThrallOfTheAbsolute_TryStartGameOverDialog((CHARACTER)_Player);

PROC
PROC_GLO_ThrallOfTheAbsolute_GameOverDialog((CHARACTER)_Player)
AND
NOT DB_GLO_ThrallOfTheAbsolute_DialogStarted(1)
THEN
SetFlag(GLO_ThrallOfTheAbsolute_Event_TransformPlayers_fe6b74e7-fe16-4ec0-bdf1-101abf00e79d);
PROC_GLO_ThrallOfTheAbsolute_ShowGameOverMenu();

PROC
PROC_GLO_ThrallOfTheAbsolute_TryStartGameOverDialog((CHARACTER)_Player)
AND
NOT DB_GLO_ThrallOfTheAbsolute_DialogStarted(1)
AND
NOT QRY_GLO_ThrallOfTheAbsolute_OverrideGlobalGameOverDialog(_Player) //Used in the case the user wants to use a custom dialog instead of the global dialog (make sure to set DB_GLO_ThrallOfTheAbsolute_DialogStarted(1) in those instances)
AND
QRY_StartDialogCustom_Fixed(GLO_ThrallOfTheAbsolute_GameOver_d736e2e2-5ee8-8b12-4680-3906c1265202, S_GLO_ThrallOfTheAbsolute_AbsoluteHelper_15d4bd12-855b-4483-a2e5-661bfeb96d1f, _Player, NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000, 0, 0, 0, 0)
THEN
DB_GLO_ThrallOfTheAbsolute_DialogStarted(1);

PROC
PROC_StartDialog_AddExtraSpeakers((DIALOGRESOURCE)GLO_ThrallOfTheAbsolute_GameOver_d736e2e2-5ee8-8b12-4680-3906c1265202, _ID)
AND
DB_Players(_Player)
AND
DB_Defeated(_Player)
AND
NOT DB_Downed(_Player)
AND
NOT DB_DialogPlayers(_ID, _Player, _)
THEN
DialogAddActorAtReservedSlot(_ID, _Player, 1, 0, 1);

PROC
PROC_StartDialog_AddExtraSpeakers((DIALOGRESOURCE)GLO_ThrallOfTheAbsolute_GameOver_d736e2e2-5ee8-8b12-4680-3906c1265202, _ID)
AND
DB_Downed(_Speaker)
AND
NOT DB_DialogEnding(_,_ID)
AND
QRY_DownedSpeakerIsAvailable((GUIDSTRING)_Speaker, 1)
AND
DB_DialogRequestCache_DialogInstance(_Dialog,_ID)
AND
QRY_PrepForInteractiveDialog_IfInteractive(_Dialog,_ID,_Speaker)
THEN
DialogAddActorAtReservedSlot(_ID,_Speaker,1,0,1);
PROC_DialogRequestCache_MakeSpeakerLists_Evaluate(_Dialog,_ID,_Speaker);

QRY
QRY_DialogStarted_CheckPlayerForStopConditions_IgnoreDowned((INTEGER)_ID,(CHARACTER)_Player)
AND
DB_Players(_Player)
AND
QRY_GLO_ThrallOfTheAbsolute_InProgress()
THEN
DB_NOOP(1);

PROC
PROC_StartDialog_AddExtraSpeakers((DIALOGRESOURCE)GLO_ThrallOfTheAbsolute_GameOver_d736e2e2-5ee8-8b12-4680-3906c1265202, _ID)
AND
DB_Players(_Player)
AND
NOT DB_Defeated(_Player)
AND
NOT DB_DialogPlayers(_ID, _Player, _)
THEN
PROC_DialogAddListeningActor(_ID, _Player);

PROC
PROC_StartDialog_AddExtraSpeakers(GLO_ThrallOfTheAbsolute_GameOver_d736e2e2-5ee8-8b12-4680-3906c1265202, _ID)
AND
DB_PartyMembers(_Char)
AND
NOT DB_Players(_Char)
AND
NOT DB_DialogPlayers(_ID,_Char,_)
THEN
PROC_DialogAddListeningActor(_ID, _Char);

PROC
PROC_StartDialog_AddExtraSpeakers((DIALOGRESOURCE)GLO_ThrallOfTheAbsolute_GameOver_d736e2e2-5ee8-8b12-4680-3906c1265202, _ID)
AND
DB_CurrentLevel("END_Main") //Generic Daisy handling does not apply to END_Main
AND
NOT DB_PermaDefeated(S_GLO_Emperor_73d49dc5-8b8b-45dc-a98c-927bb4e3169b)
THEN
PROC_DialogAddSpeakingActor(_ID, S_GLO_Emperor_73d49dc5-8b8b-45dc-a98c-927bb4e3169b,0,1);

IF
DialogEnded(GLO_ThrallOfTheAbsolute_GameOver_d736e2e2-5ee8-8b12-4680-3906c1265202, _)
THEN
PROC_GLO_ThrallOfTheAbsolute_ShowGameOverMenu();

//So if the player makes a save while the game over has already triggered, it will maintain the game over state on save load
PROC
PROC_GLO_ThrallOfTheAbsolute_ShowGameOverMenu()
AND
NOT DB_GLO_ThrallOfTheAbsolute_GameOverOnLoadSave(1)
THEN
DB_GLO_ThrallOfTheAbsolute_GameOverOnLoadSave(1);

PROC
PROC_GLO_ThrallOfTheAbsolute_ShowGameOverMenu()
AND
DB_GLO_ThrallOfTheAbsolute_ActivationFlags(_, _Flag, _StringKey)
AND
DB_GlobalFlag(_Flag)
THEN
DB_GLO_ThrallOfTheAbsolute_GameOverMenuReady(1);
ShowGameOverMenu(_StringKey);

//Generic
PROC
PROC_GLO_ThrallOfTheAbsolute_ShowGameOverMenu()
AND
NOT DB_GLO_ThrallOfTheAbsolute_GameOverMenuReady(1)
AND
DB_GLO_ThrallOfTheAbsolute_ActivationFlags(_, NULL_00000000-0000-0000-0000-000000000000, _StringKey)
THEN
ShowGameOverMenu(_StringKey);

IF
FlagSet(GLO_ThrallOfTheAbsolute_Event_TransformPlayers_fe6b74e7-fe16-4ec0-bdf1-101abf00e79d, _, _)
THEN
PROC_GLO_ThrallOfTheAbsolute_ResetFlags();
PROC_GLO_ThrallOfTheAbsolute_TransformAndMovePlayers();

PROC
PROC_GLO_ThrallOfTheAbsolute_TransformAndMovePlayers()
AND
DB_Players(_Player)
AND
NOT DB_Defeated(_Player)
AND
HasActiveStatus(_Player, "MIND_FLAYER_FORM", 0)
AND
IsTagged(_Player,(TAG)ILLITHID_1eec74e8-3673-4500-abec-57b7ed8469ed, 1)
THEN
ApplyStatus(_Player, "MIND_FLAYER_FORM", -1.0, 1, S_GLO_ThrallOfTheAbsolute_AbsoluteHelper_15d4bd12-855b-4483-a2e5-661bfeb96d1f);

PROC
PROC_GLO_ThrallOfTheAbsolute_TransformAndMovePlayers()
AND
DB_Players(_Player)
AND
NOT DB_Defeated(_Player)
THEN
DB_GLO_ThrallOfTheAbsolute_TeleportedPlayer(_Player);
DetachFromPartyGroup(_Player);

PROC
PROC_GLO_ThrallOfTheAbsolute_TransformAndMovePlayers()
AND
DB_CurrentLevel("END_Main")
THEN
SetOnStage(S_END_MindBrain_f8bb04a3-22e5-41b0-aed7-5dcf852343d1, 0);

PROC
PROC_GLO_ThrallOfTheAbsolute_TransformAndMovePlayers()
AND
DB_Players(_Player)
AND
DB_Defeated(_Player)
THEN
DetachFromPartyGroup(_Player);

IF
DetachedFromPartyGroup(_Player)
AND
DB_GlobalFlag(GLO_ThrallOfTheAbsolute_Event_TransformPlayers_fe6b74e7-fe16-4ec0-bdf1-101abf00e79d)
AND
DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers(_Level, _Trigger)
AND
DB_GLO_ThrallOfTheAbsolute_TeleportedPlayer(_Player)
AND
DB_CurrentLevel(_Level)
THEN
NOT DB_GLO_ThrallOfTheAbsolute_TeleportedPlayer(_Player);
NOT DB_GLO_ThrallOfTheAbsolute_MindArenaTriggers(_Level,_Trigger);
PROC_GLO_ThrallOfTheAbsolute_TeleportMindFlayeredPlayer(_Player, _Trigger);

PROC
PROC_GLO_ThrallOfTheAbsolute_TeleportMindFlayeredPlayer((CHARACTER)_Player,(TRIGGER)_Trigger)
AND
NOT DB_GlobalFlag(END_General_State_CurrentlyInBrainBattle_0d7205b2-0d55-4540-8737-543253873cd6)
THEN
TeleportTo(_Player, _Trigger);

PROC
PROC_GLO_ThrallOfTheAbsolute_TeleportMindFlayeredPlayer((CHARACTER)_Player,(TRIGGER)_)
AND
DB_GlobalFlag(END_General_State_CurrentlyInBrainBattle_0d7205b2-0d55-4540-8737-543253873cd6)
THEN
PROC_END_BrainBattle_UsedMindTeleporter(_Player); //Will find a valid spot for them to appear on
//END_REGION

//REGION Finding Preferred Speaker
QRY
QRY_GLO_ThrallOfTheAbsolute_GetNearestTadpoledPlayer((GUIDSTRING)_)
AND
DB_QRYRTN_GLO_ThrallOfTheAbsolute_NearestPlayer(_Player, _Dist)
THEN
NOT DB_QRYRTN_GLO_ThrallOfTheAbsolute_NearestPlayer(_Player, _Dist);

QRY
QRY_GLO_ThrallOfTheAbsolute_GetNearestTadpoledPlayer((GUIDSTRING)_Source)
AND
DB_Players(_Player)
AND
QRY_GLO_ThrallOfTheAbsolute_ValidPlayer((CHARACTER)_Player)
AND
GetDistanceTo(_Player, _Source, _Dist)
THEN
DB_QRYRTN_GLO_ThrallOfTheAbsolute_NearestPlayer(_Player, _Dist);

//Couldn't find tadpoled player, attempt non-tadpoled
QRY
QRY_GLO_ThrallOfTheAbsolute_GetNearestTadpoledPlayer((GUIDSTRING)_Source)
AND
NOT DB_QRYRTN_GLO_ThrallOfTheAbsolute_NearestPlayer(_, _)
AND
DB_Players(_Player)
AND
NOT DB_Defeated(_Player)
AND
GetDistanceTo(_Player, _Source, _Dist)
THEN
DB_QRYRTN_GLO_ThrallOfTheAbsolute_NearestPlayer(_Player, _Dist);

IF
DB_QRYRTN_GLO_ThrallOfTheAbsolute_NearestPlayer(_Player1, _Dist1)
AND
DB_QRYRTN_GLO_ThrallOfTheAbsolute_NearestPlayer(_Player2, _Dist2)
AND
_Player2 != _Player1
AND
_Dist2 >= _Dist1
THEN
NOT DB_QRYRTN_GLO_ThrallOfTheAbsolute_NearestPlayer(_Player2, _Dist2);

//Someone who is not dead and is tadpoled (so not Halsin or Jaheira)
QRY
QRY_GLO_ThrallOfTheAbsolute_ValidPlayer((CHARACTER)_Player)
AND
DB_Players(_Player)
AND
NOT DB_Defeated(_Player)
AND
IsTagged(_Player, ILLITHID_1eec74e8-3673-4500-abec-57b7ed8469ed, 1)
THEN
DB_NOOP(1);
//END_REGION

//Cautionary clear of flags once the flow has completed
PROC
PROC_GLO_ThrallOfTheAbsolute_ResetFlags()
AND
DB_Players(_Player)
AND
DB_GLO_ThrallOfTheAbsolute_ActivationFlags(_EventFlag, _, _)
AND
GetFlag(_EventFlag, _Player, 1)
THEN
ClearFlag(_EventFlag, _Player, 0);

PROC
PROC_GLO_ThrallOfTheAbsolute_ResetFlags()
AND
DB_GLO_ThrallOfTheAbsolute_ActivationFlags(_, _StateFlag, _)
AND
DB_GlobalFlag(_StateFlag)
THEN
ClearFlag(_StateFlag);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Start"
