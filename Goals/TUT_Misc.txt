Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION Hacked Pod Girl

SetVisible((CHARACTER)S_TUT_Misc_PodMindflayer_14cbbbbb-b10e-444a-ba2d-5960db071fe7, 0);
PROC_CharacterDisableAllCrimes((CHARACTER)S_TUT_Misc_PodMindflayer_14cbbbbb-b10e-444a-ba2d-5960db071fe7);
PROC_CharacterDisableAllCrimes((CHARACTER)S_TUT_Misc_PodHuman_92b60869-b880-4f29-8cd3-bb2a8c771af1);
PROC_SelfHealing_Disable((CHARACTER)S_TUT_Misc_PodMindflayer_14cbbbbb-b10e-444a-ba2d-5960db071fe7);
PROC_SelfHealing_Disable((CHARACTER)S_TUT_Misc_PodHuman_92b60869-b880-4f29-8cd3-bb2a8c771af1);
SetTag((CHARACTER)S_TUT_Misc_PodMindflayer_14cbbbbb-b10e-444a-ba2d-5960db071fe7, (TAG)IGNORE_DANGEROUS_SURFACES_bbf62461-4af4-4631-807a-eb0bd988d9a6);
SetTag((CHARACTER)S_TUT_Misc_PodHuman_92b60869-b880-4f29-8cd3-bb2a8c771af1, (TAG)IGNORE_DANGEROUS_SURFACES_bbf62461-4af4-4631-807a-eb0bd988d9a6);
SetDetached((CHARACTER)S_TUT_Misc_PodMindflayer_14cbbbbb-b10e-444a-ba2d-5960db071fe7, 1);
SetOnStage((CHARACTER)S_TUT_Misc_PodMindflayer_14cbbbbb-b10e-444a-ba2d-5960db071fe7, 0);
SetVisible(S_TUT_Misc_HackChair_5aa4e961-af89-44b6-802f-7f87e21015b7, 0);
SetCanInteract((ITEM)S_TUT_Misc_HackChair_5aa4e961-af89-44b6-802f-7f87e21015b7, 0);
SetDetached(S_TUT_Misc_PodHuman_92b60869-b880-4f29-8cd3-bb2a8c771af1, 1);
Use(S_TUT_Misc_PodHuman_92b60869-b880-4f29-8cd3-bb2a8c771af1, (ITEM)S_TUT_Misc_HackChair_5aa4e961-af89-44b6-802f-7f87e21015b7, 1, 1, "TUT_Misc_VictimSeated");

//END_REGION

DB_OneShotPlayerTrigger((TRIGGER)S_TUT_Misc_UpperFloorJournalPos_7789fd25-490a-4885-9c17-f214ecb1c745);
DB_OneShotPlayerTrigger((TRIGGER)S_TUT_Misc_AntechamberJournalPos_b97895e5-f509-44f0-a771-0b7d1741afbe);
DB_OneShotPlayerTrigger((TRIGGER)S_TUT_Misc_StartRoomJournalPos_d426edee-d7e0-4faa-86a1-510a0eaf9eaf);

DB_TUT_Misc_JournalHelmPos(S_TUT_Misc_UpperFloorJournalPos_7789fd25-490a-4885-9c17-f214ecb1c745, "UpperFloor");
DB_TUT_Misc_JournalHelmPos(S_TUT_Misc_AntechamberJournalPos_b97895e5-f509-44f0-a771-0b7d1741afbe, "Antechamber");
DB_TUT_Misc_JournalHelmPos(S_TUT_Helm_ConsolePointArea_870b647f-5853-4f1a-9c39-a4915808201a, "Transponder");

DB_TUT_Misc_Aquariums((ITEM)FUR_MF_Aquarium_A_3e45e634-a585-415b-bfa1-d72699e3f52f);
DB_TUT_Misc_Aquariums((ITEM)FUR_MF_Aquarium_A_Destruct_20213cab-012a-4f5c-b107-6686a03a8ac9);
DB_TUT_Misc_Aquariums((ITEM)FUR_MF_Aquarium_A_Glowing_760b9f69-d486-4844-988a-9a2a4789b88f);

DB_TUT_Misc_AquariumNotBrain((ITEM)FUR_MF_Aquarium_B_Destruct_e6eeaa3f-d862-4288-9cf1-799661c17dc1);
DB_TUT_Misc_AquariumNotBrain((ITEM)FUR_MF_Aquarium_B_a3aae9e6-505f-4a7c-ac71-d0f74f0385a4);

DB_TUT_Misc_TankTemplate((ITEM)PUZ_MF_Explosive_Contraption_A_24e01b77-0a74-410d-869f-919918f20278);
DB_TUT_Misc_TankTemplate((ITEM)PUZ_MF_Explosive_Contraption_B_29a36662-ccd9-4599-b2a1-16fb2cb31366);

DB_FlagReactionAfterDialog((FLAG)TUT_Misc_State_SeenTransformation_407e132e-bcde-49f3-8e42-8100e7bad9cb, (DIALOGRESOURCE)TUT_Misc_MindflayerTransformation_091620a4-3081-0e67-142d-281b2812de47);

DB_TUT_Misc_Pods((ITEM)S_TUT_Misc_TransformPod_000_5c4561ef-26e5-49c6-8d19-11cc0f1e428b);

DB_TUT_Misc_GuideInclusion(S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12, (TRIGGER)S_TUT_TransformChamber_SpotPodStorage_a4ac6721-fa25-4973-81c3-1e497a5a8af7, "TUT_TransformChamber_PodStorage", (VOICEBARKRESOURCE)TUT_TransformChamber_VB_PodStorage_f0c7472f-c449-2bef-2e1e-2f638aab0e96, (DIALOGRESOURCE)TUT_TransformChamber_AD_LaezelPodStorage_5f95c11c-4335-f2e4-eb69-df5022d97d47);
DB_TUT_Misc_GuideInclusion(S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12, (TRIGGER)S_TUT_LowerDeck_RoomTrigger_ebbf3bb5-d79a-49f0-b085-eae705c8fc66, "TUT_LowerDeck_AfterFight", (VOICEBARKRESOURCE)TUT_LowerDeck_VB_AfterFight_907a3adf-46fe-533f-9c17-c41a2a015761, (DIALOGRESOURCE)TUT_LowerDeck_AD_LaezelAfterFight_7e259d7b-eb4f-0b55-b87a-61d8704bedd1);
DB_TUT_Misc_GuideInclusion(S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12, (TRIGGER)S_TUT_Helm_MindflayerFall_bbbfe493-b66d-4d54-856b-5d862e4a94ad, "TUT_Helm_MindflayerDefeated", (VOICEBARKRESOURCE)TUT_Helm_VB_MindflayerFall_e8a94613-b645-8723-9306-ea440732be51, (DIALOGRESOURCE)TUT_Helm_AD_LaezelMindflayerFall_07cf8f03-d5a4-0d6d-abdb-4866a1fcdb90);
DB_TUT_Misc_GuideInclusion(S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12, (TRIGGER)S_TUT_TransformChamber_PanelProximity_7e338e5b-846b-4fb7-b289-8354009dbb7f, "TUT_TransformChamber_ControlPanel", (VOICEBARKRESOURCE)TUT_TransformChamber_VB_ControlPanel_d561cc80-2941-85fd-7580-1cdd1dc8a2e4, (DIALOGRESOURCE)TUT_TransformChamber_PAD_ControlPanel_08dd2903-bb39-7608-7851-90644287d2e5);

DB_TUT_Misc_GuideInclusion(S_TUT_GithGuide_201a0ee6-6411-4564-aaea-1ca057bfea04, (TRIGGER)S_TUT_TransformChamber_SpotPodStorage_a4ac6721-fa25-4973-81c3-1e497a5a8af7, "TUT_TransformChamber_PodStorage", (VOICEBARKRESOURCE)TUT_TransformChamber_VB_PodStorage_f0c7472f-c449-2bef-2e1e-2f638aab0e96, (DIALOGRESOURCE)TUT_TransformChamber_AD_AltGuide_PodStorage_231d298d-4b82-1cb2-2d97-f7a3af1950d9);
DB_TUT_Misc_GuideInclusion(S_TUT_GithGuide_201a0ee6-6411-4564-aaea-1ca057bfea04, (TRIGGER)S_TUT_LowerDeck_RoomTrigger_ebbf3bb5-d79a-49f0-b085-eae705c8fc66, "TUT_LowerDeck_AfterFight", (VOICEBARKRESOURCE)TUT_LowerDeck_VB_AfterFight_907a3adf-46fe-533f-9c17-c41a2a015761, (DIALOGRESOURCE)TUT_LowerDeck_AD_AltGuide_AfterFight_56ce74ad-fdbc-91f2-6a18-bafee9baa510);
DB_TUT_Misc_GuideInclusion(S_TUT_GithGuide_201a0ee6-6411-4564-aaea-1ca057bfea04, (TRIGGER)S_TUT_Helm_MindflayerFall_bbbfe493-b66d-4d54-856b-5d862e4a94ad, "TUT_Helm_MindflayerDefeated", (VOICEBARKRESOURCE)TUT_Helm_VB_MindflayerFall_e8a94613-b645-8723-9306-ea440732be51, (DIALOGRESOURCE)TUT_Helm_AD_AltGuide_MindflayerFall_35bb2fbe-7438-01a4-1743-2119a339086b);
DB_TUT_Misc_GuideInclusion(S_TUT_GithGuide_201a0ee6-6411-4564-aaea-1ca057bfea04, (TRIGGER)S_TUT_TransformChamber_PanelProximity_7e338e5b-846b-4fb7-b289-8354009dbb7f, "TUT_TransformChamber_ControlPanel", (VOICEBARKRESOURCE)TUT_TransformChamber_VB_ControlPanel_d561cc80-2941-85fd-7580-1cdd1dc8a2e4, (DIALOGRESOURCE)TUT_TransformChamber_AD_AltGuide_ControlPanel_db32acfd-a7ca-165d-400c-1cee2c5eb433);

//REGION Bactracking
DB_TUT_Misc_GuideBacktrackADs((DIALOGRESOURCE)TUT_Misc_AD_LaezelBacktrack_02e1902d-4be6-49be-83ec-cb0d0901c157, (CHARACTER)S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12);
DB_TUT_Misc_GuideBacktrackADs((DIALOGRESOURCE)TUT_Misc_AD_AltGuide_Backtrack_8cf0937f-6dbd-ff25-53c9-10d3e5a934db, (CHARACTER)S_TUT_GithGuide_201a0ee6-6411-4564-aaea-1ca057bfea04);
//DB_TUT_Misc_TutorialProgress(_Trigger, _Weight)
DB_TUT_Misc_TutorialProgressTrigger((TRIGGER)S_TUT_ProgressTrigger_StartingRoom_7c8c2de9-b411-4f1f-a54b-56251e44932a, 1);
DB_TUT_Misc_TutorialProgressTrigger((TRIGGER)S_TUT_ProgressTrigger_Lab_7fb8fefd-38b3-4069-a831-37a9d0ba2a1f, 2);
DB_TUT_Misc_TutorialProgressTrigger((TRIGGER)S_TUT_ProgressTrigger_JumpingArea_7b36eb10-29f3-488e-90ca-987b1d073351, 3);
DB_TUT_Misc_TutorialProgressTrigger((TRIGGER)S_TUT_ProgressTrigger_LowerDeck_ec344bbe-3353-4076-825d-ee901fa8675c, 4);
DB_TUT_Misc_TutorialProgressTrigger((TRIGGER)S_TUT_ProgressTrigger_MindDrainMachine_5f77dd78-9771-4218-8952-e1d4d63c8a6d, 5);
DB_TUT_Misc_TutorialProgressTrigger((TRIGGER)S_TUT_ProgressTrigger_ControlRoom_b7bf106f-e3b9-4b74-b228-a1c229605dcb, 6);

DB_TUT_Misc_TutorialProgress((TRIGGER)S_TUT_ProgressTrigger_StartingRoom_7c8c2de9-b411-4f1f-a54b-56251e44932a, 1);
//END_REGION


DB_TUT_Misc_Devils(S_TUT_Helm_Devil_005_ed103005-fd71-457d-ae6c-39654bbd8f2e, (DIALOGRESOURCE)TUT_Helm_AD_GreaterDevil_c53efb1a-e5e2-98d1-2b70-8d335e5c960e);

PROC_TriggerRegisterForPlayers(S_TUT_LowerDeck_Stairs_42dfb01f-bdc3-4d92-b8fc-d4f2980332c0);
PROC_TriggerRegisterForPlayers(S_TUT_Helm_GuideWarningBounds_f63b25b7-e77c-4d7b-874e-b5c61ab35802);
PROC_TriggerRegisterForPlayers(S_TUT_TransformChamber_PanelProximity_7e338e5b-846b-4fb7-b289-8354009dbb7f);

DB_TUT_Misc_CorpsePods((ITEM)S_TUT_PlayerPod_004_9cc50df2-46eb-4a32-90c3-9363c85cdd5d);
DB_TUT_Misc_CorpsePods((ITEM)S_TUT_Start_CorpsePod_001_6e768f67-8fd7-4f30-9004-24ddae403721);

DB_TUT_Misc_RumbleRooms((ITEM)S_TUT_Misc_RubbleHelper_000_66b80d8d-20fb-4d6f-9690-5f94c642a51e, (TRIGGER)S_TUT_Misc_RubbleRoom_000_4994b558-2fc9-46eb-8ec4-1dc1b5acd2b4, 50);
DB_TUT_Misc_RumbleRooms((ITEM)S_TUT_Misc_RubbleHelper_001_92bd3839-899c-4312-ab69-770538114d37, (TRIGGER)S_TUT_Misc_RubbleRoom_001_5873b16d-43e6-48c8-970b-bde226c6b515, 30);
DB_TUT_Misc_RumbleRooms((ITEM)S_TUT_Misc_RubbleHelper_002_cc212976-6124-4230-a3e1-265e54d74c20, (TRIGGER)S_TUT_Misc_RubbleRoom_002_1ef9dea6-6e95-47dc-8c06-a570ffcc11d3, 20);
DB_TUT_Misc_RumbleRooms((ITEM)S_TUT_Misc_RubbleHelper_003_5d5e9902-ae8c-4d6c-8daa-ef6d9d973a45, (TRIGGER)S_TUT_Misc_RubbleRoom_002_a79b9aae-847c-42bb-a109-7772d5a4b376, 20);

DB_TUT_Misc_Elevator((LEVELTEMPLATE)LT_PLT_TUT_Elevator_002_fda62ae9-c8c3-4ae9-ae87-3c0c5e18b42f, (TRIGGER)S_TUT_Misc_Elevator002_Down_7e0e9655-913c-4d42-9a24-21c31d42d7e5, (ITEM)S_TUT_Misc_Elevator002_DownLever_8db8f360-c12a-464f-bb4c-0361160663a8, (TRIGGER)S_TUT_Misc_Elevator002_Up_2a33cb01-673d-4740-8661-02c019d655c5, (ITEM)S_TUT_Misc_Elevator002_UpLever_41dedacc-3e20-4dcb-abb9-52829e2fb89e, S_TUT_Misc_ElevatorSwitch_03c4a72e-5129-8a69-3ae7-85e5f549079b);
DB_TUT_Misc_ElevatorLevers((LEVELTEMPLATE)LT_PLT_TUT_Elevator_002_fda62ae9-c8c3-4ae9-ae87-3c0c5e18b42f, (ITEM)S_TUT_Misc_Elevator002_DownLever_8db8f360-c12a-464f-bb4c-0361160663a8, "Down");
DB_TUT_Misc_ElevatorLevers((LEVELTEMPLATE)LT_PLT_TUT_Elevator_002_fda62ae9-c8c3-4ae9-ae87-3c0c5e18b42f, (ITEM)S_TUT_Misc_Elevator002_UpLever_41dedacc-3e20-4dcb-abb9-52829e2fb89e, "Up");
DB_TUT_Misc_ElevatorLevers((LEVELTEMPLATE)LT_PLT_TUT_Elevator_002_fda62ae9-c8c3-4ae9-ae87-3c0c5e18b42f, (ITEM)S_TUT_Misc_ElevatorSwitch_03c4a72e-5129-8a69-3ae7-85e5f549079b, "");

DB_TUT_Misc_RestingStations((ITEM)S_TUT_Misc_RestingStation_000_e96e33f7-6f59-4058-a5bb-8cd38b502d92);
DB_TUT_Misc_RestingStations((ITEM)S_TUT_Misc_RestingStation_001_716e7a77-cdba-483f-b5e7-8727e0fe9b78);
DB_TUT_Misc_RestingStations((ITEM)S_TUT_Misc_RestingStation_002_9dd6015a-ddd2-4719-a456-99abebc90e79);
DB_TUT_Misc_RestingStations((ITEM)S_TUT_Misc_RestingStation_003_4c29b59f-e549-480a-b062-fc4fde3770ab);

DB_TUT_Misc_Tentacles((ITEM)S_TUT_Misc_Tentacle_000_e05e6677-2c82-4833-bc9a-94afb89bfed8);
DB_TUT_Misc_Tentacles((ITEM)S_TUT_Misc_Tentacle_001_b71aae2a-9350-4ede-ac5d-ddf907d8c814);
DB_TUT_Misc_Tentacles((ITEM)S_TUT_Misc_Tentacle_002_0c6851bc-3cdc-491b-b50e-b53a76ed82c3);
DB_TUT_Misc_Tentacles((ITEM)S_TUT_Misc_Tentacle_003_d0d2522e-8f6f-439d-9f3e-79b620bb79a9);

DB_TUT_Misc_WhisperingChairs((ITEM)S_TUT_MindflayerRoom_Chair_001_691f1ad9-7d75-4205-9267-7952d2d24726);
DB_TUT_Misc_WhisperingChairs((ITEM)S_TUT_MindflayerRoom_Chair_002_84d003a8-a216-4efe-b3ef-ccaf2b84a370);
DB_TUT_Misc_WhisperingChairs((ITEM)S_TUT_MindflayerRoom_Chair_003_92b47b70-33b6-4079-a2df-4023c2fe723b);
DB_TUT_Misc_WhisperingChairs((ITEM)S_TUT_MindflayerRoom_Chair_004_53497b8e-2a46-4f0e-91df-34b467a689ac);
DB_TUT_Misc_WhisperingChairs((ITEM)S_TUT_MindflayerRoom_Chair_005_fdd83aae-fcee-4d2f-9e6f-d14d05fe2a46);
DB_TUT_Misc_WhisperingChairs((ITEM)S_TUT_MindflayerRoom_Chair_006_a6e7946f-e7e3-498c-be54-fbf86721f2fe);

DB_DialogBlockAttackButton((DIALOGRESOURCE)TUT_Misc_PodMindflayer_8fa2d3b2-c869-85fb-2733-e481feefe0f4);
DB_DialogBlockAttackButton((DIALOGRESOURCE)TUT_Misc_MindflayerTransformation_091620a4-3081-0e67-142d-281b2812de47);

PROC_TUT_Misc_TentacleStart();

PROC_TUT_Misc_RegisterRumbleRooms();
PROC_TUT_Misc_ElevatorInit();

//REGION Save triggers
DB_AutoSaveTrigger((TRIGGER)S_TUT_SaveTrigger_FinalEncounter_225d10f9-06f1-49d4-8e30-5317e18ecd98);
DB_AutoSaveTrigger((TRIGGER)S_TUT_SaveTrigger_Start_ee28dfe3-0cf9-42d5-8ae5-3cfd7efebc52);
DB_AutoSaveTrigger((TRIGGER)S_TUT_SaveTrigger_ImpEncounter_6218b139-a1c2-4bfe-8d82-0ad2d2b2397e);
DB_AutoSaveTrigger((TRIGGER)S_TUT_SaveTrigger_DrainMachineEncounter_3fcfd053-b5c8-430e-ba33-65c4205445cc);
//END_REGION

//REGION Pods

DB_TUT_Misc_TutPods((ITEM)DEC_MFL_Pod_C_001_f1916334-e26f-4dea-8cf3-36ed755da3dd);
DB_TUT_Misc_TutPods((ITEM)S_TUT_TransformChamber_Pod_007_1eebfdb5-8814-da8d-2d3e-64b2924f95b0);
DB_TUT_Misc_TutPods((ITEM)S_TUT_Misc_TransformPod_000_5c4561ef-26e5-49c6-8d19-11cc0f1e428b);
DB_TUT_Misc_TutPods((ITEM)DEC_MFL_Pod_C_000_9e92d736-6ae6-4b25-b00f-46db73b7bdab);
DB_TUT_Misc_TutPods((ITEM)S_TUT_Start_LaezelPod_0aa6b2a4-808a-4eb3-84d7-face5ac13e44);

//END_REGION

//REGION Books



//END_REGION

//REGION Generic Item Templates and Exceptions

DB_TUT_Misc_GenericItemsException((ITEM)S_TUT_Lab_AdventurerBrainJar_2c5bca7e-8649-4217-9ebb-52b20a506e99);
DB_TUT_Misc_GenericItemsException((ITEM)S_TUT_Lab_ClericBrainJar_f938e0a4-fb50-4d69-8b40-18e9bba30a22);
DB_TUT_Misc_GenericItemsException((ITEM)S_TUT_Lab_MindflayerBrain_95534767-eef6-468b-9712-37ba4e509544);

DB_TUT_Misc_GenericItems((ITEM)DEC_MF_Brain_Jar_A_e28ad82a-f0eb-4f80-b3ec-bfedcbbe877d);
DB_TUT_Misc_GenericItems((ITEM)DEC_MF_Brain_Jar_B_d8cad0bc-4d38-4ecf-a716-dd78ee9cec4f);
DB_TUT_Misc_GenericItems((ITEM)DEC_MF_Brain_Jar_C_915aeb6b-6804-47d5-a5a6-c1b4da2dab81);
DB_TUT_Misc_GenericItems((ITEM)DEC_MF_Brain_Jar_C_105_6cbe1143-8f60-4fa7-aea6-d68c73d42dac);
DB_TUT_Misc_GenericItems((ITEM)DEC_MF_Brain_Jar_B_013_71677352-8164-458b-91a6-2374658dafbe);
DB_TUT_Misc_GenericItems((ITEM)DEC_MF_Brain_Jar_D_010_d82d8fc9-6a45-45a3-b8b1-e496a77c20e2);
DB_TUT_Misc_GenericItems((ITEM)DEC_MF_Brain_Jar_D_85ff87eb-ccaa-43e4-9c35-83b69801dc60);
//END_REGION

//REGION Deck cannons
DB_TUT_Misc_Cannons((CHARACTER)S_TUT_Deck_Cannon_000_cccf952e-2ef3-41a1-ab8f-c2b341057852);
DB_TUT_Misc_Cannons((CHARACTER)S_TUT_Deck_Cannon_002_191606bc-7db1-4293-be6c-a69b5c840941);
DB_TUT_Misc_Cannons((CHARACTER)S_TUT_Deck_Cannon_001_a4640cb3-c080-4f35-ab0d-f17b339aa961);
DB_TUT_Misc_Cannons((CHARACTER)S_TUT_Deck_Cannon_005_fbb3bfb5-85d9-43f7-bdbc-5c23ac6762e1);
DB_TUT_Misc_Cannons((CHARACTER)S_TUT_Deck_Cannon_004_61f27316-d2dc-4203-b995-d99843a71a9c);
DB_TUT_Misc_Cannons((CHARACTER)S_TUT_Deck_Cannon_003_3fb35aac-3ca3-42b6-94b4-072b597c403c);
//END_REGION

DB_TUT_PlayerLikeCompanion((CHARACTER)S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679);
DB_TUT_PlayerLikeCompanion(S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12);

// Journal update for reaching helm
DB_QuestDef_State(TUT_Misc_Quest_ReachedHelm_f61440b7-4118-4936-b165-40677fff1a9c, "TUT_NautiloidEscape", "ReachedHelm");
KBSECTION
//REGION Tutorial Companions

//disable attack button for our fake tutorial companions
//bad things would happen
QRY
QRY_StartDialog_BlockAttackButton(_,(GUIDSTRING)_Speaker1,(GUIDSTRING)_Speaker2,(GUIDSTRING)_Speaker3,(GUIDSTRING)_Speaker4,(GUIDSTRING)_Speaker5,(GUIDSTRING)_Speaker6)
AND
DB_TutorialCompanion((CHARACTER)_Speaker1)	
THEN
DB_Noop(1);

IF
AttackedBy(_Pod, _Player, _,_,_,_,_)
AND
DB_TUT_Misc_TutPods((ITEM)_Pod)
AND
_Player != S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12
AND
QRY_SpeakerIsAvailable(S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12)
AND
CanSee((CHARACTER)S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12,_Pod,1)
AND
QRY_OnlyOnce("TUT_Misc_LaezelCommentOnAttackPods")
THEN
PROC_TryStartAD((DIALOGRESOURCE)TUT_TransformChamber_AD_AttackOnPod_c44b549c-4ad3-08d2-813f-264f32df9d3d,S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12);

PROC
PROC_TUT_Misc_TutorialCompanion_Recruitment((CHARACTER)_AltGuide,(CHARACTER)_Player)
THEN
SetFaction(_AltGuide,(FACTION)Hero_a1542c81-6895-929e-4522-10ce218bb360);
MakePlayer(_AltGuide,_Player);
RequestInitialLevel(_AltGuide);
DB_Players(_AltGuide);
RegisterAsCompanion(_AltGuide, _Player);
SetCanJoinCombat(_AltGuide, 1);

PROC
PROC_TUT_Misc_TutorialCompanion_RemoveCompanion((CHARACTER)_Companion,(STRING)_LeaveType,(STRING)_Reason)
AND
NOT QRY_IsPartyFollower(_Companion)
AND
DB_TUT_Misc_TutorialCompanion_OldFaction(_Companion, _Faction)
THEN
SetFaction(_Companion, _Faction);
MakeNPC(_Companion);
UnregisterAsCompanion(_Companion);
NOT DB_TutorialCompanion(_Companion);	//added for tutorial messages
NOT DB_TUT_Misc_TutorialCompanion_OldFaction(_Companion, _Faction);
ClearFlag((FLAG)ORI_State_Recruited_e78c0aab-fb48-98e9-3ed9-773a0c39988d, _Companion);
PROC_GLO_PartyMembers_Remove((CHARACTER)_Companion,1);
PROC_SetRelationToPlayers((FACTION)_Faction,50);
DetachFromPartyGroup((CHARACTER)_Companion);
NOT DB_Players((CHARACTER)_Companion);
NOT DB_PartyMembers((CHARACTER)_Companion);
NOT DB_PartOfTheTeam((CHARACTER)_Companion);
SetTag(_Companion,DOWNED_DISABLED_7095912e-fcb9-41dd-aec3-3cf7803e4b22);

PROC
PROC_TUT_Misc_TutorialCompanion_RemoveCompanion((CHARACTER)_Follower,(STRING)_LeaveType,(STRING)_Reason)
AND
QRY_IsPartyFollower(_Follower)
AND
DB_TUT_Misc_TutorialCompanion_OldFaction(_Follower, _Faction)
THEN
SetFaction(_Follower, _Faction);
PROC_RemovePartyFollower(_Follower);
NOT DB_TutorialCompanion(_Follower);	//added for tutorial messages
NOT DB_TUT_Misc_TutorialCompanion_OldFaction(_Follower, _Faction);
ClearFlag((FLAG)ORI_State_Recruited_e78c0aab-fb48-98e9-3ed9-773a0c39988d, _Follower);
PROC_SetRelationToPlayers((FACTION)_Faction,50);
NOT DB_PartyMembers((CHARACTER)_Follower);
NOT DB_PartOfTheTeam((CHARACTER)_Follower);
SetTag(_Follower,DOWNED_DISABLED_7095912e-fcb9-41dd-aec3-3cf7803e4b22);


PROC
PROC_TUT_Misc_TutorialCompanion_RemoveCompanion((CHARACTER)S_TUT_Lab_TrappedDevourer_6ca92237-eb1e-4ed8-a1ee-18b3bef21558,"dismissed",(STRING)_Reason)
AND
NOT DB_SceneManager(S_TUT_Lab_TrappedDevourer_6ca92237-eb1e-4ed8-a1ee-18b3bef21558,"TUT_Laboratory_UsDimissed")
AND
NOT DB_OnlyOnce("TUT_Lab_UsStartsEscape")
THEN
DB_SceneManager(S_TUT_Lab_TrappedDevourer_6ca92237-eb1e-4ed8-a1ee-18b3bef21558,"TUT_Laboratory_UsDimissed");

PROC
PROC_TUT_Misc_TutorialCompanion_RemoveCompanion((CHARACTER)_Companion,(STRING)_LeaveType,(STRING)_Reason)
AND
CanJoinCombat(_Companion, 0)
THEN
SetCanJoinCombat(_Companion, 1);

//END_REGION


//REGION Chasm Death
IF
EnteredChasm(_Companion, _CauseObj, _, _ChasmX, _ChasmY, _ChasmZ)
AND
DB_CurrentLevel("TUT_Avernus_C")
AND
DB_TutorialCompanion((CHARACTER)_Companion)
THEN
PROC_Camp_PlayerEnteredChasm(_Companion, _CauseObj, _ChasmX, _ChasmY, _ChasmZ);
//END_REGION

//REGION Generic Items


IF
UseFinished(_Player, _Chair,1)
AND
DB_TUT_Misc_WhisperingChairs(_Chair)
AND
DB_Players(_Player)
AND
QRY_OnlyOnce("TUT_TransformChamber_ChairWhispers")
THEN
StartVoiceBark((VOICEBARKRESOURCE)TUT_TransformChamber_VB_ChairWhispers_eba2ca27-7006-b4f9-10e4-0e089e147aa5,_Player);

IF
UseStarted(_Player, _Aquarium)
AND
DB_Players(_Player)
AND
GetTemplate(_Aquarium, _Template)
AND
DB_TUT_Misc_Aquariums((ITEM)_Template)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)TUT_Misc_PAD_Aquarium_b18bce9b-6d48-f031-db2f-6a56c1362e37, _Player)
THEN
DB_NOOP(1);

IF
UseStarted(_Player, _Aquarium)
AND
DB_Players(_Player)
AND
GetTemplate(_Aquarium, _Template)
AND
DB_TUT_Misc_AquariumNotBrain((ITEM)_Template)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)TUT_Misc_PAD_NonBrainAquarium_44d8b26b-b2b1-116d-e5bd-0283e92c6705, _Player)
THEN
DB_NOOP(1);


IF
UseStarted(_Player, _Item)
AND
DB_Players(_Player)
AND
NOT DB_TUT_Misc_GenericItemsException((ITEM)_Item)
AND
GetTemplate(_Item, _Template)
AND
DB_TUT_Misc_GenericItems((ITEM)_Template)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)TUT_Misc_PAD_GenericNautiloidItem_0d3ac251-5485-4008-a38c-afa195baba60, _Player)
THEN
DB_NOOP(1);


//END_REGION

//REGION Mindflayer Pods

IF
EntityEvent(S_TUT_Misc_PodHuman_92b60869-b880-4f29-8cd3-bb2a8c771af1, "TUT_Misc_VictimSeated")
THEN
ApplyStatus(S_TUT_Misc_PodHuman_92b60869-b880-4f29-8cd3-bb2a8c771af1, "TUT_MINDFLAYERPOD", -1.0, 1);

IF
UseStarted(_Player, _Pod)
AND
DB_TUT_Misc_Pods(_Pod)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)TUT_Misc_PodMindflayer_8fa2d3b2-c869-85fb-2733-e481feefe0f4, _Player, (CHARACTER)S_TUT_Misc_PodMindflayer_14cbbbbb-b10e-444a-ba2d-5960db071fe7, (CHARACTER)S_TUT_Misc_PodHuman_92b60869-b880-4f29-8cd3-bb2a8c771af1)
THEN
DB_NOOP(1);

PROC
PROC_TUT_Misc_MindflayerPodView((CHARACTER)_Player, (ITEM)_Pod)
AND
NOT QRY_StartDialog_Fixed((DIALOGRESOURCE)TUT_Misc_MindflayerTransformation_091620a4-3081-0e67-142d-281b2812de47, _Player, (CHARACTER)S_TUT_Misc_PodMindflayer_14cbbbbb-b10e-444a-ba2d-5960db071fe7, (CHARACTER)S_TUT_Misc_PodHuman_92b60869-b880-4f29-8cd3-bb2a8c771af1)
THEN
NOT DB_OnlyOnce("TUT_TransformChamber_Transform");

IF
DialogStarted((DIALOGRESOURCE)TUT_Misc_MindflayerTransformation_091620a4-3081-0e67-142d-281b2812de47,_ID)
THEN
DB_TUT_Misc_CurrentTransformationID(_ID);

IF
DialogEnded((DIALOGRESOURCE)TUT_Misc_MindflayerTransformation_091620a4-3081-0e67-142d-281b2812de47,_ID)
AND
DB_TUT_Misc_CurrentTransformationID(_ID)
THEN
NOT DB_TUT_Misc_CurrentTransformationID(_ID);

IF
DialogEnded((DIALOGRESOURCE)TUT_Misc_MindflayerTransformation_091620a4-3081-0e67-142d-281b2812de47, _)
AND
NOT DB_GlobalFlag((FLAG)TUT_Misc_State_TransformationOccured_fb6cd0a5-d41d-4c47-8f44-8b4d93746f72)
THEN
NOT DB_OnlyOnce("TUT_TransformChamber_Transform");

IF
UseStarted(_Player,(ITEM)S_TUT_TransformChamber_Lever_005_85ff4c93-d1e1-4ac1-b244-2d62e2c60f7c)
AND
DB_GlobalFlag((FLAG)TUT_Misc_State_TransformationOccured_fb6cd0a5-d41d-4c47-8f44-8b4d93746f72)
AND
DB_Players(_Player)
AND
NOT DB_TUT_TransformChamber_FinishedADSaid(1)
THEN
PROC_TryStartAD((DIALOGRESOURCE)TUT_TransformChamber_PAD_TransformationFinished_3b1c54b5-c837-e24e-2fd3-2a03e7238eaa, _Player);

IF
AutomatedDialogStarted((DIALOGRESOURCE)TUT_TransformChamber_PAD_TransformationFinished_3b1c54b5-c837-e24e-2fd3-2a03e7238eaa,_)
THEN
DB_TUT_TransformChamber_FinishedADSaid(1);

PROC
PROC_FlagReactionAfterDialog(_Player, (FLAG)TUT_Misc_State_SeenTransformation_407e132e-bcde-49f3-8e42-8100e7bad9cb)
THEN
RealtimeObjectTimerLaunch(_Player, "TUT_Misc_MindflayerTransform_Reaction", 1000);

IF
FlagSet((FLAG)TUT_Misc_State_TransformationOccured_fb6cd0a5-d41d-4c47-8f44-8b4d93746f72, _, _)
THEN
PROC_Poof(S_TUT_Misc_PodHuman_92b60869-b880-4f29-8cd3-bb2a8c771af1);
PROC_Foop((CHARACTER)S_TUT_Misc_PodMindflayer_14cbbbbb-b10e-444a-ba2d-5960db071fe7);

IF
WentOnStage(S_TUT_Misc_PodMindflayer_14cbbbbb-b10e-444a-ba2d-5960db071fe7,1)
THEN
Use((CHARACTER)S_TUT_Misc_PodMindflayer_14cbbbbb-b10e-444a-ba2d-5960db071fe7, S_TUT_Misc_HackChair_5aa4e961-af89-44b6-802f-7f87e21015b7, 1, 1, "TUT_Misc_MindflayerSeated");

IF
EntityEvent((CHARACTER)S_TUT_Misc_PodMindflayer_14cbbbbb-b10e-444a-ba2d-5960db071fe7, "TUT_Misc_MindflayerSeated")
THEN
ApplyStatus(S_TUT_Misc_PodMindflayer_14cbbbbb-b10e-444a-ba2d-5960db071fe7, "TUT_MINDFLAYERPOD", -1.0, 1);
SetVisible((CHARACTER)S_TUT_Misc_PodMindflayer_14cbbbbb-b10e-444a-ba2d-5960db071fe7, 1);


IF
ObjectTimerFinished(_Player, "TUT_Misc_MindflayerTransform_Reaction")
THEN
StartVoiceBark((VOICEBARKRESOURCE)TUT_Misc_VB_TransformationReaction_06fe8ef0-1b0c-e15c-f699-59975d2d0aea, (CHARACTER)_Player);


//END_REGION

//REGION Companion Pods

//Pod with corpse
IF 
UseStarted(_Player, _Pod)
AND
_Player != S_TUT_Lab_TrappedDevourer_6ca92237-eb1e-4ed8-a1ee-18b3bef21558
AND
DB_TUT_Misc_CorpsePods(_Pod)
AND
NOT DB_DialogNPCs(_, _Pod, _)
THEN
PROC_TryStartAD((DIALOGRESOURCE)TUT_Start_PAD_PodWithCorpse_66819a06-0cb8-b8b5-07e9-400f48076bc0, _Player);

IF
DB_Tutorial_Start_PlayerPodAssigned(_, (ITEM)S_TUT_Start_Pod_004_ec8fb6fb-871a-4720-9f81-c8fe1bd5be48)
THEN
NOT DB_TUT_Misc_CorpsePods((ITEM)S_TUT_Start_Pod_004_ec8fb6fb-871a-4720-9f81-c8fe1bd5be48);
PROC_SetOnStage(S_TUT_Start_Corpse_001_d5facc61-61b6-4285-ae4a-22676c54ff35,0);


IF // Empty Pod
UseStarted(_Player, _Pod)
AND
_Player != S_TUT_Lab_TrappedDevourer_6ca92237-eb1e-4ed8-a1ee-18b3bef21558
AND
DB_TUT_Start_Pods(_Pod,_)
AND
NOT DB_TUT_Misc_CorpsePods(_Pod)
AND
NOT DB_Tutorial_Start_PlayerPodAssigned((CHARACTER)_Player, (ITEM)_Pod)
AND
NOT DB_DialogNPCs(_, _Pod, _)
THEN
StartVoiceBark((VOICEBARKRESOURCE)TUT_Misc_VB_OriginPod_Empty_aadcfa0d-c58b-76f0-9d31-356e1c746ef7, _Player);

IF // Empty Pod - Belongs to player
UseStarted(_Player, _Pod)
AND
_Player != S_TUT_Lab_TrappedDevourer_6ca92237-eb1e-4ed8-a1ee-18b3bef21558
AND
DB_TUT_Start_Pods(_Pod,_)
AND
NOT DB_TUT_Misc_CorpsePods(_Pod)
AND
DB_Tutorial_Start_PlayerPodAssigned((CHARACTER)_Player, (ITEM)_Pod)
AND
NOT DB_DialogNPCs(_, _Pod, _)
THEN
StartVoiceBark((VOICEBARKRESOURCE)TUT_Misc_VB_OriginPod_PlayerEmpty_7691d759-d302-143a-1aa2-58de2a8a7651, _Player);

IF 
UseStarted(S_TUT_Lab_TrappedDevourer_6ca92237-eb1e-4ed8-a1ee-18b3bef21558, _Pod)
AND
DB_TUT_Start_Pods(_Pod,_)
THEN
PROC_TryStartAD((DIALOGRESOURCE)GLO_AD_CannotUseNow_Muted_79fe3a97-7e74-6f79-9afb-6a5d947a4b0b, S_TUT_Lab_TrappedDevourer_6ca92237-eb1e-4ed8-a1ee-18b3bef21558);



//END_REGION

//REGION Laezel Comments

IF
EnteredTrigger(_Player, _Trigger)
AND
DB_TUT_Guide(_Guide)
AND
DB_TUT_Misc_GuideInclusion(_Guide, _Trigger, _OnlyOnce, _VoiceBark, _GuideAD)
AND
QRY_OnlyOnce(_OnlyOnce)
AND
NOT QRY_TUT_Misc_GuidePresent((CHARACTER)_Player, (VOICEBARKRESOURCE)_VoiceBark, (DIALOGRESOURCE)_GuideAD, (CHARACTER)_Guide)
THEN
StartVoiceBark(_VoiceBark, _Player);

QRY
QRY_TUT_Misc_GuidePresent((CHARACTER)_Player, (VOICEBARKRESOURCE)_VoiceBark, (DIALOGRESOURCE)_GuideAD,(CHARACTER)_Guide)
AND
QRY_SpeakerIsAvailableAndInDialogRange(_Guide, _Player)
THEN
PROC_TUT_Misc_GuideComment((VOICEBARKRESOURCE)_VoiceBark, (DIALOGRESOURCE)_GuideAD,(CHARACTER)_Guide);

PROC
PROC_TUT_Misc_GuideComment((VOICEBARKRESOURCE)_VoiceBark, (DIALOGRESOURCE)_GuideAD,(CHARACTER)_Guide)
AND
QRY_StartDialog_Fixed(_GuideAD, _Guide)
THEN
DB_NOOP(1);


//END_REGION

//REGION Devil Shouts

IF
TurnStarted(_Devil)
AND
DB_TUT_Misc_Devils(_Devil, _AD)
AND
GetFlag((FLAG)TUT_Misc_State_AlreadyTalked_a8e396e9-bcf8-4b3a-ab6e-71e7f6e0f486, _Devil, 1)
THEN
ClearFlag((FLAG)TUT_Misc_State_AlreadyTalked_a8e396e9-bcf8-4b3a-ab6e-71e7f6e0f486, _Devil, 0);

IF
TurnStarted(_Devil)
AND
DB_TUT_Misc_Devils(_Devil, _AD)
AND
NOT DB_CantTalk_IgnoreCombat(_Devil)
AND
GetFlag((FLAG)TUT_Misc_State_AlreadyTalked_a8e396e9-bcf8-4b3a-ab6e-71e7f6e0f486, _Devil, 0)
THEN
PROC_TryStartAD(_AD, _Devil);
SetFlag((FLAG)TUT_Misc_State_AlreadyTalked_a8e396e9-bcf8-4b3a-ab6e-71e7f6e0f486, _Devil, 0);

//END_REGION

//REGION Rubble Fall

PROC
PROC_TUT_Misc_RegisterRumbleRooms()
AND
DB_TUT_Misc_RumbleRooms(_Helper, _Trigger, _Amplifier)
THEN
PROC_TriggerRegisterForPlayers(_Trigger);

QRY
QRY_TUT_Misc_CheckForPlayers((TRIGGER)_Trigger, (CHARACTER)_Char)
AND
DB_Players(_Player)
AND
_Player != _Char
AND
DB_InRegion(_Player, _Trigger)
THEN
DB_NOOP(1);

IF
EnteredTrigger(_Char, _Trigger)
AND
DB_TUT_Misc_RumbleRooms(_Helper, _Trigger, _Amplifier)
AND
NOT QRY_TUT_Misc_CheckForPlayers((TRIGGER)_Trigger, (CHARACTER)_Char)
THEN
DB_TUT_Misc_RoomRumbleActive(_Trigger);
PROC_TUT_Misc_RumbleStart((ITEM)_Helper, (TRIGGER)_Trigger, (INTEGER)_Amplifier);

IF
LeftTrigger(_Char, _Trigger)
AND
DB_TUT_Misc_RumbleRooms(_Helper, _Trigger, _)
AND
NOT QRY_TUT_Misc_CheckForPlayers((TRIGGER)_Trigger, (CHARACTER)_Char)
THEN
NOT DB_TUT_Misc_RoomRumbleActive(_Trigger);

PROC
PROC_TUT_Misc_RumbleStart((ITEM)_Helper, (TRIGGER)_Trigger, (INTEGER)_Multiplier)
AND
DB_TUT_Misc_RoomRumbleActive(_Trigger)
AND
Random(10000, _Random)
AND
IntegerProduct(_Random, _Multiplier, _Add)
AND
IntegerSum(30000, _Add, _Delay)
THEN
ObjectTimerCancel(_Helper, "TUT_Misc_RubbleDelay");
ObjectTimerLaunch(_Helper, "TUT_Misc_RubbleDelay", _Delay);

IF
ObjectTimerFinished(_Helper, "TUT_Misc_RubbleDelay")
AND
DB_TUT_Misc_RumbleRooms((ITEM)_Helper, (TRIGGER)_Trigger, _Amplifier)
THEN
PROC_LoopEffect((EFFECTRESOURCE)VFX_Script_Tutorial_CameraShake_01_0661ac5d-e5e7-79ad-0a1e-538a31a4ce0a, _Helper, "TUT_Misc_RubbleCollapse", "TUT_Avernus_C", "");
ObjectTimerCancel(_Helper, "TUT_Misc_RubbleCollapse");
ObjectTimerLaunch(_Helper, "TUT_Misc_RubbleCollapse", 5000);

IF
ObjectTimerFinished(_Helper, "TUT_Misc_RubbleCollapse")
AND
DB_TUT_Misc_RumbleRooms((ITEM)_Helper, (TRIGGER)_Trigger, _Amplifier)
THEN
PROC_StopLoopEffect(_Helper, "TUT_Misc_RubbleCollapse");
PROC_TUT_Misc_RumbleStart((ITEM)_Helper, (TRIGGER)_Trigger, (INTEGER)_Amplifier);

//END_REGION

//REGION Elevators


PROC
PROC_TUT_Misc_ElevatorInit()
AND
DB_TUT_Misc_Elevator(_Elevator, _, _, _, _, _)
THEN
DB_TUT_Misc_ElevatorPosition(_Elevator, "Down");

//REGION Elevator Lever
IF
UseStarted(_Char, _Lever)
AND
DB_TUT_Misc_Elevator(_Elevator, _Target, _, _, _, _Lever)
AND
NOT DB_TUT_Misc_ElevatorMoving(_Elevator)
AND
DB_TUT_Misc_ElevatorPosition(_Elevator, "Up")
THEN
NOT DB_TUT_Misc_ElevatorPosition(_Elevator, "Up");
DB_TUT_Misc_ElevatorPosition(_Elevator, "Down");
DB_TUT_Misc_ElevatorMoving(_Elevator);
PROC_TUT_Misc_FindDestination((LEVELTEMPLATE)_Elevator, (TRIGGER)_Target);

IF
UseStarted(_Char, _Lever)
AND
DB_TUT_Misc_Elevator(_Elevator, _, _, _Target, _, _Lever)
AND
NOT DB_TUT_Misc_ElevatorMoving(_Elevator)
AND
DB_TUT_Misc_ElevatorPosition(_Elevator, "Down")
THEN
NOT DB_TUT_Misc_ElevatorPosition(_Elevator, "Down");
DB_TUT_Misc_ElevatorPosition(_Elevator, "Up");
DB_TUT_Misc_ElevatorMoving(_Elevator);
PROC_TUT_Misc_FindDestination((LEVELTEMPLATE)_Elevator, (TRIGGER)_Target);
//END_REGION

IF
UseStarted(_Char, _DownLever)
AND
DB_TUT_Misc_Elevator(_Elevator, _Target, _DownLever, _, _, _)
AND
NOT DB_TUT_Misc_ElevatorMoving(_Elevator)
AND
DB_TUT_Misc_ElevatorPosition(_Elevator, "Up")
THEN
NOT DB_TUT_Misc_ElevatorPosition(_Elevator, "Up");
DB_TUT_Misc_ElevatorPosition(_Elevator, "Down");
DB_TUT_Misc_ElevatorMoving(_Elevator);
PROC_TUT_Misc_FindDestination((LEVELTEMPLATE)_Elevator, (TRIGGER)_Target);

IF
UseStarted(_Char, _UpLever)
AND
DB_TUT_Misc_Elevator(_Elevator, _, _, _Target, _UpLever, _)
AND
NOT DB_TUT_Misc_ElevatorMoving(_Elevator)
AND
DB_TUT_Misc_ElevatorPosition(_Elevator, "Down")
THEN
NOT DB_TUT_Misc_ElevatorPosition(_Elevator, "Down");
DB_TUT_Misc_ElevatorPosition(_Elevator, "Up");
DB_TUT_Misc_ElevatorMoving(_Elevator);
PROC_TUT_Misc_FindDestination((LEVELTEMPLATE)_Elevator, (TRIGGER)_Target);

PROC
PROC_TUT_Misc_FindDestination((LEVELTEMPLATE)_Elevator, (TRIGGER)_Target)
AND
GetPosition(_Target, _X, _Y, _Z)
THEN
PROC_TUT_Misc_ElevatorMove((LEVELTEMPLATE)_Elevator, (REAL)_X, (REAL)_Y, (REAL)_Z);

PROC
PROC_TUT_Misc_ElevatorMove((LEVELTEMPLATE)_Elevator, (REAL)_X, (REAL)_Y, (REAL)_Z)
THEN
PlatformMoveTo(_Elevator, _X, _Y, _Z, 1.5, "TUT_Misc_ElevatorMoved", 0);

IF
PlatformMovementFinished(_Elevator, "TUT_Misc_ElevatorMoved")
AND
DB_TUT_Misc_Elevator((LEVELTEMPLATE)_Elevator, _, _, _, _, _)
THEN
NOT DB_TUT_Misc_ElevatorMoving(_Elevator);

PROC
PROC_BlockUseOfItem(_Player, _Lever)
AND
DB_Players(_Player)
AND
DB_TUT_Misc_ElevatorLevers(_, _Lever, _)
AND
QRY_TUT_Misc_ElevatorLeverBlocked(_Lever)
THEN
DB_CustomUseItemResponse(_Player, _Lever, 0);

PROC
PROC_BlockUseOfItem(_Player, _Lever)
AND
DB_TUT_Misc_ElevatorLevers(_, _Lever, _)
AND
NOT DB_TUT_Misc_UsedElevatorLever(_Player)
AND
QRY_TUT_Misc_ElevatorLeverBlocked(_Lever)
AND
DB_Players(_Player)
THEN
DB_TUT_Misc_UsedElevatorLever(_Player); // Avoid spamming the PAD
PROC_TryStartAD((DIALOGRESOURCE)TUT_Misc_PAD_BlockedElevatorLever_8a553ab0-d62e-c043-4734-6c1cda29da32, _Player);


// Elevator moving case
QRY
QRY_TUT_Misc_ElevatorLeverBlocked((ITEM)_Lever)
AND
DB_TUT_Misc_ElevatorLevers(_Elevator, _Lever, _)
AND
DB_TUT_Misc_ElevatorMoving(_Elevator)
THEN
DB_NOOP(1);

// Elevator in position case
QRY
QRY_TUT_Misc_ElevatorLeverBlocked((ITEM)_Lever)
AND
DB_TUT_Misc_ElevatorLevers(_Elevator, _Lever, _Position)
AND
DB_TUT_Misc_ElevatorPosition(_Elevator, _Position)
THEN
DB_NOOP(1);

IF
AutomatedDialogEnded((DIALOGRESOURCE)TUT_Misc_PAD_BlockedElevatorLever_8a553ab0-d62e-c043-4734-6c1cda29da32, _ID)
AND
DB_DialogPlayers(_ID, _Player, 1)
AND
DB_TUT_Misc_UsedElevatorLever((CHARACTER)_Player)
THEN
NOT DB_TUT_Misc_UsedElevatorLever((CHARACTER)_Player);

IF
AutomatedDialogRequestFailed((DIALOGRESOURCE)TUT_Misc_PAD_BlockedElevatorLever_8a553ab0-d62e-c043-4734-6c1cda29da32, _ID)
AND
DB_DialogPlayers(_ID, _Player, 1)
AND
DB_TUT_Misc_UsedElevatorLever((CHARACTER)_Player)
THEN
NOT DB_TUT_Misc_UsedElevatorLever((CHARACTER)_Player);
//END_REGION

//REGION Resting Station

IF
DB_TUT_Misc_RestingStations((ITEM)_Station)
THEN
DB_GLO_RestorationStation(_Station);

IF
UseStarted(_Player, _Station)
AND
DB_Players(_Player)
AND
DB_TUT_Misc_RestingStations(_Station)
AND
QRY_OncePerUserAndNearbyPlayers(_Player, "TUT_Misc_UsedRestingStation")
THEN
PROC_TryStartAD((DIALOGRESOURCE)TUT_Misc_PAD_RestingStation_2190b457-fb74-1914-5889-94927b96df21, _Player);
//END_REGION

//REGION Final Guide Dialogue

IF
EnteredTrigger(_Player, S_TUT_Helm_GuideWarningBounds_f63b25b7-e77c-4d7b-874e-b5c61ab35802)
AND
DB_TUT_Guide(_Guide)
AND
_Guide != _Player
AND
QRY_SpeakerIsAvailableAndInDialogRange((CHARACTER)_Guide, _Player)
AND
DB_TUT_GuideDialogues("BeforeHelm", _Guide, _Dialogue)
AND
NOT DB_OnlyOnce("TUT_LowerDeck_BeforeHelm")
AND
QRY_StartDialog_Fixed(_Dialogue, _Guide, _Player)
AND
QRY_OnlyOnce("TUT_LowerDeck_BeforeHelm")
THEN
DB_NOOP(1);

//END_REGION

//REGION Mindflayer Hostile

IF
RelationChanged((FACTION)Hero_a1542c81-6895-929e-4522-10ce218bb360, (FACTION)ACT0a_TUT_HelmMindflayer_8edc7770-b83c-412e-8a5a-5427a63e5b8a, _Relation, _)
AND
_Relation < 50
AND
DB_TUT_Guide(_Guide)
AND
DB_Players(_Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(_Guide, _Player)
AND
DB_TUT_GuideDialogues("MindflayerHostile", (CHARACTER)_Guide, _Dialogue)
AND
NOT DB_OnlyOnce("TUT_Misc_MindflayersHostile")
AND
QRY_StartDialog_Fixed(_Dialogue, _Guide)
AND
QRY_OnlyOnce("TUT_Misc_MindflayersHostile")
THEN
DB_NOOP(1);


//END_REGION

//REGION Nautiloid Tank VB

IF
UseStarted(_Player, _Tank)
AND
DB_Players(_Player)
AND
GetTemplate(_Tank, _Template)
AND
DB_TUT_Misc_TankTemplate((ITEM)_Template)
AND
QRY_OnlyOnce("TUT_Misc_TankCommentFinished")
THEN
StartVoiceBark(TUT_Misc_VB_NautiloidTank_1e8307f1-07a9-584e-858f-e4188689be37, _Player);


//END_REGION

//REGION Tentacles

PROC
PROC_TUT_Misc_TentacleStart()
AND
DB_TUT_Misc_Tentacles(_Tentacle)
THEN
SetForceUpdate(_Tentacle, 1);

IF
OnShutdown(_)
AND
DB_TUT_Misc_Tentacles(_Tentacle)
THEN
SetForceUpdate(_Tentacle, 0);


//END_REGION

//REGION Progress Triggers
IF
DB_TUT_Misc_TutorialProgressTrigger(_Trigger, _)
THEN
PROC_TriggerRegisterForPlayers(_Trigger);

IF
EnteredTrigger(_Player, _Trigger)
AND
DB_TUT_Misc_TutorialProgressTrigger(_Trigger, _)
AND
NOT DB_TUT_Guide(_Player)
THEN
PROC_TUT_Misc_UpdateProgress((TRIGGER)_Trigger, (CHARACTER)_Player);

// Update progress if going forward
PROC
PROC_TUT_Misc_UpdateProgress((TRIGGER)_Trigger, (CHARACTER)_Player)
AND
QRY_OnlyOnce_Reset("TUT_UpdateProgress")
AND
DB_TUT_Misc_TutorialProgressTrigger(_Trigger, _Weight)
AND
DB_TUT_Misc_TutorialProgress(_, _PrevWeight)
AND
_Weight > _PrevWeight
AND
QRY_OnlyOnce("TUT_UpdateProgress")
THEN
PROC_TUT_Misc_UpdateProgress_Internal(_Trigger);

// Only regress progress if the difference is big enough
PROC
PROC_TUT_Misc_UpdateProgress((TRIGGER)_Trigger, (CHARACTER)_Player)
AND
QRY_OnlyOnce("TUT_UpdateProgress")
AND
DB_TUT_Misc_TutorialProgressTrigger(_Trigger, _Weight)
AND
DB_TUT_Misc_TutorialProgress(_PrevTrigger, _MaxWeight)
AND
IntegerSubtract(_MaxWeight, _Weight, _Difference)
AND
_Difference >= 2
THEN
PROC_TUT_Misc_TryPlayBacktrackAD(_Player);
PROC_TUT_Misc_UpdateProgress_Internal(_Trigger);

// Special case for transformation room.
PROC
PROC_TUT_Misc_UpdateProgress((TRIGGER)S_TUT_ProgressTrigger_TransformationRoom_87b40fb6-2271-4ed7-a87b-397c0853c6d9, (CHARACTER)_)
THEN
NOT DB_TUT_Misc_TutorialProgressTrigger((TRIGGER)S_TUT_ProgressTrigger_TransformationRoom_87b40fb6-2271-4ed7-a87b-397c0853c6d9, 4);
PROC_TriggerUnregisterForPlayers(S_TUT_ProgressTrigger_TransformationRoom_87b40fb6-2271-4ed7-a87b-397c0853c6d9);

IF //Makes it so the guide only does the back track AD on the second time of entering the room
LeftTrigger(_, (TRIGGER)S_TUT_TransformChamber_SpotPodStorage_a4ac6721-fa25-4973-81c3-1e497a5a8af7)
AND
QRY_OnlyOnce("TUT_Misc_HadSpotVB")
THEN
DB_TUT_Misc_TutorialProgressTrigger((TRIGGER)S_TUT_ProgressTrigger_TransformationRoom_87b40fb6-2271-4ed7-a87b-397c0853c6d9, 4);

// Play AD if the guide is close enough to the player backtracking.
PROC
PROC_TUT_Misc_TryPlayBacktrackAD((CHARACTER)_Player)
AND
DB_TUT_Guide(_Guide)
AND
_Player != _Guide
AND
GetDistanceTo(_Player, _Guide, _Dist)
AND
_Dist <= 10.0
AND
DB_TUT_Misc_GuideBacktrackADs(_AD, _Guide)
THEN
PROC_TryStartAD(_AD, _Guide);

PROC
PROC_TUT_Misc_UpdateProgress_Internal((TRIGGER)_Trigger)
AND
DB_TUT_Misc_TutorialProgressTrigger(_Trigger, _Weight)
AND
DB_TUT_Misc_TutorialProgress(_PrevTrigger, _PrevWeight)
THEN
NOT DB_TUT_Misc_TutorialProgress(_PrevTrigger, _PrevWeight);
DB_TUT_Misc_TutorialProgress(_Trigger, _Weight);
//END_REGION

//REGION Deck cannons
IF
DB_TUT_Misc_Cannons(_Cannon)
THEN
DB_IgnoreAssault(_Cannon);
PROC_CharacterDisableAllCrimes(_Cannon,1);
PROC_SetInvulnerable(_Cannon, 1);
//END_REGION

//REGION Journal

PROC
PROC_TUT_Misc_UpdateHelmMarkerPosition((STRING)_Position)
AND
DB_TUT_Misc_JournalHelmPos(_PosTrigger,_Position)
THEN
TeleportTo(S_TUT_NautiloidEscape_Helm_b784add8-f417-4650-aa47-080cb29dc6d0,_PosTrigger,"");

PROC
PROC_OneShotTriggerEntered(_,(TRIGGER)S_TUT_Misc_StartRoomJournalPos_d426edee-d7e0-4faa-86a1-510a0eaf9eaf)
THEN
TeleportTo(S_TUT_NautiloidEscape_Escape_31a613b5-9fb5-4949-8fc3-730cc82cec30, S_TUT_NautiloidEscape_Helm_b784add8-f417-4650-aa47-080cb29dc6d0,"");

PROC
PROC_OneShotTriggerEntered(_,(TRIGGER)S_TUT_Misc_UpperFloorJournalPos_7789fd25-490a-4885-9c17-f214ecb1c745)
THEN
PROC_TUT_Misc_UpdateHelmMarkerPosition("Antechamber");

PROC
PROC_OneShotTriggerEntered(_,(TRIGGER)S_TUT_Misc_AntechamberJournalPos_b97895e5-f509-44f0-a771-0b7d1741afbe)
THEN
PROC_TUT_Misc_UpdateHelmMarkerPosition("Transponder");

PROC
PROC_TUT_JournalStart()
AND
DB_Players(_Player)
THEN
QuestUpdate((CHARACTER)_Player, "TUT_NautiloidEscape", "Start");
PROC_GLO_ActivitiesPS5_TutorialStart();


PROC
PROC_TUT_JournalStart()
AND
DB_TUT_Start_JournalUpdate((CHARACTER) NULL_00000000-0000-0000-0000-000000000000,_Quest, _Step)
THEN
QuestUpdate(_Quest, _Step);
NOT DB_TUT_Start_JournalUpdate((CHARACTER) NULL_00000000-0000-0000-0000-000000000000,_Quest, _Step);

PROC
PROC_TUT_JournalStart()
AND
DB_TUT_Start_JournalUpdate(_Char,_Quest, _Step)
THEN
QuestUpdate(_Char,_Quest, _Step);
NOT DB_TUT_Start_JournalUpdate(_Char,_Quest, _Step);

IF
FlagSet((FLAG)TUT_Misc_Quest_LearnedHelm_Laezel_674e2be1-0afe-448d-bfcb-5d8bdb6892e2, _Player, _)
AND
QRY_OnlyOnce("TUT_Misc_LearnedHelm")
THEN
QuestUpdate((CHARACTER)_Player, "TUT_NautiloidEscape", "LearnedHelm_Laezel");

IF
FlagSet((FLAG)TUT_Misc_Quest_LearnedHelm_AltGuide_ef78d70d-50c0-4b4b-a5f0-2d125341f4ce, _Player, _)
AND
QRY_OnlyOnce("TUT_Misc_LearnedHelm")
THEN
QuestUpdate((CHARACTER)_Player, "TUT_NautiloidEscape", "LearnedHelm_AltGuide");

IF
FlagSet((FLAG)TUT_Misc_Quest_LearnedHelm_Devourer_4c776485-6495-49a9-aa4d-b9816b421105, _Player, _)
AND
QRY_OnlyOnce("TUT_Misc_LearnedHelm")
THEN
QuestUpdate((CHARACTER)_Player, "TUT_NautiloidEscape", "LearnedHelm_Devourer");

//REGION Shadowheart Escape

IF
DialogEnded((DIALOGRESOURCE)TUT_TransformChamber_PodGirl_02fa0520-60ff-573c-0d96-4782711cbdde, _)
AND
NOT DB_QuestIsAccepted("TUT_ShadowheartEscape")
THEN
QuestUpdate("TUT_ShadowheartEscape", "FindCaptive");

IF
AddedTo(S_TUT_TransformChamber_PodKey_19df3203-a4fb-4513-be14-fe036acffc0e, _Char, _)
AND
DB_PartyMembers((CHARACTER)_Char)
AND
QRY_OnlyOnce("TUT_Misc_FoundRune")
THEN
QuestUpdate("TUT_ShadowheartEscape", "FoundRune");

PROC
PROC_TUT_TransformChamber_FreeShadowheart()
AND
DB_GlobalFlag((FLAG)TUT_TransformChamber_PodGirl_Event_BarbarianStrengthSuccess_551a3ad7-67d1-48dd-81f5-f5f9cd10d63d)
THEN
QuestUpdate("TUT_ShadowheartEscape", "UsedForce");

PROC
PROC_TUT_TransformChamber_FreeShadowheart()
AND
NOT DB_GlobalFlag((FLAG)TUT_TransformChamber_PodGirl_Event_BarbarianStrengthSuccess_551a3ad7-67d1-48dd-81f5-f5f9cd10d63d)
THEN
QuestUpdate("TUT_ShadowheartEscape", "UsedRune");

IF
DialogEnded((DIALOGRESOURCE)TUT_TransformChamber_ShadowheartFreed_5ac55171-4df5-a168-9dad-856667d44988, _ID)
AND
NOT QRY_TUT_TransformChamber_RecruitedShadowheart()
THEN
QuestUpdate("TUT_ShadowheartEscape", "FreedDidNotRecruit");

IF
DialogEnded((DIALOGRESOURCE)TUT_TransformChamber_ShadowheartFreed_5ac55171-4df5-a168-9dad-856667d44988, _ID)
AND
QRY_TUT_TransformChamber_RecruitedShadowheart()
THEN
QuestUpdate("TUT_ShadowheartEscape", "FreedAndRecruited");

PROC
PROC_TUT_Helm_EndTutorial()
AND
HasActiveStatus(S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679, "TUT_SHADOWHEARTINPOD", 0)
THEN
QuestUpdate("TUT_ShadowheartEscape", "FreedLeftBehind");

PROC
PROC_TUT_Helm_EndTutorial()
AND
HasActiveStatus(S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679, "TUT_SHADOWHEARTINPOD", 1)
THEN
QuestUpdate("TUT_ShadowheartEscape", "LeftBehind");

//END_REGION

//END_REGION

//REGION Wild Magic supression
PROC
PROC_Tutorial_Start()
AND
DB_Avatars(_Character)
AND
IsTagged(_Character, (TAG)SORCERER_WILDMAGIC_885f8675-e400-4d53-924d-6204ff1d9558, 1)
THEN
PROC_WildMagic_Supress(_Character);

PROC
PROC_TUT_Helm_StartFinalCombat()
AND
DB_Avatars(_Character)
AND
IsTagged(_Character, (TAG)SORCERER_WILDMAGIC_885f8675-e400-4d53-924d-6204ff1d9558, 1)
THEN
PROC_WildMagic_Guarantee(_Character);

PROC
PROC_TUT_Helm_EndTutorial()
AND
DB_Avatars(_Character)
AND
IsTagged(_Character, (TAG)SORCERER_WILDMAGIC_885f8675-e400-4d53-924d-6204ff1d9558, 1)
THEN
PROC_WildMagic_Normal(_Character);
//END_REGION

//REGION Not adequate in battle

//Special defeat tracking for the first combat, so Lae'zel won't praise players for being bad at this game

PROC
PROC_StateSet_Defeated(_Player)
AND
NOT DB_GlobalFlag((FLAG)TUT_LowerDeck_State_ImpsDead_c505f03a-c6d4-4ac5-a0a1-ea805fc1a589)
AND
GetFlag((FLAG)TUT_LowerDeck_Event_LaezelBecomesFollower_6625adc5-8c6d-448f-8963-26dcf458e6ae,_Player,1)
THEN
SetFlag(TUT_LowerDeck_Event_PlayerCharacterIsDefeated_7100ed8d-6402-84b1-d5e0-ee9ddcad8f75);


//END_REGION

PROC
PROC_TUT_Helm_EndTutorial()
THEN
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Tutorial"
