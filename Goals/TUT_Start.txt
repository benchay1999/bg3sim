Version 1
SubGoalCombiner SGC_AND
INITSECTION
PROC_TriggerRegisterForPlayers(S_TUT_Start_HoleBounds_e594a65c-17fb-44e9-893d-82a4e58557f8);
PROC_TriggerRegisterForPlayers(S_TUT_Start_HoleBounds_e594a65c-17fb-44e9-893d-82a4e58557f8);
DB_TUT_Start_Pods((ITEM)S_TUT_PlayerPod_001_91b65f9b-8f2c-404c-95e4-d7eed9f2b767, S_TUT_StartPoint_001_45c9c4f9-0f81-ee38-9d45-bdefc07761d9);
DB_TUT_Start_Pods((ITEM)S_TUT_PlayerPod_002_4a9d344d-8bfa-4bea-a8dd-cc54327b0cfb, S_TUT_StartPoint_002_5d2768b0-14a7-e49a-8a17-486ac9de3776);
DB_TUT_Start_Pods((ITEM)S_TUT_PlayerPod_003_e2df8852-6539-403c-9eef-f169d129307e, S_TUT_StartPoint_003_183557b7-4de8-ebd3-8999-5c4c9e8d89ed);
DB_TUT_Start_Pods((ITEM)S_TUT_PlayerPod_004_9cc50df2-46eb-4a32-90c3-9363c85cdd5d, S_TUT_StartPoint_004_001d60b8-5096-e3a0-9e7b-a5af6e47ad25);
DB_TUT_Start_Pods((ITEM)S_TUT_Start_LaezelPod_0aa6b2a4-808a-4eb3-84d7-face5ac13e44,S_TUT_Start_LaezelStartTrigger_435ca1e3-3e5a-4e9b-9497-d61dc849e124);

DB_TUT_Start_DefaultPodDialog((ITEM)S_TUT_PlayerPod_001_91b65f9b-8f2c-404c-95e4-d7eed9f2b767, (DIALOGRESOURCE)TUT_Start_OriginIntroduction_Default_2a464bcd-c513-d282-e54e-17e6bdbf67b6);
DB_TUT_Start_DefaultPodDialog((ITEM)S_TUT_PlayerPod_002_4a9d344d-8bfa-4bea-a8dd-cc54327b0cfb, (DIALOGRESOURCE)TUT_Start_OriginIntroduction_Default_Pod2_f45b93bf-3a45-3eb7-fc7f-a6cad4f02f76);
DB_TUT_Start_DefaultPodDialog((ITEM)S_TUT_PlayerPod_003_e2df8852-6539-403c-9eef-f169d129307e, (DIALOGRESOURCE)TUT_Start_OriginIntroduction_Default_Pod3_480f2fad-233a-509a-6205-bedb2d879cf9);
DB_TUT_Start_DefaultPodDialog((ITEM)S_TUT_PlayerPod_004_9cc50df2-46eb-4a32-90c3-9363c85cdd5d, (DIALOGRESOURCE)TUT_Start_OriginIntroduction_Default_Pod4_e63db1c5-e411-0177-569d-3c47f1b15e0c);


PROC_TriggerRegisterForPlayers((TRIGGER)S_TUT_Start_HoleBounds_e594a65c-17fb-44e9-893d-82a4e58557f8);

DB_FlagReactionAfterDialog((FLAG)TUT_Start_Event_GrabTadpole_a9f4bd39-19cf-4280-a8b3-aa3b89ad39e4, (DIALOGRESOURCE)TUT_Start_Brinepool_279b424b-b9dd-f053-33ca-7d42969280fc);
DB_FlagReactionAfterDialog((FLAG)TUT_Start_Event_TouchedPool_a7ac838d-0187-4d48-93d8-0bb19d148821, (DIALOGRESOURCE)TUT_Start_Brinepool_279b424b-b9dd-f053-33ca-7d42969280fc);
DB_Dialogs(S_TUT_Start_Brinepool_5efb6f66-b2ce-403e-b7c0-b68f607ef13a, (DIALOGRESOURCE)TUT_Start_Brinepool_279b424b-b9dd-f053-33ca-7d42969280fc);
DB_DialogBlockAttackButton((DIALOGRESOURCE)TUT_Start_Brinepool_279b424b-b9dd-f053-33ca-7d42969280fc);

DB_TUT_Start_CurrentDeadThrall(S_TUT_DeadTrall_000_44779e34-e013-4b03-b452-8c1cd9e3a658,1);

DB_TUT_Start_KeepRangedWpnClass((TAG)BARD_d93434bd-6b71-4789-b128-ee24156057cc);
DB_TUT_Start_KeepRangedWpnClass((TAG)RANGER_37a733c1-a862-4157-b92a-9cff46232c6a);

DB_TUT_Start_DeadThrallWeaponContainer(S_TUT_DeadTrall_000_44779e34-e013-4b03-b452-8c1cd9e3a658,1);
DB_TUT_Start_DeadThrallWeaponContainer(S_TUT_DeadTrall_001_9cbdd9c3-6282-49e0-8cff-a61d0df6ae67,2);
DB_TUT_Start_DeadThrallWeaponContainer(S_TUT_DeadTrall_002_1b804a0e-1914-4905-b624-4553525510a8,3);
DB_TUT_Start_DeadThrallWeaponContainer(S_TUT_DeadTrall_004_542fcc67-3f17-43bf-b637-d9893be10677,4);

//REGION Origin_Introduction

DB_TUT_Start_NonRandomPodStart((CHARACTER)S_Player_Wyll_c774d764-4a17-48dc-b470-32ace9ce447d,(DIALOGRESOURCE)TUT_Start_OriginIntroduction_Wyll_40222c0c-d221-e78f-8922-07c49c023072,(ITEM)S_TUT_PlayerPod_003_e2df8852-6539-403c-9eef-f169d129307e);
DB_TUT_Start_NonRandomPodStart((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)TUT_Start_OriginIntroduction_Karlach_f0987bdb-c3b0-d6c0-cc19-648c54efb5f7,(ITEM)S_TUT_PlayerPod_003_e2df8852-6539-403c-9eef-f169d129307e);
DB_TUT_Start_NonRandomPodStart((CHARACTER)S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679, (DIALOGRESOURCE)TUT_Start_OM_Shadowheart_AOM_c989b41e-3af8-40ec-31d6-309d3f37b129,(ITEM)S_TUT_PlayerPod_001_91b65f9b-8f2c-404c-95e4-d7eed9f2b767);
DB_TUT_Start_NonRandomPodStart((CHARACTER)S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12, (DIALOGRESOURCE)TUT_Start_OriginIntroduction_Laezel_72f81075-55c1-5ff2-db2e-b73014f3ca15, (ITEM)S_TUT_Start_LaezelPod_0aa6b2a4-808a-4eb3-84d7-face5ac13e44); 

DB_IgnoreMutingStatussesDialog((DIALOGRESOURCE)TUT_Start_OriginIntroduction_Default_2a464bcd-c513-d282-e54e-17e6bdbf67b6);

//END_REGION

//REGION Trigger Areas for Movement Tutorial
DB_TUT_StartPointAreas((TRIGGER)S_TUT_MovementTutorialArea_Laezel_8df45efc-b122-475e-baf3-fcae1538a6b0);
DB_TUT_StartPointAreas((TRIGGER)S_TUT_MovementTutorialArea_001_b0b8d325-fcbd-4a46-aa1d-181e44d47766);
DB_TUT_StartPointAreas((TRIGGER)S_TUT_MovementTutorialArea_002_6b453f1f-4b9b-4b3b-bb01-4d25562088ce);
DB_TUT_StartPointAreas((TRIGGER)S_TUT_MovementTutorialArea_003_3629d352-a458-4949-aa38-46fe1dfce871);
DB_TUT_StartPointAreas((TRIGGER)S_TUT_MovementTutorialArea_004_d7c3e306-444e-4b39-b8c2-c2b34b11189b);
//END_REGION
//REGION Guide

DB_TUT_GuideDialogues("TransformLever", S_TUT_GithGuide_201a0ee6-6411-4564-aaea-1ca057bfea04, (DIALOGRESOURCE)TUT_TransformChamber_AD_AltGuide_LeverResponse_bebfd2a7-2251-349a-2322-0d952633c3da);
DB_TUT_GuideDialogues("TransformLever", S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12, (DIALOGRESOURCE)TUT_TransformChamber_AD_LaezelLeverResponse_457b1451-fd30-d2e6-c93d-ab682e5303c4);

DB_TUT_GuideDialogues("Stairs", S_TUT_GithGuide_201a0ee6-6411-4564-aaea-1ca057bfea04, (DIALOGRESOURCE)TUT_LowerDeck_AltGuide_Stairs_1548f91b-d731-a538-7bc5-b1a526114759);
DB_TUT_GuideDialogues("Stairs", S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12, (DIALOGRESOURCE)TUT_LowerDeck_LaezelStairs_f4918acb-48dc-4dca-341f-6815dc29e325);

DB_TUT_GuideDialogues("BeforeHelm", S_TUT_GithGuide_201a0ee6-6411-4564-aaea-1ca057bfea04, (DIALOGRESOURCE)TUT_Helm_AltGuide_BeforeHelm_a4d10973-e462-b08d-7eae-80ad803a1c0e);
DB_TUT_GuideDialogues("BeforeHelm", S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12, (DIALOGRESOURCE)TUT_Helm_LaezelBeforeHelm_19d3f607-f047-e32e-53d1-8b6aba0006d7);

DB_TUT_GuideDialogues("MindflayerHostile", S_TUT_GithGuide_201a0ee6-6411-4564-aaea-1ca057bfea04, (DIALOGRESOURCE)TUT_Misc_AD_AltGuide_MindflayerHostile_022edd17-2e69-63fa-bda0-259f70d4a6b8);
DB_TUT_GuideDialogues("MindflayerHostile", S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12, (DIALOGRESOURCE)TUT_Misc_AD_LaezelMindflayerHostile_c77787ee-9971-aca6-9493-1510db736c25);

DB_Inclusion_Object(S_TUT_GithGuide_201a0ee6-6411-4564-aaea-1ca057bfea04,(FLAG)TUT_GithGuide_StartInclusion_3e793a76-6764-4789-a239-ec34b99e41e3,(FLAG)TUT_GithGuide_EndInclusion_6e2842a1-334e-4e5b-a974-104b98854cc5);

//END_REGION

KBSECTION
//REGION Guide Start
PROC
PROC_Tutorial_Start()
AND
NOT DB_Avatars((CHARACTER)S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12)
THEN
PROC_GLO_Origins_RemoveRecruitmentDialogs(S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12);
SetHasDialog(S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12, 0);
PROC_RemoveDialog(S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12);
PROC_SetCanFight(S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12, 1);
TriggerSetSoundState(Amb_SV_TUT_Avernus_QD_8e69df50-c71a-491d-a3cb-51e8851adfec,"State_Mix_Level","TUT_Avernus",1);

PROC
PROC_Tutorial_Start()
AND
DB_Avatars((CHARACTER)S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679)
THEN
RequestDelete((ITEM)S_TUT_TransformChamber_PodKey_19df3203-a4fb-4513-be14-fe036acffc0e);

PROC
PROC_Tutorial_Start()
AND
NOT DB_Avatars((CHARACTER)S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679)
THEN
DB_HasItemEvent((ITEM)S_TUT_TransformChamber_PodKey_19df3203-a4fb-4513-be14-fe036acffc0e,(FLAG)TUT_TransformChamber_State_HasPodKey_8321a9b7-746a-4bd5-8388-9cd838d061ad);

// Register Movement Tutorial triggers
IF
DB_TUT_StartPointAreas(_Trigger)
THEN
PROC_TriggerRegisterForPlayers(_Trigger);


IF
DB_PartyMembers(_PartyMember)
AND
HasActiveStatus(_PartyMember,"TUT_NO_CRIT_MISS",0)
THEN
ApplyStatus(_PartyMember,"TUT_NO_CRIT_MISS",-1.0);

PROC 
PROC_Tutorial_Start()
AND
DB_PartyMembers(_PartyMember)
AND
HasActiveStatus(_PartyMember,"TUT_NO_CRIT_MISS",0)
THEN
ApplyStatus(_PartyMember,"TUT_NO_CRIT_MISS",-1.0);

PROC
PROC_TUT_Helm_EndTutorial()
AND
DB_PartyMembers(_PartyMember)
AND
HasActiveStatus(_PartyMember,"TUT_NO_CRIT_MISS",1)
THEN
RemoveStatus(_PartyMember,"TUT_NO_CRIT_MISS");

PROC 
PROC_Tutorial_Start()
AND
NOT DB_Avatars((CHARACTER)S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12)
THEN
DB_TUT_Guide((CHARACTER)S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12);
PROC_RemoveDialog((CHARACTER)S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12);
PROC_ForceStopDialog((CHARACTER)S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12);
PROC_SetOnStage(S_TUT_GithGuide_201a0ee6-6411-4564-aaea-1ca057bfea04, 0);
PROC_SetOnStage(S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12, 0);

PROC 
PROC_Tutorial_Start()
AND
DB_Avatars((CHARACTER)S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12)
THEN
DB_TUT_Guide((CHARACTER)S_TUT_GithGuide_201a0ee6-6411-4564-aaea-1ca057bfea04);

IF
DB_TUT_Guide(_Guide)
THEN
DB_Inclusion_NPCDialog((DIALOGRESOURCE)TUT_Helm_DragonAppears_6ffc2909-a928-4b8b-6901-02d823e68880, (CHARACTER)_Guide);

IF
DB_TUT_Start_NonRandomPodStart(_,_Dialog,_)
THEN
DB_IgnoreMutingStatussesDialog(_Dialog);

//END_REGION


//REGION Pod Start

PROC
PROC_Tutorial_Start()
AND
NOT DB_TUT_Start_IsEditor(1)
AND
QRY_OnlyOnce("TUT_Start_MuteSound")
AND
DB_Players(_Player)
THEN
PlayHUDSound(_Player,"TUT_Start_MuteSound");
//Unmute is called from the Mute event to make sure it is always unmuted


PROC
PROC_Tutorial_Start()
AND
GetMaxPartySize(_Max)
THEN
DB_TUT_Start_InitialPartySizeAfterTUT(_Max);

IF
FlagSet((FLAG)TUT_LowerDeck_Event_LaezelBecomesFollower_6625adc5-8c6d-448f-8963-26dcf458e6ae,_,_)
THEN
PROC_TUT_Misc_IncreasePartySize();

IF
FlagSet((FLAG)TUT_TransformChamber_State_RecruitedShadowheart_2c531120-0297-4e8d-9783-762f5ea67c66,_,_)
THEN
PROC_TUT_Misc_IncreasePartySize();

PROC
PROC_TUT_Misc_IncreasePartySize()
AND
GetMaxPartySize(_Max)
AND
IntegerSum(_Max,1,_New)
THEN
SetMaxPartySizeOverride(_New);

IF
FlagSet((FLAG)TUT_Misc_Event_CheckCompanionCanJoin_2aec3d37-19f6-42c1-846c-db857e7cb9c4,_,_)
THEN
PROC_CheckPartyFull();
ClearFlag((FLAG)TUT_Misc_Event_CheckCompanionCanJoin_2aec3d37-19f6-42c1-846c-db857e7cb9c4);

PROC
PROC_TUT_Start_MoveStarterWeapon((CHARACTER)_Player)
AND
QRY_TUT_Start_ChooseWpnToRemove(_Player)
AND
DB_QRYRTN_TUT_Start_ChooseWpnToRemove(_Weapon)
AND
DB_TUT_Start_CurrentDeadThrall(_Thrall,_Delta)
AND
IntegerSum(_Delta, 1, _Sum)
AND
_Sum <= 4
AND
DB_TUT_Start_DeadThrallWeaponContainer(_NewThrall,_Sum)
THEN
ToInventory(_Weapon,_Thrall,1,0,1);
NOT DB_TUT_Start_CurrentDeadThrall(_Thrall,_Delta);
DB_TUT_Start_CurrentDeadThrall(_NewThrall,_Sum);


QRY
QRY_TUT_Start_ChooseWpnToRemove((CHARACTER)_Player)
AND
DB_TUT_Start_KeepRangedWpnClass(_Tag)
AND
IsTagged(_Player,_Tag,1)
AND
GetEquippedItem(_Player,"Melee Main Weapon",_Weapon)
AND
GetEquippedItem(_Player,"Ranged Main Weapon",_RangedWeapon)
THEN
DB_QRYRTN_TUT_Start_ChooseWpnToRemove(_Weapon);
PROC_TUT_Start_EquipRangedWeapon((CHARACTER)_Player, _RangedWeapon);


QRY
QRY_TUT_Start_ChooseWpnToRemove((CHARACTER)_Player)
AND
NOT DB_QRYRTN_TUT_Start_ChooseWpnToRemove(_)
AND
GetEquippedItem(_Player,"Ranged Main Weapon",_Weapon)
THEN
DB_QRYRTN_TUT_Start_ChooseWpnToRemove(_Weapon);

//Unequiping and reequiping ranged weapon to make it be the one selected when starting game
PROC
PROC_TUT_Start_EquipRangedWeapon((CHARACTER)_Player, (ITEM)_Weapon)
THEN
DB_TUT_Start_StartingWeaponTempRemoved(_Player,_Weapon);
Unequip(_Player,_Weapon);

IF
UnequipFailed(_Weapon,_Player)
AND
DB_TUT_Start_StartingWeaponTempRemoved(_Player,_Weapon)
THEN
NOT DB_TUT_Start_StartingWeaponTempRemoved(_Player,_Weapon);

IF
Unequipped(_Weapon,_Player)
AND
DB_TUT_Start_StartingWeaponTempRemoved(_Player,_Weapon)
THEN
NOT DB_TUT_Start_StartingWeaponTempRemoved(_Player,_Weapon);
Equip(_Player,_Weapon,0,0,0);

PROC
PROC_LevelLoadedOnce("TUT_Avernus_C")
AND
DB_Players(_Player)
AND
QRY_OnlyOncePerUser(_Player,"TUT_Start_IntroCinematic")
THEN
PROC_TUT_Start_AssignPods((CHARACTER)_Player);
PROC_TUT_Start_StartIntro((CHARACTER)_Player);

//For debug if reloadedStory
PROC
PROC_Tutorial_Start()
AND
DB_Players(_Player)
AND
NOT DB_TUT_Start_IsEditor(1)
AND
QRY_OnlyOncePerUser(_Player,"TUT_Start_IntroCinematic")
THEN
PROC_TUT_Start_AssignPods((CHARACTER)_Player);
PROC_TUT_Start_StartIntro((CHARACTER)_Player);

PROC
PROC_Tutorial_Start()
AND
QRY_OnlyOnce("TUT_Start_PositionPlayers")
AND
DB_Players(_Player)
THEN
PROC_TUT_Start_MoveStarterWeapon((CHARACTER)_Player);

//Always assigning the same pod to avatars with unique OMs that play in the same location - Shadowheart, Wyll, Karlach
PROC
PROC_TUT_Start_AssignPods((CHARACTER)_Player)
AND
DB_TUT_Start_NonRandomPodStart(_Player,_,_Pod)
AND
DB_TUT_Start_Pods(_Pod, _StartPoint)
THEN
PROC_SetOnStage(_Player,1);
SetCanInteract((ITEM)_Pod, 1);
DB_Tutorial_Start_PlayerPodAssigned(_Player, _Pod); 
TeleportTo(_Player, _StartPoint, "TUT_Start_PodFilled", 0, 0, 0);

//Assign rest of pods
PROC
PROC_TUT_Start_AssignPods((CHARACTER)_Player)
AND
NOT DB_TUT_Start_NonRandomPodStart(_Player,_,_)
AND
DB_TUT_Start_Pods(_Pod, _Trigger)
AND
NOT DB_Tutorial_Start_PlayerPodAssigned((CHARACTER)_Player, _)
AND
NOT DB_Tutorial_Start_PlayerPodAssigned(_, (ITEM)_Pod)
THEN
SetCanInteract((ITEM)_Pod, 1);
DB_Tutorial_Start_PlayerPodAssigned(_Player, _Pod); 
TeleportTo(_Player, _Trigger, "TUT_Start_PodFilled", 0, 0, 0);

PROC
PROC_TUT_Start_StartIntro((CHARACTER)_Player)
AND
DB_TUT_Start_NonRandomPodStart(_Player,_Dialog,_)
AND
QRY_StartDialog_Fixed(_Dialog, _Player)
THEN
DB_NOOP(1);

PROC
PROC_TUT_Start_StartIntro((CHARACTER)_Player)
AND
NOT DB_TUT_Start_NonRandomPodStart(_Player,_,_)
AND
DB_Tutorial_Start_PlayerPodAssigned(_Player, _Pod)
AND
DB_TUT_Start_DefaultPodDialog(_Pod, _Dialog)
AND
QRY_StartDialog_Fixed(_Dialog, _Player)
THEN
DB_NOOP(1);

//END_REGION

//REGION Getting Up

IF
DialogEnded(_Dialog, _ID)
AND
QRY_TUT_Start_IsIntroDialog(_Dialog)
AND
DB_DialogSpeakers(_ID,_Player,1)
THEN
ObjectTimerLaunch(_Player, "TUT_Start_GetUp", 10000);
ObjectTimerLaunch(_Player, "TUT_Start_VoiceBark", 1500);


PROC
PROC_StartDialog_AddExtraSpeakers(_Dialog, (INTEGER)_DialogInstanceID)
AND
QRY_TUT_Start_IsIntroDialog(_Dialog)
THEN
SetLevelStartingDialog(_DialogInstanceID, 1);


QRY
QRY_TUT_Start_IsIntroDialog((DIALOGRESOURCE)_Dialog)
AND
DB_TUT_Start_DefaultPodDialog(_,_Dialog)
THEN
DB_NOOP(1);

QRY
QRY_TUT_Start_IsIntroDialog((DIALOGRESOURCE)_Dialog)
AND
DB_TUT_Start_NonRandomPodStart(_,_Dialog,_)
THEN
DB_NOOP(1);

IF
ObjectTimerFinished(_Player, "TUT_Start_GetUp")
AND
QRY_OnlyOnce("TUT_Start_FadeDone")
THEN
PROC_TUT_JournalStart();


IF
ObjectTimerFinished(_Player, "TUT_Start_VoiceBark")
THEN
PROC_TUT_Start_Voicebark((CHARACTER)_Player);

PROC
PROC_TUT_Start_Voicebark((CHARACTER)_Player)
AND
DB_TUT_Start_CurrentlyPlayingInitialPAD(1)
THEN
DB_TUT_Start_QueuedInitialPAD(_Player);

PROC
PROC_TUT_Start_Voicebark((CHARACTER)_Player)
AND
NOT DB_TUT_Start_CurrentlyPlayingInitialPAD(1)
THEN
DB_TUT_Start_CurrentlyPlayingInitialPAD(1);
PROC_TryStartAD((DIALOGRESOURCE)TUT_Start_PAD_Start_3ef36a5f-64b2-ce27-0696-f93b1cbd846f, _Player);

IF
AutomatedDialogEnded((DIALOGRESOURCE)TUT_Start_PAD_Start_3ef36a5f-64b2-ce27-0696-f93b1cbd846f,_)
AND
DB_TUT_Start_CurrentlyPlayingInitialPAD(1)
THEN
NOT DB_TUT_Start_CurrentlyPlayingInitialPAD(1);

IF
AutomatedDialogEnded((DIALOGRESOURCE)TUT_Start_PAD_Start_3ef36a5f-64b2-ce27-0696-f93b1cbd846f,_)
AND
DB_DialogSpeakers(_ID,_Player,1)
AND
DB_TUT_Start_QueuedInitialPAD((CHARACTER)_Player2)
AND
NOT DB_TUT_Start_CurrentlyPlayingInitialPAD(1)
THEN
DB_TUT_Start_CurrentlyPlayingInitialPAD(1);
NOT DB_TUT_Start_QueuedInitialPAD(_Player2);
PROC_TryStartAD((DIALOGRESOURCE)TUT_Start_PAD_Start_3ef36a5f-64b2-ce27-0696-f93b1cbd846f, _Player2);


//END_REGION

//REGION Brine Pool Explosion

PROC
PROC_FlagReactionAfterDialog(_Player, (FLAG)TUT_Start_Event_TouchedPool_a7ac838d-0187-4d48-93d8-0bb19d148821)
AND
QRY_OnlyOnce("TUT_Start_BrinePoolExplode")
THEN
PROC_TUT_Start_BrinepoolBurst((CHARACTER)_Player);


IF
AttackedBy(S_TUT_Start_Brinepool_5efb6f66-b2ce-403e-b7c0-b68f607ef13a, _Player, _, _, _Damage, _, _)
AND
_Damage > 0
AND
QRY_OnlyOnce("TUT_Start_BrinePoolExplode")
THEN
PROC_ForceStopDialog(S_TUT_Start_Brinepool_5efb6f66-b2ce-403e-b7c0-b68f607ef13a);
PROC_TUT_Start_BrinepoolBurst((CHARACTER)_Player);

PROC
PROC_TUT_Start_BrinepoolBurst((CHARACTER)_Player)
THEN
Die(S_TUT_Start_Brinepool_5efb6f66-b2ce-403e-b7c0-b68f607ef13a, DEATHTYPE.Physical, NULL_00000000-0000-0000-0000-000000000000, 0, 0);
PROC_TUT_Start_BrinepoolExplode();

PROC
PROC_TUT_Start_BrinepoolBurst((CHARACTER)_Player)
AND
DB_Players(_Player)
THEN
RealtimeObjectTimerLaunch(_Player, "TUT_Start_BrinePoolExploded", 1000);

IF
ObjectTimerFinished(_Player, "TUT_Start_BrinePoolExploded")
THEN
StartVoiceBark((VOICEBARKRESOURCE)TUT_Start_VB_BrinePoolExplode_950dd6e6-a213-2463-c762-142267712901, (CHARACTER)_Player);

PROC
PROC_TUT_Start_BrinepoolExplode()
THEN
CreateExplosion(S_TUT_Start_Brinepool_5efb6f66-b2ce-403e-b7c0-b68f607ef13a, "Projectile_TUT_BrineExplode", -1, S_TUT_Start_Brinepool_5efb6f66-b2ce-403e-b7c0-b68f607ef13a);



//END_REGION

//REGION Mindflayer Interaction

IF
CharacterLootedCharacter(_Player, S_TUT_Start_DeadMindflayer_af055273-29fb-4e83-bef3-9dbec5c66211)
AND
QRY_OnlyOnce("TUT_Start_LootedMindflayer")
THEN
StartVoiceBark((VOICEBARKRESOURCE)TUT_Start_VB_DeadMindflayer_40f36fe8-e960-d9fa-f3a8-d7dd4516141c, _Player);


//END_REGION

//REGION Hole

IF
EnteredTrigger(_Player, S_TUT_Start_HoleBounds_e594a65c-17fb-44e9-893d-82a4e58557f8)
AND
NOT QRY_TUT_Start_ShouldNotCommentOnHole(_Player)
AND 
QRY_OnlyOnce("TUT_Start_HoleSeen")
THEN
StartVoiceBark((VOICEBARKRESOURCE)TUT_Start_VB_Hole_61d63da7-9594-320f-7f33-6fcd8c911a1f, _Player);

QRY
QRY_TUT_Start_ShouldNotCommentOnHole((CHARACTER)_Player)
AND
_Player == S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12
AND
DB_Avatars((CHARACTER)S_Player_Laezel_58a69333-40bf-8358-1d17-fff240d7fb12)
THEN
DB_NOOP(1);

//END_REGION

PROC
PROC_TUT_Helm_EndTutorial()
THEN
GoalCompleted;

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Tutorial"
