Version 1
SubGoalCombiner SGC_AND
INITSECTION
PROC_TriggerRegisterForParty((TRIGGER)S_Tutorials_CameraRotationControlsTrigger_929cc41b-1914-4190-b2cd-bdae280a2230);
PROC_TriggerRegisterForParty((TRIGGER)S_Tutorials_CameraMovementControlsTrigger_a4a5f576-0f6e-4754-b29d-a590f686bf49);
KBSECTION
//REGION Tutorials
// Basic Movement Tutorial
IF
DialogEnded(_, _)
AND
DB_InRegion(_Player, _Trigger)
AND
DB_TUT_StartPointAreas(_Trigger)
AND
DB_Players(_Player)
THEN
PROC_GLO_Tutorials_TrackedTimerStart(_Player, "GLO_Tutorials_BasicMovementDelay", 15000);

PROC
PROC_GLO_Tutorials_TrackedTimerFinished(_Player, "GLO_Tutorials_BasicMovementDelay")
THEN
PROC_ShowUnifiedTutorialForPlayer((CHARACTER)_Player, (UNIFIEDTUTORIAL)Basic_Movement_cf87d64f-f75b-49df-aab5-d54477f7ab99);

IF
LeftTrigger(_Player, _Trigger)
AND
DB_Players(_Player)
AND
DB_TUT_StartPointAreas(_Trigger)
THEN
ObjectTimerCancel((GUIDSTRING)_Player, "GLO_Tutorials_BasicMovementDelay");
HideTutorial((CHARACTER)_Player, (UNIFIEDTUTORIAL)Basic_Movement_cf87d64f-f75b-49df-aab5-d54477f7ab99);
SetTutorialShown((CHARACTER)_Player, (UNIFIEDTUTORIAL)Basic_Movement_cf87d64f-f75b-49df-aab5-d54477f7ab99);


//Companions tutorial
IF //simply after TUT imp fight to prevent overlaps with combat tutorials. Laezel or her stand-in are guaranteed at that point
DB_GlobalFlag((FLAG)TUT_LowerDeck_State_ImpsDead_c505f03a-c6d4-4ac5-a0a1-ea805fc1a589)
THEN
TimerLaunch("GLO_Tutorials_AfterImpFightDelay", 5000);

IF
TimerFinished("GLO_Tutorials_AfterImpFightDelay")
AND
DB_Players(_Player)
THEN
PROC_ShowUnifiedTutorialForPlayer(_Player, (UNIFIEDTUTORIAL)Companions_d9652ca7-56a7-4f42-a6b8-96c73984e34d);


//Combat Start + Your Turn In Combat Tutorials
//Cache players who are in active turn
IF
TurnStarted(_Player)
AND
DB_PartyMembers((CHARACTER)_Player)
AND
IsInCombat((CHARACTER)_Player, 1)
THEN
DB_GLO_Tutorials_ActiveCombatTurn_PartyMembers(_Player);

IF
TurnEnded(_Player)
AND
DB_GLO_Tutorials_ActiveCombatTurn_PartyMembers((CHARACTER)_Player)
THEN
NOT DB_GLO_Tutorials_ActiveCombatTurn_PartyMembers(_Player);

IF
LeftCombat(_Player, _)
AND
DB_GLO_Tutorials_ActiveCombatTurn_PartyMembers((CHARACTER)_Player)
THEN
NOT DB_GLO_Tutorials_ActiveCombatTurn_PartyMembers(_Player);

IF
UserDisconnected(_UserId, _, _)
AND
DB_PartyMembers((CHARACTER)_Player)
AND
GetReservedUserID(_Player, _UserId)
THEN
NOT DB_GLO_Tutorials_ActiveCombatTurn_PartyMembers(_Player);

//combat start
IF
EnteredCombat(_Player, _)
AND
DB_Players((CHARACTER)_Player)
THEN
PROC_ShowUnifiedTutorialForPlayer((CHARACTER)_Player, (UNIFIEDTUTORIAL)Combat_Start_5c7409cf-b71f-4186-a6b0-9bde451b76f0);

IF
TutorialClosed(_UserId, (UNIFIEDTUTORIAL)Combat_Start_5c7409cf-b71f-4186-a6b0-9bde451b76f0)
AND
GetUserProfileID(_UserId, _ProfileId)
THEN
DB_GLO_Tutorials_DismissedCombatStartTutorial(_ProfileId);

IF //show You Turn tutorial after dismissal if turn active
TutorialClosed(_UserId, (UNIFIEDTUTORIAL)Combat_Start_5c7409cf-b71f-4186-a6b0-9bde451b76f0)
AND
DB_GLO_Tutorials_ActiveCombatTurn_PartyMembers(_Player)
AND
GetReservedUserID(_Player, _UserId)
AND
GetUserProfileID(_UserId, _ProfileId)
THEN
PROC_ShowUnifiedTutorialForPlayer((CHARACTER)_Player, (UNIFIEDTUTORIAL)Your_Turn_bc1a4fcf-af3c-4741-b47e-ad818803e902);
DB_GLO_Tutorials_DismissedCombatStartTutorial(_ProfileId);

IF
TurnStarted(_Player)
AND
DB_Players((CHARACTER)_Player)
AND
GetReservedUserID(_Player, _UserId)
AND
GetUserProfileID(_UserId, _ProfileId)
AND
DB_GLO_Tutorials_DismissedCombatStartTutorial(_ProfileId)
AND
DB_Is_InCombat((GUIDSTRING)_Player, _CombatID)
THEN
PROC_ShowUnifiedTutorialForPlayer((CHARACTER)_Player, (UNIFIEDTUTORIAL)Your_Turn_bc1a4fcf-af3c-4741-b47e-ad818803e902);

IF
TutorialEvent(_Player, (TUTORIALEVENT)Action_Depleted_82a77570-4865-4eda-b767-7d32bcab4928)
THEN
HideTutorial(_Player, (UNIFIEDTUTORIAL)Your_Turn_bc1a4fcf-af3c-4741-b47e-ad818803e902);

IF
TutorialEvent(_Player, (TUTORIALEVENT)Bonus_Depleted_eae49012-92e7-44e0-9774-ad7a320e2817)
THEN
HideTutorial(_Player, (UNIFIEDTUTORIAL)Your_Turn_bc1a4fcf-af3c-4741-b47e-ad818803e902);

IF
TutorialEvent(_Player, (TUTORIALEVENT)ActionRadials_Opened_f651618e-d396-4d4a-ba5a-8f50c3697708)
THEN
HideTutorial(_Player, (UNIFIEDTUTORIAL)Your_Turn_bc1a4fcf-af3c-4741-b47e-ad818803e902);

IF //edge-case - player skipped turn
TurnEnded(_Player)
AND
DB_Players((CHARACTER)_Player)
THEN
HideTutorial(_Player, (UNIFIEDTUTORIAL)Your_Turn_bc1a4fcf-af3c-4741-b47e-ad818803e902);

IF //cleanup to better support tutorial resets
CombatEnded(_)
AND
DB_GLO_Tutorials_DismissedCombatStartTutorial(_ProfileId)
THEN
NOT DB_GLO_Tutorials_DismissedCombatStartTutorial(_ProfileId);

// Ending Your Turn Tutorial
IF
TutorialEvent(_Player, (TUTORIALEVENT)Action_Depleted_82a77570-4865-4eda-b767-7d32bcab4928)
AND
DB_Is_InCombat(_Player, _)
THEN
PROC_ShowUnifiedTutorialForPlayer((CHARACTER)_Player, (UNIFIEDTUTORIAL)Ending_your_Turn_d5ab128a-fd43-45bd-87d9-95c496ad904f);

IF
TurnEnded(_Player)
AND
DB_Players((CHARACTER)_Player)
THEN
HideTutorial(_Player, (UNIFIEDTUTORIAL)Ending_your_Turn_d5ab128a-fd43-45bd-87d9-95c496ad904f);


//Camera Rotation Tutorial
IF
DB_InRegion(_Player, S_Tutorials_CameraRotationControlsTrigger_929cc41b-1914-4190-b2cd-bdae280a2230)
THEN
PROC_ShowUnifiedTutorialForPlayer(_Player, (UNIFIEDTUTORIAL)Camera_Rotation_27b3873b-0353-4284-b901-32edbcfa9fe1);

IF
TutorialEvent(_Player, (TUTORIALEVENT)UserAction_CameraRotate_af1cba9d-9bfe-440f-971d-d6e0213c428d)
THEN
HideTutorial(_Player, (UNIFIEDTUTORIAL)Camera_Rotation_27b3873b-0353-4284-b901-32edbcfa9fe1);


//Camera Movement Tutorial
IF
DB_InRegion(_Player, S_Tutorials_CameraMovementControlsTrigger_a4a5f576-0f6e-4754-b29d-a590f686bf49)
THEN
PROC_ShowUnifiedTutorialForPlayer((CHARACTER)_Player, (UNIFIEDTUTORIAL)Camera_Movement_211b326e-4f31-4622-bfdb-07d6d5491ce7);

IF
TutorialEvent(_Player, (TUTORIALEVENT)UserAction_CameraMove_33b6c863-0575-470b-b7aa-d91cc82132f8)
THEN
HideTutorial(_Player, (UNIFIEDTUTORIAL)Camera_Movement_211b326e-4f31-4622-bfdb-07d6d5491ce7);


//Restoration Pods Tutorial
IF
HitpointsChanged(_Player, _HpPercentage)
AND
DB_Players((CHARACTER)_Player)
AND
NOT DB_Is_InCombat(_Player, _)
AND
_HpPercentage <= 70
THEN
PROC_ShowUnifiedTutorialForPlayer(_Player, (UNIFIEDTUTORIAL)Restoration_Pods_76ee8943-42f3-4b1f-ada2-a5c0965f6587);
//END_REGION


//REGION DB Cleanups
PROC
PROC_LevelUnloading("TUT_Avernus_C")
THEN
SysClear("DB_GLO_Tutorials_ActiveCombatTurn_PartyMembers", 1);
SysClear("DB_GLO_Tutorials_DismissedCombatStartTutorial", 1);

PROC
PROC_LevelUnloading("TUT_Avernus_C")
AND
DB_GLO_Tutorials_AdvancedMovement_StartStopTriggerPairs(_ActivationTrigger, _DeactivationTrigger)
THEN
PROC_TriggerUnregisterForParty(_ActivationTrigger);
PROC_TriggerUnregisterForParty(_DeactivationTrigger);

//END_REGION


PROC
PROC_LevelUnloading("TUT_Avernus_C")
THEN
PROC_TriggerUnregisterForParty((TRIGGER)S_Tutorials_CameraRotationControlsTrigger_929cc41b-1914-4190-b2cd-bdae280a2230);
PROC_TriggerUnregisterForParty((TRIGGER)S_Tutorials_CameraMovementControlsTrigger_a4a5f576-0f6e-4754-b29d-a590f686bf49);
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Tutorial"
