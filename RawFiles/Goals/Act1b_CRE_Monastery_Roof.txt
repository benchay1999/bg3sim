Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_CheckPoint((CHARACTER)S_CRE_Eagle1_54cf8690-31e8-452c-8f9d-1cc199581885, (CHARACTER)S_CRE_Eagle2_7cb5758d-6521-43f4-936d-b1e4ed91a1e2, (TRIGGER)S_CRE_Eagles_DialogZone_0e8c88a5-165c-4115-86e7-069b5c9292e2, 
(TRIGGER)S_CRE_Eagles_WarningZone_7586d4e1-53ad-49a5-9074-a2f9f3bb1625, (TRIGGER)S_CRE_Eagles_AggroZone_d358e756-50ab-473b-b661-af99bc7401d9, "CRE_RoofTopEagles_Checkpoint", 
(FLAG)CRE_Eagles_State_CheckpointPassed_9280337a-6fe5-4cd3-9125-98f7919dc23d, "Trespassing", S_CRE_Eagles_TrespassTeleportOutPoint_460d07bd-96a5-4d65-81bb-b888b0dff471);

DB_DropMutingStatussesDialog((DIALOGRESOURCE)CRE_RooftopEagles_SWA_e77f9642-8977-3ba6-5f71-be62af70a029);
DB_DropMutingStatussesDialog((DIALOGRESOURCE)CRE_RooftopEagles_SWA_SecondaryEagle_64109849-5c27-28fc-ba42-d6198821b233);
DB_Dialogs(S_CRE_Eagle1_54cf8690-31e8-452c-8f9d-1cc199581885, (DIALOGRESOURCE)CRE_RooftopEagles_SWA_e77f9642-8977-3ba6-5f71-be62af70a029);
DB_Dialogs(S_CRE_Eagle2_7cb5758d-6521-43f4-936d-b1e4ed91a1e2, (DIALOGRESOURCE)CRE_RooftopEagles_SWA_SecondaryEagle_64109849-5c27-28fc-ba42-d6198821b233);

DB_FlagReactionAfterDialog(CRE_Eagles_TeleportBackAfterDialog_3f6e5a31-0308-486c-bd8c-2b38a4627258, CRE_RooftopEagles_SWA_e77f9642-8977-3ba6-5f71-be62af70a029);
DB_FlagReactionAfterDialog(CRE_Eagles_TeleportBackAfterDialog_3f6e5a31-0308-486c-bd8c-2b38a4627258, CRE_RooftopEagles_SWA_SecondaryEagle_64109849-5c27-28fc-ba42-d6198821b233);

DB_CRE_Eagles(S_CRE_Eagle1_54cf8690-31e8-452c-8f9d-1cc199581885);
DB_CRE_Eagles(S_CRE_Eagle2_7cb5758d-6521-43f4-936d-b1e4ed91a1e2);

PROC_SetCorpseOwner((CHARACTER)S_CRE_EagleDinnerKobold_b27e36bd-a33a-4e6e-971d-1cee977a807d, (CHARACTER)S_CRE_Eagle1_54cf8690-31e8-452c-8f9d-1cc199581885);

DB_CRE_Eagles_EntryZones(S_CRE_Eagles_EntryZone1_41f06b8d-8af9-451a-b039-f6dc34927733, S_CRE_Eagles_ExitPoint1_0ec7fde2-a6dc-41ba-8696-fd0de4d7cfa5);
DB_CRE_Eagles_EntryZones(S_CRE_Eagles_EntryZone2_be1e8278-641b-44c5-a150-69c88783d510, S_CRE_Eagles_ExitPoint2_460d07bd-96a5-4d65-81bb-b888b0dff471);
DB_CRE_Eagles_EntryZones(S_CRE_Eagles_EntryZone3_7013015c-75c5-4366-8e55-7388532db2cc, S_CRE_Eagles_ExitPoint3_3ca8fd44-07bf-441f-86ce-7d763e1d1d5a);
DB_CRE_Eagles_EntryZones(S_CRE_Eagles_EntryZone4_a0d085ed-9724-4869-9f40-71f361abc140, S_CRE_Eagles_ExitPoint4_8de1f3b8-e3ab-4e88-83ab-1ea95d488add);
DB_CRE_Eagles_EntryZones(S_CRE_Eagles_EntryZone5_20838c09-0e49-4fde-a824-88bdc59bd7d2, S_CRE_Eagles_ExitPoint5_80dd84e7-97e3-4ba2-8aef-9df1900f84ec);

PROC_TriggerRegisterForPlayers(S_CRE_Eagles_EntryZone1_41f06b8d-8af9-451a-b039-f6dc34927733);
PROC_TriggerRegisterForPlayers(S_CRE_Eagles_EntryZone2_be1e8278-641b-44c5-a150-69c88783d510);
PROC_TriggerRegisterForPlayers(S_CRE_Eagles_EntryZone3_7013015c-75c5-4366-8e55-7388532db2cc);
PROC_TriggerRegisterForPlayers(S_CRE_Eagles_EntryZone4_a0d085ed-9724-4869-9f40-71f361abc140);
PROC_TriggerRegisterForPlayers(S_CRE_Eagles_EntryZone5_20838c09-0e49-4fde-a824-88bdc59bd7d2);

DB_CRE_Eagles_CallForHelpEagles((CHARACTER)S_CRE_Eagle3_2034f3af-9d18-4248-96d5-1645c9f0a056, (TRIGGER)S_CRE_Eagles_Eagle1Landing_472f3e45-1664-4178-a760-f8ef5974d51d);
DB_CRE_Eagles_CallForHelpEagles((CHARACTER)S_CRE_Eagle4_c10d59d3-cbab-41f3-aca9-8b22b17ac384, (TRIGGER)S_CRE_Eagles_Eagle2Landing_b15f9053-c7ed-48ae-b6c7-276706837b0e);
DB_CRE_Eagles_CallForHelpEagles((CHARACTER)S_CRE_Eagle5_ae251160-9a84-4dfc-a5da-e84599866732, (TRIGGER)S_CRE_Eagles_Eagle3Landing_246e6317-ebd8-4667-9850-aac0a5a39095);

DB_CRE_Eagles_CallForHelpEagles_Hardcore((CHARACTER)S_CRE_Eagle6_14538763-8edb-4019-9f09-073dee1a66b3, (TRIGGER)S_CRE_Eagles_Eagle4Landing_4370a8e2-d809-42f2-9269-a5285dfff9ef);
DB_CRE_Eagles_CallForHelpEagles_Hardcore((CHARACTER)S_CRE_Eagle7_5a9c16fb-2238-4919-ad3d-4f45e29d13c2, (TRIGGER)S_CRE_Eagles_Eagle5Landing_cbeb7985-d684-467e-aeb7-79538d745571);

DB_GLO_DefeatCounter(S_CRE_Eagle1_54cf8690-31e8-452c-8f9d-1cc199581885, "CRE_RooftopEagles");
DB_GLO_DefeatCounter(S_CRE_Eagle2_7cb5758d-6521-43f4-936d-b1e4ed91a1e2, "CRE_RooftopEagles");
DB_GLO_DefeatCounter_AllDefeatedGlobalFlag("CRE_RooftopEagles", (FLAG)CRE_Eagles_State_AllDefeated_7028c3aa-86be-43e6-a7ea-86c129c75188);

DB_SpotPlayers(S_CRE_Eagle1_54cf8690-31e8-452c-8f9d-1cc199581885, "CRE_RooftopEagles_HostilitySpotting", NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);
DB_SpotPlayers(S_CRE_Eagle2_7cb5758d-6521-43f4-936d-b1e4ed91a1e2, "CRE_RooftopEagles_HostilitySpotting", NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);
DB_SpotPlayers_SpotTrigger(S_CRE_Eagle1_54cf8690-31e8-452c-8f9d-1cc199581885, "CRE_RooftopEagles_HostilitySpotting", S_CRE_Eagles_AggroZone_d358e756-50ab-473b-b661-af99bc7401d9);
DB_SpotPlayers_SpotTrigger(S_CRE_Eagle2_7cb5758d-6521-43f4-936d-b1e4ed91a1e2, "CRE_RooftopEagles_HostilitySpotting", S_CRE_Eagles_AggroZone_d358e756-50ab-473b-b661-af99bc7401d9);
DB_SpotPlayers_IncludeFollowers(S_CRE_Eagle1_54cf8690-31e8-452c-8f9d-1cc199581885, "CRE_RooftopEagles_HostilitySpotting");
DB_SpotPlayers_IncludeFollowers(S_CRE_Eagle2_7cb5758d-6521-43f4-936d-b1e4ed91a1e2, "CRE_RooftopEagles_HostilitySpotting");
DB_SpotPlayers_IncludeSummons(S_CRE_Eagle1_54cf8690-31e8-452c-8f9d-1cc199581885, "CRE_RooftopEagles_HostilitySpotting");
DB_SpotPlayers_IncludeSummons(S_CRE_Eagle2_7cb5758d-6521-43f4-936d-b1e4ed91a1e2, "CRE_RooftopEagles_HostilitySpotting");

DB_GlobalFlagReactionAfterDialog(CRE_Eagles_State_Hostile_88ac57fb-1227-4458-851e-0213ca52a500, (DIALOGRESOURCE)CRE_RooftopEagles_SWA_e77f9642-8977-3ba6-5f71-be62af70a029);
DB_GlobalFlagReactionAfterDialog(CRE_Eagles_State_Hostile_88ac57fb-1227-4458-851e-0213ca52a500, (DIALOGRESOURCE)CRE_RooftopEagles_SWA_SecondaryEagle_64109849-5c27-28fc-ba42-d6198821b233);
KBSECTION
PROC
PROC_CheckPointDialogue(_Player, (CHARACTER)S_CRE_Eagle1_54cf8690-31e8-452c-8f9d-1cc199581885)
AND
QRY_StartCheckpointWarningDialog("CRE_RoofTopEagles_Checkpoint", (DIALOGRESOURCE)CRE_RooftopEagles_SWA_e77f9642-8977-3ba6-5f71-be62af70a029,(CHARACTER)S_CRE_Eagle1_54cf8690-31e8-452c-8f9d-1cc199581885, _Player)
THEN
DB_NOOP(1);

PROC
PROC_CheckPointDialogue(_Player, S_CRE_Eagle2_7cb5758d-6521-43f4-936d-b1e4ed91a1e2)
AND
QRY_StartCheckpointWarningDialog("CRE_RoofTopEagles_Checkpoint", (DIALOGRESOURCE)CRE_RooftopEagles_SWA_SecondaryEagle_64109849-5c27-28fc-ba42-d6198821b233, S_CRE_Eagle2_7cb5758d-6521-43f4-936d-b1e4ed91a1e2, _Player)
THEN
DB_NOOP(1);

//Teleport out to a position closest to your position.
PROC
PROC_FlagReactionAfterDialog(_Player, CRE_Eagles_TeleportBackAfterDialog_3f6e5a31-0308-486c-bd8c-2b38a4627258)
AND
DB_InRegion((CHARACTER)_Player, _EntryZone)
AND
DB_CRE_Eagles_EntryZones(_EntryZone, _ExitPoint)
THEN
TeleportTo(_Player, _ExitPoint);
ClearFlag(CRE_Eagles_TeleportBackAfterDialog_3f6e5a31-0308-486c-bd8c-2b38a4627258, _Player, 0);
DB_CRE_Eagles_TeleportedOut(_Player);

//If you are in none of the entryzones, use the default teleport position
PROC
PROC_FlagReactionAfterDialog(_Player, CRE_Eagles_TeleportBackAfterDialog_3f6e5a31-0308-486c-bd8c-2b38a4627258)
AND
NOT DB_CRE_Eagles_TeleportedOut((CHARACTER)_Player)
THEN
TeleportTo(_Player, S_CRE_Eagles_ExitPoint2_460d07bd-96a5-4d65-81bb-b888b0dff471);
ClearFlag(CRE_Eagles_TeleportBackAfterDialog_3f6e5a31-0308-486c-bd8c-2b38a4627258, _Player, 0);

//Setting hostility from dialogs correctly
PROC
PROC_GlobalFlagReactionAfterDialog(CRE_Eagles_State_Hostile_88ac57fb-1227-4458-851e-0213ca52a500)
THEN
PROC_SetRelationToPlayers((FACTION)Act1B_CRE_GiantEagles_5429a886-daaa-4746-8cf6-1f5db16ed92d, 0);

//Ensuring the whole faction turns hostile in case of combat (to avoid the additional eagles ignoring out-of-combat party members)
IF
DB_Is_InCombat(_Eagle, _ID)
AND
DB_CRE_Eagles(_Eagle)
AND
DB_Is_InCombat(_Player, _ID)
AND
DB_Players((CHARACTER)_Player)
AND
QRY_OnlyOnce("CRE_Eagles_HostilitySet")
THEN
PROC_SetRelationToPlayers((FACTION)Act1B_CRE_GiantEagles_5429a886-daaa-4746-8cf6-1f5db16ed92d, 0);


PROC
PROC_SpotPlayers_Spotted(_Eagle, "CRE_RooftopEagles_HostilitySpotting", _Player)
THEN
PROC_SetRelationToPlayers((FACTION)Act1B_CRE_GiantEagles_5429a886-daaa-4746-8cf6-1f5db16ed92d, 0);


IF
DB_GlobalFlag((FLAG)CRE_Lance_State_ExplosionCountdownActive_8b52ae9e-0cf3-46e5-924d-64e11aefa0e3)
AND
DB_CRE_Eagles(_Eagle)
AND
NOT DB_Defeated(_Eagle)
THEN
PlayAnimation(_Eagle, CUST_TakeOff_01_5f3d7746-eb42-4599-849e-cdd8e6fecce6, "");
PROC_CRE_SetEagleAnimationTimer(_Eagle);

PROC
PROC_CRE_SetEagleAnimationTimer((GUIDSTRING)_Eagle)
AND
QRY_OnlyOnce("CRE_EagleAnimationTimerSet")
THEN
RealtimeObjectTimerLaunch(_Eagle, "CRE_EagleFlyawayAnimTime", 1500);

IF
ObjectTimerFinished(_, "CRE_EagleFlyawayAnimTime")
AND
DB_CRE_Eagles(_Eagle)
AND
NOT DB_Defeated(_Eagle)
THEN
PROC_SetOnStage(_Eagle, 0);
SetFlag((FLAG)CRE_Eagles_State_AllDefeated_7028c3aa-86be-43e6-a7ea-86c129c75188, NULL_00000000-0000-0000-0000-000000000000, 0);

//Call for help Eagles Init
IF
DB_CRE_Eagles_CallForHelpEagles(_Eagle,_)
THEN
SetOnStage(_Eagle, 0);

IF
DB_CRE_Eagles_CallForHelpEagles_Hardcore(_Eagle,_)
THEN
SetOnStage(_Eagle, 0);

// Spawn eagles when the eagle ends the cry for help
IF
StatusApplied((CHARACTER)_Char, "CALLING_FOR_HELP_SPAWNALLIES", _, _)
AND
DB_CRE_Eagles_CallForHelpEagles(_Eagle, _Trigger)
THEN
DB_GLO_DefeatCounter(_Eagle,"CRE_RooftopEagles");
DB_CRE_Eagles(_Eagle);
AppearAt(_Eagle, _Trigger, 1, DFLT_UTIL_Spawn_01_510ff37e-0618-40e4-8ce0-236e3dc9c291, "");
SteerTo(_Eagle, _Char, 1);

// In Hardcore, add extra eagles when called
IF
StatusApplied((CHARACTER)_Char, "CALLING_FOR_HELP_SPAWNALLIES", _, _)
AND
CheckRulesetModifierString(SCRIPTED_COMBAT_MECHANICS_cac2d8bd-c197-4a84-9df1-f86f54ad4521,"HARD",1)
AND
DB_CRE_Eagles_CallForHelpEagles_Hardcore(_Eagle, _Trigger)
THEN
DB_GLO_DefeatCounter(_Eagle,"CRE_RooftopEagles");
DB_CRE_Eagles(_Eagle);
AppearAt(_Eagle, _Trigger, 1, DFLT_UTIL_Spawn_01_510ff37e-0618-40e4-8ce0-236e3dc9c291, "");
SteerTo(_Eagle, _Char, 1);

IF
FlagSet((FLAG)CRE_HungryBird_State_RewardGiven_e155685d-9e99-4eff-874a-1f2754a29ac9, _, _)
AND
NOT DB_GlobalFlag(CRE_HungryBird_State_OnRoof_6412736a-0194-4415-a624-8778583fe65c)
THEN
SetFlag(CRE_HungryBird_Event_FlyToRoof_e2e029bf-9e5b-401c-b634-352f11566500);

IF
FlagSet(CRE_HungryBird_Event_FlyToRoof_e2e029bf-9e5b-401c-b634-352f11566500, _, _)
THEN
PROC_CRE_FlyBirdToRoof();

IF
FlagSet((FLAG)CRE_Eagles_State_AllDefeated_7028c3aa-86be-43e6-a7ea-86c129c75188,_,_)
AND
DB_GlobalFlag((FLAG)CRE_HungryBird_State_QuestAccepted_788e3e27-ca09-4e2d-ba24-edf768905b0b)
THEN
PROC_CRE_FlyBirdToRoof();

PROC
PROC_CRE_FlyBirdToRoof()
THEN
PlayAnimation(S_CRE_MonasteryApproach_Bird_c85a9bab-49d6-476b-a07b-15e920c3cc24, CUST_TakeOff_01_5f3d7746-eb42-4599-849e-cdd8e6fecce6, "S_CRE_BluejayTeleportEvent");
SetFlag((FLAG)CRE_HungryBird_State_OnRoof_6412736a-0194-4415-a624-8778583fe65c);

IF
EntityEvent(S_CRE_MonasteryApproach_Bird_c85a9bab-49d6-476b-a07b-15e920c3cc24,"S_CRE_BluejayTeleportEvent")
THEN
TeleportTo(S_CRE_MonasteryApproach_Bird_c85a9bab-49d6-476b-a07b-15e920c3cc24, S_CRE_Monastery_Roof_BluejayNestPos_d2a3fc17-2ed0-406a-a189-744090015f17);
PlayAnimation(S_CRE_MonasteryApproach_Bird_c85a9bab-49d6-476b-a07b-15e920c3cc24, CUST_Landing_01_72f2b73f-ab59-4ea7-99fa-ac1c8b298e80, "CRE_Roof_BlueBirdLanded"); 

IF
EntityEvent(_, "CRE_Roof_BlueBirdLanded")
THEN
PROC_CRE_BirdExecuteEagles();

PROC
PROC_CRE_BirdExecuteEagles()
AND
DB_CRE_Eagles(_Eagle)
AND
NOT DB_Dead((CHARACTER)_Eagle)
AND
QRY_OnlyOnce("CRE_BirdExecuteEagles")
THEN
Attack(S_CRE_MonasteryApproach_Bird_c85a9bab-49d6-476b-a07b-15e920c3cc24, _Eagle, 1);

IF
KilledBy(_Eagle, S_CRE_MonasteryApproach_Bird_c85a9bab-49d6-476b-a07b-15e920c3cc24, _, _)
AND
QRY_OnlyOnce_Reset("CRE_BirdExecuteEagles")
THEN
PROC_CRE_BirdExecuteEagles();


IF
StatusRemoved(_Vine,"BLIGHT_CONSTRICT",_,_)
THEN
PlayAnimation(_Vine,CMBT_Skill_Constrict_01_Recover_652cbc1a-a8d9-45b0-b460-daf2400ca0f0,"");

IF
StatusApplied(_Vine,"BLIGHT_CONSTRICT_TECHNICAL",_,_)
THEN
PlayAnimation(_Vine,CMBT_Skill_Constrict_01_Recover_652cbc1a-a8d9-45b0-b460-daf2400ca0f0,"");

//REGION Eagle combatreact
IF
CastedSpell(S_CRE_Eagle1_54cf8690-31e8-452c-8f9d-1cc199581885, "Shout_CallForHelp_GiantEagle", _, _, _)
AND
DB_Is_InCombat(S_CRE_Eagle1_54cf8690-31e8-452c-8f9d-1cc199581885, _ID)
AND
DB_Is_InCombat(_Player, _ID)
AND
DB_Players((CHARACTER)_Player)
AND
QRY_OnlyOnce("CRE_Eagles_EagleAbilityCombatReact")
THEN
DB_CombatReact_AD_OnNextTurn((GUIDSTRING)_Player, (DIALOGRESOURCE)CRE_RooftopEagles_PAD_EagleAbilityCombatReact_3b637968-b504-148f-ff87-c292561002ce);

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1b"
