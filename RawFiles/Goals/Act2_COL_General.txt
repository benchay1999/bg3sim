Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_DangerZone(S_COL_DangerZone_e1c5bff0-5b2e-41cc-9be3-2ec8b6258da2, "COL_DangerZone");

DB_GLO_RestorationStation((ITEM)S_COL_RegenStation_16382bcc-bff9-419b-a66b-662d98c6abe2);

DB_COL_Elevator_Positions(1, (TRIGGER)S_COL_Elevator_Up_453c18eb-a671-4bca-94be-d1942165eaaa);
DB_COL_Elevator_Positions(0, S_COL_Elevator_Down_e35c68de-b6c7-49ff-9318-c963beda013d);

DB_COL_Elevator_CurrentPosition(1);

DB_COL_Elevator_Controllers(0, (ITEM)S_COL_Elevator_Controller_Down_3caa8daf-2cf4-4f16-9f35-7398063cd7b6);
DB_COL_Elevator_Controllers(1, S_COL_Elevator_Controller_Up_9d984a5b-a8b5-47e0-b4a0-f7b559f6d401);
DB_COL_Elevator_Controllers(-1, S_COL_Elevator_Controller_38c76a3f-beaf-417b-be7e-62773c705c8f);
PROC_TriggerRegisterForParty(S_COL_OnElevator_a7e75c66-71e1-4326-84a3-adc73964d56c);

DB_COL_Elevator_NearElevator(1, (TRIGGER)S_COL_NearElevator_Up_a32a3736-abc5-4303-82cd-62fe2ac06008);
DB_COL_Elevator_NearElevator(0, S_COL_NearElevator_Down_3984d061-e0f0-4785-b151-7f9c72c98e75);

// Custom inconspicuous forms in the colony
DB_Shapeshifting_Inconspicuous_CustomTriggerExpr((TRIGGER)S_COL_CrimeRegionTrigger_94247c94-4980-457a-9b23-f64985bedd05, "(SUMMON_26c78224-a4c1-43e4-b943-75e7fa1bfa41 & SPIDER_8c8932c5-45f7-4d45-8f62-182db82ddcb0) | GASEOUS_FORM_22d7c29e-28d8-4126-ab03-8a7adbf98850");

DB_OneShot_VoiceBarkTrigger(S_COL_VB_Nautiloid_cb4b5cdf-8fee-4e56-b330-a067d2abec72, COL_Nautiloid_VB_bfbba7a8-7114-92da-2a4f-99265a9dcdf9);
DB_OneShot_VoiceBarkTrigger(S_COL_Elevator_Banter_Trigger_57f5ef78-b506-45e3-91b6-c1db02d3efe9, COL_General_ElevatorBanter_VB_2adb3a58-30bd-aea4-8676-ec35e9ff9f98);
PROC_TriggerUnregisterForPlayers((TRIGGER)S_COL_Elevator_Banter_Trigger_57f5ef78-b506-45e3-91b6-c1db02d3efe9);

//REGION Neutral intellect devourers

DB_COL_IntDevWithDialogue((CHARACTER)S_COL_Corridor_IntDev_001_419f338c-3860-40fe-b426-2d5a2140e794);
DB_COL_IntDevWithDialogue((CHARACTER)S_COL_Corridor_IntDev_002_6c739b5d-57fd-4978-afaf-c88b9eac47c2);
DB_COL_IntDevWithDialogue((CHARACTER)S_COL_Corridor_IntDev_003_cfc746e2-6b07-4f48-9fd1-319ed9a89c0e);
DB_COL_IntDevWithDialogue((CHARACTER)S_COL_Corridor_IntDev_004_4dbfa464-d92a-4af5-a599-df0f475c2981);
DB_COL_IntDevWithDialogue((CHARACTER)S_COL_Corridor_IntDev_005_ccec75cd-eaa1-4127-816f-a9aed5094dcf);
DB_COL_IntDevWithDialogue((CHARACTER)S_COL_Corridor_IntDev_006_055afb3f-dd91-40b3-a8a8-d9e35ff86cb1);
DB_COL_IntDevWithDialogue((CHARACTER)S_COL_Corridor_IntDev_007_c5fbaea1-9af5-4dad-bd6e-57b6c11b1bde);
DB_COL_IntDevWithDialogue((CHARACTER)S_COL_Corridor_IntDev_008_d2a11f55-3061-4109-9998-da606ebb6f4b);

//END_REGION

DB_AutoSaveTrigger(S_COL_Autosave1_3b6ca0e1-2879-47a6-8722-a0d7beb36c9a);
DB_AutoSaveTrigger(S_COL_Autosave2_565c0e6c-5fad-4a5e-a832-04d861df97f5);

PROC_TriggerRegisterForPlayers((TRIGGER)S_COL_ColonyArea_NoOubliette_e35841eb-e9ae-4c13-8b46-832184d1ec5b);
DB_InRegionFlag((TRIGGER)S_COL_ColonyArea_NoOubliette_e35841eb-e9ae-4c13-8b46-832184d1ec5b, (FLAG)COL_State_InColony_NoOubliette_24d7035e-8698-4b83-9152-b09cb5ea5ded);

PROC_RegisterWorldGossipTrigger(S_PartyBanterTrigger_Colony_612058ff-58e5-49d8-a66c-62615c7bf688);
DB_WorldGossip_RegionTrigger(S_PartyBanterTrigger_Colony_612058ff-58e5-49d8-a66c-62615c7bf688, BANTERREGION_COL_Colony_93526d07-7ce5-46b6-8774-d5bef03d43f0);

DB_COL_Elevator_ChasmKillTriggers((TRIGGER)S_COL_Elevator_ChasmKillTrigger_001_1e1bc128-42f3-441f-878c-f006ef828db7,(TRIGGER)S_COL_Elevator_ChasmKillTrigger_ReturnPosition_001_ebedfca2-141c-48eb-aaf7-9c2e7560d414);
DB_COL_Elevator_ChasmKillTriggers((TRIGGER)S_COL_Elevator_ChasmKillTrigger_002_e3ebf46c-a9c7-41a6-8c5e-1f0a9992a9f3,(TRIGGER)S_COL_Elevator_ChasmKillTrigger_ReturnPosition_002_6ca3b0bf-3d95-4f40-88c7-daf4f42ac269);
KBSECTION
PROC
PROC_COL_KethericShowdown_ChosenThree_CleanUp()
THEN
PROC_RemoveOneShotVoiceBarkTrigger(S_COL_VB_Nautiloid_cb4b5cdf-8fee-4e56-b330-a067d2abec72, COL_Nautiloid_VB_bfbba7a8-7114-92da-2a4f-99265a9dcdf9);
PROC_RemoveOneShotVoiceBarkTrigger(S_COL_Elevator_Banter_Trigger_57f5ef78-b506-45e3-91b6-c1db02d3efe9, COL_General_ElevatorBanter_VB_2adb3a58-30bd-aea4-8676-ec35e9ff9f98);

PROC
PROC_COL_KethericShowdown_Init()
AND
DB_DialogName((DIALOGRESOURCE)WRLD_SCL_FloatingNautiloids_c6bea6fc-e0a2-4a53-855e-5688d7a68afa,_DialogId)
THEN
StopWorldTimeline(_DialogId);

IF
DB_InRegion(_,(TRIGGER)S_COL_ColonyArea_NoOubliette_e35841eb-e9ae-4c13-8b46-832184d1ec5b)
AND
QRY_OnlyOnce("COL_OnFirstEntry")
THEN
PROC_COL_OnFirstEntry();

PROC
PROC_COL_OnFirstEntry()
THEN
DB_NOOP(1);

IF
DB_GlobalFlag((FLAG)SCE_Epilogue_State_HasStarted_9b4bae0b-6464-4b0e-a0e3-41201c0c3c9e)
AND
NOT DB_COL_LivingPartyMemberInColony((CHARACTER)_)
AND
QRY_OnlyOnce("COL_OnLastLeave")
THEN
PROC_COL_OnLastLeave();

IF
DB_InRegion(_PartyMember,(TRIGGER)S_COL_ColonyArea_NoOubliette_e35841eb-e9ae-4c13-8b46-832184d1ec5b)
AND
DB_PartyMembers(_PartyMember)
AND
NOT DB_COL_NecroLab_PlayerZombie(_,_PartyMember)
AND
NOT DB_COL_LivingPartyMemberInColony(_PartyMember)
THEN
DB_COL_LivingPartyMemberInColony(_PartyMember);

IF
DB_COL_LivingPartyMemberInColony(_PartyMember)
AND
NOT DB_InRegion(_PartyMember,(TRIGGER)S_COL_ColonyArea_NoOubliette_e35841eb-e9ae-4c13-8b46-832184d1ec5b)
THEN
NOT DB_COL_LivingPartyMemberInColony(_PartyMember);

IF
DB_COL_LivingPartyMemberInColony(_PartyMember)
AND
DB_COL_NecroLab_PlayerZombie(_,_PartyMember)
THEN
NOT DB_COL_LivingPartyMemberInColony(_PartyMember);

PROC
PROC_COL_OnLastLeave()
THEN
DB_NOOP(1);

//REGION Elevator

IF
DB_COL_Elevator_NearElevator(_, _Trigger)
THEN
PROC_TriggerUnregisterForParty(_Trigger);

IF
UseFinished(_Character, _Controller, 1)
AND
DB_COL_Elevator_Controllers(_, _Controller)
AND
NOT QRY_COL_BlockElevatorController(_Character, _Controller)
THEN
PROC_COL_Elevator_Move();

IF
PlatformMovementFinished(_, "COL_Elevator_Arrived")
AND
DB_COL_Elevator_CurrentPosition(_Pos)
AND
DB_Negate(_Pos, _NewPos)
THEN
NOT DB_COL_Elevator_CurrentPosition(_Pos);
DB_COL_Elevator_CurrentPosition(_NewPos);

IF
PlatformMovementFinished(_, "COL_Elevator_Arrived")
THEN
NOT DB_COL_Elevator_Moving(1);

IF
PlatformMovementFinished(_, "COL_Elevator_Arrived")
AND
DB_COL_Elevator_Controllers(_, _Controller)
THEN
SetCanInteract(_Controller, 1);

QRY
QRY_COL_BlockElevatorController((CHARACTER)_Character, (ITEM)_Controller)
AND
DB_Players((CHARACTER)_Player)
AND
DB_COL_Elevator_CurrentPosition(_CurrentPos)
AND
DB_COL_Elevator_Controllers(_CurrentPos, _Controller)
AND
NOT DB_DialogSpeakers(_,_Player,_)
THEN
PROC_TryStartAD((DIALOGRESOURCE)COL_General_PAD_CallLiftFail_b59783b0-9efc-bf7e-fe85-573ccf0bc5a1, _Character);

//Fallback options, shouldn't be needed
QRY
QRY_COL_BlockElevatorController(_Character, _)
AND
DB_COL_Elevator_Moving(1)
AND
NOT DB_DialogSpeakers(_,_Character,_)
THEN
PROC_PlayCantUseItemAD(_Character);

QRY
QRY_COL_BlockElevatorController(_Character, _)
AND
DB_COL_Elevator_Embarking(_)
AND
NOT DB_DialogSpeakers(_,_Character,_)
THEN
PROC_PlayCantUseItemAD(_Character);


QRY
QRY_COL_BlockElevatorController(_Character, _)
AND
DB_Is_InCombat(_Character, _)
AND
NOT DB_DialogSpeakers(_,_Character,_)
THEN
PROC_PlayCantUseItemAD(_Character);

PROC
PROC_COL_Elevator_Move()
AND
QRY_OnlyOnce("MusicNeuralApparatus_Elevator")
THEN
MusicPlayGeneral("MusicNeuralApparatus_Elevator");
//TODO: only play for players near elevator

PROC
PROC_COL_Elevator_Move()
AND
DB_COL_Elevator_Controllers(_, _Controller)
THEN
SetCanInteract(_Controller, 0);

PROC
PROC_COL_Elevator_Move()
AND
NOT QRY_COL_ElevatorEmbark()
THEN
PROC_COL_Elevator_DoMove();

QRY
QRY_COL_ElevatorEmbark()
AND
DB_InRegion(_Character, (TRIGGER)S_COL_OnElevator_a7e75c66-71e1-4326-84a3-adc73964d56c)
AND
DB_COL_Elevator_CurrentPosition(_CurrentPos)
AND
DB_COL_Elevator_NearElevator(_CurrentPos, _NearElevator)
AND
DB_InRegion(_OtherCharacter, _NearElevator)
AND
NOT DB_CantMove(_OtherCharacter)
AND
NOT DB_InRegion(_OtherCharacter, (TRIGGER)S_COL_OnElevator_a7e75c66-71e1-4326-84a3-adc73964d56c)
AND
NOT DB_COL_Elevator_Embarking(_OtherCharacter)
AND
InSamePartyGroup(_Character, _OtherCharacter, 1)
THEN
PROC_CharacterMoveTo(_OtherCharacter, _Character, "Walk", "COL_Elevator_Embarked");
DB_COL_Elevator_Embarking(_OtherCharacter);

IF
DB_COL_Elevator_Embarking(_Character)
AND
DB_InRegion(_AnyCharacter, (TRIGGER)S_COL_OnElevator_a7e75c66-71e1-4326-84a3-adc73964d56c)
AND
NOT DB_COL_Elevator_Embarking(_AnyCharacter)
AND
NOT DB_COL_Elevator_WaitingForEmbark(_AnyCharacter)
THEN
DB_COL_Elevator_WaitingForEmbark(_Character);
Freeze(_Character);

IF
DB_COL_Elevator_WaitingForEmbark(_Character)
AND
NOT DB_InRegion(_Character, (TRIGGER)S_COL_OnElevator_a7e75c66-71e1-4326-84a3-adc73964d56c)
THEN
NOT DB_COL_Elevator_WaitingForEmbark(_Character);
Unfreeze(_Character);

IF
EntityEvent(_Character, "COL_Elevator_Embarked")
THEN
NOT DB_COL_Elevator_Embarking((CHARACTER)_Character);

IF
EntityEvent(_, "COL_Elevator_Embarked")
AND
NOT DB_COL_Elevator_Embarking(_)
THEN
PROC_COL_Elevator_Move();

PROC
PROC_COL_Elevator_DoMove()
AND
DB_COL_Elevator_WaitingForEmbark(_Character)
THEN
NOT DB_COL_Elevator_WaitingForEmbark(_Character);
Unfreeze(_Character);

PROC
PROC_COL_Elevator_DoMove()
AND
DB_COL_Elevator_CurrentPosition(_CurrentPos)
AND
DB_Negate(_CurrentPos, _TargetPos)
AND
DB_COL_Elevator_Positions(_TargetPos, _Trigger)
AND
GetPosition(_Trigger, _X, _Y, _Z)
THEN
DB_COL_Elevator_Moving(1);
PlatformMoveTo(PLT_COL_Elevator_000_ed92ff43-774c-4bf9-a961-4d7aada54fcf, _X, _Y, _Z, 4.0, "COL_Elevator_Arrived", 0);

//Banter -
//The banter only need to happen when the lift is en-route.
//Don't  unregister the banter trigger completely because that causes the delay timer to not work (when it ends, there's no
//corresponding trigger registered) and therefore would cause the banter to be repeated even if the player immediately goes 
//up again after going down.
PROC
PROC_COL_Elevator_DoMove()
THEN
TimerLaunch("COL_Lift_Gossip", 2000);

IF
TimerFinished("COL_Lift_Gossip")
THEN
PROC_TriggerRegisterForPlayers((TRIGGER)S_COL_Elevator_Banter_Trigger_57f5ef78-b506-45e3-91b6-c1db02d3efe9);

IF
PlatformMovementFinished(_, "COL_Elevator_Arrived")
THEN
PROC_TriggerUnregisterForPlayers((TRIGGER)S_COL_Elevator_Banter_Trigger_57f5ef78-b506-45e3-91b6-c1db02d3efe9);
PROC_RemoveOneShotVoiceBarkTrigger((TRIGGER)S_COL_Elevator_Banter_Trigger_57f5ef78-b506-45e3-91b6-c1db02d3efe9, COL_General_ElevatorBanter_VB_2adb3a58-30bd-aea4-8676-ec35e9ff9f98);
PROC_RemoveOneShotVoiceBarkTrigger(S_COL_VB_Nautiloid_cb4b5cdf-8fee-4e56-b330-a067d2abec72, COL_Nautiloid_VB_bfbba7a8-7114-92da-2a4f-99265a9dcdf9);


//Bugfix chasm kill triggers
IF
DB_COL_Elevator_ChasmKillTriggers((TRIGGER)_ChasmKillTrigger,(TRIGGER)_ReturnPosition)
THEN
PROC_TriggerRegisterForParty(_ChasmKillTrigger);

IF
EnteredTrigger(_Character,_ChasmKillTrigger)
AND
DB_COL_Elevator_ChasmKillTriggers((TRIGGER)_ChasmKillTrigger,(TRIGGER)_ReturnPosition)
AND
GetPosition(_ReturnPosition,_X,_Y,_Z)
THEN
PROC_Camp_PlayerEnteredChasm((CHARACTER)_Character, (GUIDSTRING)NULL_00000000-0000-0000-0000-000000000000, _X, _Y, _Z);

IF
Resurrected(_Character)
AND
DB_InRegion(_Character,_ChasmKillTrigger)
AND
DB_COL_Elevator_ChasmKillTriggers((TRIGGER)_ChasmKillTrigger,(TRIGGER)_ReturnPosition)
AND
GetPosition(_ReturnPosition,_X,_Y,_Z)
THEN
TeleportToPosition(_Character, _X, _Y, _Z, "", 0, 0, 1);

//END_REGION

//REGION Neutral intellect devourers

IF
DB_COL_TadpolingCentre_IntDevs(_IntDev)
THEN
DB_COL_IntDevWithDialogue(_IntDev);

IF
DB_COL_Butcher_IntDev(_IntDev)
THEN
DB_COL_IntDevWithDialogue(_IntDev);

IF
DB_COL_IntDevWithDialogue(_IntDev)
THEN
DB_AD_Dialog(_IntDev, (DIALOGRESOURCE)COL_GEN_AD_IntellectDevourer_1cde5899-11a9-e82f-a49d-f038a554490e);

IF
AutomatedDialogEnded((DIALOGRESOURCE)COL_GEN_AD_IntellectDevourer_1cde5899-11a9-e82f-a49d-f038a554490e,_DialogId)
AND
DB_DialogSpeakers(_DialogId,_IntellectDevourer,1)
AND
QRY_GetClosestAvailableCharacterTo(_IntellectDevourer, 0)
AND
DB_ClosestAvailableCharacterTo(_Player,_IntellectDevourer,_)
AND
QRY_OncePerUserAndNearbyPlayers((CHARACTER)_Player,"COL_GEN_PAD_IntellectDevourerComment")
THEN
StartVoiceBark(COL_GEN_VB_IntellectDevourerComment_67ad17ce-2f21-a31f-1ac0-da124fe0f94e,(CHARACTER)_Player);

//END_REGION

//REGION Inparty Suppression

PROC
PROC_StateSet_PermaDefeated(S_MOO_Ketheric_e9918f3e-5b87-40a3-a9bd-61545151573f)
THEN
DB_PartyDialogSuppressionZone((TRIGGER)S_COL_DefeatedKetheric_PartySuppressionZone_feba40be-b1ad-4dd8-b181-94318bb6cb39);
DB_PartyDialogSuppression_CustomDialogForParty((TRIGGER)S_COL_DefeatedKetheric_PartySuppressionZone_feba40be-b1ad-4dd8-b181-94318bb6cb39, (DIALOGRESOURCE)COL_PartySuppression_PAD_DefeatedKetheric_0e5a2a17-35a8-edfe-b4e4-24db8debe8c5);

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
