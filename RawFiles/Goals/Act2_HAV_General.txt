Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION Haven Area State Manager
DB_State_Priority(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Area", "HAV_Area_State_BarrierIsOn", 0);
DB_State_Priority(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Area", "HAV_Area_State_BarrierIsOff", 0);
DB_State_Priority(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Area", "HAV_Area_State_CurseIsGone", 0);

DB_State_Flags(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Area", "HAV_Area_State_BarrierIsOn", (FLAG)HAV_Area_State_BarrierIsOn_b7e135d1-8f49-4510-b17b-df7eb258658c);
DB_State_Flags(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Area", "HAV_Area_State_BarrierIsOff", (FLAG)HAV_Area_State_BarrierIsOff_922c2f39-0a46-4000-8296-9c3e90f93e7e);
DB_State_Flags(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Area", "HAV_Area_State_CurseIsGone", (FLAG)HAV_Area_State_CurseIsGone_ce569364-51c8-4538-b07b-c67804e9ffa4);

DB_State_Current(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Area", "HAV_Area_State_BarrierIsOn");
SetFlag(HAV_Area_State_BarrierIsOn_b7e135d1-8f49-4510-b17b-df7eb258658c, NULL_00000000-0000-0000-0000-000000000000);
//END_REGION Haven Area State Manager

//REGION Haven Mood State Manager
DB_State_Priority(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_Protected", 0);
DB_State_Priority(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_GhoulAttack", 500);
DB_State_Priority(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_Alert", 1000);
DB_State_Priority(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_NightsongFreed", 1250);
DB_State_Priority(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_Siege", 1500);
DB_State_Priority(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_Unprotected", 2000);

DB_State_Flags(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_Protected", (FLAG)HAV_Mood_State_Protected_2ba16a80-e4f8-40b5-aaf5-37791bd76542);
DB_State_Flags(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_GhoulAttack", (FLAG)HAV_Mood_State_GhoulAttack_ffe29cf4-8ad3-4b8e-881b-e1c9be99c6c4);
DB_State_Flags(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_Alert", (FLAG)HAV_Mood_State_Alert_b073eeb9-1d29-449f-baf5-74307e63d085);
DB_State_Flags(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_NightsongFreed", (FLAG)HAV_Mood_State_NightsongFreed_92fd7d61-0eec-480f-8c1e-0cabfc4074a6);
DB_State_Flags(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_Siege", (FLAG)HAV_Mood_State_Siege_b509af8b-a331-47ac-8230-3b8c5ef9956b);
DB_State_Flags(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_Unprotected", (FLAG)HAV_Mood_State_Unprotected_7ac5d939-2c81-4871-81a5-fd5c6a0adba1);

DB_State_Current(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_Protected");
SetFlag(HAV_Mood_State_Protected_2ba16a80-e4f8-40b5-aaf5-37791bd76542, NULL_00000000-0000-0000-0000-000000000000);
//END_REGION Haven Mood State Manager

DB_Inclusion_Object((CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, (FLAG)GLO_Jaheira_Inclusion_Start_45daca8e-51d2-4aa0-b65a-e406e5ab7f7f, (FLAG)GLO_Jaheira_Inclusion_End_78bdb931-e4c1-4ffc-89f2-8595db9df932);

//REGION General Haven NPCs 
DB_Dialogs((CHARACTER)S_HAV_HarperGuard_001_8082f1d4-8942-492e-8d26-1e1ed6bd881f, (DIALOGRESOURCE)HAV_HarperGuard_001_21882fc6-fa7e-ec07-3c02-7c25941b0de4);
DB_Dialogs((CHARACTER)S_HAV_HarperGuard_002_34a9bfd8-1ca2-4a1c-b72e-2bf1cfc046a4, (DIALOGRESOURCE)HAV_HarperGuard_002_c3dc6db9-be42-a443-62ba-a1bd823bb37b);

DB_HAV_General_Tieflings(S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b);
DB_HAV_General_Tieflings(S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c);
DB_HAV_General_Tieflings(S_DEN_ThiefHideoutLeader_c8ab1ca6-96bb-467e-91c9-af87bc4d3925);
DB_HAV_General_Tieflings(S_DEN_BackupLeader_b304e07d-019c-4ec6-b5ea-c4ccb29ed892);
DB_HAV_General_Tieflings(S_DEN_PickpocketTrader_e1fda08b-45d4-41db-84b5-20e9931ba5af);
DB_HAV_General_Tieflings(S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387);
DB_HAV_General_Tieflings(S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f);
DB_HAV_General_Tieflings(S_DEN_CharmedKid_3b92c689-6024-4446-a6c9-584e9e8d77ca);


//END_REGION General Haven NPCs 

DB_HAV_BarrierFX_Center(S_HAV_Selune_Barrier_Moonritual_Pivot_01_18f18b6c-0a0c-4d1b-9c2c-5dde90630a3d);
DB_HAV_BarrierFX_Contact(S_HAV_Selune_Barrier_Contact_000_52bf1a42-db3c-43c6-9cff-2b1fc85b43fd);
DB_HAV_BarrierFX_Contact(S_HAV_Selune_Barrier_Contact_001_8e81f97a-59dc-44fb-bf72-763e9ccf19aa);
PROC_HAV_ProtectionFX_On();


//REGION Hide the corpses
PROC_SetOnStage(S_HAV_Lootstash_b83d4303-4234-43fd-9be2-e3f6aae2ebfd, 0);
SetOnStage(S_HAV_FlamingFistCorpse_Rocks_000_a6a401cf-8c77-4278-bcb8-5893fa61a27c, 0);

//END_REGION

DB_HAV_TorchZones((TRIGGER)S_HAV_Torches_EventZone_aad4e102-2da0-43de-8167-c70cbbb88af2);
DB_HAV_TorchZones(S_HAV_Torches_EventZone_Basement_b91f9009-2ca5-4ab8-9d18-b893376cd8c7);

DB_KnowledgeCheckItem_AD("HAV_DarkStuffedBear",DEC_GEN_StuffedBear_Standing_A_000_48f69ac4-edc3-4478-82ec-ac1c1f91dd49,"Nature",
	Act2_Medium_89f0acd4-346f-479d-8b7a-1a3eb5382f6d,HAV_PAD_StuffedBear_e599ad14-eb3d-0943-0b92-18549704485e,
	HAV_Skillcheck_StuffedBear_Success_3e39c5c2-b5d5-40f8-924c-4ec91bd82ca0);
DB_KnowledgeSkillcheck_AlwaysPlayAD("HAV_DarkStuffedBear", DEC_GEN_StuffedBear_Standing_A_000_48f69ac4-edc3-4478-82ec-ac1c1f91dd49);

SetOnStage(S_HAV_FlamingFistCorpse_66ef8153-9c8c-4677-9893-0003b5f12d9d, 0);

PROC_TriggerRegisterForPlayers((TRIGGER)S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab);
PROC_TriggerRegisterForPlayers((TRIGGER)S_HAV_Haven_SUB_Cellar_eee18cb6-443a-4b8f-ad19-58dff3d731d8);

//REGION GUARD COMBAT REACTIONS

DB_CombatReact_AD_OnTurn(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, (DIALOGRESOURCE)HAV_Jaheira_AD_CombatTaunts_de500568-ec04-eac5-bc32-718ab14efc67, 1);
DB_CombatReact_AD_OnTurn(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, (DIALOGRESOURCE)HAV_Jaheira_AD_CombatTaunts_de500568-ec04-eac5-bc32-718ab14efc67, 2);
DB_CombatReact_AD_OnTurn(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, (DIALOGRESOURCE)HAV_Jaheira_AD_CombatTaunts_de500568-ec04-eac5-bc32-718ab14efc67, 3);

DB_CombatReact_AD_OnNextTurn(S_HAV_HavenOutcasts_BarricadeGuards_Melee_Dwarf_b1d8b327-1f27-4921-aa2b-dd18a9d67fc8, (DIALOGRESOURCE)HAV_HavenOutcasts_AD_BarricadeGuards_Melee_Dwarf_InCombat_053a22ae-422c-8e77-7e62-28f1c74a4b55);
DB_CombatReact_AD_OnDeathOther(S_HAV_HavenOutcasts_BarricadeGuards_Melee_Dwarf_b1d8b327-1f27-4921-aa2b-dd18a9d67fc8, S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, (DIALOGRESOURCE)HAV_HavenOutcasts_AD_BarricadeGuards_Melee_Dwarf_JaheiraDefeated_3073c8dc-b6de-5ff0-8502-b2fb8dacba9d);
DB_CombatReact_AD_OnNextTurn(S_HAV_HavenOutcasts_BarricadeRunners_Ranger_572690b6-68f5-4e7b-807b-b9376b658b65, (DIALOGRESOURCE)HAV_HavenOutcasts_AD_BarricadeRunners_Ranger_InCombat_e2e755ef-4e17-65a1-2073-5b10d336dd93);
DB_CombatReact_AD_OnNextTurn(S_HAV_HavenOutcasts_BarricadeYeller_Ranger_b612ef5f-3381-4486-8959-84dd399fb1ae, (DIALOGRESOURCE)HAV_HavenOutcasts_AD_BarricadeYeller_Ranger_InCombat_72709b5e-2971-2bab-2c8a-8bd3a29b9388);
DB_CombatReact_AD_OnNextTurn(S_HAV_HavenOutcasts_DockGuard_Melee_f1f837ad-8e6a-442d-bba4-46f665c47675, (DIALOGRESOURCE)HAV_HavenOutcasts_AD_DockGuard_Melee_InCombat_6f173380-cd84-44f8-3d97-3f2873580985);
DB_CombatReact_AD_OnDeathOther(S_HAV_HavenOutcasts_DockGuard_Melee_f1f837ad-8e6a-442d-bba4-46f665c47675, S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, (DIALOGRESOURCE)HAV_HavenOutcasts_AD_DockGuard_Melee_JaheiraDefeated_28cc8f3a-5ff8-2659-0bcc-308c36ca5423);
DB_CombatReact_AD_OnNextTurn(S_HAV_HavenOutcasts_DockGuard_Ranger_a0f9d7cb-d87c-4af8-87fa-d273a8ecba93, (DIALOGRESOURCE)HAV_HavenOutcasts_AD_DockGuard_Ranger_InCombat_4b5d214f-24db-9f32-d140-db981f586855);
DB_CombatReact_AD_OnNextTurn(S_HAV_HavenOutcasts_EntranceGuards_Halfling_Melee_c8bc8f03-c91c-42d4-829c-d16bb1df4a67, (DIALOGRESOURCE)HAV_HavenOutcasts_EntranceGuards_AD_Halfling_Melee_InCombat_02182941-600d-408b-cfac-cf5b0ad946ff);
DB_CombatReact_AD_OnDeathOther(S_HAV_HavenOutcasts_EntranceGuards_Halfling_Melee_c8bc8f03-c91c-42d4-829c-d16bb1df4a67, S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, (DIALOGRESOURCE)HAV_HavenOutcasts_EntranceGuards_AD_Halfling_Melee_JaheiraDefeated_5d1cd11b-c9fe-d227-20ae-fe214b309882);
DB_CombatReact_AD_OnNextTurn(S_HAV_HavenOutcasts_FountainGuards_Melee_2_271478dd-7efb-4ca7-b40c-593bbea91a85, (DIALOGRESOURCE)HAV_HavenOutcasts_AD_FountainGuards_Melee_2_InCombat_46b9447d-0c86-5763-ee95-f896af448554);
DB_CombatReact_AD_OnNextTurn(S_HAV_HavenOutcasts_HarperQuartermaster_f769815c-d437-4a45-9ae4-aebd53ec8f7c, (DIALOGRESOURCE)HAV_HavenOutcasts_AD_HarperQuartermaster_InCombat_948bc12e-126a-c877-8986-ab7aae521627);
DB_CombatReact_AD_OnNextTurn(S_HAV_HavenOutcasts_RoofWatcher_Caster_d44b6aee-7737-4961-bb2b-e7920b003107, (DIALOGRESOURCE)HAV_HavenOutcasts_AD_RoofWatcher_Caster_InCombat_6b210024-ca0b-0b99-d840-812811838cbf);

DB_CombatReact_AD_OnNextTurn(S_HAV_FlamingFist_001_47058367-9c0c-4756-a467-07b87b28e0d6, (DIALOGRESOURCE)HAV_SavingPrisoners_AD_FlamingFist_001_InCombat_cea8f1df-019d-e0e6-a220-d8b813ecbea3);
DB_CombatReact_AD_OnNextTurn(S_HAV_FlamingFist_003_128c0f54-fea0-49df-89ca-c2ca8d996a31, (DIALOGRESOURCE)HAV_SavingPrisoners_AD_FlamingFist_003_InCombat_a9424036-5ad8-c433-48d2-66a90224ce97);
DB_CombatReact_AD_OnNextTurn(S_HAV_FlamingFist_005_0e691c0a-562b-43bb-8554-cde1194decd5, (DIALOGRESOURCE)HAV_SavingPrisoners_AD_FlamingFist_005_InCombat_83ba1f11-01a8-fe5d-5ecb-6aedf62e73df);
DB_CombatReact_AD_OnNextTurn(S_HAV_FlamingFist_006_9629c27e-8e50-4258-8bda-6639af768ba6, (DIALOGRESOURCE)HAV_SavingPrisoners_AD_FlamingFist_006_InCombat_1cc74e8d-72e4-28ed-3037-3efd18729e96);
DB_CombatReact_AD_OnDeathOther(S_HAV_FlamingFist_001_47058367-9c0c-4756-a467-07b87b28e0d6, S_GLO_Desire_8b9fa503-7205-4ddd-bdfa-bfaa781d9ea9, (DIALOGRESOURCE)HAV_SavingPrisoners_AD_FlamingFist_001_CouncillorDefeated_906e15c9-6843-48d9-2e0b-c058c06eb0a3);
//END_REGION

//REGION OWNERSHIP
DB_ItemOwnerShipTriggers("SCL_Main_A",(TRIGGER)S_HAV_Quartermaster_Ownership_ec38e077-880d-48f2-9eef-892d677dab58, (CHARACTER)S_HAV_HavenOutcasts_HarperQuartermaster_f769815c-d437-4a45-9ae4-aebd53ec8f7c);
DB_ItemShopTrigger((TRIGGER)S_HAV_Quartermaster_Ownership_ec38e077-880d-48f2-9eef-892d677dab58, (CHARACTER)S_HAV_HavenOutcasts_HarperQuartermaster_f769815c-d437-4a45-9ae4-aebd53ec8f7c);

//Clearing ownership on a couple specific items for legibility
DB_ItemOwnerShipClearItem("SCL_Main_A", DOOR_Dungeon_Shar_Temple_Door_Single_B_000_91b5ca4a-330c-4122-bbae-1576714d8714);
//END_REGION OWNERSHIP

//REGION Torches

DB_HAV_HarpersWithTorches((CHARACTER)S_SCL_HarperScout_000_c6dfb2aa-809f-4b3e-8b32-968c10184ec3);
DB_HAV_HarpersWithTorches((CHARACTER)S_SCL_HarperScout_001_25e154f6-d762-4bea-862e-98c48d7ebe3e);
DB_HAV_HarpersWithTorches((CHARACTER)S_SCL_HarperScout_002_a0fecf82-7dde-4dd7-af67-eb0ef5aa67e0);
// S_SCL_HarperScout_003 has custom scripting for handling his torch.
DB_HAV_HarpersWithTorches((CHARACTER)S_SCL_Drider_Harper_Melee_000_b9f6bb1e-f4cd-4bb3-a7f0-80aad9bae8c8);
DB_HAV_HarpersWithTorches((CHARACTER)S_SCL_Drider_Harper_Caster_000_ef609a8e-08bf-4648-bbb4-380a7f4bc887);
DB_HAV_HarpersWithTorches((CHARACTER)S_SCL_Drider_Harper_Ranger_000_ce089a64-1a91-4ab0-ac71-a2712680780d);
DB_HAV_HarpersWithTorches((CHARACTER)S_SCL_Drider_Harper_Ranger_001_71c140fa-0ebd-4f36-8728-6e28a445a6ff);

//END_REGION Torches

//REGION Knows about Haven
DB_GLO_CompoundFlag(SCL_Harpers_Event_ShowHavenLocation_5565fa4f-ec30-472a-a9ea-7bb7b09390b3, GLO_Haven_KnowsLocation_16cb0598-a205-47dc-8de9-5229b6897012);
DB_GLO_CompoundFlag(SCL_HarperScouts_Event_InvitedToHaven_9ade36a5-8ea6-ab0e-929c-ac323741435a, GLO_Haven_KnowsLocation_16cb0598-a205-47dc-8de9-5229b6897012);
DB_GLO_CompoundFlag(GLO_Haven_KnowsLocation_16cb0598-a205-47dc-8de9-5229b6897012, GLO_Haven_HeardAbout_e2fba874-5443-415d-8c50-26c51d6993b3);
DB_GLO_CompoundFlag(GLO_Haven_EverEnteredBefore_f1176fa0-ec29-410e-bffa-24623b19c8e4, GLO_Haven_KnowsLocation_16cb0598-a205-47dc-8de9-5229b6897012);
//END_REGION

//REGION Crimes
DB_CRIME_Guards_RegionInfo("SCL_Main_A_HAV", "HAV_Prison", (FLAG)HAV_Siege_State_NoProtection_2da0dbf1-88ca-4cab-9c8f-ee8921822958);
DB_CRIME_Guards_RegionReinforcements("SCL_Main_A_HAV", 2);
DB_CRIME_Guards_RegionGuard("SCL_Main_A_HAV", "HAV_Prison", (CHARACTER)S_HAV_HavenOutcasts_DockGuard_Melee_f1f837ad-8e6a-442d-bba4-46f665c47675);
DB_CRIME_Guards_RegionGuard("SCL_Main_A_HAV", "HAV_Prison", (CHARACTER)S_HAV_EnteringHaven_Guard_f48078e6-ec51-49a2-a244-9d0a72af5257);
DB_CRIME_Guards_RegionGuard("SCL_Main_A_HAV", "HAV_Prison", (CHARACTER)S_HAV_HavenOutcasts_BarricadeGuards_Melee_Dwarf_b1d8b327-1f27-4921-aa2b-dd18a9d67fc8);
DB_CRIME_Guards_RegionGuard("SCL_Main_A_HAV", "HAV_Prison", (CHARACTER)S_HAV_HavenOutcasts_DockGuard_Left_Melee_bd46130e-1c2c-4888-9e9c-6dd58965ab4f);
DB_CRIME_Guards_RegionGuard("SCL_Main_A_HAV", "HAV_Prison", (CHARACTER)S_HAV_HavenOutcasts_DockGuard_Left_Ranger_3a856945-e4a5-4214-89af-84a2b50789d8);
DB_CRIME_Guards_RegionGuard("SCL_Main_A_HAV", "HAV_Prison", (CHARACTER)S_HAV_HavenOutcasts_DockGuard_Ranger_a0f9d7cb-d87c-4af8-87fa-d273a8ecba93);
DB_CRIME_Guards_RegionGuard("SCL_Main_A_HAV", "HAV_Prison", (CHARACTER)S_HAV_HavenOutcasts_EntranceGuards_Caster_HighElf_825714dd-7df9-4cd1-aecb-edf577baa487);
DB_CRIME_Guards_RegionGuard("SCL_Main_A_HAV", "HAV_Prison", (CHARACTER)S_HAV_HavenOutcasts_EntranceGuards_Halfling_Melee_c8bc8f03-c91c-42d4-829c-d16bb1df4a67);
DB_CRIME_Guards_RegionGuard("SCL_Main_A_HAV", "HAV_Prison", (CHARACTER)S_HAV_HavenOutcasts_FountainGuards_Melee_1_3e2db417-c434-4f08-ab6a-dd60c8e9f3b5);
DB_CRIME_Guards_RegionGuard("SCL_Main_A_HAV", "HAV_Prison", (CHARACTER)S_HAV_HavenOutcasts_FountainGuards_Melee_2_271478dd-7efb-4ca7-b40c-593bbea91a85);
DB_CRIME_Guards_RegionGuard("SCL_Main_A_HAV", "HAV_Prison", (CHARACTER)S_HAV_ArrivalGuard_232f53d1-d291-4e2a-bdfd-d2fed2dba630);
DB_CRIME_Guards_RegionGuard("SCL_Main_A_HAV", "HAV_Prison", (CHARACTER)S_HAV_HarperGuard_001_8082f1d4-8942-492e-8d26-1e1ed6bd881f);
DB_CRIME_Guards_RegionGuard("SCL_Main_A_HAV", "HAV_Prison", (CHARACTER)S_HAV_HarperGuard_002_34a9bfd8-1ca2-4a1c-b72e-2bf1cfc046a4);
//END_REGION Crimes
KBSECTION
//REGION Debug
IF
TextEvent("hav_area_barrierison")
THEN
PROC_State_Progress(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Area", "HAV_Area_State_BarrierIsOn");

IF
TextEvent("hav_area_barrierisoff")
THEN
PROC_State_Progress(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Area", "HAV_Area_State_BarrierIsOff");

IF
TextEvent("hav_area_curseisgone")
THEN
PROC_State_Progress(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Area", "HAV_Area_State_CurseIsGone");

IF
TextEvent("hav_mood_ghoulattack")
THEN
PROC_State_Progress(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_GhoulAttack");

IF
TextEvent("hav_mood_alert")
THEN
PROC_State_Progress(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_Alert");

IF
TextEvent("hav_mood_siege")
THEN
PROC_State_Progress(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_Siege");

IF
TextEvent("hav_mood_unprotected")
THEN
PROC_State_Progress(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_Unprotected");

IF
TextEvent("hav_mood_nightsongfreed")
THEN
PROC_State_Progress(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_NightsongFreed");

IF
TextEvent("hav_atm_on")
THEN
TriggerSetAtmosphere(ATM_SCL_RuinsBattlefield_G_Haven_e2fa7637-8749-4dad-bf76-03c8c23c745b,"1aeab267-8219-39da-fa04-97064a522ef1"); // ATM_Default_EV10_SCL_RuinsBattlefield

IF
TextEvent("hav_atm_off")
THEN
TriggerResetAtmosphere(ATM_SCL_RuinsBattlefield_G_Haven_e2fa7637-8749-4dad-bf76-03c8c23c745b);
//END_REGION Debug


//REGION Atmosphere and protection FX changes
PROC
PROC_HAV_ProtectionFX_On()
AND
DB_HAV_BarrierFX_Center(_Center)
THEN
TriggerSetLighting((TRIGGER)LTN_SCL_RuinsBattlefield_G_4d0e202a-4ecd-403d-959c-69fcc76cdefc, "dc6c186c-c483-786c-d5a2-69c4ecfd3c57");
LoadLevelTemplate(LT_PLT_SCL_Haven_GameplayLight_A_000_bf5e341b-8342-4b79-9fe0-12e2f2ce616d);
PROC_LoopEffect(VFX_Script_HAV_SeluneBarrier_01_39741db0-bb39-876e-1760-def8eb2924f0,_Center,"HAV_SeluneBarrier","SCL_Main_A","");

PROC
PROC_HAV_ProtectionFX_On()
AND
DB_HAV_BarrierFX_Contact(_Pos)
THEN
PROC_LoopEffectAtPositionAndRotation(VFX_Script_HAV_SeluneBarrier_Contact_01_ffee1ddb-b25a-3132-7968-315eac643103,_Pos,"HAV_SeluneBarrierContact","SCL_Main_A",1.0);

PROC
PROC_HAV_ProtectionFX_Off()
AND
DB_HAV_BarrierFX_Center(_Center)
THEN
TriggerSetLighting((TRIGGER)LTN_SCL_RuinsBattlefield_G_4d0e202a-4ecd-403d-959c-69fcc76cdefc, "51a77cca-71a1-42f5-2555-508239fac683");
UnloadLevelTemplate(LT_PLT_SCL_Haven_GameplayLight_A_000_bf5e341b-8342-4b79-9fe0-12e2f2ce616d);
PROC_StopLoopEffect(_Center,"HAV_SeluneBarrier");

PROC
PROC_HAV_ProtectionFX_Off()
AND
DB_HAV_BarrierFX_Contact(_Pos)
THEN
PROC_StopLoopEffect(_Pos,"HAV_SeluneBarrierContact");

PROC
PROC_HAV_ProtectionFX_Off()
THEN
TriggerSetSoundState(SV_HavenSphere_fca0c225-07ab-4646-9aa0-ada5de2c918b, "Amb_IsobelsRitual", "Off", 0);

PROC
PROC_State_Changed(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Area", "HAV_Area_State_BarrierIsOn")
THEN
PROC_HAV_ProtectionFX_On();
LoadLevelTemplate(SCL_HavenBasementLights_000_c19a1383-f13c-4660-91c1-252604277f0e);

PROC
PROC_State_Changed(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Area", "HAV_Area_State_BarrierIsOff")
THEN
TriggerSetAtmosphere(ATM_SCL_RuinsBattlefield_G_Haven_e2fa7637-8749-4dad-bf76-03c8c23c745b,"1aeab267-8219-39da-fa04-97064a522ef1"); // ATM_Default_EV10_SCL_RuinsBattlefield
PROC_HAV_ProtectionFX_Off();
PROC_HAV_Torches_Off();
UnloadLevelTemplate(SCL_HavenBasementLights_000_c19a1383-f13c-4660-91c1-252604277f0e);

PROC
PROC_State_Changed(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Area", "HAV_Area_State_CurseIsGone")
THEN
TriggerResetAtmosphere(ATM_SCL_RuinsBattlefield_G_Haven_e2fa7637-8749-4dad-bf76-03c8c23c745b);
PROC_HAV_ProtectionFX_Off();

//END_REGION Atmosphere and protection FX changes

//REGION Curse and safe zone

PROC
PROC_State_Changed(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Area", "HAV_Area_State_BarrierIsOn")
THEN
DB_SCL_Shadowcurse_SafeZones((TRIGGER)S_ShadowCurse_SafeZone_Haven_f4f81c2b-5428-4d06-a65e-3ba21e105cba);
DB_SCL_Shadowcurse_SafeZones((TRIGGER)S_ShadowCurse_SafeZone_HavenBasement_ccd7ad8c-6315-4fe6-898b-051a74f0dbb4);

PROC
PROC_State_Changed(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Area", "HAV_Area_State_BarrierIsOff")
THEN
NOT DB_SCL_Shadowcurse_SafeZones((TRIGGER)S_ShadowCurse_SafeZone_Haven_f4f81c2b-5428-4d06-a65e-3ba21e105cba);
NOT DB_SCL_Shadowcurse_SafeZones((TRIGGER)S_ShadowCurse_SafeZone_HavenBasement_ccd7ad8c-6315-4fe6-898b-051a74f0dbb4);

//END_REGION Curse and safe zone

//REGION Torches behaviour helpers

PROC
PROC_HAV_Torches_Off()
AND
DB_HAV_Torches(_Torch)
THEN
RemoveStatus(_Torch,"BURNING",NULL_00000000-0000-0000-0000-000000000000);

IF
EnteredTrigger(_Character, _Trigger)
AND
DB_HAV_TorchZones(_Trigger)
AND
NOT DB_PartyMembers(_Character)
AND
Exists(_Character,1)
AND
GetVarInteger(_Character, "HAV_Torches_CareForTorches", 1)
THEN
SetDualEntityEvent(S_HAV_Torches_InvisHelper_d39270f8-086b-4a25-89ae-f3aa8b6666d9,_Character,"HAV_Torches_AddNPC",1);
	
IF
LeftTrigger(_Character, _Trigger)
AND
DB_HAV_TorchZones(_Trigger)
AND
NOT DB_PartyMembers(_Character)	//TODO: we might not want to check this here, but Anubis checks it, so....
AND
Exists(_Character,1)
AND
GetVarInteger(_Character, "HAV_Torches_CareForTorches", 1)
THEN
SetDualEntityEvent(S_HAV_Torches_InvisHelper_d39270f8-086b-4a25-89ae-f3aa8b6666d9,_Character,"HAV_Torches_RemoveNPC",1);	

IF
ItemEnteredTrigger(_Torch,_Trigger,_)
AND
DB_HAV_TorchZones(_Trigger)
AND
Exists(_Torch,1)
AND
IsTorch(_Torch,1)
AND
IsMovable(_Torch,0)
THEN
DB_HAV_Torches(_Torch);

IF
ItemLeftTrigger(_Torch,_Trigger,_)
AND
DB_HAV_TorchZones(_Trigger)
AND
Exists(_Torch,1)
AND
IsTorch(_Torch,1)
AND
IsMovable(_Torch,0)
THEN
SetDualEntityEvent(S_HAV_Torches_InvisHelper_d39270f8-086b-4a25-89ae-f3aa8b6666d9,_Torch,"HAV_Torches_RemoveTorch",1);
NOT DB_HAV_Torches(_Torch);

IF
DestroyingBy(_Torch,_,_,_)
AND
DB_HAV_Torches(_Torch)
THEN
SetDualEntityEvent(S_HAV_Torches_InvisHelper_d39270f8-086b-4a25-89ae-f3aa8b6666d9,_Torch,"HAV_Torches_RemoveTorch",1);
NOT DB_HAV_Torches(_Torch);

IF
DB_HAV_Torches(_Torch)
AND
HasAppliedStatus(_Torch,"BURNING",0)
THEN
SetDualEntityEvent(S_HAV_Torches_InvisHelper_d39270f8-086b-4a25-89ae-f3aa8b6666d9,_Torch,"HAV_Torches_AddTorch",1);

IF
StatusApplied(_Torch,"BURNING",_,_)
AND
DB_HAV_Torches((ITEM)_Torch)
THEN
SetDualEntityEvent(S_HAV_Torches_InvisHelper_d39270f8-086b-4a25-89ae-f3aa8b6666d9,_Torch,"HAV_Torches_RemoveTorch",1);

IF
StatusRemoved(_Torch,"BURNING",_,_)
AND
DB_HAV_Torches((ITEM)_Torch)
THEN
SetDualEntityEvent(S_HAV_Torches_InvisHelper_d39270f8-086b-4a25-89ae-f3aa8b6666d9,_Torch,"HAV_Torches_AddTorch",1);

IF
UseStarted(_Player,_Torch)
AND
DB_Players(_Player)
AND
DB_HAV_Torches(_Torch)
AND
NOT DB_GlobalFlag(GLO_LiftingTheCurse_State_BreathHasBeenRestored_2113b54e-a140-4aa0-97ac-219fa7b7d038)
AND
HasActiveStatus(_Torch,"BURNING",1)
AND
CrimeGetNewID(_CrimeID)
THEN
PROC_CharacterRegisterCrime(_Player,"HAV_DouseTorch",_Torch,NULL_00000000-0000-0000-0000-000000000000,_CrimeID);

IF
DB_GlobalFlag(GLO_LiftingTheCurse_State_BreathHasBeenRestored_2113b54e-a140-4aa0-97ac-219fa7b7d038)
AND
DB_Crime_RequestedDialogWithTension("HAV_DouseTorch",_,_Npc,_Criminal,_,_,_)
THEN
PROC_CharacterDisableCrime(_Npc,"HAV_DouseTorch");


IF
DB_HAV_HarpersWithTorches(_Harper)
AND
GetEquippedItem(_Harper,"Melee Main Weapon",_Torch)
AND
IsTorch(_Torch,1)
THEN
DB_HAV_HarpersWithTorches(_Harper,_Torch);

IF
DB_HAV_HarpersWithTorches(_Harper)
AND
GetEquippedItem(_Harper,"Melee Offhand Weapon",_Torch)
AND
IsTorch(_Torch,1)
THEN
DB_HAV_HarpersWithTorches(_Harper,_Torch);

IF
Equipped(_Torch,_Harper)
AND
DB_HAV_HarpersWithTorches(_Harper)
AND
IsTorch(_Torch,1)
THEN
DB_HAV_HarpersWithTorches(_Harper,_Torch);

IF
Unequipped(_Torch,_Harper)
AND
DB_HAV_HarpersWithTorches(_Harper,_Torch)
THEN
NOT DB_HAV_HarpersWithTorches(_Harper,_Torch);

IF
StatusRemoved(_Torch,"BURNING",_,_)
AND
DB_HAV_HarpersWithTorches(_Harper,(ITEM)_Torch)
THEN
SetDualEntityEvent(_Harper,_Torch,"HAV_Torches_Reequip",1);

//END_REGION Torches behaviour helpers

//REGION Dead Flaming Fist
IF
FlagSet(HAV_FlamingSpy_Knows_HiddenCorpse_f30cddd1-1f88-53dd-5c4b-dd676fdfbcaf, _, _)
AND
IsOnStage(S_HAV_FlamingFistCorpse_Rocks_108b4837-0ba7-44de-ba6c-d31e8492fea5, 1)
AND
DB_Players(_Player)
THEN
PROC_ShowMarker(_Player, "HAV_FlamingfistCorpse");
SetCanInteract(S_HAV_FlamingFistCorpse_Rocks_108b4837-0ba7-44de-ba6c-d31e8492fea5, 1);

IF
CharacterLootedCharacter(_Player, S_HAV_FlamingFistCorpse_66ef8153-9c8c-4677-9893-0003b5f12d9d)
AND
DB_GlobalFlag(HAV_FlamingSpy_Knows_HiddenCorpse_f30cddd1-1f88-53dd-5c4b-dd676fdfbcaf)
AND
DB_PartOfTheTeam(_Player)
THEN
PROC_HideMarker(_Player, "HAV_FlamingfistCorpse");

QRY
QRY_HAV_FlamingFistCorpse_TooHeavy((CHARACTER)_Player)
AND
GetAbility(_Player, "Strength", _Value)
AND
_Value < 15
THEN
PROC_HAV_FlamingFistCorpse_TooHeavy((CHARACTER)_Player);

PROC
PROC_HAV_FlamingFistCorpse_TooHeavy((CHARACTER)_Player)
AND
DB_Players(_Player)
AND
NOT QRY_StartDialog_Fixed((DIALOGRESOURCE)HAV_FlamingFistCorpse_PAD_TooHeavy_e64318dc-e0db-974a-628f-3347e0ad7295, _Player)
THEN
ShowError(_Player, "TooHeavy");

PROC
PROC_HAV_FlamingFistCorpse_TooHeavy((CHARACTER)_Player)
AND
NOT DB_Players(_Player)
THEN
ShowError(_Player, "TooHeavy");

IF
UseFinished(_Player, S_HAV_FlamingFistCorpse_Rocks_108b4837-0ba7-44de-ba6c-d31e8492fea5, 1)
AND
NOT QRY_HAV_FlamingFistCorpse_TooHeavy(_Player)
THEN
PROC_HAV_FlamingFistCorpse_Remove(_Player);

PROC
PROC_HAV_FlamingFistCorpse_Remove((CHARACTER)_Player)
THEN
SetOnStage(S_HAV_FlamingFistCorpse_Rocks_000_a6a401cf-8c77-4278-bcb8-5893fa61a27c, 1);
SetOnStage(S_HAV_FlamingFistCorpse_Rocks_108b4837-0ba7-44de-ba6c-d31e8492fea5, 0);
PlayAnimation(S_HAV_FlamingFistCorpse_Rocks_000_a6a401cf-8c77-4278-bcb8-5893fa61a27c, OBJ_SpreadOut_01_b8341ae5-ad88-472b-b586-7c9fe6d05a42, "HAV_FoundCorpse");
PROC_Foop(S_HAV_FlamingFistCorpse_66ef8153-9c8c-4677-9893-0003b5f12d9d);
SetDisableUse(S_HAV_FlamingFistCorpse_Rocks_108b4837-0ba7-44de-ba6c-d31e8492fea5, 1);

PROC
PROC_HAV_FlamingFistCorpse_Remove(_Player)
AND
DB_PartyMembers(_Player)
THEN
StartVoiceBark(HAV_FlamingFistCorpse_VB_Found_99c1447b-3682-005f-c4e2-c16d12be1415, _Player);

//END_REGION

//REGION Changes dialog if Halsin talking to Tieflings
QRY
QRY_SelectCustomDialog_AfterGenerics(_NPC, S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323)
AND
DB_HAV_General_Tieflings(_NPC)
THEN
DB_SelectedDialog(HAV_TieflingDismissesHalsin_f03ff601-79f0-eaf4-587b-738f9cd9f3e8, _NPC, S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323);

//END_REGION

//REGION Changes dialog if Halsin talking to Jaheira too  //Note for Chris - What I'm trying to catch here is that the setup to Haven has played so Jaheira is free to update beyond the intro, but will still catch the players in the setup if 
														//                   They've done nonsense things like sneak around everywhere etc. I don't know if this will also risk breaking the truth serum drink dialogue, however. It's perfectly
														//					 ok to not fire it, but I'm hoping it's still possible to trigger it by talking with a non-Halsin by doing this.
QRY
QRY_SelectCustomDialog_AfterGenerics(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323)
AND
DB_GlobalFlag(HAV_EnteringHaven_State_GainedAccess_07c776da-353a-9050-e9be-c42d51a99412)
THEN
DB_SelectedDialog(HAV_Jaheira_MeetsHalsin_0729704c-8d3a-5c2e-d8e4-8320305596a7, S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323);

QRY
QRY_GLO_SkillCheck_CheckAdvantage(_Player,DEC_GEN_StuffedBear_Standing_A_000_48f69ac4-edc3-4478-82ec-ac1c1f91dd49,"HAV_DarkStuffedBear")
AND
IsTagged(_Player,BARD_d93434bd-6b71-4789-b128-ee24156057cc,1)
THEN
DB_QRYRTN_GLO_Skillcheck_CheckAdvantage(2);
//END_REGION

//REGION Suppression Zone

PROC
PROC_State_Changed(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_Siege")
THEN
DB_PartyDialogSuppressionZone((TRIGGER)S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab);
DB_PartyDialogSuppressionZone((TRIGGER)S_HAV_Haven_SUB_Cellar_eee18cb6-443a-4b8f-ad19-58dff3d731d8);

PROC
PROC_State_Changed(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_Unprotected")
THEN
NOT DB_PartyDialogSuppressionZone((TRIGGER)S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab);
NOT DB_PartyDialogSuppressionZone((TRIGGER)S_HAV_Haven_SUB_Cellar_eee18cb6-443a-4b8f-ad19-58dff3d731d8);

//END_REGION

//REGION Art Cullough flag handling
IF
FlagSet(HAV_TakingIsobel_Event_SpawnGhouls_e8d0fd32-5eff-4101-bb7c-6d2e1acd4ba4, _, _)
THEN
PROC_GlobalSetFlagAndCache(HAV_TakingIsobel_State_AttackInProgress_9b6706d3-9df8-45d7-86c8-e5b675d77e99);

IF
FlagSet(HAV_TakingIsobel_State_AbductionCombatOver_b0bb8648-79a1-4b4c-a0ea-822bcc0cd917, _, _)
THEN
PROC_GlobalClearFlagAndCache(HAV_TakingIsobel_State_AttackInProgress_9b6706d3-9df8-45d7-86c8-e5b675d77e99);

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
