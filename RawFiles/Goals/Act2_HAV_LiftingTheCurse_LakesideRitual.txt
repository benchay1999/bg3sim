Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION Shadows
PROC_DeclareCounter("HAV_LiftingTheCurse_TurnCount");
PROC_DeclareCounter("HAV_LiftingTheCurse_TetherDropped_TurnCount");

DB_HAV_LiftingTheCurse_ReactivityVBs((VOICEBARKRESOURCE)HAV_HalsinQuest_VB_WaveSpawned0_8308d5dc-108e-4a0c-b9b7-e9c0b763ee05, 0);
//DB_HAV_LiftingTheCurse_ReactivityVBs((VOICEBARKRESOURCE)HAV_HalsinQuest_VB_WaveSpawned1_bcee5dd5-b2b3-4dfb-805d-35c973078613, 1);
DB_HAV_LiftingTheCurse_ReactivityVBs((VOICEBARKRESOURCE)HAV_HalsinQuest_VB_WaveSpawned2_6f411e3a-3315-74c1-8b73-d026ab864789, 1);
DB_HAV_LiftingTheCurse_ReactivityVBs((VOICEBARKRESOURCE)HAV_HalsinQuest_VB_WaveSpawned3_b6dd47f8-988d-00d0-25c9-f6b87fcd8687, 2);
DB_HAV_LiftingTheCurse_ReactivityVBs((VOICEBARKRESOURCE)HAV_HalsinQuest_VB_WaveSpawned4_d80b8045-6a27-0478-6647-2cc284733fc5, 3);

DB_HAV_LiftingTheCurse_Waves(0, (CHARACTER)S_HAV_Wave0_Wraith_001_d51f412c-8b40-4141-a65f-057a81d549fb, 4200);
DB_HAV_LiftingTheCurse_Waves(0, (CHARACTER)S_HAV_Wave0_ShadowCursed_Warrior_Pilgrim_003_1a9b065b-d2cd-44c5-95e4-307d6bd82fe7, 3300);
DB_HAV_LiftingTheCurse_Waves(0, (CHARACTER)S_HAV_Wave0_Shadow_007_7e722c07-0bd0-4091-aa5a-d7391404229a, 4000);
DB_HAV_LiftingTheCurse_Waves(0, (CHARACTER)S_HAV_Wave0_Shadow_008_01e7cde6-a4df-403c-87c3-d5377682a6eb, 4400);
DB_HAV_LiftingTheCurse_Waves(0, (CHARACTER)S_HAV_Wave0_Shadow_009_702751d5-1ac0-4399-b02e-972c7258e974, 4600);
DB_HAV_LiftingTheCurse_Waves(0, (CHARACTER)S_HAV_Wave0_ShadowCursed_Warrior_Pilgrim_005_be15abb7-2ea6-4cf8-bf56-bd8465b570e3, 1600);
DB_HAV_LiftingTheCurse_Waves(0, (CHARACTER)S_HAV_Wave0_ShadowCursed_Warrior_Pilgrim_006_2c25ec5b-c162-4579-9f1e-435fbdcfd6e3, 2000);
DB_HAV_LiftingTheCurse_Waves(0, (CHARACTER)S_HAV_Wave4_ShadowMastiff_003_06c47934-bc30-48f0-b297-4f99272d1241, 900);
DB_HAV_LiftingTheCurse_Waves(0, (CHARACTER)S_HAV_Wave4_ShadowMastiff_004_06451f28-98fa-4ab9-9b6b-92edb5ceb9af, 500);
DB_HAV_LiftingTheCurse_Waves(0, (CHARACTER)S_HAV_Wave1_ShadowCursed_Ranger_Harper_005_5e59d1af-a2b7-43b7-8d6c-5a6439a129f6, 2400);
DB_HAV_LiftingTheCurse_Waves(0, (CHARACTER)S_HAV_Wave1_ShadowCursed_Ranger_Harper_007_2112e248-7cbc-4db6-a629-34dd87dc2386, 2900);

DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)S_HAV_Wave1_ShadowCursed_Ranger_Harper_004_9430b751-a5f9-40bc-888d-e3e8abe4dcf3, 500);
DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)S_HAV_Wave0_ShadowCursed_Warrior_Pilgrim_004_9f16d360-533a-43b2-8149-dc39632a4fed, 1300);
DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)S_HAV_Wave0_ShadowCursed_Warrior_Pilgrim_007_e0d36900-5a00-4f11-b19b-74512d1554c8, 1600);
DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)S_HAV_Wave1_ShadowCursed_Ranger_Harper_006_1108b8a5-0d22-4a0e-8973-37b0442f2070, 800);
DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)DarkVine_Tentacle_000_bb3e4ea7-7f7a-4c65-84c9-de788a2b3d7c, 4000);
DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)DarkVine_Tentacle_001_abd0896f-555c-43a1-b9d1-71144ff7e87d, 4500);
DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)DarkVine_Tentacle_002_c172ed73-4f81-4dbe-a577-0d5a56b42ffb, 5000);
DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)S_HAV_Wave3_ShadowCursed_Melee_FlamingFist_007_66deff82-b495-489b-872a-4b544bc941bc, 1900);
DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)S_HAV_Wave3_ShadowCursed_Melee_FlamingFist_005_da1e6ca1-6c54-4b82-b94a-901745d64f2e, 2200);
DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)S_HAV_Wave3_ShadowCursed_Melee_FlamingFist_008_a410b3f0-f025-4fd1-8b90-d8c0d8fd21e6, 2500);
DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)S_HAV_Wave0_Raven_Dire_ShadowCursed_007_2ad074e8-a5a4-452e-bcfe-7a946887a539, 3000);
DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)S_HAV_Wave0_Raven_Dire_ShadowCursed_008_91b23b1d-0583-4118-81b6-a3f9c18e9fe8, 3100);
DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)S_HAV_Wave0_Raven_Dire_ShadowCursed_009_2edb73bb-9831-44d3-bc6f-76b829e5d898, 3200);
DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)S_HAV_Wave0_Raven_Dire_ShadowCursed_010_ddb92490-a75d-496e-9b8d-78dcf1f9fef3, 3300);
DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)S_HAV_Wave0_Raven_Dire_ShadowCursed_011_77f588f6-e792-4b41-a61c-09bfe44d0bdc, 3400);
DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)S_HAV_Wave0_Raven_Dire_ShadowCursed_012_1303ff46-5200-4986-9d73-d85ad7918ec5, 3500);
DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)S_HAV_Wave0_Raven_Dire_ShadowCursed_013_fc6da3fd-f4d7-4ebf-b125-7d77387a746f, 3600);
DB_HAV_LiftingTheCurse_Waves(1, (CHARACTER)S_HAV_Wave0_Raven_Dire_ShadowCursed_014_1e41607c-ed56-43bb-9bd2-98de3796d52a, 3700);

DB_HAV_LiftingTheCurse_Waves(2, (CHARACTER)S_HAV_Wave1_ShadowCursed_Warrior_Zealot_005_7c4c46d9-6d0d-4e8a-8473-476eb071bd49, 2000);
DB_HAV_LiftingTheCurse_Waves(2, (CHARACTER)S_HAV_Wave3_ShadowCursed_Melee_FlamingFist_004_1ede9b50-0b85-4a48-8872-738ddd1fc4cd, 2400);
DB_HAV_LiftingTheCurse_Waves(2, (CHARACTER)S_HAV_Wave1_ShadowCursed_Warrior_Zealot_003_4337f668-9c2d-47cc-8530-b620109de1b4, 2800);
DB_HAV_LiftingTheCurse_Waves(2, (CHARACTER)S_HAV_Wave2_Shadow_001_61dfdf61-6860-464a-8e38-bce3458846a7, 4000);
DB_HAV_LiftingTheCurse_Waves(2, (CHARACTER)S_HAV_Wave3_ShadowCursed_Melee_FlamingFist_006_e81fbbc9-a2ec-40e4-800e-fe02acc4fc69, 3500);
DB_HAV_LiftingTheCurse_Waves(2, (CHARACTER)S_HAV_Wave2_Wraith_001_98dde2d9-06c1-4cfd-83b9-a9d2338b514d, 4400);
DB_HAV_LiftingTheCurse_Waves(2, (CHARACTER)S_HAV_Wave3_ShadowCursed_Warrior_Githyanki_005_7052676a-7ba6-442b-8e63-b9670e4384a9, 600);
DB_HAV_LiftingTheCurse_Waves(2, (CHARACTER)S_HAV_Wave3_ShadowCursed_Warrior_Githyanki_004_3ef3995a-a195-4982-987d-4cdf22b98235, 900);
DB_HAV_LiftingTheCurse_Waves(2, (CHARACTER)S_HAV_Wave3_ShadowCursed_Warrior_Githyanki_003_06fe050e-86f5-47f8-bda9-53d91b9f6fdf, 1200);
DB_HAV_LiftingTheCurse_Waves(2, (CHARACTER)S_HAV_Wave1_ShadowCursed_Warrior_Zealot_004_f72a9c81-7fac-4bc2-a6ce-b775e3ea7721, 3200);

/*
DB_HAV_LiftingTheCurse_ArenaCams(0, "HAV_LakesideCombat_Wave0_ArenaStart");
DB_HAV_LiftingTheCurse_ArenaCams(1, "HAV_LakesideCombat_Wave1_ArenaStart");
DB_HAV_LiftingTheCurse_ArenaCams(2, "HAV_LakesideCombat_Wave2_ArenaStart");
*/

PROC_HAV_LiftingTheCurse_ShadowsInit();
//END_REGION

//REGION Spirit of the Land
SetOnStage((CHARACTER)S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a, 0);
DB_Dialogs((CHARACTER)S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a, (DIALOGRESOURCE)HAV_HalsinQuest_Spirit_85232c87-9912-7c37-c068-3196688f88b3);
//END_REGION

DB_GlobalFlagReactionAfterDialog((FLAG)HAV_LiftingTheCurse_State_ReachedPortal_9deedd00-3ffc-4b91-98ff-56aa4873c850, (DIALOGRESOURCE)HAV_LiftingTheCurse_Portal_0eee8461-c736-e852-ff90-376d32b3823a);

PROC_TriggerRegisterForPlayers((TRIGGER)S_HAV_TetherLeaveBounds_7ae97f8d-1809-4236-b8b2-d64ef1142f3c);

DB_GLO_DefeatCounter_AllPermaDefeatedGlobalFlag("HAV_LiftingTheCurse_Shadows", HAV_LiftingTheCurse_State_PortalEnemiesDefeated_3836d472-c42f-438c-94bb-e2e83cdc6629);

SetOnStage(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, 0);
SetCanExamine((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6,0);

//Item Difficulty Changes DB
DB_Combat_DifficultyItems((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6,"HALSIN_SHADOWPORTAL_HARDCORE", "HARD"); //Halsin's Portal
KBSECTION
//REGION Debug
IF
TextEvent("HalsinLakeside")
AND
GetHostCharacter(_Player)
THEN
SetFlag((FLAG)GLO_LiftingTheCurse_Debug_HalsinAtLakeSide_89683244-0daf-4175-9808-2e87f1a4240b);
TeleportTo(_Player, (TRIGGER)S_HAV_HalsinShorePoint_64c1cdf5-0a4c-462e-a9ea-71e5edc3031d, "", 0, 0, 0, 1, 1);
ApplyStatus(_Player, "SCL_MOONSHIELD", -1.0, 1, NULL_00000000-0000-0000-0000-000000000000);

/*
PROC_GlobalClearFlagAndCache((FLAG)GLO_LiftingTheCurse_Debug_HalsinAtLakeSide_89683244-0daf-4175-9808-2e87f1a4240b);
DB_Dialogs(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, (DIALOGRESOURCE)HAV_HalsinQuest_Portal_0eee8461-c736-e852-ff90-376d32b3823a);
TeleportTo(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, (TRIGGER)S_HAV_HalsinShorePoint_64c1cdf5-0a4c-462e-a9ea-71e5edc3031d, "", 0, 0, 0, 1, 1);
TeleportTo(_Player, (TRIGGER)S_HAV_HalsinShorePoint_64c1cdf5-0a4c-462e-a9ea-71e5edc3031d, "", 0, 0, 0, 1, 1);
SetOnStage(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, 1);
ApplyStatus(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, "SCL_MOONSHIELD", -1.0, 1, NULL_00000000-0000-0000-0000-000000000000);
PROC_State_Progress(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, "SCL_LiftingTheCurse", "SCL_LiftingTheCurse_State_HalsinAtPortal");
*/

IF
FlagSet((FLAG)GLO_LiftingTheCurse_Debug_HalsinAtLakeSide_89683244-0daf-4175-9808-2e87f1a4240b, _, _)
THEN
PROC_GLO_LiftingTheCurse_Debug_HalsinSetUp("GLO_LiftingTheCurse_Debug_LakeSideRitual");

PROC
PROC_GLO_LiftingTheCurse_Debug_HalsinSetUp_State("GLO_LiftingTheCurse_Debug_LakeSideRitual")
THEN
PROC_GlobalClearFlagAndCache((FLAG)GLO_LiftingTheCurse_Debug_HalsinAtLakeSide_89683244-0daf-4175-9808-2e87f1a4240b);
DB_Dialogs(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, (DIALOGRESOURCE)HAV_HalsinQuest_Portal_0eee8461-c736-e852-ff90-376d32b3823a);
TeleportTo(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, (TRIGGER)S_HAV_HalsinShorePoint_64c1cdf5-0a4c-462e-a9ea-71e5edc3031d, "", 0, 0, 0, 1, 1);
SetOnStage(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, 1);
ApplyStatus(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, "SCL_MOONSHIELD", -1.0, 1, NULL_00000000-0000-0000-0000-000000000000);
PROC_State_Progress(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, "SCL_LiftingTheCurse", "SCL_LiftingTheCurse_State_HalsinAtPortal");

IF
FlagSet((FLAG)GLO_LiftingTheCurse_Debug_HalsinPostPortal_69608804-1ae8-467f-b3d9-a425d82beb17, _, _)
THEN
PROC_GLO_LiftingTheCurse_Debug_HalsinSetUp("GLO_LiftingTheCurse_Debug_PostRitual");

PROC
PROC_GLO_LiftingTheCurse_Debug_HalsinSetUp_State("GLO_LiftingTheCurse_Debug_PostRitual")
THEN
PROC_GlobalSetFlagAndCache((FLAG)SCL_ShadowCurse_Knows_Oliver_Is_Thaniel_56af8bbd-eafe-3861-22a2-c253f16952e2);
DB_Dialogs(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, (DIALOGRESOURCE)HAV_HalsinQuest_PortalConclusion_c1112847-6f1b-2a3e-2807-4b0937b16a52);
TeleportTo(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, (TRIGGER)S_HAV_HalsinShorePoint_64c1cdf5-0a4c-462e-a9ea-71e5edc3031d, "", 0, 0, 0, 1, 1);
SetOnStage(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, 1);
PROC_Foop(S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a);
PROC_State_Progress(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, "SCL_LiftingTheCurse", "SCL_LiftingTheCurse_State_HalsinAtPostPortal");

IF
DialogEnded((DIALOGRESOURCE)HAV_HalsinQuest_Portal_0eee8461-c736-e852-ff90-376d32b3823a, _)
AND
DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_Debug_SkipLakeSideCombat_48201e5e-2683-458a-8fdd-cd5f0b2f56af)
THEN
PROC_GlobalSetFlagAndCache((FLAG)HAV_LiftingTheCurse_State_HalsinIsInRitual_3350ed7d-835e-4b5e-886e-3c544d27ce61);
PROC_GlobalSetFlagAndCache((FLAG)HAV_LiftingTheCurse_State_ChannelingComplete_1afd47bc-e8d5-464d-823a-7dba82fcf5c3);

//END_REGION

//REGION Lakeside Ritual

PROC
PROC_HAV_LiftingTheCurse_ShadowsInit()
AND
DB_HAV_LiftingTheCurse_Waves(_, _Shadow, _)
THEN
DB_GLO_DefeatCounter(_Shadow, "HAV_LiftingTheCurse_Shadows");
SetOnStage(_Shadow, 0);

IF
FlagSet(HAV_LiftingTheCurse_State_HalsinGoToLake_bc4cd1f7-5e60-40e1-a583-aa886fe5b682, _, _)
THEN
SetHasDialog(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, 0);


IF
FlagSet(HAV_LiftingTheCurse_State_HalsinArrivedAtLake_78dc335f-ec52-4c22-8b3a-33705205c2a0, _, _)
THEN
DB_Camp_BlockCamperPosAdjustment(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323);
PROC_State_Progress(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, "SCL_LiftingTheCurse", "SCL_LiftingTheCurse_State_HalsinAtPortal");
PROC_RemoveDialog(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323);
DB_Dialogs(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, (DIALOGRESOURCE)HAV_HalsinQuest_Portal_0eee8461-c736-e852-ff90-376d32b3823a);
SetHasDialog(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, 1);

//If players leave and return to level while in this state
IF
DB_InCamp(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323)
AND
DB_CurrentLevel("SCL_Main_A")
AND
DB_GlobalFlag(HAV_LiftingTheCurse_State_HalsinAtLakeside_0bb942dc-420d-45f9-9594-83805760fd29)
THEN
DB_Camp_BlockCamperPosAdjustment(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323);
TeleportTo(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, (TRIGGER)S_HAV_HalsinShorePoint_64c1cdf5-0a4c-462e-a9ea-71e5edc3031d, "", 0, 0, 0, 1, 1);
PROC_RemoveDialog(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323);
DB_Dialogs(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, (DIALOGRESOURCE)HAV_HalsinQuest_Portal_0eee8461-c736-e852-ff90-376d32b3823a);
SetHasDialog(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, 1);

IF
FlagSet((FLAG)HAV_LiftingTheCurse_Event_PortalAppears_333d9c5e-501a-470d-9708-90b9d9b1ec01, _, _)
THEN
PROC_Foop((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6);

IF
WentOnStage(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6,1)
AND
DB_HAV_LiftingTheCurse_Waves(_, (CHARACTER)_Char, _)
THEN
AiAddInterestingItem((CHARACTER)_Char, S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6);

//END_REGION

//REGION Lakeside Ritual Combat

//REGION Combat ADs

IF
DB_PermaDefeated(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323)
AND
DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_HalsinIsInRitual_3350ed7d-835e-4b5e-886e-3c544d27ce61)
AND
DB_Players(_Player)
AND
DB_Is_InCombat(_Player, _ID)
AND
DB_HAV_LiftingTheCurse_Waves(_, _Char, _)
AND
DB_Is_InCombat(_Char, _ID)
AND
IsEnemy(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, _Player, 0)
AND
QRY_OnlyOnce("HAV_LiftingTheCurse_DeathPAD")
THEN
StartVoiceBark((VOICEBARKRESOURCE)HAV_HalsinQuest_VB_HalsinDeath_f52dd9f7-22ec-9c3c-97c1-89368af1aa60, _Player);

IF
FlagSet((FLAG)HAV_LiftingTheCurse_State_ReachedPortal_9deedd00-3ffc-4b91-98ff-56aa4873c850, _, _)
AND
QRY_GetClosestAvailableCharacterTo((TRIGGER)S_HAV_PortalPoint_520a70e1-f45b-42ef-9be6-0fc8d1a3d82b, 0, 0, NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000, 1)
AND
DB_ClosestAvailableCharacterTo(_Player, (TRIGGER)S_HAV_PortalPoint_520a70e1-f45b-42ef-9be6-0fc8d1a3d82b, _Dist)
AND
_Dist <= 30.0
THEN
StartVoiceBark((VOICEBARKRESOURCE)HAV_HalsinQuest_VB_HalsinReachedPortal_c80ca01b-903d-11b2-b36b-745c26b47355, _Player);


//END_REGION

//REGION Shadow in Haven

IF
EnteredTrigger(_Char, (TRIGGER)S_ShadowCurse_SafeZone_Haven_f4f81c2b-5428-4d06-a65e-3ba21e105cba)
AND
DB_HAV_LiftingTheCurse_Waves(_, _Char, _)
AND
HasActiveStatus(_Char, "HAV_SHIELDSHADOWBURN", 0)
THEN
ApplyStatus(_Char, "HAV_SHIELDSHADOWBURN", -1.0, 1, NULL_00000000-0000-0000-0000-000000000000);

IF
LeftTrigger(_Char, (TRIGGER)S_ShadowCurse_SafeZone_Haven_f4f81c2b-5428-4d06-a65e-3ba21e105cba)
AND
DB_HAV_LiftingTheCurse_Waves(_, _Char, _)
AND
HasActiveStatus(_Char, "HAV_SHIELDSHADOWBURN", 1)
THEN
RemoveStatus(_Char, "HAV_SHIELDSHADOWBURN", NULL_00000000-0000-0000-0000-000000000000);


//END_REGION

QRY
QRY_HAV_IsRitualActive()
AND
DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_HalsinIsInRitual_3350ed7d-835e-4b5e-886e-3c544d27ce61)
AND
NOT DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_ChannelingComplete_1afd47bc-e8d5-464d-823a-7dba82fcf5c3)
THEN
DB_NOOP(1);

PROC
PROC_GlobalFlagReactionAfterDialog((FLAG)HAV_LiftingTheCurse_State_ReachedPortal_9deedd00-3ffc-4b91-98ff-56aa4873c850)
THEN
PROC_State_Progress(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, "SCL_LiftingTheCurse", "SCL_LiftingTheCurse_State_HalsinInShadowFell");
PROC_Poof(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323);
PROC_CharacterDisableAllCrimes((CHARACTER)S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a, 1);

IF
DialogEnded((DIALOGRESOURCE)HAV_LiftingTheCurse_Portal_0eee8461-c736-e852-ff90-376d32b3823a, _)
AND
NOT DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_Debug_SkipLakeSideCombat_48201e5e-2683-458a-8fdd-cd5f0b2f56af)
AND
GetClosestPlayer(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _Player, _Dist)
THEN
//SendArenaCameraEvent(_Player, S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, "HAV_LakesideCombat_Wave0_ArenaStart");
PROC_HAV_LiftingTheCurse_SpawnWave(0);


IF
DialogEnded((DIALOGRESOURCE)HAV_LiftingTheCurse_Portal_0eee8461-c736-e852-ff90-376d32b3823a, _)
AND
NOT DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_Debug_SkipLakeSideCombat_48201e5e-2683-458a-8fdd-cd5f0b2f56af)
AND
QRY_OnlyOnce("HAV_LiftingTheCurse_StartedChanneling")
THEN
PROC_GlobalSetFlagAndCache((FLAG)HAV_LiftingTheCurse_State_HalsinIsInRitual_3350ed7d-835e-4b5e-886e-3c544d27ce61);

IF
DB_Is_InCombat(_Shade, _ID)
AND
DB_HAV_LiftingTheCurse_Waves(_, (CHARACTER)_Shade, _)
AND
DB_HAV_LiftingTheCurse_Waves(_, (CHARACTER)_OtherShade, _)
AND
DB_Is_InCombat(_OtherShade, _OtherID)
AND
NOT DB_Is_InCombat(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _OtherID)
THEN
SetCombatGroupAndEnterCombat(_Shade, "8e09a1fc-35e8-4c34-9ecf-089009cb1d80", S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6);

QRY
QRY_HAV_LakesideCombat_PlayerShouldJoinCombat()
AND
DB_PartyMembers(_Player)
AND
NOT DB_HiddenCharacters(_Player, _)
AND
GetDistanceTo((GUIDSTRING)S_HAV_HalsinShorePoint_64c1cdf5-0a4c-462e-a9ea-71e5edc3031d, _Player, _Dist)
AND
_Dist <= 10.0
THEN
PROC_EnterCombat(S_HAV_Wave0_Shadow_007_7e722c07-0bd0-4091-aa5a-d7391404229a, _Player);

IF
WentOnStage(S_HAV_Wave0_Shadow_007_7e722c07-0bd0-4091-aa5a-d7391404229a, 1)
AND
NOT QRY_HAV_LakesideCombat_PlayerShouldJoinCombat()
AND
DB_PartyMembers(_Player)
AND
GetDistanceTo((GUIDSTRING)S_HAV_HalsinShorePoint_64c1cdf5-0a4c-462e-a9ea-71e5edc3031d, _Player, _Dist)
AND
_Dist <= 10.0
AND
QRY_OnlyOnce("HAV_LakesideCombat_FallbackForceJoin")
THEN
PROC_EnterCombat(S_HAV_Wave0_Shadow_007_7e722c07-0bd0-4091-aa5a-d7391404229a, _Player);

IF
EnteredCombat(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _ID)
AND
DB_Players(_Player)
AND
NOT DB_HiddenCharacters(_Player, _)
AND
GetDistanceTo((GUIDSTRING)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _Player, _Dist)
AND
_Dist <= 30.0
THEN
PROC_EnterCombat(_Player, S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6);


IF
EnteredCombat(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _ID)
AND
DB_GlobalCounter("HAV_LiftingTheCurse_TurnCount", _Count)
AND
IntegerSubtract(5, _Count, _RemainingCount)
AND
_RemainingCount > 0
AND
IntegerProduct(_RemainingCount, 6000, _MsCount)
AND
DB_Players(_Player)
THEN
ObjectQuestTimerLaunch(_Player, "HAV_LikesideCombat_CombatRoundTimer", "HAV_HalsinPortalTimer", _MsCount, 1); 

IF
TurnStarted(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6)
AND
QRY_OnlyOnce_Reset("HAV_HalsinQuest_PlayedVoiceBark")
THEN
DB_NOOP(1);


IF
LeftCombat(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _)
AND
NOT DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_ChannelingComplete_1afd47bc-e8d5-464d-823a-7dba82fcf5c3)
THEN
DB_OneShotPartyTrigger(S_HAV_BUGFIX_TryRestartCombat_1424dbe2-ef81-44c3-a75d-b3eb16045765);

PROC
PROC_OneShotTriggerEntered(_, S_HAV_BUGFIX_TryRestartCombat_1424dbe2-ef81-44c3-a75d-b3eb16045765)
THEN
PROC_IncreaseCounter("HAV_LiftingTheCurse_TurnCount");

IF
TurnStarted(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6)
THEN
PROC_IncreaseCounter("HAV_LiftingTheCurse_TurnCount");

PROC
PROC_CounterIncreased("HAV_LiftingTheCurse_TurnCount",(INTEGER)_NewIndex)
THEN
PROC_HAV_LiftingTheCurse_SpawnWave((INTEGER)_NewIndex);
PROC_HAV_LiftingTheCurse_DeclareRound((INTEGER)_NewIndex);


//Disabled Arena Cam stuff
/*
PROC
PROC_HAV_LiftingTheCurse_SpawnWave((INTEGER)_NewIndex)
AND
DB_HAV_LiftingTheCurse_ArenaCams(_NewIndex, _CameraEvent)
AND
DB_Is_InCombat(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _ID)
AND
GetClosestPlayer(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _Player, _Dist)
THEN
PauseCombat(_ID);
SendArenaCameraEvent(_Player, S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _CameraEvent);
TimerLaunch("HAV_LiftingTheCurse_CombatUnpauseTimer", 5000);


IF
TimerFinished("HAV_LiftingTheCurse_CombatUnpauseTimer")
AND
DB_Is_InCombat(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _ID)
THEN
ResumeCombat(_ID);
*/


PROC
PROC_CounterIncreased("HAV_LiftingTheCurse_TurnCount",(INTEGER)_NewIndex)
AND
DB_HAV_LiftingTheCurse_Waves(_, (CHARACTER)_Shade, _)
AND
DB_Is_InCombat(_Shade, _ID)
AND
DB_Players(_Player)
AND
DB_Is_InCombat(_Player, _ID)
AND
DB_HAV_LiftingTheCurse_ReactivityVBs(_VB, (INTEGER)_NewIndex)
AND
QRY_OnlyOnce("HAV_HalsinQuest_PlayedVoiceBark")
THEN
StartVoiceBark(_VB, _Player);

//Ends combat at the end of the round if all enemies are dead after the last wave has spawned
PROC
PROC_CounterIncreased("HAV_LiftingTheCurse_TurnCount",(INTEGER)_NewIndex)
AND
_NewIndex >= 3
AND
NOT QRY_HAV_LiftingtheCurse_AnyShadesAlive()
AND
DB_Is_InCombat(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _ID)
THEN
PROC_GlobalSetFlagAndCache((FLAG)HAV_LiftingTheCurse_State_ChannelingComplete_1afd47bc-e8d5-464d-823a-7dba82fcf5c3);
EndCombat(_ID);

QRY
QRY_HAV_LiftingtheCurse_AnyShadesAlive()
AND
DB_HAV_LiftingTheCurse_Waves(_, (CHARACTER)_Shade, _)
AND
DB_Is_InCombat(_Shade, _)
THEN
DB_NOOP(1);

PROC
PROC_HAV_LiftingTheCurse_SpawnWave((INTEGER)_Index)
THEN
PROC_RemoveOneShotTrigger(S_HAV_BUGFIX_TryRestartCombat_1424dbe2-ef81-44c3-a75d-b3eb16045765);

PROC
PROC_HAV_LiftingTheCurse_SpawnWave((INTEGER)_Index)
AND
NOT DB_HAV_LiftingTheCurse_Waves(_Index, _, _)
THEN
EndTurn(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6);

PROC
PROC_HAV_LiftingTheCurse_SpawnWave((INTEGER)_Index)
AND
NOT DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_AllWavesSpawned_928db093-f718-483a-9e0a-aaa0a8765ff9)
AND
DB_HAV_LiftingTheCurse_Waves(_Index, _Char, _Timer)
THEN
ObjectTimerLaunch(_Char, "HAV_LiftingTheCurse_ShadowSpawn", _Timer, 0);

PROC
PROC_HAV_LiftingTheCurse_SpawnWave(4)
THEN
PROC_GlobalSetFlagAndCache((FLAG)HAV_LiftingTheCurse_State_AllWavesSpawned_928db093-f718-483a-9e0a-aaa0a8765ff9);

IF
ObjectTimerFinished(_Char, "HAV_LiftingTheCurse_ShadowSpawn")
AND
DB_HAV_LiftingTheCurse_Waves(_Wave, (CHARACTER)_Char, _)
THEN
DB_HAV_LiftingTheCurse_ShadeSpawned(_Char);
TriggerRegisterForCharacter((TRIGGER)S_ShadowCurse_SafeZone_Haven_f4f81c2b-5428-4d06-a65e-3ba21e105cba, _Char);
AppearAt(_Char, _Char, 1, DFLT_UTIL_Spawn_01_510ff37e-0618-40e4-8ce0-236e3dc9c291, "", 0);
PROC_HAV_LiftingTheCurse_EndTurn((INTEGER)_Wave);

IF
WentOnStage(_Char, 1)
AND
DB_HAV_LiftingTheCurse_Waves(_, (CHARACTER)_Char, _)
THEN
EnterCombat(_Char, S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6);

PROC
PROC_HAV_LiftingTheCurse_EndTurn((INTEGER)_Wave)
AND
NOT QRY_HAV_LiftingTheCurse_CheckSpawned((INTEGER)_Wave)
THEN
EndTurn(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6);

QRY
QRY_HAV_LiftingTheCurse_CheckSpawned((INTEGER)_Wave)
AND
DB_HAV_LiftingTheCurse_Waves(_Wave, (CHARACTER)_Char, _)
AND
NOT DB_HAV_LiftingTheCurse_ShadeSpawned(_Char)
THEN
DB_NOOP(1);

PROC // TEMP: Will be replaced with the coding round UI timer when it is available.
PROC_HAV_LiftingTheCurse_DeclareRound((INTEGER)_NewIndex)
AND
IntegerSubtract(5, _NewIndex, _Count)
THEN
PROC_HAV_LiftingTheCurse_CheckRound((INTEGER)_Count);

PROC
PROC_HAV_LiftingTheCurse_CheckRound((INTEGER)_Count)
AND
NOT DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_ChannelingComplete_1afd47bc-e8d5-464d-823a-7dba82fcf5c3)
AND
_Count > 0
AND
IntegerToString(_Count, _CountString)
AND
Concatenate(_CountString, "rounds left till Halsin returns.", _String)
THEN
DebugText(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _String);

PROC
PROC_HAV_LiftingTheCurse_CheckRound((INTEGER)_Count)
AND
NOT DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_ChannelingComplete_1afd47bc-e8d5-464d-823a-7dba82fcf5c3)
AND
_Count == 0
THEN
PROC_GlobalSetFlagAndCache((FLAG)HAV_LiftingTheCurse_State_ChannelingComplete_1afd47bc-e8d5-464d-823a-7dba82fcf5c3);

//END_REGION

//REGION Halsin Defeat

IF
DB_PermaDefeated(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323)
AND
DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_HalsinIsInRitual_3350ed7d-835e-4b5e-886e-3c544d27ce61)
THEN
PROC_GlobalClearFlagAndCache((FLAG)HAV_LiftingTheCurse_State_HalsinIsInRitual_3350ed7d-835e-4b5e-886e-3c544d27ce61);
Die((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6,DEATHTYPE.Disintegrate,(ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6,0,0,1.0);
//PROC_Poof((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6);

PROC
PROC_HAV_LiftingTheCurse_DeathVB()
AND
DB_Players(_Player)
AND 
QRY_OnlyOnce("HAV_LiftingTheCurse_DeathPAD")
THEN
StartVoiceBark((VOICEBARKRESOURCE)HAV_HalsinQuest_VB_HalsinDeath_f52dd9f7-22ec-9c3c-97c1-89368af1aa60, _Player);

//END_REGION

//REGION Portai is closed prematurely

IF
DestroyedBy(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _, _, _)
AND
NOT DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_ChannelingComplete_1afd47bc-e8d5-464d-823a-7dba82fcf5c3)
THEN
Die(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, DEATHTYPE.Physical, NULL_00000000-0000-0000-0000-000000000000, 0, 1, 0.0);
PROC_HAV_LiftingTheCurse_RemoveShadows();
PROC_HAV_LiftingTheCurse_DeathVB();

IF
LeftCombat(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _)
AND
NOT DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_PortalEnemiesDefeated_3836d472-c42f-438c-94bb-e2e83cdc6629)
AND
QRY_HAV_IsRitualActive()
THEN
PROC_HAV_LiftingTheCurse_DestroyPortal();

PROC
PROC_HAV_LiftingTheCurse_DestroyPortal()
THEN
PROC_HAV_LiftingTheCurse_RemoveShadows();
PROC_GlobalClearFlagAndCache((FLAG)HAV_LiftingTheCurse_State_HalsinIsInRitual_3350ed7d-835e-4b5e-886e-3c544d27ce61);
Die((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6,DEATHTYPE.Disintegrate,(ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6,0,0,1.0);
//PROC_Poof((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6);

PROC
PROC_HAV_LiftingTheCurse_RemoveShadows()
AND
DB_HAV_LiftingTheCurse_Waves(_, _Shadow, _)
AND
NOT DB_Dead(_Shadow)
AND
IsOnStage(_Shadow, 1)
AND
GetPosition(_Shadow, _X, _Y, _Z)
AND
GetClosestPlayer(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _Player, _Dist)
THEN
Die(_Shadow,DEATHTYPE.Radiant,_Player,0,0,1.0);
PlayEffectAtPosition((EFFECTRESOURCE)VFX_Spells_Cast_Damage_Radiant_DivineIntervention_Attack_Impact_BodyFX_Textkey_01_1aee90d5-d5b2-9f9b-90e7-117e755bd8b1, _X, _Y, _Z);

//END_REGION

//REGION Halsin escapes with child

IF
DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_PortalEnemiesDefeated_3836d472-c42f-438c-94bb-e2e83cdc6629)
AND
NOT DB_Defeated(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323)
THEN
PROC_GlobalSetFlagAndCache((FLAG)HAV_LiftingTheCurse_State_ChannelingComplete_1afd47bc-e8d5-464d-823a-7dba82fcf5c3);

IF
FlagSet((FLAG)HAV_LiftingTheCurse_State_ChannelingComplete_1afd47bc-e8d5-464d-823a-7dba82fcf5c3, _, _)
THEN
PlayEffect(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, (EFFECTRESOURCE)VFX_Spells_Cast_Intent_Whitebox_ZoneCone_CastFX_Textkey_02_9ecfa347-83b7-a697-53e9-c81f9a9ad184);
TimerLaunch("HAV_LiftingTheCurse_HalsinAppearance", 1000);

IF
TimerFinished("HAV_LiftingTheCurse_HalsinAppearance")
AND
NOT DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_PortalEnemiesDefeated_3836d472-c42f-438c-94bb-e2e83cdc6629)
AND
DB_HAV_LiftingTheCurse_Waves(_, _Shadow, _)
AND
NOT DB_Dead(_Shadow)
AND
IsOnStage(_Shadow, 1)
AND
GetPosition(_Shadow, _X, _Y, _Z)
AND
GetClosestPlayer(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _Player, _Dist)
THEN
Die(_Shadow,DEATHTYPE.Radiant,_Player,0,0,1.0);
PlayEffectAtPosition((EFFECTRESOURCE)VFX_Spells_Cast_Damage_Radiant_DivineIntervention_Attack_Impact_BodyFX_Textkey_01_1aee90d5-d5b2-9f9b-90e7-117e755bd8b1, _X, _Y, _Z);
//PROC_Poof(_Shadow);

IF
TimerFinished("HAV_LiftingTheCurse_HalsinAppearance")
AND
NOT DB_Defeated(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323)
AND
DB_Players(_Player)
AND
NOT DB_Defeated(_Player)
AND
DB_InRegion(_Player, (TRIGGER)S_HAV_TetherLeaveBounds_7ae97f8d-1809-4236-b8b2-d64ef1142f3c)
AND
QRY_SpeakerIsAvailable(_Player, 1)
AND
QRY_OnlyOnce("HAV_LiftingTheCurse_HalsinReturned")
THEN
DB_HAV_LiftingTheCurse_EmergeSpeaker((CHARACTER)_Player);
PROC_HAV_LiftingTheCurse_EmergeDialog((CHARACTER)_Player);

IF
DialogStarted(HAV_HalsinQuest_PortalEmerge_603c48af-cffb-2998-f1ee-6f6ece03a4f2, _)
THEN
PROC_Foop(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323);
PROC_Foop(S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a);

IF
DialogStarted(HAV_HalsinQuest_PortalEmerge_603c48af-cffb-2998-f1ee-6f6ece03a4f2, _)
AND
HasActiveStatus(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, "HAV_SELUNEOINTMENT", 0)
THEN
ApplyStatus(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, "HAV_SELUNEOINTMENT", -1.0, 1, NULL_00000000-0000-0000-0000-000000000000);

IF
FlagSet((FLAG)HAV_LiftingTheCurse_State_ChannelingComplete_1afd47bc-e8d5-464d-823a-7dba82fcf5c3, _, _)
AND
DB_HAV_LiftingTheCurse_Waves(_, _Shadow, _)
AND
IsOnStage(_Shadow, 0)
THEN
NOT DB_GLO_DefeatCounter(_Shadow, "HAV_LiftingTheCurse_Shadows");

/*
IF
WentOnStage(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, 1)
AND
DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_HalsinIsInRitual_3350ed7d-835e-4b5e-886e-3c544d27ce61)
AND
DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_ChannelingComplete_1afd47bc-e8d5-464d-823a-7dba82fcf5c3)
AND
DB_Players(_Player)
AND
NOT DB_Defeated(_Player)
AND
DB_InRegion(_Player, (TRIGGER)S_HAV_TetherLeaveBounds_7ae97f8d-1809-4236-b8b2-d64ef1142f3c)
AND
QRY_SpeakerIsAvailable(_Player, 1)
AND
QRY_OnlyOnce("HAV_LiftingTheCurse_HalsinReturned")
THEN
DB_HAV_LiftingTheCurse_EmergeSpeaker((CHARACTER)_Player);
PROC_HAV_LiftingTheCurse_EmergeDialog((CHARACTER)_Player);
*/

PROC
PROC_HAV_LiftingTheCurse_EmergeDialog((CHARACTER)_Player)
AND
NOT QRY_StartDialogCustom((DIALOGRESOURCE)HAV_HalsinQuest_PortalEmerge_603c48af-cffb-2998-f1ee-6f6ece03a4f2, S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a, _Player, 0, 0, 0, 0)
THEN
PROC_GlobalClearFlagAndCache((FLAG)HAV_LiftingTheCurse_State_HalsinIsInRitual_3350ed7d-835e-4b5e-886e-3c544d27ce61);
Die((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6,DEATHTYPE.Disintegrate,(ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6,0,0,1.0);
PROC_Foop(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323);
PROC_Foop(S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a);
//PROC_Poof((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6);

//Catching if the EmergeDialog Failed
IF
DialogEnded(HAV_HalsinQuest_PortalEmerge_603c48af-cffb-2998-f1ee-6f6ece03a4f2, _)
AND
NOT DB_GlobalFlag(HAV_LiftingTheCurse_Event_HalsinClosesPortal_33aa334a-3127-4be1-ad94-518aa4f24ef4)
THEN
NOT DB_OnlyOnce("HAV_LiftingTheCurse_HalsinReturnedFallback");
TimerLaunch("HAV_Halsin_EmergeDialog_Fallback", 1000);

IF 
TimerFinished("HAV_Halsin_EmergeDialog_Fallback")
AND
DB_Players(_Player)
AND
NOT DB_Defeated(_Player)
AND
DB_InRegion(_Player, (TRIGGER)S_HAV_TetherLeaveBounds_7ae97f8d-1809-4236-b8b2-d64ef1142f3c)
AND
QRY_SpeakerIsAvailable(_Player, 1)
AND
QRY_OnlyOnce("HAV_LiftingTheCurse_HalsinReturnedFallback")
THEN
DB_HAV_LiftingTheCurse_EmergeSpeaker((CHARACTER)_Player);
PROC_HAV_LiftingTheCurse_EmergeDialog((CHARACTER)_Player);


PROC
PROC_StartDialog_AddExtraSpeakers((DIALOGRESOURCE)HAV_LiftingTheCurse_PortalEmerge_603c48af-cffb-2998-f1ee-6f6ece03a4f2, (INTEGER)_ID)
AND
DB_Players(_Player)
AND
NOT DB_HAV_LiftingTheCurse_EmergeSpeaker((CHARACTER)_Player)
AND
NOT DB_Defeated(_Player)
AND
DB_InRegion(_Player, (TRIGGER)S_HAV_TetherLeaveBounds_7ae97f8d-1809-4236-b8b2-d64ef1142f3c)
THEN
PROC_DialogAddSpeakingActor(_ID, _Player);

IF
TurnStarted(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323)
AND
DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_HalsinIsInRitual_3350ed7d-835e-4b5e-886e-3c544d27ce61)
AND
NOT DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_FinishedADs_5a7704ad-6b00-4a7f-ac2d-f4540dc00899)
THEN
PROC_TryStartAD((DIALOGRESOURCE)HAV_HalsinQuest_AD_HalsinCombat_2cd7375e-0cc0-e52e-d0c2-cb8643346f3e, S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323);

IF
FlagSet((FLAG)HAV_LiftingTheCurse_Event_HalsinClosesPortal_33aa334a-3127-4be1-ad94-518aa4f24ef4, _, _)
THEN
PROC_GlobalClearFlagAndCache((FLAG)HAV_LiftingTheCurse_State_HalsinIsInRitual_3350ed7d-835e-4b5e-886e-3c544d27ce61);
Die((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6,DEATHTYPE.Disintegrate,(ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6,0,0,1.0);
//PROC_Poof((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6);

IF
DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_PortalEnemiesDefeated_3836d472-c42f-438c-94bb-e2e83cdc6629)
AND
QRY_OnlyOnce("HAV_LiftingTheCurse_FinishedRitual")
THEN
PROC_State_Progress(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, "SCL_LiftingTheCurse", "SCL_LiftingTheCurse_State_HalsinAtPostPortal");


IF
DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_ChannelingComplete_1afd47bc-e8d5-464d-823a-7dba82fcf5c3)
AND
QRY_OnlyOnce("HAV_LiftingTheCurse_FinishedRitual")
THEN
PROC_State_Progress(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, "SCL_LiftingTheCurse", "SCL_LiftingTheCurse_State_HalsinAtPostPortal");

IF
DialogEnded((DIALOGRESOURCE)HAV_HalsinQuest_PortalEmerge_603c48af-cffb-2998-f1ee-6f6ece03a4f2, _)
AND
DB_GlobalFlag(HAV_LiftingTheCurse_Event_HalsinClosesPortal_33aa334a-3127-4be1-ad94-518aa4f24ef4)
THEN
PROC_Poof(S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a);
PROC_SetOnStage(S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a, 0);
PROC_DisappearOutOfSight(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, "Walk", 0, "HAV_LiftingTheCurse_Halsin_HalsinLeavesLakeSide");
NOT DB_Camp_BlockCamperPosAdjustment(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323);

//END_REGION

//REGION Try Enter Portal

IF
UseStarted(_Player, (ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6)
AND
DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_Event_PortalAppears_333d9c5e-501a-470d-9708-90b9d9b1ec01)
AND
NOT DB_GlobalFlag((FLAG)HAV_LiftingTheCurse_State_ChannelingComplete_1afd47bc-e8d5-464d-823a-7dba82fcf5c3)
AND
NOT DB_OnlyOnce("HAV_LiftingTheCurse_TriedEnteringPortal")
AND
QRY_StartDialogCustom_Fixed((DIALOGRESOURCE)HAV_HalsinQuest_PortalEntry_082e8b98-87c8-72ed-5d62-a3da28a76f8c, _Player, 1, 1, 0, 0)
AND
QRY_OnlyOnce("HAV_LiftingTheCurse_TriedEnteringPortal")
THEN
DB_NOOP(1);

IF
FlagSet((FLAG)HAV_LiftingTheCurse_RemovePortal_a114f810-3258-401e-8dfe-f5ee628604ec, _, _)
AND
IsOnStage(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, 1)
THEN
Die(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, DEATHTYPE.Necrotic, (ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, 0, 1, 1.0);

IF
FlagSet((FLAG)HAV_LiftingTheCurse_RemovePortal_a114f810-3258-401e-8dfe-f5ee628604ec, _, _)
THEN
PROC_HAV_LiftingTheCurse_DestroyPortal();

//END_REGION

//REGION Portal Damage Feedback

IF
AttackedBy((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6,_,_,_,_,_,_)
AND
HasActiveStatus ((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, "HAV_HALSINLAKESIDE_DAMAGEDPORTAL_FIRST", 0)
AND
GetHitpointsPercentage((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _Percentage)
AND
_Percentage < 70.0
AND
NOT DB_Defeated(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6)
THEN
ApplyStatus((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6,"HAV_HALSINLAKESIDE_DAMAGEDPORTAL_FIRST",-1.0,1,(ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6);


IF
AttackedBy((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6,_,_,_,_,_,_)
AND
HasActiveStatus ((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, "HAV_HALSINLAKESIDE_DAMAGEDPORTAL_SECOND", 0)
AND
GetHitpointsPercentage((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6, _Percentage)
AND
_Percentage < 40.0
AND
NOT DB_Defeated(S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6)
THEN
ApplyStatus((ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6,"HAV_HALSINLAKESIDE_DAMAGEDPORTAL_SECOND",-1.0,1,(ITEM)S_HAV_HalsinPortal_f2b5ad7f-013c-4c9e-a755-5fe9ff3287f6);

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
