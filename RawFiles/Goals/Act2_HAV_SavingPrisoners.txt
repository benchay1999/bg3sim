Version 1
SubGoalCombiner SGC_AND
INITSECTION
//Returned Prisoners
DB_HAV_SavingPrisoners_Returner((CHARACTER)S_GLO_ProdigySister_44d09b0b-0568-456c-b18d-559a0a3f981d, "Tiefling", (TRIGGER)S_HAV_ReunionSisterPos_4405f53c-83dd-40f0-8d6e-07a81b44220b, (DIALOGRESOURCE)HAV_ProdigyLament_Sister_40cc4552-2c6b-8ae3-a989-10b2919f11d2, (FLAG)HAV_SavingPrisoners_State_SisterReturned_54cbf85c-163b-d0f7-2519-93ff58c843aa, "HAV_ProdigySister");
DB_HAV_SavingPrisoners_Returner((CHARACTER)S_GLO_ProdigyBrother_d0187724-c0e2-4019-a01d-a03ee8153f1b, "Tiefling", S_HAV_ReunionBrotherPos_15db6e37-d07d-48c1-8fa6-11e65661df51, (DIALOGRESOURCE)HAV_ProdigyLament_Brother_a1778458-a7c8-c51d-2136-051ba860bc43, HAV_SavingPrisoners_State_BrotherReturned_2b696962-1d09-4cb6-b034-34d86d73dba0, "HAV_ProdigyBrother");
DB_HAV_SavingPrisoners_Returner((CHARACTER)S_DEN_Tiefling_010_23129d6c-8d39-4a4c-a4f6-cfc6637b597c, "Tiefling", S_HAV_FlirtyTieflingPos_e310f982-5765-49a2-afc9-32dddcb2018d, (DIALOGRESOURCE)HAV_SavingPrisoners_FlirtyTiefling_b6e8c50e-b3e0-0951-cb59-3c464cc97b59, HAV_SavingPrisoners_State_FlirtyReturned_67cf2fcc-e872-40c7-bcb5-7e4ccc915fa6, "HAV_FlirtyTiefling");
DB_HAV_SavingPrisoners_Returner((CHARACTER)S_GLO_YoungLover_01_dd9178e7-1e3f-43bc-8a38-666dfcac7d0c, "Tiefling", S_HAV_LoverPos_001_42ef3169-e468-43b2-b467-6fb55184e5f0, (DIALOGRESOURCE)HAV_LoneLover_Danis_58c1dcd0-0ec4-1fa4-324f-d20e32adaf45, HAV_SavingPrisoners_State_LoverReturned_b143075b-cc85-4d85-ba72-b24bb981b5e4, "HAV_YoungLover_001");

DB_HAV_SavingPrisoners_Returner((CHARACTER)S_GLO_GnomePrisoner_001_4fbd62e8-a486-471e-8a65-00966b058dd5, "Gnomes", S_HAV_GnomePrisonerPos_001_d51e670e-0ee1-438d-8bc7-e0a99f071fb6, (DIALOGRESOURCE)HAV_WrootRequest_Gnome001_394a7bf2-999c-bcb8-0a38-3072daa6192d, HAV_SavingPrisoners_State_Gnome001Returned_d9df5c72-356e-4649-8eda-3f844d084371, "HAV_GnomePrisoner_001");
DB_HAV_SavingPrisoners_Returner((CHARACTER)S_GLO_Wulbren_2d70edbf-1a70-4047-8099-48f3e75d93bf, "Gnomes", S_HAV_GnomePrisonerPos_002_91c1ffeb-cb7c-4a52-9bbd-0e9928aa8eb3, (DIALOGRESOURCE)HAV_WrootRequest_Wulbren_b425af3a-13d7-e049-45f4-cf5c60feb153, HAV_SavingPrisoners_State_WulbrenReturned_73a0c737-a7d1-9945-01d3-003fde6ed946, "HAV_Wulbren");
DB_HAV_SavingPrisoners_Returner((CHARACTER)S_GLO_GnomePrisoner_002_c9a7d921-bef0-4829-8792-d48c529daee5, "Gnomes", S_HAV_GnomePrisonerPos_003_a9a4049a-6f91-4326-b494-7a6e198173f2, (DIALOGRESOURCE)HAV_WrootRequest_Gnome002_8df53a19-28a0-b944-40b6-34fb3adebc3f, HAV_SavingPrisoners_State_Gnome002Returned_60ef28ec-9557-4c00-b3be-41982550e228, "HAV_GnomePrisoner_002");

DB_HAV_EscapeBoat_BoatParts((ITEM)S_HAV_EscapeBoat_bc17445c-5b7a-486c-a131-985e94d42f23);
DB_HAV_EscapeBoat_BoatParts(S_HAV_EscapeBoatSeats_b2cfa2f1-9dc8-44b0-8898-1c247c70c6b4);
DB_HAV_EscapeBoat_BoatParts(S_HAV_EscapeBoatTie_002_faeb6f78-1ed2-4a50-bafa-38ed44739e80);
DB_HAV_EscapeBoat_BoatParts(S_HAV_EscapeBoatTie_001_09764f6f-1a1e-4377-90a2-b6604cd48e40);

//REGION Prisoner Status

DB_SeenDeadNPCGlobalFlag((CHARACTER)S_GLO_ProdigySister_44d09b0b-0568-456c-b18d-559a0a3f981d, (FLAG)HAV_SavingPrisoners_State_SawSisterDead_bdd316cd-7849-92f5-7c81-7a5b0720455b);

//END_REGION

//REGION Wroot's Request

DB_Dialogs((CHARACTER)S_HAV_GnomeCat_33e545f6-9a0a-48ae-88b4-896a0c21c8ca, (DIALOGRESOURCE)HAV_Cat_f0f605ba-c88e-10ee-22a4-f46464c43e17);

//END_REGION

//REGION Lone Lover

DB_HAV_LoneLover_PromenadeEvents("HAV_LoneLover_AtFountain", (FLAG)HAV_LoneLover_State_PromenadeAtFountain_ea211a48-6664-2b47-deb3-0ec4f62a515d);
DB_HAV_LoneLover_PromenadeEvents("HAV_LoneLover_SideInn", (FLAG)HAV_LoneLover_State_PromenadeSideInn_753ec857-2ec7-12b3-a3ab-922a3d5236fe);
DB_HAV_LoneLover_PromenadeEvents("HAV_LoneLover_UnderBridge", (FLAG)HAV_LoneLover_State_PromenadeUnderBridge_ab6c598e-36c9-da96-d727-9e09e0cd0be7);
DB_HAV_LoneLover_PromenadeEvents("HAV_LoneLover_SidePath", (FLAG)HAV_LoneLover_State_PromenadeSidePath_78dad0d6-b25d-f5ee-1ec3-a75c65e5453c);
DB_HAV_LoneLover_PromenadeEvents("HAV_LoneLover_OnBridge", (FLAG)HAV_LoneLover_State_PromenadeOnBridge_4d8c7000-4451-e2f3-7ec0-aa4fec4abd42);

//END_REGION

//REGION Prodigy Lament

DB_HAV_ProdigyLament_ReunionCharacter((CHARACTER)S_GLO_ProdigySister_44d09b0b-0568-456c-b18d-559a0a3f981d);
DB_HAV_ProdigyLament_ReunionCharacter((CHARACTER)S_GLO_ProdigyBrother_d0187724-c0e2-4019-a01d-a03ee8153f1b);
DB_HAV_ProdigyLament_ReunionCharacter((CHARACTER)S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b);

SetOnStage(S_HAV_ProdigyHologram_0bf4cdfe-ea4f-4683-86f4-815853c56086, 0);

//END_REGION

//REGION Arrival Inspection

DB_FlagReactionAfterDialog(HAV_EnteringHaven_TrespasserSpotted_368a6fef-ece0-a1db-f1da-146dd32a6634, HAV_SavingPrisoners_Arrival_94148af7-5528-7890-be2d-c41bf919bd9f);

PROC_TriggerRegisterForPlayers(S_HAV_ArrivalArea_c7b0b988-3584-4f38-a387-66333bbefa69);

DB_AddCharactersInTriggerToDialog(HAV_SavingPrisoners_Arrival_94148af7-5528-7890-be2d-c41bf919bd9f, S_HAV_ArrivalArea_c7b0b988-3584-4f38-a387-66333bbefa69, 1, 0);

DB_DropMutingStatussesDialog(HAV_SavingPrisoners_Arrival_94148af7-5528-7890-be2d-c41bf919bd9f);

DB_Dialogs(S_HAV_ArrivalGuard_232f53d1-d291-4e2a-bdfd-d2fed2dba630, HAV_SavingPrisoners_ArrivalInspector_0f7d10e2-1d54-d96f-d7b1-70b8ef92a559);

SetOnStage(S_HAV_ArrivalGuard_232f53d1-d291-4e2a-bdfd-d2fed2dba630, 0);

DB_HAV_SavingPrisoners_InspectionPos((TRIGGER)S_HAV_ArrivalInspectionPos_001_7253799d-b02d-46e9-b295-e15a3df2e8a7);
DB_HAV_SavingPrisoners_InspectionPos((TRIGGER)S_HAV_ArrivalInspectionPos_002_0b25c9fe-b4f6-4286-91c2-ec23ed923c3b);
DB_HAV_SavingPrisoners_InspectionPos((TRIGGER)S_HAV_ArrivalInspectionPos_003_16f6f450-6a95-4370-9e2c-2902dc33349d);
DB_HAV_SavingPrisoners_InspectionPos((TRIGGER)S_HAV_ArrivalInspectionPos_004_d8a7604b-9eb2-41e5-a744-20c93271baac);
DB_HAV_SavingPrisoners_InspectionPos((TRIGGER)S_HAV_ArrivalInspectionPos_005_93fcd05e-7797-4736-8501-039ea3f0dc67);
DB_HAV_SavingPrisoners_InspectionPos((TRIGGER)S_HAV_ArrivalInspectionPos_006_f13b536d-8b98-40c4-be1c-d6fcddfffd8e);
DB_HAV_SavingPrisoners_InspectionPos((TRIGGER)S_HAV_ArrivalInspectionPos_007_c84ccc58-d996-4de4-a4a0-224b37d710a5);

DB_HAV_SavingPrisoners_InspectionAD((CHARACTER)S_GLO_Wulbren_2d70edbf-1a70-4047-8099-48f3e75d93bf, (DIALOGRESOURCE)HAV_SavingPrisoners_AD_WulbrenInspection_c3b38bc4-9d73-e508-1d9c-aae8e5ae0ae7);
DB_HAV_SavingPrisoners_InspectionAD((CHARACTER)S_GLO_GnomePrisoner_001_4fbd62e8-a486-471e-8a65-00966b058dd5, (DIALOGRESOURCE)HAV_SavingPrisoners_AD_Gnome01Inspection_e80263f0-aaca-04ef-28d2-77501240b730);
DB_HAV_SavingPrisoners_InspectionAD((CHARACTER)S_GLO_GnomePrisoner_002_c9a7d921-bef0-4829-8792-d48c529daee5, (DIALOGRESOURCE)HAV_SavingPrisoners_AD_Gnome02Inspection_205c3adf-016c-3e88-41ff-ca3fa86bab90);
DB_HAV_SavingPrisoners_InspectionAD((CHARACTER)S_GLO_ProdigySister_44d09b0b-0568-456c-b18d-559a0a3f981d, (DIALOGRESOURCE)HAV_SavingPrisoners_AD_SisterInspection_b8065a77-9f75-61ca-da9f-092ee745a00b);
DB_HAV_SavingPrisoners_InspectionAD((CHARACTER)S_GLO_ProdigyBrother_d0187724-c0e2-4019-a01d-a03ee8153f1b, (DIALOGRESOURCE)HAV_SavingPrisoners_AD_BrotherInspection_ac310849-c149-9ab9-8d28-c802dc76006a);
DB_HAV_SavingPrisoners_InspectionAD((CHARACTER)S_DEN_Tiefling_010_23129d6c-8d39-4a4c-a4f6-cfc6637b597c, (DIALOGRESOURCE)HAV_SavingPrisoners_AD_FlirtyInspection_609d4e78-d9ec-ce40-9088-d9f17aae6383);
DB_HAV_SavingPrisoners_InspectionAD((CHARACTER)S_GLO_YoungLover_01_dd9178e7-1e3f-43bc-8a38-666dfcac7d0c, (DIALOGRESOURCE)HAV_SavingPrisoners_AD_LoverInspection_e77c90b2-faa5-4d13-30e6-9706d066d081);

//END_REGION

//REGION Do not face

DB_DoNotFaceDialog(S_GLO_YoungLover_01_dd9178e7-1e3f-43bc-8a38-666dfcac7d0c, HAV_LoneLover_AD_Promenade_bb8fc5af-6ad0-f2af-752c-8a9376a088d1);
DB_DoNotFaceDialog(S_GLO_YoungLover_02_dcf597c0-36c5-4c81-957a-6fe9fca50a68, HAV_LoneLover_AD_Promenade_bb8fc5af-6ad0-f2af-752c-8a9376a088d1);
DB_DoNotFaceDialog(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, HAV_WrootRequest_AD_GnomeAndCat_61192811-7619-2951-269e-6b846cb6a461);
DB_DoNotFaceDialog(S_GLO_Wulbren_2d70edbf-1a70-4047-8099-48f3e75d93bf, HAV_WrootRequest_AD_ReturnedPrisoners_b571d8a6-556d-07d1-4a74-707311599249);
DB_DoNotFaceDialog(S_GLO_GnomePrisoner_001_4fbd62e8-a486-471e-8a65-00966b058dd5, HAV_WrootRequest_AD_ReturnedPrisoners_b571d8a6-556d-07d1-4a74-707311599249);
DB_DoNotFaceDialog(S_GLO_GnomePrisoner_002_c9a7d921-bef0-4829-8792-d48c529daee5, HAV_WrootRequest_AD_ReturnedPrisoners_b571d8a6-556d-07d1-4a74-707311599249);
DB_DoNotFaceDialog(S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, HAV_ProdigyLament_AD_AfterReunion_5dac2ca1-b7d5-78e0-7064-28e66e697d14);
DB_DoNotFaceDialog(S_GLO_ProdigyBrother_d0187724-c0e2-4019-a01d-a03ee8153f1b, HAV_ProdigyLament_AD_AfterReunion_5dac2ca1-b7d5-78e0-7064-28e66e697d14);
DB_DoNotFaceDialog(S_GLO_ProdigySister_44d09b0b-0568-456c-b18d-559a0a3f981d, HAV_ProdigyLament_AD_AfterReunion_5dac2ca1-b7d5-78e0-7064-28e66e697d14);
DB_DoNotFaceDialog(S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, HAV_ProdigyLament_AD_SiblingsReturn_3bbff999-507e-e4e2-251d-ac290f200768);
DB_DoNotFaceDialog(S_GLO_ProdigyBrother_d0187724-c0e2-4019-a01d-a03ee8153f1b, HAV_ProdigyLament_AD_SiblingsReturn_3bbff999-507e-e4e2-251d-ac290f200768);
DB_DoNotFaceDialog(S_GLO_ProdigySister_44d09b0b-0568-456c-b18d-559a0a3f981d, HAV_ProdigyLament_AD_SiblingsReturn_3bbff999-507e-e4e2-251d-ac290f200768);
//END_REGION

DB_PermaDefeatedFlag(S_GLO_Wulbren_2d70edbf-1a70-4047-8099-48f3e75d93bf,GLO_Wulbren_State_PermaDefeated_1e67b51c-a13d-484b-9bf4-65fbc178b152);
DB_PermaDefeatedFlag(S_GLO_GnomePrisoner_001_4fbd62e8-a486-471e-8a65-00966b058dd5,(FLAG)MOO_GnomePrisoner001_State_PermaDefeated_a75d7674-a909-4252-be15-673b4eeda75c);
DB_PermaDefeatedFlag(S_GLO_GnomePrisoner_002_c9a7d921-bef0-4829-8792-d48c529daee5,(FLAG)MOO_GnomePrisoner002_State_PermaDefeated_fc079d0c-2c74-437c-aa00-fb358d4658a2);

PROC_HAV_SavingPrisoners_Init();
KBSECTION
//REGION Init

IF
DB_HAV_EscapeBoat_BoatParts(_BoatPart)
THEN
SetOnStage(_BoatPart, 0);

//END_REGION

//REGION Wroot's Request

//Only move Barcus when players enter Hav.
IF
DB_GlobalFlag(HAV_WrootRequest_State_ReadyForHAV_ebc6fb5b-32ac-41bf-bfc7-5fb5a7d24298)
AND
DB_InRegion(_Player, S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab)
AND
NOT DB_PermaDefeated(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976)
AND
DB_Players(_Player)
THEN
ClearFlag(HAV_WrootRequest_State_ReadyForHAV_ebc6fb5b-32ac-41bf-bfc7-5fb5a7d24298, NULL_00000000-0000-0000-0000-000000000000, 0);
PROC_SpotPlayers_StopSpotting(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, "FOR_UnfortunateGnome");	//Debug
RemoveHarmfulStatuses(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976);
PROC_HAV_WrootRequest_BarcusToHaven();

PROC
PROC_HAV_WrootRequest_BarcusToHaven()
AND
DB_InCamp(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976)
THEN
SetFlag(HAV_WrootRequest_Event_BarcusCameFromCamp_c95fcfe6-d9f5-3e7f-6bf2-03fbb75afcea, NULL_00000000-0000-0000-0000-000000000000, 0);

PROC
PROC_HAV_WrootRequest_BarcusToHaven()
AND
NOT DB_InCamp(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976)
THEN
TeleportTo(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, S_HAV_UnfortunateGnomePos_b9b53c7f-2201-4f82-bcf0-6295f334b8c2, "HAV_SavingPrisoners_BarcusToHaven");
SetOnStage(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, 1);
PROC_SetCustomTradeTreasure(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, "HAV_UnfortunateGnome_Trade");

PROC
PROC_HAV_WrootRequest_BarcusToHaven()
AND
DB_InCamp(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976)
AND
QRY_Camp_PlayerNotInCamp()
THEN
TeleportTo(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, S_HAV_UnfortunateGnomePos_b9b53c7f-2201-4f82-bcf0-6295f334b8c2, "HAV_SavingPrisoners_BarcusToHaven");
SetOnStage(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, 1);
PROC_SetCustomTradeTreasure(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, "HAV_UnfortunateGnome_Trade");

PROC
PROC_HAV_WrootRequest_BarcusToHaven()
AND
DB_InCamp(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976)
AND
NOT QRY_Camp_PlayerNotInCamp()
THEN
PROC_DisappearOutOfSight(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, "Run", 1, "HAV_UnfortunateGnome_LeftCamp");

IF
EntityEvent(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, "HAV_UnfortunateGnome_LeftCamp")
THEN
TeleportTo(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, S_HAV_UnfortunateGnomePos_b9b53c7f-2201-4f82-bcf0-6295f334b8c2, "HAV_SavingPrisoners_BarcusToHaven");
SetOnStage(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, 1);
PROC_SetCustomTradeTreasure(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, "HAV_UnfortunateGnome_Trade");

IF
EntityEvent(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, "HAV_SavingPrisoners_BarcusToHaven")
THEN
PROC_RemoveAllDialogEntriesForSpeaker((CHARACTER)S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976);
SetFaction(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, Act2_HAV_Gnomes_075f180d-1260-4d52-a4f8-af545277cbc5);
DB_Dialogs((CHARACTER)S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, (DIALOGRESOURCE)HAV_WrootRequest_UnfortunateGnome_0ae97fbf-5572-a4f5-794d-c4095179e43c);
SetFlag(GLO_UnfortunateGnome_State_InHaven_a5673d70-d182-4dee-a93b-a2d7715856dc, NULL_00000000-0000-0000-0000-000000000000, 0);

IF
DB_GlobalFlag(HAV_SavingPrisoners_State_WulbrenReturned_73a0c737-a7d1-9945-01d3-003fde6ed946)
AND
DB_GlobalFlag(HAV_SavingPrisoners_State_Gnome001Returned_d9df5c72-356e-4649-8eda-3f844d084371)
AND
DB_GlobalFlag(HAV_SavingPrisoners_State_Gnome002Returned_60ef28ec-9557-4c00-b3be-41982550e228)
THEN
SetFlag(HAV_SavingPrisoners_State_AllGnomesReturned_fbce4b87-50f6-195d-18f7-8a811cae524e, NULL_00000000-0000-0000-0000-000000000000, 0);

//Reunion dialog --TODO: Clarify states under which this can happen.
QRY
QRY_SelectCustomDialog_AfterGenerics(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, _Player)
AND
DB_GlobalFlag(HAV_WrootRequest_State_WithWulbren_35bd10c9-fb1f-4670-8069-191282c0da34)
AND
NOT DB_GlobalFlag(HAV_WrootRequest_Event_FriendTalkOver_ae967954-99fd-5e27-e083-98884ba8f9f0)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, _Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_GLO_Wulbren_2d70edbf-1a70-4047-8099-48f3e75d93bf, _Player)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)HAV_WrootRequest_Reunion_1daf117e-b3e7-a4b3-bf94-17f3f4257f08, S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, S_GLO_Wulbren_2d70edbf-1a70-4047-8099-48f3e75d93bf, _Player)
THEN
DB_NOOP(1);

QRY
QRY_SelectCustomDialog_AfterGenerics(S_GLO_Wulbren_2d70edbf-1a70-4047-8099-48f3e75d93bf, _Player)
AND
DB_GlobalFlag(HAV_WrootRequest_State_WithWulbren_35bd10c9-fb1f-4670-8069-191282c0da34)
AND
NOT DB_GlobalFlag(HAV_WrootRequest_Event_FriendTalkOver_ae967954-99fd-5e27-e083-98884ba8f9f0)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, _Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_GLO_Wulbren_2d70edbf-1a70-4047-8099-48f3e75d93bf, _Player)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)HAV_WrootRequest_Reunion_1daf117e-b3e7-a4b3-bf94-17f3f4257f08, S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976, S_GLO_Wulbren_2d70edbf-1a70-4047-8099-48f3e75d93bf, _Player)
THEN
DB_NOOP(1);

//END_REGION

//REGION Return of prisoners

PROC
PROC_HAV_SavingPrisoners_ReturnPendingEscapees()
AND
NOT DB_GlobalFlag(HAV_SavingPrisoners_State_CheckForReturnCompletion_e57e5f98-503d-4a58-9737-2000bba7ed8b)
THEN
SetFlag(HAV_SavingPrisoners_State_CheckForReturnCompletion_e57e5f98-503d-4a58-9737-2000bba7ed8b, NULL_00000000-0000-0000-0000-000000000000, 0);

PROC
PROC_MOO_Jailbreak_BoatBringsNearbyPlayers()
AND
NOT DB_GlobalFlag((FLAG)HAV_EnteringHaven_State_GainedAccess_07c776da-353a-9050-e9be-c42d51a99412)
THEN
PROC_RemoveDBTrespassTrigger(S_HAV_EnteringHaven_ForbiddenArea_307bf271-9af2-4572-967a-e87edf41bf54);

PROC
PROC_MOO_Jailbreak_BoatBringsNearbyPlayers()
AND
DB_PartyMembers(_PartyMember)
AND
IsInTrigger(_PartyMember, S_MOO_GetawayBoatPlayerArea_02e5e8aa-6987-4988-971f-315d0b489ec3, 1)
THEN
PROC_CRIME_TryRemoveFugitiveStatus(_PartyMember);
TeleportTo(_PartyMember, S_HAV_BoatArrivalPoint_ce01de09-0bdf-4b53-9851-f8df90ec4984, "");

PROC
PROC_MOO_Jailbreak_BoatBringsNearbyPlayers()
THEN
PROC_HAV_SavingPrisoners_ReturnPendingEscapees();

PROC
PROC_MOO_Jailbreak_BoatBringsNearbyPlayers()
AND
QRY_OnlyOnce("HAV_SavingPrisoners_ShowBoat")
AND
DB_HAV_EscapeBoat_BoatParts(_BoatPart)
THEN
SetOnStage(_BoatPart, 1);

PROC
PROC_HAV_SavingPrisoners_ReturnPendingEscapees()
AND
DB_HAV_SavingPrisoners_PendingReturn(_Character, _ReturnType)
THEN
PROC_HAV_SavingPrisoners_CharacterReturns(_Character, _ReturnType);

//Player alone on boat
PROC
PROC_HAV_SavingPrisoners_ReturnPendingEscapees()
AND
NOT DB_HAV_SavingPrisoners_InspectionReady(1)
AND
NOT DB_GlobalFlag(HAV_Siege_State_NoProtection_2da0dbf1-88ca-4cab-9c8f-ee8921822958)
AND
NOT DB_GlobalFlag(HAV_EnteringHaven_State_GainedAccess_07c776da-353a-9050-e9be-c42d51a99412)
THEN
PROC_HAV_SavingPrisoners_StartArrivalDialog();

PROC
PROC_HAV_SavingPrisoners_ReturnPendingEscapees()
AND
DB_HAV_SavingPrisoners_InspectionReady(1)
AND
NOT DB_GlobalFlag(HAV_Siege_State_NoProtection_2da0dbf1-88ca-4cab-9c8f-ee8921822958)
THEN
PROC_HAV_SavingPrisoners_StartArrivalDialog();
NOT DB_HAV_SavingPrisoners_InspectionReady(1);

IF
DB_HAV_SavingPrisoners_PendingReturn(_Character, _ReturnType)
AND
DB_PermaDefeated(_Character)
THEN
NOT DB_HAV_SavingPrisoners_PendingReturn(_Character, _ReturnType);

PROC
PROC_HAV_SavingPrisoners_CharacterReturns((CHARACTER)_Character, "Road")
AND
DB_HAV_SavingPrisoners_Returner(_Character, _, _, _, _, _Config)
THEN
NOT DB_HAV_SavingPrisoners_PendingReturn(_Character, "Road");
DB_HAV_Siege_NPCs(_Character);
PROC_AppearOutOfSightTo(_Character, S_HAV_PrisonerReturnPos_a1ad7248-0e06-4a87-994d-aab8dba88675, S_HAV_PrisonerReturnPos_a1ad7248-0e06-4a87-994d-aab8dba88675, "HAV_SavingPrisoners_AppearedAtHaven_Road");
DB_HAV_SavingPrisoners_Returning(_Character);
PROC_SetAnubisConfig(_Character, _Config);

IF
EntityEvent(_Character, "HAV_SavingPrisoners_AppearedAtHaven_Road")
THEN
PROC_HAV_SavingPrisoners_CharacterGoesToHavenPosition((CHARACTER)_Character);


PROC
PROC_HAV_SavingPrisoners_CharacterReturns((CHARACTER)_Character, "Boat")
AND
QRY_OnlyOnce("HAV_SavingPrisoners_ShowBoat")
AND
DB_HAV_EscapeBoat_BoatParts(_BoatPart)
THEN
SetOnStage(_BoatPart, 1);

PROC
PROC_HAV_SavingPrisoners_CharacterReturns((CHARACTER)_Character, "Boat")
AND
QRY_OnlyOnce("HAV_SavingPrisoners_ReadyInspection")
THEN
DB_HAV_SavingPrisoners_InspectionReady(1);
SetOnStage(S_HAV_ArrivalGuard_232f53d1-d291-4e2a-bdfd-d2fed2dba630, 1);
NOT DB_OffStage(S_HAV_ArrivalGuard_232f53d1-d291-4e2a-bdfd-d2fed2dba630);

//Player that instigated boat departure handles the boat arrival
PROC
PROC_HAV_SavingPrisoners_StartArrivalDialog()
AND
DB_MOO_Jailbreak_BoatPlayer(_Player)
AND
IsInTrigger(_Player, S_HAV_ArrivalArea_c7b0b988-3584-4f38-a387-66333bbefa69, 1)
AND
QRY_StartDialogCustom(HAV_SavingPrisoners_Arrival_94148af7-5528-7890-be2d-c41bf919bd9f, S_HAV_ArrivalGuard_232f53d1-d291-4e2a-bdfd-d2fed2dba630, _Player, 0, 0, 0, 1) 
THEN
DB_OnlyOnce("HAV_SavingPrisoners_ArrivalDialog");

//Just in case, pick another player if dialog couldn't start
PROC
PROC_HAV_SavingPrisoners_StartArrivalDialog()
AND
DB_Players(_Player)
AND
NOT DB_OnlyOnce("HAV_SavingPrisoners_ArrivalDialog")
AND
IsInTrigger(_Player, S_HAV_ArrivalArea_c7b0b988-3584-4f38-a387-66333bbefa69, 1)
AND
QRY_StartDialogCustom(HAV_SavingPrisoners_Arrival_94148af7-5528-7890-be2d-c41bf919bd9f, S_HAV_ArrivalGuard_232f53d1-d291-4e2a-bdfd-d2fed2dba630, _Player, 0, 0, 0, 1) 
THEN
DB_OnlyOnce("HAV_SavingPrisoners_ArrivalDialog");

PROC
PROC_HAV_SavingPrisoners_StartArrivalDialog()
AND
NOT DB_OnlyOnce("HAV_SavingPrisoners_ArrivalDialog")
THEN
SetFlag(HAV_SavingPrisoners_Event_InspectionFinished_4cc39f58-ad6d-e898-0a20-953e2cd61abc, NULL_00000000-0000-0000-0000-000000000000, 0);
PROC_TryStartAD(HAV_SavingPrisoners_AD_Inspection_959205e3-79a0-8320-3c96-cdbd0c866b51, S_HAV_ArrivalGuard_232f53d1-d291-4e2a-bdfd-d2fed2dba630);

IF
DB_OnlyOnce("HAV_SavingPrisoners_ArrivalDialog")
AND
DB_InRegion(_Player, S_HAV_ArrivalArea_c7b0b988-3584-4f38-a387-66333bbefa69)
AND
NOT DB_HAV_SavingPrisoners_CheckInspectionEnd(1)
THEN
DB_HAV_SavingPrisoners_CheckInspectionEnd(1);

IF
DB_HAV_SavingPrisoners_CheckInspectionEnd(1)
AND
NOT DB_InRegion(_, S_HAV_ArrivalArea_c7b0b988-3584-4f38-a387-66333bbefa69)
AND
NOT DB_GlobalFlag(HAV_SavingPrisoners_Event_InspectionFinished_4cc39f58-ad6d-e898-0a20-953e2cd61abc)
THEN
SetFlag(HAV_SavingPrisoners_Event_InspectionFinished_4cc39f58-ad6d-e898-0a20-953e2cd61abc, NULL_00000000-0000-0000-0000-000000000000, 0);

PROC
PROC_LongRest()
AND
DB_HAV_SavingPrisoners_CheckInspectionEnd(1)
AND
DB_OnlyOnce("HAV_SavingPrisoners_ArrivalDialog")
AND
NOT DB_GlobalFlag(HAV_SavingPrisoners_Event_InspectionFinished_4cc39f58-ad6d-e898-0a20-953e2cd61abc)
THEN
SetFlag(HAV_SavingPrisoners_Event_InspectionFinished_4cc39f58-ad6d-e898-0a20-953e2cd61abc, NULL_00000000-0000-0000-0000-000000000000, 0);


PROC
PROC_StartDialog_AddExtraSpeakers(HAV_SavingPrisoners_Arrival_94148af7-5528-7890-be2d-c41bf919bd9f, _ID)
AND
DB_HAV_SavingPrisoners_InspectedPrisoner(_Character)
THEN
PROC_DialogAddSpeakingActor(_ID, _Character);

PROC
PROC_HAV_SavingPrisoners_CharacterReturns((CHARACTER)_Character, "Boat")
AND
DB_HAV_SavingPrisoners_Returner(_Character, _, _, _, _, _Config)
THEN
NOT DB_HAV_SavingPrisoners_PendingReturn(_Character, "Boat");
DB_HAV_SavingPrisoners_InspectedPrisoner(_Character);
DB_HAV_Siege_NPCs(_Character);
SetOnStage(_Character, 1);
TeleportTo(_Character, S_HAV_BoatArrivalPoint_ce01de09-0bdf-4b53-9851-f8df90ec4984, "");
NOT DB_OffStage(_Character);
DB_HAV_SavingPrisoners_Returning(_Character);
PROC_SetAnubisConfig(_Character, _Config);

//After appearing at the designate appearance point, returning characters go to their designated Haven spots.
IF
EntityEvent(_Character, "HAV_SavingPrisoners_AppearedAtHaven_Boat")
AND
DB_HAV_SavingPrisoners_InspectionPos(_Pos)
AND
NOT DB_HAV_SavingPrisoners_AssignedInspectionPos(_Character)
THEN
DB_HAV_SavingPrisoners_AssignedInspectionPos(_Character);
NOT DB_HAV_SavingPrisoners_InspectionPos(_Pos);
SetDualEntityEvent(_Character, _Pos, "HAV_SavingPrisoners_SetInspectionPos");

IF
DialogEnded(HAV_SavingPrisoners_Arrival_94148af7-5528-7890-be2d-c41bf919bd9f, _)
THEN
DB_HAV_SavingPrisoners_ArrivalSceneOver(1);
ObjectTimerLaunch(S_HAV_ArrivalGuard_232f53d1-d291-4e2a-bdfd-d2fed2dba630, "HAV_SavingPrisoners_ForceInspectionTimer", 10000);

IF
ObjectTimerFinished(S_HAV_ArrivalGuard_232f53d1-d291-4e2a-bdfd-d2fed2dba630, "HAV_SavingPrisoners_ForceInspectionTimer")
AND
NOT DB_GlobalFlag(HAV_SavingPrisoners_Event_InspectionReady_71a80c02-6254-48b8-bab0-a5e0b5022120)
THEN
SetFlag(HAV_SavingPrisoners_Event_InspectionReady_71a80c02-6254-48b8-bab0-a5e0b5022120);

IF
DialogStarted(HAV_SavingPrisoners_Arrival_94148af7-5528-7890-be2d-c41bf919bd9f, _ID)
THEN
DB_HAV_Arrival_TrespassGoToCheckpoint(_ID);

IF
DB_HAV_Arrival_TrespassGoToCheckpoint(_ID)
AND
DB_DialogPlayers(_ID, _Player, _)
THEN
DB_HAV_Arrival_Trespassers(_Player);
DB_CharacterCrimeDisabled((CHARACTER)_Player, "HAV_EnteringHaven_ForbiddenArea");

PROC
PROC_FlagReactionAfterDialog((CHARACTER)_Character, (FLAG)HAV_EnteringHaven_TrespasserSpotted_368a6fef-ece0-a1db-f1da-146dd32a6634, (DIALOGRESOURCE)HAV_SavingPrisoners_Arrival_94148af7-5528-7890-be2d-c41bf919bd9f)
AND
DB_DialogName(HAV_EnteringHaven_TadpoleCheckpoint_e51408cc-4f7a-5a5f-52e8-caf1359c1d6c, _ID)
AND
DB_HAV_Arrival_Trespassers(_Player)
THEN
CharacterStopCrime((CHARACTER)_Player, "HAV_EnteringHaven_ForbiddenArea", NULL_00000000-0000-0000-0000-000000000000);
TeleportTo(_Player, S_SCL_HarperBridgePoint_000_b3096b76-99c8-4b51-8820-224d78e003c2, "", 1, 1, 1 ,1 ,1);
PROC_DialogAddListeningActor(_ID, _Player);

PROC
PROC_FlagReactionAfterDialog((CHARACTER)_Player, (FLAG)HAV_EnteringHaven_TrespasserSpotted_368a6fef-ece0-a1db-f1da-146dd32a6634, (DIALOGRESOURCE)HAV_SavingPrisoners_Arrival_94148af7-5528-7890-be2d-c41bf919bd9f)
AND
NOT DB_DialogName(HAV_EnteringHaven_TadpoleCheckpoint_e51408cc-4f7a-5a5f-52e8-caf1359c1d6c, _)
THEN
CharacterStopCrime(_Player, "HAV_EnteringHaven_ForbiddenArea", NULL_00000000-0000-0000-0000-000000000000);
TeleportTo(_Player, S_SCL_HarperBridgePoint_000_b3096b76-99c8-4b51-8820-224d78e003c2, "", 1, 1, 1, 1, 1);
PROC_HAV_EnteringHaven_TriggerDialog(_Player, S_HAV_ArrivalGuard_232f53d1-d291-4e2a-bdfd-d2fed2dba630);

PROC
PROC_FlagReactionAfterDialog((CHARACTER)_Character, (FLAG)HAV_EnteringHaven_TrespasserSpotted_368a6fef-ece0-a1db-f1da-146dd32a6634, (DIALOGRESOURCE)HAV_SavingPrisoners_Arrival_94148af7-5528-7890-be2d-c41bf919bd9f)
AND
NOT DB_DialogName(HAV_EnteringHaven_TadpoleCheckpoint_e51408cc-4f7a-5a5f-52e8-caf1359c1d6c, _)
AND
DB_HAV_Arrival_Trespassers(_Player)
THEN
CharacterStopCrime((CHARACTER)_Player, "HAV_EnteringHaven_ForbiddenArea", NULL_00000000-0000-0000-0000-000000000000);
TeleportTo(_Player, S_SCL_HarperBridgePoint_000_b3096b76-99c8-4b51-8820-224d78e003c2, "");

PROC
PROC_FlagReactionAfterDialog((CHARACTER)_Character, (FLAG)HAV_EnteringHaven_TrespasserSpotted_368a6fef-ece0-a1db-f1da-146dd32a6634, (DIALOGRESOURCE)HAV_SavingPrisoners_Arrival_94148af7-5528-7890-be2d-c41bf919bd9f)
AND
NOT DB_DialogName(HAV_EnteringHaven_TadpoleCheckpoint_e51408cc-4f7a-5a5f-52e8-caf1359c1d6c, _)
AND
DB_MOO_MintharaFate_DelayEscapedDialogForHAV(_Player)
THEN
TeleportTo(S_GOB_DrowCommander_25721313-0c15-4935-8176-9f134385451b, S_SCL_HarperBridgePoint_000_b3096b76-99c8-4b51-8820-224d78e003c2, "");

PROC
PROC_FlagReactionAfterDialog((CHARACTER)_Character, (FLAG)HAV_EnteringHaven_TrespasserSpotted_368a6fef-ece0-a1db-f1da-146dd32a6634, (DIALOGRESOURCE)HAV_SavingPrisoners_Arrival_94148af7-5528-7890-be2d-c41bf919bd9f)
THEN
SetFlag(HAV_SavingPrisoners_Event_InspectionFinished_4cc39f58-ad6d-e898-0a20-953e2cd61abc, NULL_00000000-0000-0000-0000-000000000000, 0);
NOT DB_HAV_Arrival_TrespassGoToCheckpoint(1);

IF
DialogEnded(HAV_SavingPrisoners_Arrival_94148af7-5528-7890-be2d-c41bf919bd9f, _)
AND
NOT QRY_StartDialog(HAV_SavingPrisoners_AD_Inspection_959205e3-79a0-8320-3c96-cdbd0c866b51, S_HAV_ArrivalGuard_232f53d1-d291-4e2a-bdfd-d2fed2dba630)
THEN
SetFlag(HAV_SavingPrisoners_Event_InspectionFinished_4cc39f58-ad6d-e898-0a20-953e2cd61abc, NULL_00000000-0000-0000-0000-000000000000, 0);

IF
FlagSet(HAV_SavingPrisoners_Event_InspectionFinished_4cc39f58-ad6d-e898-0a20-953e2cd61abc, _, _)
THEN
PROC_TryStartAD(HAV_SavingPrisoners_AD_Inspection_959205e3-79a0-8320-3c96-cdbd0c866b51, S_HAV_ArrivalGuard_232f53d1-d291-4e2a-bdfd-d2fed2dba630);
SetFlag(HAV_SavingPrisoners_Event_PrisonersLeaveInspection_6582e781-ba50-4dcc-b88c-cf0bc4c144b1, NULL_00000000-0000-0000-0000-000000000000, 0);

PROC
PROC_StateSet_Defeated(S_HAV_ArrivalGuard_232f53d1-d291-4e2a-bdfd-d2fed2dba630)
THEN
SetFlag(HAV_SavingPrisoners_Event_PrisonersLeaveInspection_6582e781-ba50-4dcc-b88c-cf0bc4c144b1, NULL_00000000-0000-0000-0000-000000000000, 0);

IF
FlagSet(HAV_SavingPrisoners_Event_PrisonersLeaveInspection_6582e781-ba50-4dcc-b88c-cf0bc4c144b1, _, _)
THEN
PROC_HAV_SavingPrisoners_InspectionOver();

PROC
PROC_HAV_SavingPrisoners_InspectionOver()
THEN
PROC_DisappearOutOfSightTo((CHARACTER)S_HAV_ArrivalGuard_232f53d1-d291-4e2a-bdfd-d2fed2dba630, S_HAV_InspectorDisappearPos_57a318f0-10ec-46c7-bb72-d34e6c495d28, "Walk", 0, "");

PROC
PROC_HAV_SavingPrisoners_InspectionOver()
AND
DB_HAV_SavingPrisoners_InspectedPrisoner(_Character)
THEN
PROC_HAV_SavingPrisoners_CharacterGoesToHavenPosition(_Character);

//Writer request: block dialog with ADs during inspection:
QRY
QRY_SelectCustomDialog(_Character, _Player)
AND
DB_HAV_SavingPrisoners_InspectedPrisoner((CHARACTER)_Character)
AND
DB_HAV_SavingPrisoners_InspectionAD((CHARACTER)_Character, _AD)
AND
IsInTrigger(_Character, S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, 1)
THEN
DB_SelectedDialog(_AD, (CHARACTER)_Character);

PROC
PROC_HAV_SavingPrisoners_CharacterGoesToHavenPosition((CHARACTER)_Character)
AND
DB_HAV_SavingPrisoners_Returner(_Character, "Tiefling", _Pos, _Dialog, _, _)
THEN
NOT DB_HAV_SavingPrisoners_InspectedPrisoner(_Character);
PROC_RemoveAllDialogEntriesForSpeaker(_Character);
SetFaction(_Character, Act2_HAV_Tieflings_f4ff1850-c9fc-4ee5-80e7-fa6b2a958d39);
DB_Dialogs(_Character, _Dialog);
PROC_CharacterMoveTo(_Character, _Pos, "Run", "HAV_SavingPrisoners_AtHavenPos");


PROC
PROC_HAV_SavingPrisoners_CharacterGoesToHavenPosition((CHARACTER)_Character)
AND
DB_HAV_SavingPrisoners_Returner(_Character, "Gnomes", _Pos, _Dialog, _, _)
THEN
NOT DB_HAV_SavingPrisoners_InspectedPrisoner(_Character);
PROC_RemoveAllDialogEntriesForSpeaker(_Character);
SetFaction(_Character, Act2_HAV_Gnomes_075f180d-1260-4d52-a4f8-af545277cbc5);
DB_Dialogs(_Character, _Dialog);
PROC_CharacterMoveTo(_Character, _Pos, "Run", "HAV_SavingPrisoners_AtHavenPos");

IF
EntityEvent((CHARACTER)_Character, "HAV_SavingPrisoners_AtHavenPos")
THEN
PROC_HAV_SavingPrisoners_ArrivedAtHavenPosition(_Character);

PROC
PROC_HAV_SavingPrisoners_ArrivedAtHavenPosition((CHARACTER)S_GLO_ProdigyBrother_d0187724-c0e2-4019-a01d-a03ee8153f1b)
THEN
DB_HAV_ProdigyLament_SiblingReturned(1);

PROC
PROC_HAV_SavingPrisoners_ArrivedAtHavenPosition((CHARACTER)S_GLO_ProdigySister_44d09b0b-0568-456c-b18d-559a0a3f981d)
THEN
DB_HAV_ProdigyLament_SiblingReturned(1);

//Return for the character is complete, mark it by setting the corresponding flag.
PROC
PROC_HAV_SavingPrisoners_ArrivedAtHavenPosition((CHARACTER)_Character)
AND
DB_HAV_SavingPrisoners_Returner(_Character, _, _, _, _ReturnFlag, _)
THEN
SetFlag(_ReturnFlag, NULL_00000000-0000-0000-0000-000000000000, 0);
NOT DB_HAV_SavingPrisoners_Returning(_Character);

PROC
PROC_CharacterMoveToCancelled(_Character,"HAV_SavingPrisoners_AtHavenPos")
AND
DB_HAV_SavingPrisoners_Returning(_Character)
THEN
NOT DB_HAV_SavingPrisoners_Returning(_Character);

//No more characters are pending in the return phase. Mark the return to Haven as completed.
IF
DB_GlobalFlag(HAV_SavingPrisoners_State_CheckForReturnCompletion_e57e5f98-503d-4a58-9737-2000bba7ed8b)
AND
NOT DB_MOO_Jailbreak_Started(1)
AND
NOT DB_HAV_SavingPrisoners_Returning(_)
AND
NOT DB_GlobalFlag(HAV_SavingPrisoners_State_ReturnCompleted_3d6f8326-f5cc-c124-b810-3f03fe1d6b62)
THEN
SetFlag(HAV_SavingPrisoners_State_ReturnCompleted_3d6f8326-f5cc-c124-b810-3f03fe1d6b62, NULL_00000000-0000-0000-0000-000000000000, 0);

//Clear a return flag if a character gets defeated AFTER they have managed to return.
PROC
PROC_StateSet_PermaDefeated(_Character)
AND
DB_HAV_SavingPrisoners_Returner((CHARACTER)_Character, _, _, _, _ReturnFlag, _)
AND
DB_GlobalFlag(_ReturnFlag)
THEN
ClearFlag(_ReturnFlag, NULL_00000000-0000-0000-0000-000000000000, 0);

//END_REGION

//REGION Lone Lover

PROC
PROC_HAV_SavingPrisoners_Init()
AND
DB_PermaDefeated(S_GLO_YoungLover_01_dd9178e7-1e3f-43bc-8a38-666dfcac7d0c)
THEN
SetFlag((FLAG)HAV_LoneLover_State_DanisDiedBeforeHaven_346a1ec6-f2d9-0817-a546-9ffa76b829aa,NULL_00000000-0000-0000-0000-000000000000,0);

PROC
PROC_HAV_SavingPrisoners_Init()
AND
DB_PermaDefeated(S_GLO_YoungLover_02_dcf597c0-36c5-4c81-957a-6fe9fca50a68)
THEN
SetFlag((FLAG)HAV_LoneLover_State_BexDiedBeforeHaven_aaa67679-6e86-d6df-1423-b27a3ae4ffe2,NULL_00000000-0000-0000-0000-000000000000,0);

IF
DialogStarted(HAV_LoneLover_Bex_4dafa77e-af5b-589c-c53d-7bb07700110c, _)
AND
NOT DB_GlobalFlag(HAV_LoneLover_Knows_BexInHaven_cd227c55-e027-40cf-82f1-a752f778b5ff)
THEN
SetFlag(HAV_LoneLover_Knows_BexInHaven_cd227c55-e027-40cf-82f1-a752f778b5ff, NULL_00000000-0000-0000-0000-000000000000, 0);


//Reunion dialog --TODO: Clarify states under which this can happen.
QRY
QRY_SelectCustomDialog_AfterGenerics(S_GLO_YoungLover_02_dcf597c0-36c5-4c81-957a-6fe9fca50a68, _Player)
AND
DB_GlobalFlag(HAV_SavingPrisoners_State_LoverReturned_b143075b-cc85-4d85-ba72-b24bb981b5e4)
AND
NOT DB_GlobalFlag(HAV_LoneLover_Event_ReunionDone_16fc4bc2-2eab-47a6-b83f-ff60a6aeede7)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_GLO_YoungLover_01_dd9178e7-1e3f-43bc-8a38-666dfcac7d0c, _Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_GLO_YoungLover_02_dcf597c0-36c5-4c81-957a-6fe9fca50a68, _Player)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)HAV_LoneLover_Reunited_347d2502-b980-dafa-7a9e-b931d58d257e, S_GLO_YoungLover_02_dcf597c0-36c5-4c81-957a-6fe9fca50a68, S_GLO_YoungLover_01_dd9178e7-1e3f-43bc-8a38-666dfcac7d0c, _Player)
THEN
DB_NOOP(1);

QRY
QRY_SelectCustomDialog_AfterGenerics(S_GLO_YoungLover_01_dd9178e7-1e3f-43bc-8a38-666dfcac7d0c, _Player)
AND
DB_GlobalFlag(HAV_SavingPrisoners_State_LoverReturned_b143075b-cc85-4d85-ba72-b24bb981b5e4)
AND
NOT DB_GlobalFlag(HAV_LoneLover_Event_ReunionDone_16fc4bc2-2eab-47a6-b83f-ff60a6aeede7)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_GLO_YoungLover_01_dd9178e7-1e3f-43bc-8a38-666dfcac7d0c, _Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_GLO_YoungLover_02_dcf597c0-36c5-4c81-957a-6fe9fca50a68, _Player)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)HAV_LoneLover_Reunited_347d2502-b980-dafa-7a9e-b931d58d257e, S_GLO_YoungLover_02_dcf597c0-36c5-4c81-957a-6fe9fca50a68, S_GLO_YoungLover_01_dd9178e7-1e3f-43bc-8a38-666dfcac7d0c, _Player)
THEN
DB_NOOP(1);


IF
EntityEvent(_Lover, _Event)
AND
DB_HAV_LoneLover_PromenadeEvents(_Event ,_Flag)
THEN
SetFlag(_Flag, NULL_00000000-0000-0000-0000-000000000000, 0);

IF
FlagSet(_Flag, _, _)
AND
DB_HAV_LoneLover_PromenadeEvents(_Event, _Flag)
AND
DB_HAV_LoneLover_PromenadeEvents(_Event, _OtherFlag)
AND
_Flag != _OtherFlag
AND
DB_GlobalFlag(_OtherFlag)
THEN
ClearFlag(_OtherFlag, NULL_00000000-0000-0000-0000-000000000000, 0);

IF
FlagSet(HAV_LoneLover_Event_GiveCookies_c5065617-08c1-a514-c509-2d00f3038b42, _, _ID)
AND
DB_DialogPlayers(_ID, _Player, 1)
THEN
ToInventory(S_HAV_BexCookies_b7658c26-68c5-4715-8637-6812c3e178be, _Player, 10, 1, 1);
//END_REGION

//REGION Alfira's Tale

PROC
PROC_HAV_TieflingSurvivors_Init()
AND
NOT DB_PermaDefeated(S_DEN_Trainer_02025646-347a-4235-aef7-e46b7c94b435)
THEN
SetFlag(HAV_AlfiraTale_State_AsharakWasAlive_2b9e1048-f5c9-9e1f-086c-df2e58722eb4);

PROC
PROC_HAV_TieflingSurvivors_Init()
AND
NOT DB_PermaDefeated(S_DEN_Tiefling_008_03b6fae4-d250-4ce9-9a01-19cb427deb17)
THEN
SetFlag(HAV_AlfiraTale_State_IkaronWasAlive_44372354-22b1-a639-8281-8f29596ae0a3);

//Reunion dialog --TODO: Clarify states under which this can happen.
QRY
QRY_SelectCustomDialog_AfterGenerics(S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c, _Player)
AND
DB_GlobalFlag(HAV_SavingPrisoners_State_FlirtyReturned_67cf2fcc-e872-40c7-bcb5-7e4ccc915fa6)
AND
NOT DB_GlobalFlag(HAV_AlfiraTale_State_ReunionDone_5159d798-4bb5-ce40-b215-1b5dc7bbfbbb)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_DEN_Tiefling_010_23129d6c-8d39-4a4c-a4f6-cfc6637b597c, _Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c, _Player)
AND
QRY_StartDialog_Fixed(HAV_SavingPrisoners_BardAndFlirtyReunion_5f510493-83b8-feef-dbbb-2f918a093730, S_DEN_Tiefling_010_23129d6c-8d39-4a4c-a4f6-cfc6637b597c, S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c, _Player)
THEN
DB_NOOP(1);

QRY
QRY_SelectCustomDialog_AfterGenerics(S_DEN_Tiefling_010_23129d6c-8d39-4a4c-a4f6-cfc6637b597c, _Player)
AND
DB_GlobalFlag(HAV_SavingPrisoners_State_FlirtyReturned_67cf2fcc-e872-40c7-bcb5-7e4ccc915fa6)
AND
NOT DB_GlobalFlag(HAV_AlfiraTale_State_ReunionDone_5159d798-4bb5-ce40-b215-1b5dc7bbfbbb)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_DEN_Tiefling_010_23129d6c-8d39-4a4c-a4f6-cfc6637b597c, _Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c, _Player)
AND
QRY_StartDialog_Fixed(HAV_SavingPrisoners_BardAndFlirtyReunion_5f510493-83b8-feef-dbbb-2f918a093730, S_DEN_Tiefling_010_23129d6c-8d39-4a4c-a4f6-cfc6637b597c, S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c, _Player)
THEN
DB_NOOP(1);

//At least one tiefling returned
PROC
PROC_HAV_SavingPrisoners_ArrivedAtHavenPosition(_Character)
AND
DB_HAV_SavingPrisoners_Returner(_Character, "Tiefling", _, _, _, _)
AND
NOT DB_GlobalFlag(HAV_SavingPrisoners_State_TieflingsReturned_eef04b79-3cbe-a552-2748-3f1998670c5f)
THEN
SetFlag(HAV_SavingPrisoners_State_TieflingsReturned_eef04b79-3cbe-a552-2748-3f1998670c5f, NULL_00000000-0000-0000-0000-000000000000, 0);

//All tieflings have returned
IF
DB_GlobalFlag(HAV_SavingPrisoners_State_SisterReturned_54cbf85c-163b-d0f7-2519-93ff58c843aa)
AND
DB_GlobalFlag(HAV_SavingPrisoners_State_BrotherReturned_2b696962-1d09-4cb6-b034-34d86d73dba0)
AND
DB_GlobalFlag(HAV_SavingPrisoners_State_LoverReturned_b143075b-cc85-4d85-ba72-b24bb981b5e4)
AND
DB_GlobalFlag(HAV_SavingPrisoners_State_FlirtyReturned_67cf2fcc-e872-40c7-bcb5-7e4ccc915fa6)
THEN
SetFlag(HAV_SavingPrisoners_State_AllTieflingsReturned_3f819a53-2fb8-c9e4-2d9d-1b3280812de7, NULL_00000000-0000-0000-0000-000000000000, 0);

//END_REGION

//REGION Prodigy's Lament
IF
EntityEvent(S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, "HAV_TieflingSurvivors_AtHaven")
THEN
SetFlag(GLO_Prodigy_State_InHaven_fe9b4527-9d66-45db-8e04-490fd51b7be5, NULL_00000000-0000-0000-0000-000000000000, 0);
DB_SceneManager(S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, "HAV_ProdigyLament_ProdigyAndKids");
DB_SceneManager(S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f, "HAV_ProdigyLament_ProdigyAndKids");
DB_SceneManager(S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387, "HAV_ProdigyLament_ProdigyAndKids");
DB_SceneAllowAllDisturbances("HAV_ProdigyLament_ProdigyAndKids");
DB_SceneTriggerManager("HAV_ProdigyLament_ProdigyAndKids", S_HAV_ProdigyKidsSceneArea_a7e77e36-1ea7-4d14-9e09-6ae62a0c2120);

QRY
QRY_SelectCustomDialog_AfterGenerics((CHARACTER)_Character, _Player)
AND
DB_HAV_TieflingSurvivors_RolanDialogParticipants(_Character)
AND
DB_GlobalFlag(HAV_Mood_State_Protected_2ba16a80-e4f8-40b5-aaf5-37791bd76542)
AND
NOT DB_GlobalFlag(HAV_ProdigyLament_Event_Intro_95cd5597-af9a-1fac-aada-e857199dae88)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, _Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f, _Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387, _Player)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)HAV_ProdigyLament_Rolan_ce19b6a1-2055-90e7-cc50-a141f424054d, (CHARACTER)S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, _Player, (CHARACTER)S_DEN_Trainee_001_1f839ad2-2bcb-47d2-8de9-db7a1fb5d67f, (CHARACTER)S_DEN_Trainee_002_bd7f31a6-98e8-46cd-97be-4347fa45d387)
THEN
DB_NOOP(1);

IF
FlagSet(HAV_ProdigyLament_Event_Intro_95cd5597-af9a-1fac-aada-e857199dae88, _, _)
THEN
PROC_SceneOver("HAV_ProdigyLament_ProdigyAndKids");

PROC
PROC_SceneInterrupted(_, _, "HAV_ProdigyLament_ProdigyAndKids", _Interrupt)
AND
DB_HAV_TieflingSurvivors_KidIntroInterruptions(_Interrupt)
THEN
PROC_SceneOver("HAV_ProdigyLament_ProdigyAndKids");

PROC
PROC_SceneOver("HAV_ProdigyLament_ProdigyAndKids")
AND
DB_DialogNPCs(_ID, S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, _)
AND
DB_DialogName(HAV_ProdigyLament_AD_RolanAndKid_9f9410fb-8591-51e1-cdc7-5a3304bf014f, _ID)
THEN
PROC_ForceStopDialog(S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b);

PROC
PROC_SceneOver("HAV_ProdigyLament_ProdigyAndKids")
AND
NOT DB_GlobalFlag(HAV_ProdigyLament_Event_Intro_95cd5597-af9a-1fac-aada-e857199dae88)
THEN
SetFlag(HAV_ProdigyLament_Event_Intro_95cd5597-af9a-1fac-aada-e857199dae88, NULL_00000000-0000-0000-0000-000000000000);

PROC
PROC_SceneOver("HAV_ProdigyLament_ProdigyAndKids")
THEN
DB_HAV_ProdigyLament_RolanCanLeave(1);

//TODO: implement an edge case - Shadow Siege starts and Rolan dies before 4way dialog happens, need to change the kids' behavior and dialogs

PROC
PROC_HAV_SavingPrisoners_CharacterReturns(S_GLO_ProdigyBrother_d0187724-c0e2-4019-a01d-a03ee8153f1b, _)
THEN
PROC_SceneOver("HAV_ProdigyLament_ProdigyAndKids");

PROC
PROC_HAV_SavingPrisoners_CharacterReturns(S_GLO_ProdigySister_44d09b0b-0568-456c-b18d-559a0a3f981d, _)
THEN
PROC_SceneOver("HAV_ProdigyLament_ProdigyAndKids");

//Reunion dialog --TODO: Clarify states under which this can happen.
QRY
QRY_SelectCustomDialog_AfterGenerics(_ProdigyCharacter, _Player)
AND
DB_HAV_ProdigyLament_ReunionCharacter((CHARACTER)_ProdigyCharacter)
AND
DB_HAV_ProdigyLament_SiblingReturned(1)
AND
DB_GlobalFlag(GLO_Prodigy_State_InHaven_fe9b4527-9d66-45db-8e04-490fd51b7be5)
AND
DB_GlobalFlag(HAV_SavingPrisoners_State_ReturnCompleted_3d6f8326-f5cc-c124-b810-3f03fe1d6b62)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, _Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(_ProdigyCharacter, _Player)
AND
NOT DB_GlobalFlag(HAV_ProdigyLament_State_ReunionDone_d71cdc91-884a-c64d-81b7-81a3b43c5270)
THEN
PROC_MOO_ProdigyLament_CheckReunion((CHARACTER)_Player);

//Only sister
PROC
PROC_MOO_ProdigyLament_CheckReunion((CHARACTER)_Player)
AND
DB_GlobalFlag(HAV_SavingPrisoners_State_SisterReturned_54cbf85c-163b-d0f7-2519-93ff58c843aa)
AND
NOT DB_GlobalFlag(HAV_SavingPrisoners_State_BrotherReturned_2b696962-1d09-4cb6-b034-34d86d73dba0)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, _Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_GLO_ProdigySister_44d09b0b-0568-456c-b18d-559a0a3f981d, _Player)
THEN
DB_SelectedDialog(HAV_ProdigyLament_Reunion_c7a2b462-56aa-3e03-f774-a746d4979596, S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, S_GLO_ProdigySister_44d09b0b-0568-456c-b18d-559a0a3f981d, NULL_00000000-0000-0000-0000-000000000000, _Player);

//Only brother
PROC
PROC_MOO_ProdigyLament_CheckReunion((CHARACTER)_Player)
AND
NOT DB_GlobalFlag(HAV_SavingPrisoners_State_SisterReturned_54cbf85c-163b-d0f7-2519-93ff58c843aa)
AND
DB_GlobalFlag(HAV_SavingPrisoners_State_BrotherReturned_2b696962-1d09-4cb6-b034-34d86d73dba0)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, _Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_GLO_ProdigyBrother_d0187724-c0e2-4019-a01d-a03ee8153f1b, _Player)
THEN
DB_SelectedDialog(HAV_ProdigyLament_Reunion_c7a2b462-56aa-3e03-f774-a746d4979596, S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, NULL_00000000-0000-0000-0000-000000000000, S_GLO_ProdigyBrother_d0187724-c0e2-4019-a01d-a03ee8153f1b, _Player);

//Both siblings
PROC
PROC_MOO_ProdigyLament_CheckReunion((CHARACTER)_Player)
AND
DB_GlobalFlag(HAV_SavingPrisoners_State_SisterReturned_54cbf85c-163b-d0f7-2519-93ff58c843aa)
AND
DB_GlobalFlag(HAV_SavingPrisoners_State_BrotherReturned_2b696962-1d09-4cb6-b034-34d86d73dba0)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, _Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_GLO_ProdigySister_44d09b0b-0568-456c-b18d-559a0a3f981d, _Player)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_GLO_ProdigyBrother_d0187724-c0e2-4019-a01d-a03ee8153f1b, _Player)
THEN
DB_SelectedDialog(HAV_ProdigyLament_Reunion_c7a2b462-56aa-3e03-f774-a746d4979596, S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, S_GLO_ProdigySister_44d09b0b-0568-456c-b18d-559a0a3f981d, S_GLO_ProdigyBrother_d0187724-c0e2-4019-a01d-a03ee8153f1b, _Player);

IF
FlagSet(HAV_SavingPrisoners_State_ReturnCompleted_3d6f8326-f5cc-c124-b810-3f03fe1d6b62, NULL_00000000-0000-0000-0000-000000000000, 0)
AND
NOT DB_GlobalFlag(HAV_ProdigyLament_Event_Intro_95cd5597-af9a-1fac-aada-e857199dae88)
THEN
SetFlag(HAV_ProdigyLament_Event_Intro_95cd5597-af9a-1fac-aada-e857199dae88, NULL_00000000-0000-0000-0000-000000000000, 0);

IF
FlagSet(HAV_SavingPrisoners_State_ReturnCompleted_3d6f8326-f5cc-c124-b810-3f03fe1d6b62, NULL_00000000-0000-0000-0000-000000000000, 0)
AND
DB_GlobalFlag(GLO_Prodigy_State_InHaven_fe9b4527-9d66-45db-8e04-490fd51b7be5)
AND
DB_HAV_ProdigyLament_SiblingReturned(1)
THEN
SetEntityEvent(S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, "HAV_ProdigyLament_SiblingsReturn");

IF
DB_GlobalFlag(HAV_SavingPrisoners_State_ReturnCompleted_3d6f8326-f5cc-c124-b810-3f03fe1d6b62)
AND
NOT DB_GlobalFlag(GLO_Prodigy_State_InHaven_fe9b4527-9d66-45db-8e04-490fd51b7be5)
AND
DB_HAV_ProdigyLament_SiblingReturned(1)
THEN
SetFlag(HAV_ProdigyLament_State_WaitingForRolan_898c0413-8b0d-4470-b1a6-6dfb9dbdb9f0, NULL_00000000-0000-0000-0000-000000000000, 0);

IF
TextEvent("rolanreturnstohaven")
THEN
SetEntityEvent(S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, "SCL_ProdigyLament_RolanReturnsToHaven");

IF
EntityEvent(S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, "HAV_ProdigyLament_ReturnedToHaven")
THEN
SetFlag(GLO_Prodigy_State_InHaven_fe9b4527-9d66-45db-8e04-490fd51b7be5, NULL_00000000-0000-0000-0000-000000000000, 0);
SetFlag(HAV_ProdigyLament_State_RolanReturned_56f79f7c-37be-11c5-e238-222839d27f2b, NULL_00000000-0000-0000-0000-000000000000, 0);
PROC_Poof(S_HAV_ProdigyHologram_0bf4cdfe-ea4f-4683-86f4-815853c56086);

IF
FlagSet(HAV_ProdigyLament_State_RolanReturned_56f79f7c-37be-11c5-e238-222839d27f2b, NULL_00000000-0000-0000-0000-000000000000, 0)
AND
DB_GlobalFlag(HAV_SavingPrisoners_State_ReturnCompleted_3d6f8326-f5cc-c124-b810-3f03fe1d6b62)
THEN
SetEntityEvent(S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b, "HAV_ProdigyLament_RolanReturns");

//Playing VoiceBark after talking to Rolan's Hologram
//and adding Map Marker for when Rolan is out in the Shadows
IF
UseStarted(_Player, S_HAV_ProdigyHologram_0bf4cdfe-ea4f-4683-86f4-815853c56086)
AND
QRY_StartDialog_Fixed(HAV_ProdigyLament_Hologram_78553027-ff48-dc1d-30bb-ef9339f93ce5, S_HAV_RolanHologramSpeaker_1fb677fe-8937-4aba-aab1-3811f239e7d5, _Player)
THEN
DB_HAV_ProdigyLament_HologramUser((CHARACTER)_Player);

IF
DialogEnded(HAV_ProdigyLament_Hologram_78553027-ff48-dc1d-30bb-ef9339f93ce5, _)
AND
QRY_OnlyOnce("HAV_SavingPrisoners_HeardRolansHologramMessage")
AND
DB_HAV_ProdigyLament_HologramUser(_Player)
THEN
ObjectTimerLaunch(_Player, "HAV_SavingPrisoners_HologramTimer", 500, 1);
ShowMapMarker(_Player, "HAV_SavingPrisonders_ProdigyFendingOffShadows", 1);

IF
ObjectTimerFinished(_Player, "HAV_SavingPrisoners_HologramTimer")
THEN
StartVoiceBark((VOICEBARKRESOURCE)HAV_ProdigyLament_VB_HologramReaction_72b8b3b1-8990-855c-f38a-a98f054ee20b , (CHARACTER)_Player);
NOT DB_HAV_ProdigyLament_HologramUser(_Player);

PROC
PROC_StateSet_Defeated(S_GLO_ProdigySister_44d09b0b-0568-456c-b18d-559a0a3f981d)
THEN
SetEntityEvent(S_GLO_ProdigySister_44d09b0b-0568-456c-b18d-559a0a3f981d, "HAV_ProdigyLament_Defeated");

PROC
PROC_StateSet_Defeated(S_GLO_ProdigyBrother_d0187724-c0e2-4019-a01d-a03ee8153f1b)
THEN
SetEntityEvent(S_GLO_ProdigyBrother_d0187724-c0e2-4019-a01d-a03ee8153f1b, "HAV_ProdigyLament_Defeated");

//END_REGION

//REGION Debug

IF
TextEvent("killwulbren")
THEN
Die(S_GLO_Wulbren_2d70edbf-1a70-4047-8099-48f3e75d93bf, DEATHTYPE.DoT, 1);
ClearFlag(HAV_SavingPrisoners_State_WulbrenReturned_73a0c737-a7d1-9945-01d3-003fde6ed946, NULL_00000000-0000-0000-0000-000000000000, 0);

IF
TextEvent("killcal")
THEN
Die(S_GLO_ProdigyBrother_d0187724-c0e2-4019-a01d-a03ee8153f1b, DEATHTYPE.DoT, 1);


IF
TextEvent("killlia")
THEN
Die(S_GLO_ProdigySister_44d09b0b-0568-456c-b18d-559a0a3f981d, DEATHTYPE.DoT, 1);

IF
TextEvent("prisonersreturn")
THEN
PROC_HAV_DEBUG_SavingPrisoners_EveryoneEscapes();
SetFlag(HAV_SavingPrisoners_State_ReturnReady_9b2d498d-5aad-40fa-a0df-d2a76a3b0979);

PROC
PROC_HAV_DEBUG_SavingPrisoners_EveryoneEscapes()
AND
DB_MOO_Jailbreak_Prisoner(_Prisoner, _)
AND
NOT DB_Defeated(_Prisoner)
THEN 
PROC_MOO_Jailbreak_ClearPrisoner(_Prisoner, "Boat", 0);
SetOnStage(_Prisoner, 0);

PROC
PROC_HAV_DEBUG_SavingPrisoners_EveryoneEscapes()
THEN
TimerLaunch("HAV_DEBUG_SavingPrisoners_EveryoneEscapes", 2000);

IF
TimerFinished("HAV_DEBUG_SavingPrisoners_EveryoneEscapes")
THEN
PROC_HAV_SavingPrisoners_ReturnPendingEscapees();

IF
FlagSet((FLAG)Debug_Act2Setup_State_BarcusRecruited_b7a697e4-265c-00b2-f6d1-818d0bdd5074, _, _)
THEN
PROC_HAV_WrootRequest_BarcusToHaven();

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
