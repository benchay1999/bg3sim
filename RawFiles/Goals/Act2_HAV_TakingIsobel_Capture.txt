Version 1
SubGoalCombiner SGC_AND
INITSECTION
PROC_HAV_TakingIsobel_CaptureSetup();


KBSECTION
//REGION Debug

IF
FlagSet((FLAG)Debug_HAV_SpyCapture_1f79bdf6-d3e5-0281-330d-4eba54a9199f, _, _)
THEN
TeleportTo(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6, S_HAV_SpyTransfoPoint_39b78c4b-a75e-42bb-bddf-b676a131792e);
PROC_State_Progress(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6, "HAV_FlamingSpy", "Spy_DefaultBehaviour");
PROC_State_Progress(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_LakeToRoomTransition");
SetFlag(HAV_EnteringHaven_State_GainedAccess_07c776da-353a-9050-e9be-c42d51a99412);
TeleportTo(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, S_HAV_IsobelPrayerPoint_ad61fb61-370f-4b1b-a40e-85ba0f1f1a8f);

IF
TextEvent("spycombat")
THEN
PROC_State_Progress(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6,"HAV_FlamingSpy", "Spy_CaptureStarted");
PROC_HAV_TakingIsobel_CaptureStarting();
TeleportTo(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, S_HAV_IsobelPrayerPoint_ad61fb61-370f-4b1b-a40e-85ba0f1f1a8f);
TeleportTo(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6, S_HAV_SpyInPoint_546e38ea-db44-4b12-bbb7-d486dca08124);
SetFlag(HAV_TakingIsobel_State_SidedWithSpy_5eb745b2-5043-2f36-4d3a-ee3f09ed7a82);
PROC_HAV_TakingIsobel_InitSpyCombat(1);

IF
TextEvent("playercapture")
THEN
SetFlag(MOO_FirstFloor_DukeDouble_Quest_Accepted_10d8d681-e2a3-c472-e7d9-08718f2eaea2);
SetFlag(HAV_TakingIsobel_Knows_ZevlorCanHelp_39f6c83f-fcec-e306-c500-e93dc13dd70d);

IF
TextEvent("koisobel")
THEN
ApplyStatus(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "KNOCKED_OUT", -1.0, 1, NULL_00000000-0000-0000-0000-000000000000);

//END_REGION

//REGION Setup


PROC
PROC_HAV_TakingIsobel_CaptureSetup()
AND
DB_GlobalFlag(SCE_Debrief_State_Setup_0ebc6b35-0f07-4a73-b6b8-9782ecbcaccf)
THEN
GoalCompleted;

PROC
PROC_HAV_TakingIsobel_CaptureSetup()
AND
DB_GlobalFlag(MOO_Assault_State_InProgress_0f3a8f5d-7402-4220-bebb-d4b21d3db08d)
THEN
GoalCompleted;


PROC
PROC_HAV_TakingIsobel_CaptureSetup()
AND
NOT DB_GlobalFlag(SCE_Debrief_State_Setup_0ebc6b35-0f07-4a73-b6b8-9782ecbcaccf)
AND
DB_Defeated((CHARACTER)S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6)
THEN
GoalCompleted;


PROC
PROC_HAV_TakingIsobel_CaptureSetup()
AND
NOT DB_GlobalFlag(SCE_Debrief_State_Setup_0ebc6b35-0f07-4a73-b6b8-9782ecbcaccf)
AND
NOT DB_Defeated((CHARACTER)S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6)
AND
NOT QRY_State_IsBeforeState(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_FollowUp_Protection")
THEN
GoalCompleted;

PROC
PROC_HAV_TakingIsobel_CaptureSetup()
AND
NOT DB_GlobalFlag(SCE_Debrief_State_Setup_0ebc6b35-0f07-4a73-b6b8-9782ecbcaccf)
AND
NOT DB_Defeated((CHARACTER)S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6)
AND
NOT DB_GlobalFlag(MOO_Assault_State_InProgress_0f3a8f5d-7402-4220-bebb-d4b21d3db08d)
AND
QRY_State_IsBeforeState(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_FollowUp_Protection")
THEN
PROC_HAV_TakingIsobel_CaptureSetup(1);



//REGION Dialog
PROC
PROC_HAV_TakingIsobel_CaptureSetup(1)
THEN

DB_DialogBlockTradeButton(HAV_TakingIsobel_SpySuccess_5092e0db-55bd-71a6-2746-3cd5b359cde6);
DB_DialogBlockTradeButton(HAV_TakingIsobel_GhoulSuccess_37afc763-fdfc-819b-fbe6-a490fa4a99c5);

DB_HAV_Isobel_FollowupBoxes((TRIGGER)S_HAV_IsobelRoomFollowUpBox_Balcony_ea450666-303d-4cde-b1f6-162fdc423064);
DB_HAV_Isobel_FollowupBoxes(S_HAV_IsobelRoomFollowUpBox_Room_1ddc9945-3187-4eee-a2f3-8e2c06e5c06f);
DB_HAV_Isobel_FollowupBoxes(S_HAV_IsobelRoomFollowUpBox_Balcony_2_512d08ab-d66e-4a3d-bb6d-e009de0c22b7);
DB_HAV_Isobel_FollowupBoxes(S_HAV_IsobelRoomFollowUpBox_Downstairs_1_6760074f-7d8c-4cce-9469-cc51ffbb9698);
DB_HAV_Isobel_FollowupBoxes(S_HAV_IsobelRoomFollowUpBox_Downstairs_2_0305c965-984d-414e-a1b4-d863f6fbb1c5);
DB_HAV_Isobel_FollowupBoxes(S_HAV_IsobelRoomFollowUpBox_Downstairs_3_a0612a7d-21d8-4072-919c-dad14800d736);
DB_HAV_Isobel_FollowupBoxes(S_HAV_IsobelRoomFollowUpBox_InnerBalcony_1_24455bb4-ca09-4837-8cbf-39a252fad2b6);
DB_HAV_Isobel_FollowupBoxes(S_HAV_IsobelRoomFollowUpBox_InnerBalcony_2_0877dacb-fb40-44ea-b6cc-f00509d04470);
DB_HAV_Isobel_FollowupBoxes(S_HAV_IsobelRoomFollowUpBox_InnerBalcony_3_b7d3666e-16c9-4d77-a505-50f3a19693ff);
DB_HAV_Isobel_FollowupBoxes(S_HAV_IsobelRoomFollowUpBox_InnerBalcony_4_ee50c120-9403-4738-bfe6-6e02f10279a8);

//END_REGION


//REGION Kidnapping

PROC
PROC_HAV_TakingIsobel_CaptureSetup(1)
THEN
PROC_TriggerRegisterForPlayers(S_HAV_SpyPlayerInBox_edb52dcc-651b-46c5-afd0-e6f3de7beba2);

//END_REGION

//REGION Combat setup

PROC
PROC_HAV_TakingIsobel_CaptureSetup(1)
AND
DB_HAV_TakingIsobel_FlyingGhouls(_Ghoul)
THEN
DB_GLO_DefeatCounter(_Ghoul, "HAV_TakingIsobel_InnGhoulCombat");

PROC
PROC_HAV_TakingIsobel_CaptureSetup(1)
THEN
// Triggers for starting cinematic dialog
SetCombatGroupID(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6, "bed268ba-0c09-f87f-85c6-4b315443178a");
SetCombatGroupID(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211 , "bed268ba-0c09-f87f-85c6-4b315443178a");

//END_REGION



//END_REGION

//REGION Spy Capture - SpyCapture starts

IF
FlagSet(HAV_TakingIsobel_Event_SpawnGhouls_e8d0fd32-5eff-4101-bb7c-6d2e1acd4ba4, _, _Id)
THEN
PROC_HAV_TakingIsobel_SpyCaptureStarted(_Id);

PROC
PROC_HAV_TakingIsobel_SpyCaptureStarted((INTEGER)_Id)
AND
DB_HAV_SpyCapture_PrioPlayer(_Player)
THEN
NOT DB_HAV_SpyCapture_PrioPlayer(_Player);

PROC
PROC_HAV_TakingIsobel_SpyCaptureStarted((INTEGER)_Id)
AND
DB_DialogPlayers(_Id, _Player, _)
THEN
DB_HAV_SpyCapture_PrioPlayer((CHARACTER)_Player);
PROC_HAV_TakingIsobel_CaptureStarting();
PROC_State_Progress(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_TakingIsobel_Capture", "SpyCombat_Confrontation");

PROC
PROC_HAV_TakingIsobel_CaptureStarting()
THEN
PROC_State_Progress(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6,"HAV_FlamingSpy", "Spy_CaptureStarted");
SetFlag(HAV_TakingIsobel_State_SpyConfrontationStarted_44c18469-4650-4941-bf5f-9fe1f01df6e0);
PROC_HAV_TakingIsobel_CaptureTeleport();
PROC_HAV_TakingIsobel_SpyTransform();
Open(S_HAV_IsobelBalconyDoor_33b6ec88-e2ed-4074-af35-76aaaf66f113); //TODO: make more elegant.

PROC
PROC_HAV_TakingIsobel_CaptureTeleport()
AND
IsOnStage(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6, 0)
AND
QRY_HAV_TakingIsobel_CapturePoint()
AND
DB_QRYRTN_HAV_TakingIsobel_CapturePoint(_Point)
THEN
TeleportTo(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6, _Point);
PROC_HAV_TakingIsobel_SetSpyOnStage(1);

QRY
QRY_HAV_TakingIsobel_CapturePoint()
AND
IsInTrigger(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, S_HAV_IsobelRoomFollowUpBox_Room_1ddc9945-3187-4eee-a2f3-8e2c06e5c06f, 1)
AND
IsInTrigger(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6, S_HAV_IsobelRoomFollowUpBox_Balcony_ea450666-303d-4cde-b1f6-162fdc423064, 1)
THEN
DB_QRYRTN_HAV_TakingIsobel_CapturePoint(S_HAV_SpyInPoint_546e38ea-db44-4b12-bbb7-d486dca08124);

//TODO: Add better arrival point on balcony.

QRY
QRY_HAV_TakingIsobel_CapturePoint()
AND
NOT DB_QRYRTN_HAV_TakingIsobel_CapturePoint(_)
THEN
DB_QRYRTN_HAV_TakingIsobel_CapturePoint(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211);
//END_REGION

//REGION Spy Capture - Split outcome

IF
DialogEnded(HAV_TakingIsobel_SpyCapture_1b698557-76c0-1166-6415-91fadb358985, _)
AND
GetFlag(HAV_TakingIsobel_State_SidedWithSpy_5eb745b2-5043-2f36-4d3a-ee3f09ed7a82, NULL_00000000-0000-0000-0000-000000000000, _SidedWithSpy)
THEN
PROC_HAV_TakingIsobel_InitSpyCombat(_SidedWithSpy);

PROC
PROC_HAV_TakingIsobel_InitSpyCombat((INTEGER)_)
THEN
SetFlag(HAV_TakingIsobel_Event_SpyCombatStarted_b0930689-e539-424e-bc92-e4a37fbee7d5);
SetFlag((FLAG)HAV_TakingIsobel_Event_AbductionAttempted_24605994-7bed-3f92-36f0-1472d32ab2ea);
PROC_SelfHealing_Disable(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211);
PROC_SelfHealing_Disable(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6);
SetAiHint(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, (TAG)ACT2_HAV_AI_HINT_ISOBELFLEE_3f261d03-8132-4f0d-a954-689a040f9f58); //TODO: set the tag only if she's in her room / use a different tag for the KO
SetTag(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, (TAG)ACT2_HAV_AI_HINT_ISOBELFLEE_3f261d03-8132-4f0d-a954-689a040f9f58);
AddPreferredAiTargetTag(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6, (TAG)ACT2_HAV_AI_HINT_ISOBELFLEE_3f261d03-8132-4f0d-a954-689a040f9f58);

PROC
PROC_HAV_TakingIsobel_InitSpyCombat(0) //Default: siding with Isobel against the spy
THEN
PROC_State_Progress(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_TakingIsobel_Capture", "SpyCombat_SideWithIsobel");

PROC
PROC_HAV_TakingIsobel_InitSpyCombat(1)
THEN
PROC_State_Progress(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_TakingIsobel_Capture", "SpyCombat_SideWithSpy");

//END_REGION

//REGION Sided with Isobel
PROC
PROC_State_Changed(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_TakingIsobel_Capture", "SpyCombat_SideWithIsobel")
THEN
DB_GLO_DefeatCounter(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6, "HAV_TakingIsobel_DefaultSpyCombat");
SetFaction(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6, (FACTION)Act2_HAV_FlamingSpy_Hostile_caa2c8a9-67d2-479b-91ad-f460e66cfd9b);


//END_REGION

//REGION Sided with spy
PROC
PROC_State_Changed(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_TakingIsobel_Capture", "SpyCombat_SideWithSpy")
THEN
PROC_HAV_SetupFactionsSpyAlly();


PROC
PROC_State_Changed(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_TakingIsobel_Capture", "SpyCombat_SideWithSpy")
AND
DB_GLO_DefeatCounter(_NPC, "HAV_TakingIsobel_DefaultSpyCombat")
THEN
NOT DB_GLO_DefeatCounter(_NPC, "HAV_TakingIsobel_DefaultSpyCombat");

IF
TurnStarted(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6)
AND
DB_HAV_SpyCapture_RemindKO(1)
AND
NOT DB_CantTalk_IgnoreCombat(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6)
AND
NOT DB_CantMove(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6)
AND
DB_State_Current(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_TakingIsobel_Capture", "SpyCombat_SideWithSpy")
AND
QRY_StartDialog(HAV_TakingIsobel_AD_SpyRemindsKO_0b7674d7-e58d-dbe8-c17b-5cf7f67384b9, S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6)
THEN
NOT DB_HAV_SpyCapture_RemindKO(1);

//END_REGION

//REGION Player starts capture

// Attacking via a dialog choice
IF
FlagSet(HAV_TakingIsobel_State_SidedWithSpy_5eb745b2-5043-2f36-4d3a-ee3f09ed7a82, NULL_00000000-0000-0000-0000-000000000000, 0)
THEN
PROC_State_Progress(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_TakingIsobel_Capture", "Tasked_PlayerCombat");

PROC
PROC_State_Changed(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_TakingIsobel_Capture", "Tasked_PlayerCombat")
THEN
PROC_HAV_SetupFactionsSpyAlly();

PROC
PROC_HAV_SetupFactionsSpyAlly()
AND
NOT DB_GlobalFlag((FLAG)HAV_General_State_IsHostileToPlayers_3cd1e3a7-6a9a-6558-a9f2-8db69824869b)
THEN
PROC_SetRelationToPlayers((FACTION)Act2_HAV_Haven_e52bfc8c-7826-470d-a9f5-f6fae80dde82, 51);

PROC
PROC_HAV_SetupFactionsSpyAlly()
THEN
SetFaction(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6, (FACTION)Act2_HAV_FlamingSpy_RevealedAlly_e3da5c3f-056c-4391-83c9-7a78b0703856); //TODO: rename faction to fit with the spy not being there and the ghouls being allied
PROC_HAV_TakingIsobel_SetGhoulsAlly();
PROC_HAV_TakingIsobel_TurnIsobelHostile();
DB_HAV_SpyCapture_RemindKO(1);
PROC_HAV_TakingIsobel_CaptureCombatStarted();

//--- make isobel hostile
PROC
PROC_HAV_TakingIsobel_TurnIsobelHostile()
THEN
SetFlag(HAV_TakingIsobel_Event_IsobelHostile_6719c859-499a-318d-e45b-b3923c76dff2);
SetFlag((FLAG)HAV_TakingIsobel_Event_AbductionAttempted_24605994-7bed-3f92-36f0-1472d32ab2ea);
SetFaction(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, (FACTION)Act2_HAV_Isobel_Hostile_336d0541-f1ab-4bb4-a258-9525c065bef4);
PROC_HAV_TakingIsobel_PrepareCallToArm();

//END_REGION

//REGION Call to Arm

//--- Init
PROC
PROC_HAV_TakingIsobel_PrepareCallToArm()
THEN
DB_HAV_TakingIsobel_JaheiraCallsToArm(1);


//--- Check combat

IF
DB_Is_InCombat(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, _SameCombat)
AND
DB_Is_InCombat(_NPC, _SameCombat)
AND
DB_HAV_TakingIsobel_JaheiraCallsToArm(1)
AND
_NPC != S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211
AND
_NPC != S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6
AND
NOT DB_PartyMembers((CHARACTER)_NPC)
AND
NOT DB_HAV_TakingIsobel_FlyingGhouls(_NPC)
AND
NOT DB_GlobalFlag(HAV_General_State_IsHostileToPlayers_3cd1e3a7-6a9a-6558-a9f2-8db69824869b)
AND
QRY_State_IsBeforeState(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_FollowUp_Capture")
AND
DB_Sees(_NPC, S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211)
THEN
PROC_SetRelationToPlayers((FACTION)Act2_HAV_Haven_e52bfc8c-7826-470d-a9f5-f6fae80dde82, 0);
SetFlag((FLAG)HAV_General_State_IsHostileToPlayers_3cd1e3a7-6a9a-6558-a9f2-8db69824869b, NULL_00000000-0000-0000-0000-000000000000, 0);
NOT DB_HAV_TakingIsobel_JaheiraCallsToArm(1);


IF
TurnStarted((CHARACTER)_NPC)
AND
DB_Is_InCombat(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, _SameCombat)
AND
DB_Is_InCombat(_NPC, _SameCombat)
AND
_NPC != S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211
AND
_NPC != S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6
AND
NOT DB_PartyMembers(_NPC)
AND
NOT DB_HAV_TakingIsobel_FlyingGhouls(_NPC)
AND
DB_GlobalFlag(HAV_TakingIsobel_Event_IsobelHostile_6719c859-499a-318d-e45b-b3923c76dff2)
AND
QRY_State_IsBeforeState(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_FollowUp_Capture")
AND
CanSee(_NPC, S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, 1)
AND
NOT DB_CantTalk_IgnoreCombat((CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211)
AND
QRY_StartDialog((DIALOGRESOURCE)HAV_TakingIsobel_AD_CombatBetrayalReveal_0b2e728e-337a-4a7e-de37-31bb378d84c5, (CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211)
THEN
DB_NOOP(1);
//END_REGION

//REGION Spy Capture - Combat - Ghouls

PROC
PROC_HAV_TakingIsobel_SetGhoulsAlly()
THEN
SetFaction(S_HAV_TaxiGhoul_3ebef8c7-2733-42aa-9757-84fdeadbe652, (FACTION)Act2_HAV_FlamingSpy_Ally_e3da5c3f-056c-4391-83c9-7a78b0703856);

PROC
PROC_HAV_TakingIsobel_SetGhoulsAlly()
AND
DB_HAV_TakingIsobel_FlyingGhouls(_Ghoul)
THEN
SetFaction(_Ghoul, (FACTION)Act2_HAV_FlamingSpy_Ally_e3da5c3f-056c-4391-83c9-7a78b0703856);

//END_REGION

//REGION Isobel is saved


//TODO: lots of missing checks: check if the combat is actually finished, check where the dialog is happening, etc.

//--- Isobel is saved
PROC
PROC_GLO_DefeatCounter_AllDefeated("HAV_TakingIsobel_DefaultSpyCombat")
THEN
PROC_GlobalSetFlagAndCache(HAV_TakingIsobel_State_DefendedIsobel_0d0255e4-dd08-47d7-9b40-cce21ec4a5bd);
PROC_GlobalSetFlagAndCache(HAV_TakingIsobel_State_AbductionCombatOver_b0bb8648-79a1-4b4c-a0ea-822bcc0cd917);

PROC
PROC_GLO_DefeatCounter_AllDefeated("HAV_TakingIsobel_DefaultSpyCombat")
AND
DB_GlobalFlag(HAV_TakingIsobel_Event_SpyCombatStarted_b0930689-e539-424e-bc92-e4a37fbee7d5)
THEN
PROC_State_Progress(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_FollowUp_Protection");
GoalCompleted;

//END_REGION

//REGION Isobel is defeated

//--- Isobel is defeated - close goal, open Saved (which auto-closes), and Betrayed (which handles the defeat logic).

PROC
PROC_HAV_TakingIsobel_CaptureSetup(1)
AND
DB_Defeated(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211)
THEN
NOT DB_HAV_TakingIsobel_JaheiraCallsToArm(1);
PROC_State_Progress(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_DefeatedInHaven");
GoalCompleted;

PROC
PROC_StateSet_PermaDefeated(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211)
THEN
NOT DB_HAV_TakingIsobel_JaheiraCallsToArm(1);
PROC_State_Progress(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel",  "HAV_Isobel_State_DefeatedInHaven");
GoalCompleted;

PROC
PROC_StateSet_Defeated(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211)
AND
NOT DB_PermaDefeated(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211)
THEN
NOT DB_HAV_TakingIsobel_JaheiraCallsToArm(1);
GoalCompleted;

//END_REGION

//REGION If the ghouls die first, Jaheira runs up.

IF
DB_GLO_DefeatCounter_CountMet("HAV_TakingIsobel_InnGhoulCombat")
AND
NOT DB_CantMove(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a)
THEN
PROC_CharacterMoveTo(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, S_HAV_IsobelRoomArrivalPoint_1f775852-ddf6-4644-b271-cee665cd2cdb, "Sprint", "HAV_TakingIsobel_FollowUpToIsobel");

//END_REGION

PROC
PROC_SCE_SetupDebrief()
THEN
GoalCompleted;

IF
FlagSet(SHA_NightsongPrison_State_PlayerEnteredPrison_f2cbe7cf-5223-4522-a649-879d08638282, _, _)
AND
QRY_State_IsBeforeState(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_FollowUp_Protection")
THEN
PROC_DieImmediate((CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, DEATHTYPE.Physical, 1);

PROC
PROC_ApplySavegamePatches()
AND
QRY_BG3_SaveGameIsOlderThan("Release Patch 5")
AND
DB_GlobalFlag(MOO_Assault_State_InProgress_0f3a8f5d-7402-4220-bebb-d4b21d3db08d)
AND
NOT DB_GlobalFlag(SHA_NightsongPrison_State_PlayerEnteredPrison_f2cbe7cf-5223-4522-a649-879d08638282)
AND
QRY_State_IsBeforeState(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_FollowUp_Protection")
THEN
PROC_DieImmediate((CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, DEATHTYPE.Physical, 1);

PROC
PROC_State_Changed(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", (STRING)_OldState, "HAV_Isobel_State_AtMoonrise")
THEN
PROC_State_Remove(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_TakingIsobel_Capture");
GoalCompleted;
EXITSECTION
NOT DB_DialogBlockTradeButton(HAV_TakingIsobel_SpyCapture_1b698557-76c0-1166-6415-91fadb358985);
NOT DB_DialogBlockAttackButton(HAV_TakingIsobel_SpyCapture_1b698557-76c0-1166-6415-91fadb358985);

NOT DB_GlobalFlagReactionAfterDialog(HAV_TakingIsobel_Event_IsobelHostile_6719c859-499a-318d-e45b-b3923c76dff2, HAV_DefeatingKetheric_BriefAtLake_b364a705-4541-1637-c738-fdebc6b4a737);
NOT DB_GlobalFlagReactionAfterDialog(HAV_TakingIsobel_State_PersuadedToGo_19b191d9-0386-a27d-0115-e9313bf421cb, HAV_DefeatingKetheric_BriefAtLake_b364a705-4541-1637-c738-fdebc6b4a737);

NOT DB_DialogBlockTradeButton(HAV_TakingIsobel_SpySuccess_5092e0db-55bd-71a6-2746-3cd5b359cde6);
NOT DB_DialogBlockTradeButton(HAV_TakingIsobel_GhoulSuccess_37afc763-fdfc-819b-fbe6-a490fa4a99c5);

NOT DB_HAV_TakingIsobel_JaheiraCallsToArm(1);

PROC_TriggerUnregisterForPlayers(S_HAV_SpyPlayerInBox_edb52dcc-651b-46c5-afd0-e6f3de7beba2);
ENDEXITSECTION
ParentTargetEdge "Act2_HAV_TakingIsobel"
