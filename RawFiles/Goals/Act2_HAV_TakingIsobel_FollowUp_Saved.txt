Version 1
SubGoalCombiner SGC_AND
INITSECTION
PROC_HAV_TakingIsobel_Followup_Saved_Setup();

//This goal handles any thanking after the player has sided with Isobel.
//It is skipped if no thanking is needed, for any reason.
KBSECTION
//REGION Debug

IF
TextEvent("isobelsaved")
THEN
PROC_HAV_TakingIsobel_ReactToSaved();

//END_REGION

//REGION Setup

PROC
PROC_HAV_TakingIsobel_Followup_Saved_Setup()
AND
NOT QRY_HAV_TakingIsobel_Followup_Saved_Setup_Skip()
THEN
PROC_HAV_TakingIsobel_Followup_Saved_Setup(1);

PROC
PROC_HAV_TakingIsobel_Followup_Saved_Setup(1)
THEN
DB_HAV_Jaheira_StateDialogs("HAV_State_Jaheira_CheckingOnIsobel", NULL_00000000-0000-0000-0000-000000000000); //This is handled in greater detail in the section Saved Isobel below
PROC_State_Progress((CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, "HAV_Jaheira", "HAV_State_Jaheira_CheckingOnIsobel");
DB_DialogBlockTradeButton(HAV_TakingIsobel_IsobelSaved_4033d61b-ec0e-58ac-c962-67f44608dd84);
DB_HAV_Isobel_FollowUper((CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211);
DB_HAV_Isobel_FollowUper((CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a);

PROC
PROC_HAV_TakingIsobel_Followup_Saved_Setup(1)
AND
DB_HAV_Isobel_FollowUper(_Character)
THEN
DB_SpotPlayers(_Character, "HAV_Isobel_FollowUp", NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);
PROC_SpotPlayers_StopSpotting(_Character, "HAV_Isobel_FollowUp"); //Prepare this DB for later.

//--- Flaming Fist is defeated:
// If there are some ghouls left and Isobel is in her room, Isobel tells player to go do the other combat 
// HAV_TakingIsobel_IsobelSaved is a very optional dialog.

PROC
PROC_HAV_TakingIsobel_Followup_Saved_Setup(1)
AND
NOT QRY_TakingIsobel_PrepareReactingToSaved() //if not valid, skip reaction to enable self heal
THEN
PROC_HAV_TakingIsobel_ReactedToSaved();


QRY
QRY_HAV_TakingIsobel_Followup_Saved_Setup_Skip()
AND
NOT DB_GlobalFlag((FLAG)HAV_TakingIsobel_Event_AbductionAttempted_24605994-7bed-3f92-36f0-1472d32ab2ea)
AND
DB_State_Current((CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_PostFollowUpProtection")
THEN
PROC_RemoveAllDialogEntriesForSpeaker(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211);
DB_Dialogs(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, HAV_Isobel_4a5bc7ea-4009-0de3-4ab7-da5bd379979e);
GoalCompleted;

QRY
QRY_HAV_TakingIsobel_Followup_Saved_Setup_Skip()
AND
NOT DB_GlobalFlag((FLAG)HAV_TakingIsobel_Event_AbductionAttempted_24605994-7bed-3f92-36f0-1472d32ab2ea)
AND
QRY_State_IsBeforeState((CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_PostFollowUpProtection")
THEN
PROC_State_Progress((CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_PostFollowUpProtection");

QRY
QRY_HAV_TakingIsobel_Followup_Saved_Setup_Skip()
AND
DB_GlobalFlag(MOO_Assault_State_InProgress_0f3a8f5d-7402-4220-bebb-d4b21d3db08d)
AND
NOT DB_GlobalFlag(SCE_Debrief_State_Setup_0ebc6b35-0f07-4a73-b6b8-9782ecbcaccf)
THEN
PROC_State_Progress((CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_PostFollowUpProtection");

QRY
QRY_HAV_TakingIsobel_Followup_Saved_Setup_Skip()
AND
NOT DB_GlobalFlag(SCE_Debrief_State_Setup_0ebc6b35-0f07-4a73-b6b8-9782ecbcaccf)
AND
DB_State_Current(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_DefeatedInHaven")
THEN
GoalCompleted;

//If the player isn't protecting, then the goal is moot.
QRY
QRY_HAV_TakingIsobel_Followup_Saved_Setup_Skip()
AND
NOT DB_GlobalFlag(SCE_Debrief_State_Setup_0ebc6b35-0f07-4a73-b6b8-9782ecbcaccf)
AND
NOT DB_State_Current(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_FollowUp_Protection")
AND
DB_GlobalFlag((FLAG)HAV_TakingIsobel_Event_AbductionAttempted_24605994-7bed-3f92-36f0-1472d32ab2ea)
THEN
GoalCompleted;

QRY
QRY_HAV_TakingIsobel_Followup_Saved_Setup_Skip()
AND
DB_GlobalFlag(SCE_Debrief_State_Setup_0ebc6b35-0f07-4a73-b6b8-9782ecbcaccf)
THEN
GoalCompleted;

//END_REGION


//REGION Fallback if Jaheira can't come
QRY
QRY_HAV_FollowUp_JaheiraIsUnavailable()
AND
DB_Defeated(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a)
THEN
PROC_HAV_TakingIsobel_FollowUpCleanup();

QRY
QRY_HAV_FollowUp_JaheiraIsUnavailable()
AND
DB_GlobalFlag(MOO_Assault_State_JaheiraJoined_2bea8f68-eac7-4734-9128-1b3f3c248da5)
THEN
PROC_HAV_TakingIsobel_FollowUpCleanup();

PROC
PROC_HAV_TakingIsobel_FollowUpCleanup()
AND
NOT DB_Defeated(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a)
AND
NOT DB_GlobalFlag(MOO_Assault_State_JaheiraJoined_2bea8f68-eac7-4734-9128-1b3f3c248da5)
AND
NOT DB_GlobalFlag(SCE_Debrief_State_Setup_0ebc6b35-0f07-4a73-b6b8-9782ecbcaccf)
THEN
PROC_CharacterMoveTo(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, S_HAV_Jaheira_InnHome2_68f5d526-225a-4436-abc4-7047911be5d8, "Stroll", "HAV_Isobel_ArrivedHomeAfterIsobel");

PROC
PROC_HAV_TakingIsobel_FollowUpCleanup()
AND
DB_HAV_TakingIsobel_FollowUpDialog(_Dialog)
THEN
NOT DB_HAV_TakingIsobel_FollowUpDialog(_Dialog);

PROC
PROC_HAV_TakingIsobel_FollowUpCleanup()
THEN
PROC_TriggerUnregisterForPlayers(S_HAV_InnInteriorFollowUpBox_c1d2d3f4-5461-4d30-a774-86e1867326cc);
PROC_TriggerUnregisterForPlayers(S_HAV_IsobelRoomFollowUpBox_1ddc9945-3187-4eee-a2f3-8e2c06e5c06f);
PROC_State_Progress((CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, "HAV_Jaheira", "HAV_State_Jaheira_AfterCheckingOnIsobel");
PROC_State_IfCurrentProgress(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_FollowUp_Protection", "HAV_Isobel_State_PostFollowUpProtection");
SetFlag(HAV_TakingIsobel_Event_FollowUpCleanedUp_c2f3dd52-dd3b-4c07-b3ba-cba17b36dfc3);
ClearFlag(HAV_TakingIsobel_State_CaptureFollowUpInProgress_6b7ba85f-ff9e-41ea-be1f-9008d1d503b0);

//END_REGION


//REGION Clean up

IF
DialogStarted(_Dialog, _)
AND
DB_HAV_TakingIsobel_FollowUpDialog(_Dialog)
THEN
PROC_HAV_TakingIsobel_FollowUpCleanup();

PROC
PROC_LongRest()
AND
DB_HAV_TakingIsobel_FollowUpDialog(_Dialog)
AND
DB_State_Current((CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, "HAV_Jaheira", "HAV_State_Jaheira_CheckingOnIsobel")
THEN
DB_ExecuteInLevel("HAV_TakingIsobel_FollowUpCleanUp","SCL_Main_A");

PROC
PROC_ExecuteInLevel("HAV_TakingIsobel_FollowUpCleanUp","SCL_Main_A")
THEN
PROC_HAV_TakingIsobel_FollowUpCleanup();


//END_REGION


//REGION Saved Isobel 

QRY
QRY_TakingIsobel_PrepareReactingToSaved()
AND
NOT DB_GLO_DefeatCounter_CountMet("HAV_TakingIsobel_InnGhoulCombat")
AND
NOT DB_GlobalFlag(SCE_Debrief_State_Setup_0ebc6b35-0f07-4a73-b6b8-9782ecbcaccf)
AND
NOT DB_GlobalFlag(MOO_Assault_State_InProgress_0f3a8f5d-7402-4220-bebb-d4b21d3db08d)
AND
IsInTrigger(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, S_HAV_IsobelRoomFollowUpBox_1ddc9945-3187-4eee-a2f3-8e2c06e5c06f, 1)
THEN
PROC_RemoveAllDialogEntriesForSpeaker(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211);
DB_Dialogs(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, HAV_TakingIsobel_IsobelSaved_4033d61b-ec0e-58ac-c962-67f44608dd84);
DB_HAV_TakingIsobel_IsobelFollowUp(1);
RealtimeObjectTimerLaunch(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_State_TakingIsobel_FollowUp_Protection", 1500);

// Isobel joined any dialog (most likely HAV_TakingIsobel_IsobelSaved at this point), change her dialog back
IF
DialogActorJoined(_,_,S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211,_)
AND
DB_HAV_TakingIsobel_IsobelFollowUp(1)
THEN
PROC_HAV_TakingIsobel_ReactedToSaved();

IF
ObjectTimerFinished(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_State_TakingIsobel_FollowUp_Protection")
AND
NOT DB_GLO_DefeatCounter_CountMet("HAV_TakingIsobel_InnGhoulCombat")
AND
NOT DB_CantTalk(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211)
AND
IsInTrigger(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, S_HAV_IsobelRoomFollowUpBox_1ddc9945-3187-4eee-a2f3-8e2c06e5c06f, 1)
THEN
PROC_HAV_TakingIsobel_ReactToSaved();

PROC
PROC_HAV_TakingIsobel_ReactToSaved()
THEN
DB_HAV_TakingIsobel_PrioritySpeakerTriggers((TRIGGER)S_HAV_IsobelRoomFollowUpBox_1ddc9945-3187-4eee-a2f3-8e2c06e5c06f);

// Start dialog automatically
PROC
PROC_HAV_TakingIsobel_ReactToSaved()
AND
NOT DB_DialogSpeakers(_, S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, _)
AND
QRY_HAV_TakingIsobel_FindPrioritySpeaker()
AND
DB_QRYRTN_HAV_TakingIsobel_FindPrioritySpeaker(_Speaker)
AND
QRY_StartDialogCustom_Fixed(HAV_TakingIsobel_IsobelSaved_4033d61b-ec0e-58ac-c962-67f44608dd84, S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, _Speaker, 0, 0, 0, 0)
THEN
DB_NOOP(1);

PROC
PROC_HAV_TakingIsobel_ReactToSaved()
THEN
NOT DB_HAV_TakingIsobel_PrioritySpeakerTriggers((TRIGGER)S_HAV_IsobelRoomFollowUpBox_1ddc9945-3187-4eee-a2f3-8e2c06e5c06f);

PROC
PROC_HAV_TakingIsobel_ReactToSaved()
THEN
DB_HAV_TakingIsobel_ReactedToSaved(1); //TODO: Add more granular states.

QRY
QRY_HAV_TakingIsobel_FindPrioritySpeaker()
AND
DB_QRYRTN_HAV_TakingIsobel_FindPrioritySpeaker(_Speaker)
THEN
NOT DB_QRYRTN_HAV_TakingIsobel_FindPrioritySpeaker(_Speaker);

QRY
QRY_HAV_TakingIsobel_FindPrioritySpeaker()
THEN
DB_NOOP(1);

QRY
QRY_HAV_TakingIsobel_FindPrioritySpeaker()
AND
NOT QRY_HAV_TakingIsobel_FindPrioritySpeaker_Priority()
AND
NOT QRY_HAV_TakingIsobel_FindPrioritySpeaker_Avatar()
AND
NOT QRY_HAV_TakingIsobel_FindPrioritySpeaker_Generic()
THEN
DB_NOOP(1);

QRY
QRY_HAV_TakingIsobel_FindPrioritySpeaker_Priority()
AND
DB_HAV_SpyCapture_PrioPlayer(_Player)
AND
QRY_HAV_TakingIsobel_IsBetterSpeaker(_Player)
THEN
DB_NOOP(1);

QRY
QRY_HAV_TakingIsobel_FindPrioritySpeaker_Avatar()
AND
DB_Avatars(_Player)
AND
QRY_HAV_TakingIsobel_IsBetterSpeaker(_Player)
THEN
DB_NOOP(1);

QRY
QRY_HAV_TakingIsobel_FindPrioritySpeaker_Generic()
AND
DB_Players(_Player)
AND
QRY_HAV_TakingIsobel_IsBetterSpeaker(_Player)
THEN
DB_NOOP(1);


QRY
QRY_HAV_TakingIsobel_IsBetterSpeaker((CHARACTER)_Player)
AND
NOT DB_HiddenCharacters(_Player, _)
AND
NOT DB_CantTalk(_Player)
AND
DB_HAV_TakingIsobel_PrioritySpeakerTriggers(_Trigger)
AND
NOT DB_QRYRTN_HAV_TakingIsobel_FindPrioritySpeaker(_)
AND
IsInTrigger(_Player, _Trigger, 1)
THEN
DB_QRYRTN_HAV_TakingIsobel_FindPrioritySpeaker(_Player);


QRY
QRY_HAV_TakingIsobel_IsBetterSpeaker((CHARACTER)_Player)
AND
NOT DB_HiddenCharacters(_Player, _)
AND
NOT DB_CantTalk(_Player)
AND
DB_HAV_TakingIsobel_PrioritySpeakerTriggers(_Trigger)
AND
DB_QRYRTN_HAV_TakingIsobel_FindPrioritySpeaker(_OldSpeaker)
AND
_OldSpeaker != _Player //Check not equal here to avoid an extra Trigger Is In Trigger check or a range check if the character passed on the first of several triggers
AND
IsInTrigger(_Player, _Trigger, 1)
AND
GetDistanceTo(_Player, S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, _Dist1)
AND
GetDistanceTo(_OldSpeaker, S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, _Dist2)
AND
_Dist2 > _Dist1
THEN
NOT DB_QRYRTN_HAV_TakingIsobel_FindPrioritySpeaker(_OldSpeaker);
DB_QRYRTN_HAV_TakingIsobel_FindPrioritySpeaker(_Player);


// Starting with any avatar nearby
QRY
QRY_HAV_TakingIsobel_ReactToSaved_TryWithAvatar((CHARACTER)_PrioPlayer)
AND
DB_Avatars(_Avatar)
AND
_Avatar != _PrioPlayer
AND
NOT DB_HAV_TakingIsobel_FollowUpSuccess(1)
AND
QRY_HAV_TakingIsobel_ReactToSaved_StartDialog((CHARACTER)_Avatar)
THEN
DB_NOOP(1);

// Starting with any player nearby
QRY
QRY_HAV_TakingIsobel_ReactToSaved_TryWithAny((CHARACTER)_PrioPlayer)
AND
DB_Players(_Player)
AND
_Player != _PrioPlayer
AND
NOT DB_HAV_TakingIsobel_FollowUpSuccess(1)
AND
QRY_HAV_TakingIsobel_ReactToSaved_StartDialog((CHARACTER)_Player)
THEN
DB_NOOP(1);

QRY
QRY_HAV_TakingIsobel_ReactToSaved_StartDialog((CHARACTER)_Player)
AND
NOT DB_CantTalk_IgnoreCombat(_Player)
AND
NOT DB_CantMove(_Player)
AND
IsInTrigger(_Player, S_HAV_IsobelRoomFollowUpBox_1ddc9945-3187-4eee-a2f3-8e2c06e5c06f, 1)
THEN
DB_HAV_TakingIsobel_FollowUpSuccess(1);

QRY
QRY_SelectCustomDialog_AfterGenerics((CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, _Player)
AND
DB_HAV_TakingIsobel_ReactedToSaved(1)
AND
DB_HAV_TakingIsobel_PrioritySpeakerTriggers(_Trigger) //They're both in this trigger, so the player addressing her is also in there.
AND
NOT DB_CantTalk((CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a)
AND
NOT DB_SelectedDialog((DIALOGRESOURCE)HAV_TakingIsobel_FollowUpSaved_5de4ab22-4449-3f6c-3909-f13d66a0e9bd, (CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, (CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, _)
THEN
DB_SelectedDialog((DIALOGRESOURCE)HAV_TakingIsobel_FollowUpSaved_5de4ab22-4449-3f6c-3909-f13d66a0e9bd, (CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, (CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, _Player);

QRY
QRY_SelectCustomDialog_AfterGenerics((CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, _Player)
AND
DB_HAV_TakingIsobel_ReactedToSaved(1)
AND
NOT DB_SelectedDialog((DIALOGRESOURCE)HAV_TakingIsobel_FollowUpSaved_5de4ab22-4449-3f6c-3909-f13d66a0e9bd, (CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, (CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, _Player)
THEN
DB_SelectedDialog((DIALOGRESOURCE)HAV_TakingIsobel_CoolingDown_7d761436-e31c-e9ac-ae8f-1e0f0cd136d8, (CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, _Player);

QRY
QRY_SelectCustomDialog_AfterGenerics((CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, _Player)
AND
DB_GlobalFlag(HAV_TakingIsobel_State_CaptureFollowUpInProgress_6b7ba85f-ff9e-41ea-be1f-9008d1d503b0)
AND
DB_HAV_TakingIsobel_PrioritySpeakerTriggers(_Trigger)
AND
NOT DB_CantTalk((CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a)
AND
NOT DB_SelectedDialog((DIALOGRESOURCE)HAV_TakingIsobel_FollowUpSaved_5de4ab22-4449-3f6c-3909-f13d66a0e9bd, (CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, (CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, _Player)
THEN
DB_SelectedDialog((DIALOGRESOURCE)HAV_TakingIsobel_FollowUpSaved_5de4ab22-4449-3f6c-3909-f13d66a0e9bd, (CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, (CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, _Player);

QRY
QRY_SelectCustomDialog_AfterGenerics((CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, _Player)
AND
DB_GlobalFlag(HAV_TakingIsobel_State_CaptureFollowUpInProgress_6b7ba85f-ff9e-41ea-be1f-9008d1d503b0)
AND
NOT DB_SelectedDialog((DIALOGRESOURCE)HAV_TakingIsobel_FollowUpSaved_5de4ab22-4449-3f6c-3909-f13d66a0e9bd, (CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, (CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, _Player)
THEN
DB_SelectedDialog((DIALOGRESOURCE)HAV_TakingIsobel_Jaheira_AD_ToIsobel_bcd5af07-19ac-6ee8-7c00-85e5b7a2045b, (CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, _Player);

//--- Restore default dialog
PROC
PROC_HAV_TakingIsobel_ReactedToSaved()
THEN
NOT DB_HAV_TakingIsobel_IsobelFollowUp(1);
PROC_SelfHealing_Enable(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211);

//END_REGION

//REGION Ghouls are defeated

PROC
PROC_HAV_TakingIsobel_Followup_Saved_Setup(1)
THEN
DB_HAV_TakingIsobel_Followup_Saved_SetupDone(1);

IF
DB_HAV_TakingIsobel_Followup_Saved_SetupDone(1)
AND
DB_GLO_DefeatCounter_CountMet("HAV_TakingIsobel_InnGhoulCombat")
AND
DB_GlobalFlag(HAV_TakingIsobel_State_DefendedIsobel_0d0255e4-dd08-47d7-9b40-cce21ec4a5bd) //set when the spy is defeated, when the player sided with Isobel
AND
NOT QRY_HAV_FollowUp_JaheiraIsUnavailable() //edge-case: Jaheira cannot to the followup -> skip it
THEN
PROC_HAV_TakingIsobel_SetupFollowup();

PROC
PROC_HAV_TakingIsobel_SetupFollowup()
THEN
NOT DB_HAV_TakingIsobel_PrioritySpeakerTriggers(S_HAV_IsobelRoomFollowUpBox_1ddc9945-3187-4eee-a2f3-8e2c06e5c06f);
PROC_GlobalSetFlagAndCache(HAV_TakingIsobel_State_CaptureFollowUpInProgress_6b7ba85f-ff9e-41ea-be1f-9008d1d503b0);
PROC_RemoveAllDialogEntriesForSpeaker(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211);
DB_Dialogs(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, HAV_TakingIsobel_CoolingDown_7d761436-e31c-e9ac-ae8f-1e0f0cd136d8);
DB_HAV_TakingIsobel_FollowUpDialog((DIALOGRESOURCE)HAV_TakingIsobel_FollowUpSaved_5de4ab22-4449-3f6c-3909-f13d66a0e9bd);

PROC
PROC_HAV_TakingIsobel_SetupFollowup()
AND
DB_GlobalFlag(HAV_TakingIsobel_Event_IsobelToRoom_d1f2df3a-ba92-5599-0b19-b0d9da9c0f5c)
THEN
PROC_CharacterMoveTo(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, S_HAV_IsobelRoomArrivalPoint_1f775852-ddf6-4644-b271-cee665cd2cdb, "Sprint", "HAV_TakingIsobel_FollowUpToIsobel");

PROC
PROC_HAV_TakingIsobel_SetupFollowup()
AND
NOT DB_GlobalFlag(HAV_TakingIsobel_Event_IsobelToRoom_d1f2df3a-ba92-5599-0b19-b0d9da9c0f5c)
THEN
PROC_CharacterMoveTo(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, S_HAV_IsobelBalconyArrivalPoint_b4fb428e-3578-469b-a348-3f69dba345a4, "Sprint", "HAV_TakingIsobel_FollowUpToIsobel");

PROC
PROC_HAV_TakingIsobel_SetupFollowup()
AND
DB_HAV_Isobel_FollowupBoxes(_Trigger)
AND
DB_HAV_Isobel_FollowUper(_Character)
THEN
TriggerRegisterForCharacter(_Trigger, _Character);

IF
DB_InRegion((CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, _Trigger)
AND
DB_InRegion((CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, _Trigger)
AND
DB_HAV_Isobel_FollowupBoxes(_Trigger)
AND
NOT DB_CantTalk((CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a)
AND
NOT DB_CantTalk((CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211)
THEN
PROC_HAV_Isobel_StartFollowupSpotting((TRIGGER)_Trigger);

IF
DB_HAV_TakingIsobel_PrioritySpeakerTriggers(_Trigger)
AND
DB_HAV_Isobel_FollowUper(_Character)
AND
NOT DB_InRegion(_Character, _Trigger)
THEN
PROC_HAV_Isobel_StopFollowupSpotting((TRIGGER)_Trigger);

IF
DB_HAV_TakingIsobel_PrioritySpeakerTriggers(_Trigger)
AND
DB_HAV_Isobel_FollowUper(_Character)
AND
DB_CantTalk(_Character)
THEN
PROC_HAV_Isobel_StopFollowupSpotting((TRIGGER)_Trigger);

PROC
PROC_HAV_Isobel_StopFollowupSpotting((TRIGGER)_Trigger) //If this is the only entry, stop spotting before removing it. Otherwise, the character may get Spotted outside of a trigger.
AND
DB_HAV_TakingIsobel_PrioritySpeakerTriggers(_Trigger)
AND
NOT QRY_HAV_TakingIsobel_OtherPrioritySpeakerTriggers(_Trigger)
AND
DB_HAV_Isobel_FollowUper(_Character)
THEN
PROC_SpotPlayers_StopSpotting(_Character, "HAV_Isobel_FollowUp");

PROC
PROC_HAV_Isobel_StopFollowupSpotting((TRIGGER)_Trigger)
THEN
NOT DB_HAV_TakingIsobel_PrioritySpeakerTriggers(_Trigger);

QRY
QRY_HAV_TakingIsobel_OtherPrioritySpeakerTriggers((TRIGGER)_Trigger)
AND
DB_HAV_TakingIsobel_PrioritySpeakerTriggers(_AnyTrigger)
AND
_AnyTrigger != _Trigger
THEN
DB_NOOP(1);

PROC
PROC_HAV_Isobel_StartFollowupSpotting((TRIGGER)_Trigger)
AND
DB_HAV_Isobel_FollowUper(_Character)
THEN
DB_HAV_TakingIsobel_PrioritySpeakerTriggers(_Trigger);
DB_SpotPlayers_SpotTrigger(_Character, "HAV_Isobel_FollowUp", _Trigger);
PROC_SpotPlayers_RestartSpotting(_Character, "HAV_Isobel_FollowUp");

PROC
PROC_HAV_Isobel_StartFollowupSpotting(_)
THEN
PROC_ClearStoryMove(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a); //Cancel existing movement attempt.
PROC_CharacterMoveTo(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "Sprint", "HAV_JaheiraAtIsobel");

IF
EntityEvent(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, "HAV_JaheiraAtIsobel")
THEN
SetHasDialog(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, 1);

PROC
PROC_SpotPlayers_Spotted(_, "HAV_Isobel_FollowUp", _)
AND
QRY_HAV_TakingIsobel_FindPrioritySpeaker()
AND
DB_QRYRTN_HAV_TakingIsobel_FindPrioritySpeaker(_Speaker)
AND
QRY_StartDialog((DIALOGRESOURCE)HAV_TakingIsobel_FollowUpSaved_5de4ab22-4449-3f6c-3909-f13d66a0e9bd, (CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, (CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, _Speaker)
THEN
DB_NOOP(1);

//END_REGION

//REGION Isobel Saved

//TODO: safety checks

IF
EntityEvent(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, "HAV_TakingIsobel_FollowUpToIsobel")
THEN
SetHasDialog(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, 1);
PROC_RemoveAllDialogEntriesForSpeaker(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211);


//END_REGION

IF
DB_GlobalFlag(MOO_Assault_State_InProgress_0f3a8f5d-7402-4220-bebb-d4b21d3db08d)
THEN
PROC_HAV_TakingIsobel_FollowUpCleanup();
PROC_State_Progress((CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_PostFollowUpProtection");

PROC
PROC_State_Changed(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", _, "HAV_Isobel_State_PostFollowUpProtection")
THEN
PROC_RemoveAllDialogEntriesForSpeaker(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211);
DB_Dialogs(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, HAV_Isobel_4a5bc7ea-4009-0de3-4ab7-da5bd379979e);
GoalCompleted;

IF
DB_HAV_TakingIsobel_PrioritySpeakerTriggers(_Trigger)
AND
DB_HAV_Isobel_FollowUper(_Character)
AND
DB_Defeated(_Character)
THEN
PROC_HAV_Isobel_StopFollowupSpotting((TRIGGER)_Trigger);

IF
DB_HAV_Isobel_FollowUper(_Character)
AND
DB_Defeated(_Character)
THEN
PROC_HAV_TakingIsobel_FollowUpCleanup();

PROC
PROC_StateSet_Defeated((CHARACTER)S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211)
THEN
PROC_State_Progress(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel", "HAV_Isobel_State_DefeatedInHaven");
GoalCompleted;

PROC
PROC_SCE_SetupDebrief()
THEN
GoalCompleted;
EXITSECTION
NOT DB_HAV_TakingIsobel_Followup_Saved_SetupDone(1);
NOT DB_HAV_TakingIsobel_ReactedToSaved(1);
SysClear("DB_HAV_Isobel_FollowUper", 1);
PROC_SpotPlayers_StopSpotting(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, "HAV_Isobel_FollowUp");
PROC_SpotPlayers_StopSpotting(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel_FollowUp");
NOT DB_SpotPlayers(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, "HAV_Isobel_FollowUp", NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);
NOT DB_SpotPlayers(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_Isobel_FollowUp", NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);
ENDEXITSECTION
ParentTargetEdge "Act2_HAV_TakingIsobel_Capture"
