Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_HAV_TakingIsobel_InvalidGoalFlags((FLAG)SCE_Debrief_State_Setup_0ebc6b35-0f07-4a73-b6b8-9782ecbcaccf);
DB_HAV_TakingIsobel_InvalidGoalFlags((FLAG)MOO_Assault_State_InProgress_0f3a8f5d-7402-4220-bebb-d4b21d3db08d);

PROC_HAV_TakingIsobel_CaptureCombatStarted();

DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_DEN_ThiefHideoutLeader_c8ab1ca6-96bb-467e-91c9-af87bc4d3925);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_HAV_FlamingFist_005_0e691c0a-562b-43bb-8554-cde1194decd5);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_HAV_FlamingFist_003_128c0f54-fea0-49df-89ca-c2ca8d996a31);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_HAV_FlamingFist_004_eb083eb6-066c-4693-8fb8-575bc4c71373);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_HAV_FlamingFist_006_9629c27e-8e50-4258-8bda-6639af768ba6);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_HAV_FlamingFist_002_39ace2b4-699d-468c-a664-8a06492d997a);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_FOR_UnfortunateGnome_6e45a00b-bad2-40bb-9403-147b9e92d976);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_DEN_Bard_4a405fba-3000-4c63-97e5-a8001ebb883c);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_GLO_YoungLover_02_dcf597c0-36c5-4c81-957a-6fe9fca50a68);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_HAV_HavenOutcasts_FountainPatrol_Caster_ff70f59a-7d50-4e69-a768-60a97c6fb57d);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_HAV_HavenOutcasts_BarricadeRunners_Ranger_572690b6-68f5-4e7b-807b-b9376b658b65);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_HAV_HavenOutcasts_BarricadeYeller_Ranger_b612ef5f-3381-4486-8959-84dd399fb1ae);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_HAV_HavenOutcasts_BarricadeGuards_Melee_Dwarf_b1d8b327-1f27-4921-aa2b-dd18a9d67fc8);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_HAV_HavenOutcasts_FountainGuards_Melee_1_3e2db417-c434-4f08-ab6a-dd60c8e9f3b5);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_HAV_HavenOutcasts_FountainGuards_Melee_2_271478dd-7efb-4ca7-b40c-593bbea91a85);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_HAV_HavenOutcasts_EntranceGuards_Halfling_Melee_c8bc8f03-c91c-42d4-829c-d16bb1df4a67);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_HAV_HavenOutcasts_EntranceGuards_Caster_HighElf_825714dd-7df9-4cd1-aecb-edf577baa487);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_SCL_Drider_Harper_Melee_000_b9f6bb1e-f4cd-4bb3-a7f0-80aad9bae8c8);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_SCL_Drider_Harper_Ranger_000_ce089a64-1a91-4ab0-ac71-a2712680780d);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_SCL_Drider_Harper_Ranger_001_71c140fa-0ebd-4f36-8728-6e28a445a6ff);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_SCL_Drider_Harper_Caster_000_ef609a8e-08bf-4648-bbb4-380a7f4bc887);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_HAV_HavenOutcasts_DockGuard_Left_Ranger_3a856945-e4a5-4214-89af-84a2b50789d8);
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)S_HAV_HavenOutcasts_DockGuard_Left_Melee_bd46130e-1c2c-4888-9e9c-6dd58965ab4f);

DB_HAV_TakingIsobel_SurpriseParticipants((CHARACTER)S_DEN_BackupLeader_b304e07d-019c-4ec6-b5ea-c4ccb29ed892);
DB_HAV_TakingIsobel_SurpriseParticipants((CHARACTER)S_HAV_HarperGuard_002_34a9bfd8-1ca2-4a1c-b72e-2bf1cfc046a4);
DB_HAV_TakingIsobel_SurpriseParticipants((CHARACTER)S_HAV_HarperGuard_001_8082f1d4-8942-492e-8d26-1e1ed6bd881f);
DB_HAV_TakingIsobel_SurpriseParticipants((CHARACTER)S_GLO_Prodigy_b6a3a9e9-b6eb-4c19-ab3f-4c431178fe1b);
DB_HAV_TakingIsobel_SurpriseParticipants((CHARACTER)S_HAV_FlamingFist_001_47058367-9c0c-4756-a467-07b87b28e0d6);


KBSECTION
PROC
PROC_HAV_TakingIsobel_CaptureCombatStarted()
AND
DB_HAV_TakingIsobel_InvalidGoalFlags(_Flag)
AND
DB_GlobalFlag(_Flag)
THEN
GoalCompleted;

//REGION Combat


IF
FlagSet(HAV_TakingIsobel_Event_SpawnGhouls_e8d0fd32-5eff-4101-bb7c-6d2e1acd4ba4, _, _Dialog)
THEN
DB_HAV_TakingIsobel_MainRoomCombatDialog(_Dialog);
DB_AddCharactersInTriggerToDialog(HAV_TakingIsobel_SpyCapture_1b698557-76c0-1166-6415-91fadb358985,S_HAV_TakingIsobel_InnAttackBox_Players_780977ee-c5c9-4716-bd29-61b597bad545,1);

IF
FlagSet(HAV_TakingIsobel_Event_SpawnGhouls_e8d0fd32-5eff-4101-bb7c-6d2e1acd4ba4, _, _Dialog)
AND
DB_HAV_TakingIsobel_KOParticipants(_Character)
AND
DB_DialogNPCs(_Dialog2, _Character, _)
AND
NOT DB_AutomatedDialog(_Dialog2)
AND
_Dialog != _Dialog2
THEN
PROC_ForceStopDialog(_Character);

IF
FlagSet(HAV_TakingIsobel_Event_SpawnGhouls_e8d0fd32-5eff-4101-bb7c-6d2e1acd4ba4, _, _)
AND
DB_HAV_TakingIsobel_FlyingGhouls(_Ghoul)
THEN
PROC_Foop(_Ghoul);

IF
FlagSet(HAV_TakingIsobel_Event_SpawnGhouls_e8d0fd32-5eff-4101-bb7c-6d2e1acd4ba4, _, _)
THEN
PROC_State_Progress(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_GhoulAttack");
SetTag(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, (TAG)AI_UNPREFERRED_TARGET_9787450d-f34d-43bd-be88-d2bac00bb8ee);
SetTag(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211,(TAG)AI_PREFERRED_TARGET_64bc9da1-9262-475a-a397-157600b7debd);
DB_CombatReact_AD_OnTurn(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6, (DIALOGRESOURCE)HAV_Haven_AD_FlamingSpy_IsobelCombat_000_ef1f8b3e-b0f9-166c-49ad-a6ac1693e0ee, 1);
DB_CombatReact_AD_OnTurn(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6, (DIALOGRESOURCE)HAV_Haven_AD_FlamingSpy_IsobelCombat_001_82af9a75-2416-7942-3bc3-52b4995da643, 2);
DB_CombatReact_AD_OnTurn(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6, (DIALOGRESOURCE)HAV_Haven_AD_FlamingSpy_IsobelCombat_002_7cf2dbea-d5e4-4733-f3e8-264a914afe62, 3);


PROC
PROC_HAV_TakingIsobel_CaptureCombatStarted()
AND
NOT DB_GlobalFlag(HAV_Siege_State_NoMoreSiege_2129857a-bfa2-49c4-a853-c7e08a2fa632)
AND
NOT DB_Is_InCombat(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, _)
AND
NOT DB_Defeated(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a)
THEN
PROC_ForceStopDialog(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a);
PROC_CharacterMoveTo(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, S_HAV_Jaheira_InnAttackPoint_3e11d092-e4f1-49dc-8ddc-600cf30a1dea, "Sprint", "HAV_JaheiraRunsToInn");

PROC
PROC_HAV_TakingIsobel_CaptureCombatStarted()
AND
DB_State_Current(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, "SCL_LiftingTheCurse", "SCL_LiftingTheCurse_State_HalsinAtHaven")
AND
GetFaction(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, _Faction)
THEN
SetRelation(_Faction, (FACTION)Act2_HAV_FlamingSpy_Hostile_caa2c8a9-67d2-479b-91ad-f460e66cfd9b, 0);

// Jaheira ended any other combat while combat in the main room is happening, move her.
IF
LeftCombat(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, _CombatID)
AND
NOT QRY_HAV_TakingIsobel_IsMainRoomCombat((GUIDSTRING)_CombatID)
AND
NOT DB_Defeated(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a)
AND
NOT DB_PartOfTheTeam((CHARACTER)S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a)
AND
SysCount("QRY_HAV_TakingIsobel_IsMainRoomCombat", 1, 0) //Verify the DB is not empty and it is, in fact, ongoing.
THEN
PROC_ForceStopDialog(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a);
PROC_CharacterMoveTo(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, S_HAV_Jaheira_InnAttackPoint_3e11d092-e4f1-49dc-8ddc-600cf30a1dea, "Sprint", "HAV_JaheiraRunsToInn");

IF
EntityEvent(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, "HAV_JaheiraRunsToInn")
THEN
SetHasDialog(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, 1);

IF
DB_PartyMembers(_Character)
AND
DB_Is_InCombat(_Character, _CombatID)
AND
DB_HAV_TakingIsobel_MainRoomCombat(_CombatID)
AND
DB_HAV_TakingIsobel_MainRoomCombatDialog(_DialogID)
AND
NOT DB_DialogPlayers(_DialogID, _Character, _)
THEN
PROC_DialogAddListeningActor(_DialogID, _Character);

IF
FlagSet(HAV_TakingIsobel_Event_SpawnGhouls_e8d0fd32-5eff-4101-bb7c-6d2e1acd4ba4, _, _)
AND
DB_HAV_TakingIsobel_KOParticipants(_Character)
AND
IsInTrigger(_Character, S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, 1)
THEN
RequestPassiveRoll(_Character, _Character, "SavingThrow", "Constitution", (DIFFICULTYCLASS)HAV_TakingIsobel_GhoulSpiked_13467824-03fd-4316-a0d1-5412cb6f9b2b, 0, "HAV_TakingIsobel_GhoulSpikedSave");

IF
RollResult("HAV_TakingIsobel_GhoulSpikedSave", _Character, _, _Result, _, _)
AND
_Result != 1
THEN
ApplyStatus(_Character, "HAV_GHOULSPIKED", -1.0, 1, NULL_00000000-0000-0000-0000-000000000000);

IF
StatusApplied(_Character, "HAV_GHOULSPIKED", _, _)
AND
CanFight(_Character, _CanFight)
THEN
DB_HAV_TakingIsobel_GhoulSpiked(_Character, _CanFight);
SetCanFight(_Character, 0);

IF
StatusRemoved(_Character, "HAV_GHOULSPIKED", _, _)
AND
DB_HAV_TakingIsobel_GhoulSpiked(_Character, _CanFight)
THEN
SetCanFight(_Character, _CanFight);
NOT DB_HAV_TakingIsobel_GhoulSpiked(_Character, _CanFight);

IF
DialogEnded(_Dialog, _ID)
AND
DB_HAV_TakingIsobel_MainRoomCombatDialog(_ID)
THEN
NOT DB_HAV_TakingIsobel_MainRoomCombatDialog(_ID);

IF
DB_HAV_TakingIsobel_MainRoomCombatDialog(_Dialog)
AND
DB_HAV_TakingIsobel_MainRoomCombat(_CombatID)
THEN
PauseCombat(_CombatID);

IF
DB_HAV_TakingIsobel_MainRoomCombatDialog(_Dialog)
AND
DB_HAV_TakingIsobel_MainRoomCombat(_CombatID)
AND
CombatGetActiveEntity(_CombatID, _Entity)
AND
NOT DB_PartyMembers((CHARACTER)_Entity)
THEN
SetEntityEventReal(_Entity, "GLO_CombatWait", 30.0, 1); //Needs to match duration of dialogue.
DB_HAV_TakingIsobel_MainRoomCombat_Paused(_Entity);

IF
TurnStarted(_Entity)
AND
DB_Is_InCombat(_Entity, _CombatID)
AND
DB_HAV_TakingIsobel_MainRoomCombatDialog(_Dialog)
AND
DB_HAV_TakingIsobel_MainRoomCombat(_CombatID)
AND
NOT DB_PartyMembers((CHARACTER)_Entity)
THEN
SetEntityEventReal(_Entity, "GLO_CombatWait", 30.0, 1); //Needs to match duration of dialogue.
DB_HAV_TakingIsobel_MainRoomCombat_Paused(_Entity);

IF
DB_HAV_TakingIsobel_MainRoomCombat(_CombatID)
AND
NOT DB_HAV_TakingIsobel_MainRoomCombatDialog(_)
THEN
ResumeCombat(_CombatID);

IF
DB_HAV_TakingIsobel_MainRoomCombat(_CombatID)
AND
NOT DB_HAV_TakingIsobel_MainRoomCombatDialog(_)
AND
DB_HAV_TakingIsobel_MainRoomCombat_Paused(_Entity)
THEN
NOT DB_HAV_TakingIsobel_MainRoomCombat_Paused(_Entity);
SetEntityEvent(_Entity, "GLO_CancelCombatWait", 1);

IF
DB_HAV_TakingIsobel_MainRoomCombatDialog(_ID)
AND
DB_DialogNPCs(_ID, S_DEN_ThiefHideoutLeader_c8ab1ca6-96bb-467e-91c9-af87bc4d3925, _)
THEN
PROC_HAV_TakingIsobel_KidnapMol();

// For now set her off-stage
PROC
PROC_HAV_TakingIsobel_KidnapMol()
THEN
PROC_Poof(S_DEN_ThiefHideoutLeader_c8ab1ca6-96bb-467e-91c9-af87bc4d3925);
SetFlag(HAV_TakingIsobel_Event_MolKidnapped_1a87fee7-163a-477c-b84d-cc0fbb74dbe2, NULL_00000000-0000-0000-0000-000000000000);
PROC_State_Progress(S_DEN_ThiefHideoutLeader_c8ab1ca6-96bb-467e-91c9-af87bc4d3925, "HAV_Mol", "Left");

// We consider it the moment a ghoul goes into combat that the kidnapping started
IF
DB_Is_InCombat(_Ghoul, _CombatID)
AND
DB_HAV_TakingIsobel_FlyingGhouls((CHARACTER)_Ghoul)
THEN
DB_HAV_TakingIsobel_MainRoomCombat(_CombatID);

IF
CombatEnded(_CombatID)
AND
DB_HAV_TakingIsobel_MainRoomCombat(_CombatID)
THEN
NOT DB_HAV_TakingIsobel_MainRoomCombat(_CombatID);
PROC_HAV_TakingIsobel_CheckInnAttackEnd();

PROC
PROC_HAV_TakingIsobel_CheckInnAttackEnd()
AND
NOT DB_HAV_TakingIsobel_MainRoomCombat(_)
THEN
PROC_ClearStoryMove(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a);
PROC_HAV_TakingIsobel_InnAttackEnded();

QRY
QRY_HAV_TakingIsobel_IsMainRoomCombat((GUIDSTRING)_CombatID)
AND
DB_HAV_TakingIsobel_MainRoomCombat(_CombatID)
THEN
DB_NOOP(1);

//Make harpers surprised on combat starting
IF
FlagSet(HAV_TakingIsobel_Event_SpyCombatStarted_b0930689-e539-424e-bc92-e4a37fbee7d5, _, _)
AND
QRY_OnlyOnce("HAV_TakingIsobel_IsobelCombatStarted")
THEN
PROC_HAV_TakingIsobel_GhoulCombatSurprise();

PROC
PROC_HAV_TakingIsobel_GhoulCombatSurprise()
AND
DB_HAV_TakingIsobel_KOParticipants((CHARACTER)_Character)
THEN
PROC_MakeSurprised(_Character, 1, 1);

PROC
PROC_HAV_TakingIsobel_GhoulCombatSurprise()
AND
DB_HAV_TakingIsobel_SurpriseParticipants((CHARACTER)_Character)
THEN
PROC_MakeSurprised(_Character, 1, 1);

//END_REGION

//REGION Scene Manager handling
IF
DialogEnded(HAV_TakingIsobel_SpyCapture_1b698557-76c0-1166-6415-91fadb358985, _)
AND
DB_SceneManager(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "HAV_IsobelMarcusScene")
THEN
PROC_SceneOver("HAV_IsobelMarcusScene");


PROC
PROC_SceneInterrupted(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, _Player, "HAV_IsobelMarcusScene", "Attacked") 
AND
DB_PartOfTheTeam(_Player)
THEN
PROC_GlobalClearFlagAndCache(HAV_TakingIsobel_State_SidedWithIsobel_52395bbc-a58d-44cd-bd40-8069c776875b);
PROC_GlobalSetFlagAndCache(HAV_TakingIsobel_State_SidedWithSpy_5eb745b2-5043-2f36-4d3a-ee3f09ed7a82);


PROC
PROC_SceneInterrupted(S_HAV_FlamingSpy_acb1dd9e-952b-4281-8e30-ec6f671493a6, _Player, "HAV_IsobelMarcusScene", "Attacked") 
AND
DB_PartOfTheTeam(_Player)
THEN
PROC_GlobalClearFlagAndCache(HAV_TakingIsobel_State_SidedWithSpy_5eb745b2-5043-2f36-4d3a-ee3f09ed7a82);
PROC_GlobalSetFlagAndCache(HAV_TakingIsobel_State_SidedWithIsobel_52395bbc-a58d-44cd-bd40-8069c776875b);


PROC
PROC_SceneInterrupted(_, _Player, "HAV_IsobelMarcusScene", "Attacked")
AND
DB_PartOfTheTeam(_Player)
THEN
DB_HAV_TakingIsobel_Interrupted(1);
PROC_SceneOver("HAV_IsobelMarcusScene");
PROC_ForceStopDialog(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211);
PROC_GlobalSetFlagAndCache(HAV_TakingIsobel_Event_SpawnGhouls_e8d0fd32-5eff-4101-bb7c-6d2e1acd4ba4);
PROC_HAV_TakingIsobel_SpyTransform();

IF
CombatPaused(_CombatID)
AND
DB_HAV_TakingIsobel_MainRoomCombat(_CombatID)
AND
DB_HAV_TakingIsobel_MainRoomCombatDialog(_Dialog)
AND
DB_HAV_TakingIsobel_MainRoomCombat_Paused(00000000-0000-0000-0000-000000000000)
AND
DB_HAV_TakingIsobel_Interrupted(1)
THEN
NOT DB_HAV_TakingIsobel_MainRoomCombatDialog(_Dialog);
NOT DB_HAV_TakingIsobel_MainRoomCombat_Paused(00000000-0000-0000-0000-000000000000);
ResumeCombat(_CombatID);


//END_REGION
QRY
QRY_CorpseLooting_BlockMakeOwned(_FlyingGhoul)
AND
DB_HAV_TakingIsobel_FlyingGhouls(_FlyingGhoul)
THEN
DebugText(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211, "Flying ghoul is lootable");
DB_NOOP(1);

PROC
PROC_HAV_TakingIsobel_InnAttackEnded()
AND
DB_HAV_TakingIsobel_GhoulSpiked(_Character, _CanFight)
THEN
RemoveStatus(_Character,  "HAV_GHOULSPIKED", NULL_00000000-0000-0000-0000-000000000000);
SetCanFight(_Character, _CanFight);
NOT DB_HAV_TakingIsobel_GhoulSpiked(_Character, _CanFight);

PROC
PROC_HAV_TakingIsobel_InnAttackEnded()
AND
DB_GlobalFlag((FLAG)HAV_TakingIsobel_State_DefendedIsobel_0d0255e4-dd08-47d7-9b40-cce21ec4a5bd)
THEN
SetFlag(HAV_TakingIsobel_State_AbductionCombatOver_b0bb8648-79a1-4b4c-a0ea-822bcc0cd917, NULL_00000000-0000-0000-0000-000000000000, 0);

PROC
PROC_HAV_TakingIsobel_InnAttackEnded()
THEN
ClearTag(S_Player_Jaheira_91b6b200-7d00-4d62-8dc9-99e8339dfa1a, (TAG)AI_UNPREFERRED_TARGET_9787450d-f34d-43bd-be88-d2bac00bb8ee);
ClearTag(S_GLO_Isobel_263bfbfc-6160-46f4-a9e1-1089cdb5c211,(TAG)AI_PREFERRED_TARGET_64bc9da1-9262-475a-a397-157600b7debd);
PROC_State_Progress(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", "HAV_Mood_State_Alert");
GoalCompleted;
EXITSECTION
SysClear("DB_HAV_TakingIsobel_KOParticipants", 2);
ENDEXITSECTION
ParentTargetEdge "Act2_HAV_TakingIsobel"
