Version 1
SubGoalCombiner SGC_AND
INITSECTION
//Moonrise's Rooftop, aka Chapel.
//Relates to situations: 'Audience with the Absolute' and 'Assault on Moonrise'
//Check goal Act2_MOO_KethericMisc

DB_Dialogs(S_MOO_RoofNecromite_002_0d759ded-13de-4e24-ad3b-cae83879b4c9, MOO_Audience_Skeletons_09b672de-bb84-8b0d-ae89-424ea6a4af9d);
DB_Dialogs(S_MOO_RoofNecromite_001_97c8dc13-5a94-48c2-b2d1-2bb3c1d4dd40, MOO_Audience_Skeletons_09b672de-bb84-8b0d-ae89-424ea6a4af9d);
DB_Dialogs(S_MOO_RoofNecromite_004_ee6ea72e-8a85-445f-8b48-461b4d9d5418, MOO_Audience_Skeletons_09b672de-bb84-8b0d-ae89-424ea6a4af9d);
DB_Dialogs(S_MOO_RoofNecromite_003_02edce28-9ce1-4905-a776-fd8b50ae0dcc, MOO_Audience_Skeletons_09b672de-bb84-8b0d-ae89-424ea6a4af9d);
DB_Dialogs(S_MOO_RoofNecromite_008_afd14856-feff-42be-9b6c-402889f45c13, MOO_Audience_Skeletons_09b672de-bb84-8b0d-ae89-424ea6a4af9d);
DB_Dialogs(S_MOO_RoofNecromite_005_242237d0-a129-48a6-a868-3b9211138de5, MOO_Audience_Skeletons_09b672de-bb84-8b0d-ae89-424ea6a4af9d);
DB_Dialogs(S_MOO_RoofNecromite_006_48279220-f514-48a7-8084-c8b8eb3629ff, MOO_Audience_Skeletons_09b672de-bb84-8b0d-ae89-424ea6a4af9d);
DB_Dialogs(S_MOO_RoofNecromite_007_a1fbcddf-cf6e-4222-a022-0fd12d099ce2, MOO_Audience_Skeletons_09b672de-bb84-8b0d-ae89-424ea6a4af9d);
//DB_Dialogs(S_MOO_RoofAltar_a4363e38-0d63-4cca-8fba-18374fe12dd9, MOO_Audience_Altar_ffc48fe4-842d-489b-adb3-03e7b6cedde5);

DB_OneShot_VoiceBarkTrigger(S_MOO_RoofVistaVBSphere_42433c80-fcf3-4ccb-9631-21509c51ae9e, MOO_Audience_VB_TownVista_9ae9ebbf-692f-3db8-5189-e48e3ffa46a6);

//TODO: make them uninteractive/offstage
DB_MOO_Roof_ColonyTeleporters((ITEM)S_MOO_RoofAltar_e0c3ef2e-c056-4131-b9fc-8bf0243ad2cd, (DIALOGRESOURCE)MOO_Audience_Altar_ffc48fe4-842d-489b-adb3-03e7b6cedde5, "MOO_Audience_ReadyCheck", 1);
DB_MOO_Roof_ColonyTeleporters((ITEM)S_MOO_TentaclelessTeleporter_c29cec63-82da-4579-b11d-75a5a08bbe1b, (DIALOGRESOURCE)MOO_Assault_TentaclelessJump_671ddd7b-35dc-5f76-e5cc-b82afb118fa2, "MOO_Assault_ReadyCheck", 0);

SetCanInteract(S_MOO_RoofAltar_e0c3ef2e-c056-4131-b9fc-8bf0243ad2cd, 0);
SetOnStage(S_MOO_TentaclelessTeleporter_c29cec63-82da-4579-b11d-75a5a08bbe1b, 0);

DB_DialogStarted_IgnoreStopConditions(MOO_Assault_TentaclelessJump_671ddd7b-35dc-5f76-e5cc-b82afb118fa2);
DB_DialogBlockTradeButton(MOO_Assault_TentaclelessJump_671ddd7b-35dc-5f76-e5cc-b82afb118fa2);
DB_IgnoreMutingStatussesDialog(MOO_Assault_TentaclelessJump_671ddd7b-35dc-5f76-e5cc-b82afb118fa2);

DB_DialogStarted_IgnoreStopConditions(MOO_Audience_Altar_ffc48fe4-842d-489b-adb3-03e7b6cedde5);
DB_DialogBlockTradeButton(MOO_Audience_Altar_ffc48fe4-842d-489b-adb3-03e7b6cedde5);
DB_IgnoreMutingStatussesDialog(MOO_Audience_Altar_ffc48fe4-842d-489b-adb3-03e7b6cedde5);
//REGION Myrkulites
//---> used in Act2_MOO_Assault_BoosFight
//Necromite
DB_MOO_Roof_Myrkulites((CHARACTER)S_MOO_RoofNecromite_001_97c8dc13-5a94-48c2-b2d1-2bb3c1d4dd40);
DB_MOO_Roof_Myrkulites((CHARACTER)S_MOO_RoofNecromite_002_0d759ded-13de-4e24-ad3b-cae83879b4c9);
DB_MOO_Roof_Myrkulites((CHARACTER)S_MOO_RoofNecromite_003_02edce28-9ce1-4905-a776-fd8b50ae0dcc);
DB_MOO_Roof_Myrkulites((CHARACTER)S_MOO_RoofNecromite_004_ee6ea72e-8a85-445f-8b48-461b4d9d5418);
DB_MOO_Roof_Myrkulites((CHARACTER)S_MOO_RoofNecromite_005_242237d0-a129-48a6-a868-3b9211138de5);
DB_MOO_Roof_Myrkulites((CHARACTER)S_MOO_RoofNecromite_006_48279220-f514-48a7-8084-c8b8eb3629ff);
DB_MOO_Roof_Myrkulites((CHARACTER)S_MOO_RoofNecromite_007_a1fbcddf-cf6e-4222-a022-0fd12d099ce2);
DB_MOO_Roof_Myrkulites((CHARACTER)S_MOO_RoofNecromite_008_afd14856-feff-42be-9b6c-402889f45c13);
//Master of souls
DB_MOO_Roof_Myrkulites((CHARACTER)S_MOO_RoofCultist_ebfd7bc7-0fd5-4dee-8fd5-308ded4d0a65);

LoadLevelTemplate(LT_PLT_Moo_Tower_A_198721e9-46b7-490b-92f0-187953596539);
//END_REGION
KBSECTION
//REGION DEBUG
IF
TextEvent("mooswaptower_broken")
THEN
UnloadLevelTemplate(LT_PLT_Moo_Tower_A_198721e9-46b7-490b-92f0-187953596539);
UnloadLevelTemplate(LT_PLT_Moo_Chapel_A_7be4a120-79a2-413f-9ff0-4f45b61e923a);
LoadLevelTemplate(LT_PLT_Moo_Tower_Broken_A_2356dc00-6d00-4138-b2f3-a46335d49fcf);
LoadLevelTemplate(LT_PLT_Moo_Chapel_Broken_A_a3b01ac3-c6d4-466c-8185-f516b94e859a);

IF
TextEvent("mooswaptower_fixed")
THEN
LoadLevelTemplate(LT_PLT_Moo_Tower_A_198721e9-46b7-490b-92f0-187953596539);
LoadLevelTemplate(LT_PLT_Moo_Chapel_A_7be4a120-79a2-413f-9ff0-4f45b61e923a);
UnloadLevelTemplate(LT_PLT_Moo_Tower_Broken_A_2356dc00-6d00-4138-b2f3-a46335d49fcf);
UnloadLevelTemplate(LT_PLT_Moo_Chapel_Broken_A_a3b01ac3-c6d4-466c-8185-f516b94e859a);
//END_REGION
//----------- TELEPORTING TO COLONY -----------//
//REGION Setting interaction

// Audience -> interact with Altar
PROC
PROC_State_Changed(S_MOO_MoonriseTower_SUB_14187ad9-cf83-44f9-81bf-bb46cb4cd8e6, "MOO", "MOO_State_Audience")
THEN
SetCanInteract(S_MOO_RoofAltar_e0c3ef2e-c056-4131-b9fc-8bf0243ad2cd, 1);


// else -> interact with 'hole' (also accessible through the Audience if Ketheric is attacked on that path)
PROC
PROC_MOO_Assault_KethericLeaves()
THEN
SetCanInteract(S_MOO_RoofAltar_e0c3ef2e-c056-4131-b9fc-8bf0243ad2cd, 0);
SetOnStage(S_MOO_TentaclelessTeleporter_c29cec63-82da-4579-b11d-75a5a08bbe1b, 1);


//END_REGION

//REGION Altar/Jump Teleport
//If Audience and player snuck around to the altar, start dialog with Ketheric
PROC
PROC_BlockUseOfItem(_Player, S_MOO_RoofAltar_e0c3ef2e-c056-4131-b9fc-8bf0243ad2cd)
AND
DB_Players((CHARACTER)_Player)
AND
QRY_SpotPlayers_IsSpotting((CHARACTER)S_MOO_Ketheric_e9918f3e-5b87-40a3-a9bd-61545151573f, "MOO_Ketheric_AudienceSpotting")
THEN
DB_CustomUseItemResponse(_Player, S_MOO_RoofAltar_e0c3ef2e-c056-4131-b9fc-8bf0243ad2cd, 0);
PROC_MOO_Audience_StartAudience(_Player);

IF
UseStarted(_Player, _Item)
AND
DB_MOO_Roof_ColonyTeleporters(_Item, _, _ReadyCheckMessage, _)
AND
QRY_MOO_Roof_ColonyTeleporters_CantUse(_Player, _Item)
THEN
PROC_PlayCantUseItemAD(_Player);
OpenMessageBox(_Player,"ReadyCheck_PartyMemberNotReady");

IF
UseStarted(_Player, _Item)
AND
DB_MOO_Roof_ColonyTeleporters(_Item, _, _ReadyCheckMessage, _)
AND
NOT QRY_MOO_Roof_ColonyTeleporters_CantUse(_Player, _Item)
THEN
ReadyCheckGlobal(_ReadyCheckMessage, _ReadyCheckMessage, 1, (CHARACTER)_Player);
DB_MOO_Roof_ColonyTeleporterActivator((CHARACTER)_Player, _ReadyCheckMessage);

QRY
QRY_MOO_Roof_ColonyTeleporters_CantUse((CHARACTER)_Player, (ITEM)_Item)
AND
NOT QRY_SpeakerIsAvailable(_Player)
THEN
DB_NOOP(1);

QRY
QRY_MOO_Roof_ColonyTeleporters_CantUse((CHARACTER)_, (ITEM)_)
AND
DB_Players(_Player)
AND
DB_CantMove(_Player)
THEN
DB_NOOP(1);

//See Act2_OriginMoments_Shadowheart - SHA - Shar's Wrath
QRY
QRY_MOO_Roof_ColonyTeleporters_CantUse((CHARACTER)_, (ITEM)_)
AND
DB_Players(S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679)
AND
DB_InRegion(S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679,S_SHA_NightsongPrison_SUB_0a302268-dd92-4462-99e5-ca7491815ec0)
THEN
DB_NOOP(1);

QRY
QRY_MOO_Roof_ColonyTeleporters_CantUse((CHARACTER)_Player, (ITEM)S_MOO_RoofAltar_e0c3ef2e-c056-4131-b9fc-8bf0243ad2cd)
AND
NOT QRY_SpeakerIsAvailable(S_MOO_Ketheric_e9918f3e-5b87-40a3-a9bd-61545151573f)
THEN
DB_NOOP(1);

QRY
QRY_MOO_Roof_ColonyTeleporters_CantUse(_Player, _)
AND
DB_MOO_Roof_ColonyTeleporterActivator(_, _)
THEN
DB_NOOP(1);

IF
DB_MOO_Roof_ColonyTeleporterActivator(_Player, _ReadyCheckMessage)
AND
DB_CantTalk(_Player)
THEN
NOT DB_MOO_Roof_ColonyTeleporterActivator(_Player, _ReadyCheckMessage);
ReadyCheckCancel(_ReadyCheckMessage);

IF
DB_MOO_Roof_ColonyTeleporterActivator(_Player, "MOO_Audience_ReadyCheck")
AND
DB_CantTalk(S_MOO_Ketheric_e9918f3e-5b87-40a3-a9bd-61545151573f)
THEN
NOT DB_MOO_Roof_ColonyTeleporterActivator(_Player, "MOO_Audience_ReadyCheck");
ReadyCheckCancel( "MOO_Audience_ReadyCheck");

IF
ReadyCheckFailed(_ReadyCheckMessage)
AND
DB_MOO_Roof_ColonyTeleporterActivator(_Player, _ReadyCheckMessage)
THEN
NOT DB_MOO_Roof_ColonyTeleporterActivator(_Player, _ReadyCheckMessage);

//END_REGION

// Teleport to COL
IF
ReadyCheckPassed(_ReadyCheckMessage)
AND
DB_MOO_Roof_ColonyTeleporterActivator(_Player, _ReadyCheckMessage)
AND
DB_MOO_Roof_ColonyTeleporters(_Item, _Dialog, _ReadyCheckMessage, _IsAudience)
THEN
NOT DB_MOO_Roof_ColonyTeleporterActivator(_Player, _ReadyCheckMessage);
DB_MOO_Roof_ActiveColonyTeleporterDialog(_Dialog, _IsAudience);
PROC_SceneOver("MOO_Audience_Outro");
PROC_MOO_Roof_TryTeleportDialog((CHARACTER)_Player);

PROC
PROC_MOO_Roof_TryTeleportDialog((CHARACTER)_)
AND
DB_MOO_Roof_ActiveColonyTeleporterDialog(_, 1)
THEN
PROC_SceneOver("SHA_Necromancer_MeetingNecromancer");
PROC_State_Progress(S_SHA_Necromancer_53651a9f-7ea8-444f-ba2d-224390b72f7d, "SHA_Necromancer", "SHA_Necromancer_State_AtMoonrise");

//Prefer nearby Avatar
PROC
PROC_MOO_Roof_TryTeleportDialog((CHARACTER)_Player)
AND
QRY_GetClosestAvailableCharacterTo(_Player, 0)
AND
DB_ClosestAvailableCharacterTo_PreferAvatar(_Avatar,_Player,_Dist,_)
AND
_Dist <= 15.0
AND
QRY_MOO_Roof_StartTeleportDialog((CHARACTER)_Avatar)
THEN
DB_NOOP(1);

//Try initiator
PROC
PROC_MOO_Roof_TryTeleportDialog((CHARACTER)_Player)
AND
NOT DB_MOO_Roof_TeleportDialogStarted(1)
AND
QRY_MOO_Roof_StartTeleportDialog((CHARACTER)_Player)
THEN
DB_NOOP(1);

//Try Nearest Player on roof (avatar or not)
PROC
PROC_MOO_Roof_TryTeleportDialog((CHARACTER)_Player)
AND
QRY_MOO_Roof_GetNearestAvailablePlayer(_Player)
AND
DB_QRYRTN_MOO_Roof_NearestPlayer(_NearestPlayer, _Dist)
AND
NOT DB_MOO_Roof_TeleportDialogStarted(1)
AND
QRY_MOO_Roof_StartTeleportDialog((CHARACTER)_Player)
THEN
DB_NOOP(1);

//Try Any Player in Moonrise (getting desperate)
PROC
PROC_MOO_Roof_TryTeleportDialog((CHARACTER)_)
AND
DB_Players(_Player)
AND
DB_InRegion(_Player, S_MOO_MoonriseTower_SUB_14187ad9-cf83-44f9-81bf-bb46cb4cd8e6)
AND
NOT DB_Defeated(_Player)
AND
NOT DB_MOO_Roof_TeleportDialogStarted(1)
AND
QRY_MOO_Roof_StartTeleportDialog((CHARACTER)_Player)
THEN
DB_NOOP(1);

//Something bad happened and no one was available!
PROC
PROC_MOO_Roof_TryTeleportDialog((CHARACTER)_Player)
AND
NOT DB_MOO_Roof_TeleportDialogStarted(1)
THEN
DB_MOO_Roof_FailedToFindAnyAvailableSpeakerForTeleport(1);
PROC_MOO_Roof_SendPlayersToColony();

IF
DB_MOO_Roof_FailedToFindAnyAvailableSpeakerForTeleport(1)
THEN
DB_NOOP(1);

QRY
QRY_MOO_Roof_GetNearestAvailablePlayer((CHARACTER)_Char)
AND
DB_Players(_Player)
AND
NOT DB_Defeated(_Player)
AND
DB_InRegion(_Player, S_MOO_Roof_SUB_77c94e75-2924-4ca3-b7bd-d631b18d6c73)
AND
GetDistanceTo(_Player, _Char, _Dist)
THEN
DB_QRYRTN_MOO_Roof_NearestPlayer(_Player, _Dist);

IF
DB_QRYRTN_MOO_Roof_NearestPlayer(_Player1, _Dist1)
AND
DB_QRYRTN_MOO_Roof_NearestPlayer(_Player2, _Dist2)
AND
_Player2 != _Player1
AND
_Dist2 >= _Dist1
THEN
NOT DB_QRYRTN_MOO_Roof_NearestPlayer(_Player2, _Dist2);

QRY
QRY_MOO_Roof_StartTeleportDialog((GUIDSTRING)_Player)
AND
DB_MOO_Roof_ActiveColonyTeleporterDialog(_Dialog, 0)
AND
QRY_StartDialogCustom_Fixed(_Dialog, S_MOO_TentaclelessTeleporter_c29cec63-82da-4579-b11d-75a5a08bbe1b, _Player, 0, 0, 0, 0)
THEN
DB_MOO_Roof_TeleportDialogStarted(1);

QRY
QRY_MOO_Roof_StartTeleportDialog((GUIDSTRING)_Player)
AND
DB_MOO_Roof_ActiveColonyTeleporterDialog(_Dialog, 1)
AND
QRY_StartDialogCustom_Fixed(_Dialog, S_MOO_RoofAltar_e0c3ef2e-c056-4131-b9fc-8bf0243ad2cd, S_MOO_Ketheric_e9918f3e-5b87-40a3-a9bd-61545151573f, S_SHA_Necromancer_53651a9f-7ea8-444f-ba2d-224390b72f7d, _Player, 0, 0, 0, 0)
THEN
DB_MOO_Roof_TeleportDialogStarted(1);

PROC
PROC_StartDialog_AddExtraSpeakers(_Dialog, _ID)
AND
DB_MOO_Roof_ActiveColonyTeleporterDialog(_Dialog, _)
AND
DB_Players(_Player)
AND
NOT DB_DialogPlayers(_ID,_Player,_)
THEN
PROC_DialogAddSpeakingActor(_ID, _Player, 1);

PROC
PROC_StartDialog_AddExtraSpeakers(_Dialog, _ID)
AND
DB_DialogName(_Dialog, _ID)
AND
DB_MOO_Roof_ActiveColonyTeleporterDialog(_Dialog, _)
AND
DB_PartyMembers(_Char)
AND
NOT DB_Players(_Char)
AND
NOT DB_DialogPlayers(_ID,_Char,_)
THEN
PROC_DialogAddListeningActor(_ID, _Char);

IF
DialogEnded(_Dialog, _ID)
AND
DB_MOO_Roof_ActiveColonyTeleporterDialog(_Dialog, _IsAudience)
THEN
PROC_MOO_Roof_SendPlayersToColony();

PROC
PROC_MOO_Roof_SendPlayersToColony()
AND
DB_MOO_AssaultPositions(_Char, _, _CombatGroup)
AND
GetCombatGroupID(_Char, _CombatGroup)
THEN
SetCombatGroupID(_Char, "");

PROC
PROC_MOO_Roof_SendPlayersToColony()
THEN
NOT DB_MOO_Roof_TeleportDialogStarted(1);
SysCompleteGoal("Act2_MOO_Assault");
ClearFlag(MOO_Assault_State_InProgress_0f3a8f5d-7402-4220-bebb-d4b21d3db08d);
SysCompleteGoal("Act2_MOO_Audience");

PROC
PROC_MOO_Roof_SendPlayersToColony()
AND
DB_MOO_Roof_ActiveColonyTeleporterDialog(_, _IsAudience)
AND
QRY_OnlyOnce("MOO_Roof_SendPlayersToColonyOnce")
THEN
PROC_COL_TeleportPartiesToColony(_IsAudience);

PROC
PROC_COL_TeleportPartiesToColony(_)
THEN
PROC_MOO_Jailbreak_KillRemainingPrisoners();

PROC
PROC_COL_TeleportPartiesToColony(_)
THEN
SetCanInteract(S_MOO_RoofAltar_e0c3ef2e-c056-4131-b9fc-8bf0243ad2cd, 0);
SetOnStage(S_MOO_TentaclelessTeleporter_c29cec63-82da-4579-b11d-75a5a08bbe1b, 0);
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
