Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION Forging of the Heart Act 2
DB_CharacterInRegionFlag((CHARACTER)S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79, (TRIGGER)S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, (FLAG)GLO_ForgingOfTheHeart_State_DammonInAct2_13d137f7-44ec-4217-8254-42691f3a8f63);

PROC_DefineSingleOriginMoment((DIALOGRESOURCE)HAV_TieflingSurvivors_Weaponsmith_c2312421-411c-757e-551b-3e7ddbb9d7e7, (TAG)REALLY_KARLACH_1a2f70d6-8ead-4eb5-a824-79ee1971764a, (DIALOGRESOURCE)GLO_ForgingOfTheHeart_OM_Karlach_AOM_OOM_GLO_ForgingOfTheHeart_OM_Karlach_AOM_OOM_COM_e31f3936-c198-cf82-bd8f-e26efba691f1);

PROC_HAV_ForgingOfTheHeart_Init();
//END_REGION Forging of the Heart Act 2

//REGION Tiefling Kids
PROC_DefineSingleOriginMoment((DIALOGRESOURCE)HAV_TieflingSurvivors_Mattis_4e74776f-1483-ee03-ed6a-3baae7fe5979, (TAG)KARLACH_9aab4bcd-e1f7-4008-8f7b-f1203789ae9b, (DIALOGRESOURCE)HAV_TieflingKids_OM_Karlach_AOM_OOM_d29e791f-5c42-3620-b821-f43007cbf70c, (DIALOGRESOURCE)HAV_TieflingKids_OM_Karlach_COM_3c50973f-8938-7c0f-9851-b2d1039a9319, (DIALOGRESOURCE)HAV_TieflingKids_OM_Karlach_AOM_OOM_d29e791f-5c42-3620-b821-f43007cbf70c);
//END_REGION Tiefling Kids

//REGION Mizora events
DB_ORI_Karlach_InPartyOMFlags((FLAG)ORI_Karlach_IPRD_WyllDiedAfterKillingMizora_545c24ba-e6f7-431f-af3d-484b08985ec4, 	(FLAG)ORI_Karlach_State_WyllDiedInColony_056ba9d1-010e-4b73-9eca-1fbfc42b1282);
//END_REGION

//REGION Bazaar Soul Coins
PROC_DefineSingleOriginMoment((DIALOGRESOURCE)MOO_BugBearVendor_31587a41-84c1-4bf2-ead6-ced25ccb1f03, (TAG)KARLACH_9aab4bcd-e1f7-4008-8f7b-f1203789ae9b, (DIALOGRESOURCE)MOO_MoonriseBazarSoulCoins_OM_Karlach_AOM_OOM_b4af5865-401c-2980-7b0c-84b14d2147a0, (DIALOGRESOURCE)MOO_MoonriseBazarSoulCoins_OM_Karlach_COM_2fdf81bd-f3a1-632c-52ad-0303db817b49, (DIALOGRESOURCE)MOO_MoonriseBazarSoulCoins_OM_Karlach_AOM_OOM_b4af5865-401c-2980-7b0c-84b14d2147a0);
DB_GiveTemplateFromNpcToPlayerDialogEvent((ITEMROOT)LOOT_SoulCoin_8c68e68d-96d4-45d6-804c-5f31ac948ff3, (FLAG)ORI_MoonriseBazarSoulCoins_Event_GiveSoulCoin_2089b8e7-2483-4bc0-abae-6fdb2bbe6fda,(FLAG)NULL_00000000-0000-0000-0000-000000000000, 1);
DB_HasItemTemplateScriptFlag(1, (DIALOGRESOURCE)MOO_MoonriseBazarSoulCoins_OM_Karlach_AOM_OOM_b4af5865-401c-2980-7b0c-84b14d2147a0, (ITEMROOT)LOOT_SoulCoin_8c68e68d-96d4-45d6-804c-5f31ac948ff3, 1, 1);
DB_HasItemTemplateScriptFlag(1, (DIALOGRESOURCE)MOO_MoonriseBazarSoulCoins_OM_Karlach_COM_2fdf81bd-f3a1-632c-52ad-0303db817b49, (ITEMROOT)LOOT_SoulCoin_8c68e68d-96d4-45d6-804c-5f31ac948ff3, 1, 1);

//END_REGION Bazaar Soul Coins

//REGION Gortash in COL
DB_ExclamationDialog_NeverStop((DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_GortashInColony_80314a56-b334-48fe-9055-54bcd2efc5b6);
DB_RelationshipDialog_WRD_TriggerInCamp((DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_GortashInColony_80314a56-b334-48fe-9055-54bcd2efc5b6);
//END_REGION Gortash in COL

//REGION Misc
DB_SCL_Karlach_CancelComplainCursePADTriggers((TRIGGER)S_MOO_MoonriseTower_SUB_14187ad9-cf83-44f9-81bf-bb46cb4cd8e6);
DB_SCL_Karlach_CancelComplainCursePADTriggers((TRIGGER)S_TWN_MainSub_169edf4a-bd51-4964-adb0-4d47956d5fae);
DB_SCL_Karlach_CancelComplainCursePADTriggers((TRIGGER)S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab);

DB_SCL_Karlach_ComplainAboutCurseTriggers((TRIGGER)S_SCL_Karlach_ComplainAboutCurse_000_7860ad6d-eb42-43ce-8ef6-60835db1e2c9);
DB_SCL_Karlach_ComplainAboutCurseTriggers((TRIGGER)S_SCL_Karlach_ComplainAboutCurse_001_06abceaf-2a39-4ef8-b64e-eea31d98a88d);

DB_GlobalFlagReactionAfterDialog((FLAG)GLO_Gortash_Knows_IsLord_cb144b9c-5cb9-4f3d-9b2d-26320521d4a2, (DIALOGRESOURCE)HAV_Florrick_97692dad-7127-170b-00ce-9ee3a08b1df4);


//END_REGION
KBSECTION
//REGION Forging of the Heart Act 2
QRY
QRY_OriginMoment_PreventRelaunchDialog(GLO_ForgingOfTheHeart_OM_Karlach_AOM_OOM_GLO_ForgingOfTheHeart_OM_Karlach_AOM_OOM_COM_e31f3936-c198-cf82-bd8f-e26efba691f1, S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79, _Player)
THEN
DB_NOOP(1);

// If Karlach got the upgrade in Act 1 unlock the second from the start.
PROC
PROC_HAV_ForgingOfTheHeart_Init()
AND
DB_GlobalFlag((FLAG)GLO_ForgingOfTheHeart_State_KarlachUpgraded_a818e2f5-9e0c-4ab3-8c1e-00765d3b892f)
THEN
SetFlag((FLAG)HAV_ForgingOfTheHeart_State_ReadyForSecondUpgrade_edce0ffd-21d1-7a6c-2b55-a3e749d06fca, NULL_00000000-0000-0000-0000-000000000000);

//END_REGION Forging of the Heart Act 2

//REGION Gortash in COL
// If players reach the end of COL and Karlach is part of the team enable her branch.
IF
DialogStarted((DIALOGRESOURCE)COL_KethericShowdown_ChosenThree_94ef9fa6-729b-d34a-623a-74a8c3ba187f, _)
AND
DB_PartOfTheTeam((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
SetFlag((FLAG)ORI_Karlach_Knows_GortashInColony_32199c63-86f7-4002-bd16-32ae5627249c, NULL_00000000-0000-0000-0000-000000000000);

IF
DialogStarted((DIALOGRESOURCE)COL_KethericShowdown_ChosenThree_94ef9fa6-729b-d34a-623a-74a8c3ba187f, _)
AND
DB_Players((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
SetFlag((FLAG)ORI_Karlach_State_WaitTillKethericsDefeat_0e46ab05-5b3d-482d-a2e8-c14b78556c21, NULL_00000000-0000-0000-0000-000000000000);

// If Karlach joins the dialog she learns about Gortash. This to avoid not setting her knowledge flag if she is muted.
IF
DialogActorJoined((DIALOGRESOURCE)COL_KethericShowdown_ChosenThree_94ef9fa6-729b-d34a-623a-74a8c3ba187f, _, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _)
THEN
SetFlag((FLAG)ORI_Karlach_Knows_GortashGrudge_65838018-f5f0-4ecf-acd7-0b6ca245a9a2, NULL_00000000-0000-0000-0000-000000000000);

PROC
PROC_COL_KethericShowdown_AfterDefeat()
THEN
SetFlag((FLAG)ORI_Karlach_Event_UnlockFacingDeath_8103a9ee-585b-4209-aaf3-63d44365323f, NULL_00000000-0000-0000-0000-000000000000);
ClearFlag((FLAG)ORI_Karlach_State_WaitTillKethericsDefeat_0e46ab05-5b3d-482d-a2e8-c14b78556c21, NULL_00000000-0000-0000-0000-000000000000);
DB_RelationshipDialog_AutostartTryOnce((DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_GortashInColony_80314a56-b334-48fe-9055-54bcd2efc5b6);
PROC_RelationshipDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_GortashInColony_80314a56-b334-48fe-9055-54bcd2efc5b6, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 0);

PROC
PROC_DialogStarted((DIALOGRESOURCE)CAMP_Karlach_CRD_FacingDeath_ac41b9da-c61c-aefc-d82f-91744f88b3e6, _)
THEN
PROC_CancelRelationshipDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_GortashInColony_80314a56-b334-48fe-9055-54bcd2efc5b6);

//For COL_Karlach_AvatarSawGortash logic, see Act2_COL_KethericShowdown

IF
DB_ORI_Karlach_TriggerGortashAvatarDialogAtCOL(1)
AND
NOT DB_CantTalk(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
PROC_ORI_Karlach_StartSawGortashAvatarDialog();

PROC
PROC_ORI_Karlach_StartSawGortashAvatarDialog()
AND
NOT QRY_StartDialog_Fixed((DIALOGRESOURCE)COL_Karlach_AvatarSawGortash_4f2d66c7-4a25-1881-c3c2-129fb77d4df9, (CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
TimerLaunch("ORI_Karlach_RetryGortashAvatarDialog", 3000);

IF
TimerFinished("ORI_Karlach_RetryGortashAvatarDialog")
AND
DB_CantTalk(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
ClearScreenFade((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c,1.0,"COL_KethericShowdown_KethericDefeated_6596d1d4-725f-6ba2-0c3f-07f3a5733b7f",1);

IF
TimerFinished("ORI_Karlach_RetryGortashAvatarDialog")
THEN
PROC_ORI_Karlach_StartSawGortashAvatarDialog();

IF
DialogStarted(COL_Karlach_AvatarSawGortash_4f2d66c7-4a25-1881-c3c2-129fb77d4df9, _)
THEN
TimerCancel("ORI_Karlach_RetryGortashAvatarDialog");
NOT DB_ORI_Karlach_TriggerGortashAvatarDialogAtCOL(1);
//END_REGION Gortash in COL

//REGION Tiefling Kids
PROC
PROC_State_Changed(S_HAV_Haven_SUB_6ac001f4-9c56-4a1b-963d-a509e158ffab, "HAV_Mood", _State)
AND
_State != "HAV_Mood_State_Protected"
THEN
PROC_ClearOriginMoment((DIALOGRESOURCE)HAV_TieflingKids_OM_Karlach_AOM_OOM_d29e791f-5c42-3620-b821-f43007cbf70c);
PROC_ClearOriginMoment((DIALOGRESOURCE)HAV_TieflingKids_OM_Karlach_COM_3c50973f-8938-7c0f-9851-b2d1039a9319);
//END_REGION Tiefling Kids

//REGION Mol's deal
IF
FlagSet((FLAG)HAV_MolsDeal_Event_KarlachPresentInFollowUp_26d84807-72f6-f559-a6a3-9bb45590d21f, _, _)
THEN
PROC_RelationshipDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_PresentDuringMolsDeal_3d793572-da93-405f-8846-81a0f1e1736e, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 0);
//END_REGION

//REGION Mizora events
IF
DialogEnded((DIALOGRESOURCE)TWN_MizorasCapture_OM_Wyll_COM_AOM_OOM_7d7fdf2d-7f48-b140-eb03-bb2e351c0bd0, _DialogID)
AND
NOT DB_DialogRequestFailed((DIALOGRESOURCE)TWN_MizorasCapture_OM_Wyll_COM_AOM_OOM_7d7fdf2d-7f48-b140-eb03-bb2e351c0bd0, _DialogID)
THEN
PROC_RelationshipDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_MizoraInTown_b8daafd5-f52e-4c19-a5bd-76ec45a6ede6, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 0);

IF
FlagSet(ORI_Karlach_Event_ClearMizorasCaptureCRDAndIPRD_3e573947-2c38-4358-e54e-63e295e17665, _, _)
THEN
PROC_CancelRelationshipDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_MizoraInTown_b8daafd5-f52e-4c19-a5bd-76ec45a6ede6);
NOT DB_CampNight_CRD((FLAG)NIGHT_Wyll_MizorasCapture_3279549a-c467-4630-bd6d-a2aafeb8a2b9, (CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_MizoraInTown_b8daafd5-f52e-4c19-a5bd-76ec45a6ede6);

PROC
PROC_ORI_Karlach_UnlockWyllsDeathBranch()
THEN
PROC_RelationshipDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_WyllDiedAfterKillingMizora_545c24ba-e6f7-431f-af3d-484b08985ec4, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 0);

//END_REGION

//REGION Bazaar Soul Coins
QRY
QRY_OriginMoment_PreventRelaunchDialog((DIALOGRESOURCE)MOO_MoonriseBazarSoulCoins_OM_Karlach_AOM_OOM_b4af5865-401c-2980-7b0c-84b14d2147a0, _, _)
THEN
DB_NOOP(1);

QRY
QRY_OriginMoment_PreventRelaunchDialog((DIALOGRESOURCE)MOO_MoonriseBazarSoulCoins_OM_Karlach_COM_2fdf81bd-f3a1-632c-52ad-0303db817b49, _, _)
THEN
DB_NOOP(1);

QRY
QRY_OM_BlockDisablingTrade((DIALOGRESOURCE)MOO_MoonriseBazarSoulCoins_OM_Karlach_AOM_OOM_b4af5865-401c-2980-7b0c-84b14d2147a0, _, _)
THEN
DB_NOOP(1);

QRY
QRY_OM_BlockDisablingTrade((DIALOGRESOURCE)MOO_MoonriseBazarSoulCoins_OM_Karlach_COM_2fdf81bd-f3a1-632c-52ad-0303db817b49, _, _)
THEN
DB_NOOP(1);
//END_REGION Bazaar Soul Coins

//REGION Misc
QRY
QRY_OM_BlockDisablingTrade((DIALOGRESOURCE)HAV_TieflingKids_OM_Karlach_AOM_OOM_d29e791f-5c42-3620-b821-f43007cbf70c, _, _)
THEN
DB_NOOP(1);

QRY
QRY_OM_BlockDisablingTrade((DIALOGRESOURCE)HAV_TieflingKids_OM_Karlach_COM_3c50973f-8938-7c0f-9851-b2d1039a9319, _, _)
THEN
DB_NOOP(1);

IF
DB_SCL_Karlach_ComplainAboutCurseTriggers(_Trigger)
THEN
DB_OneShot_ADTrigger(_Trigger, (DIALOGRESOURCE)SCL_Karlach_PAD_ComplainsAboutCurse_3df43415-28c7-5401-852e-bd7ffcaf91b9, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c);

IF
EnteredTrigger((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Trigger)
AND
DB_SCL_Karlach_CancelComplainCursePADTriggers(_Trigger)
THEN
PROC_SCL_Karlach_RemoveComplainTriggers();

IF
AutomatedDialogStarted((DIALOGRESOURCE)SCL_Karlach_PAD_ComplainsAboutCurse_3df43415-28c7-5401-852e-bd7ffcaf91b9, _)
THEN
PROC_SCL_Karlach_RemoveComplainTriggers();

PROC
PROC_SCL_Karlach_RemoveComplainTriggers()
AND
DB_SCL_Karlach_ComplainAboutCurseTriggers(_Trigger)
THEN
PROC_RemoveOneShotAD(_Trigger);

PROC
PROC_SCL_Karlach_RemoveComplainTriggers()
AND
DB_SCL_Karlach_CancelComplainCursePADTriggers(_Trigger)
THEN
NOT DB_SCL_Karlach_CancelComplainCursePADTriggers(_Trigger);

// Reacts if Mol was present in Jaheira's checkpoint and if Dammon has not been permadefeated.
IF
DialogEnded((DIALOGRESOURCE)HAV_EnteringHaven_TadpoleCheckpoint_e51408cc-4f7a-5a5f-52e8-caf1359c1d6c, _DialogID)
AND
NOT DB_PermaDefeated(S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79)
AND
DB_GlobalFlag((FLAG)HAV_EnteringHaven_State_MolVouched_ff4181d3-63ff-49ea-b26e-bc96c919119e)
AND
DB_DialogPlayers(_DialogID, _Player, _)
AND
QRY_GLO_ForgingOfTheHeart_CanPlayDammonInHavenPAD()
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Player)
AND
QRY_OnlyOnce("ORI_Karlach_MentionedDammonInHAV")
THEN
PROC_TryStartAD(GLO_ForgingOfTheHeart_PAD_DammonInHaven_17aff98d-0b62-695b-1fac-db04ee3843d2, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c);

QRY
QRY_GLO_ForgingOfTheHeart_CanPlayDammonInHavenPAD()
AND
DB_GlobalFlag((FLAG)GLO_ForgingOfTheHeart_Knows_DammonCanUpgradeKarlach_226d82fb-711b-64ac-e59a-b4361ce2204e)
THEN
DB_NOOP(1);

QRY
QRY_GLO_ForgingOfTheHeart_CanPlayDammonInHavenPAD()
AND
DB_GlobalFlag((FLAG)ORI_Karlach_State_InSearchOfMechanic_9943b467-dff2-4a15-abbc-e380e0b8f794)
THEN
DB_NOOP(1);

// Currently only happens in Act 2
PROC
PROC_GlobalFlagReactionAfterDialog((FLAG)GLO_Gortash_Knows_IsLord_cb144b9c-5cb9-4f3d-9b2d-26320521d4a2)
AND
DB_Players((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
PROC_RelationshipDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_LearnsGortashIsLord_c4a00a64-f98c-de56-3a04-425e943539f3, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 0);

//Gortash' letter in Ketheric's chamber, Karlach reads the letter.
IF
GameBookInterfaceClosed((ITEM)S_MOO_GortashLetter_a4aa779b-7871-41b5-8792-10d4a4fbb71d, (CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
QRY_OnlyOnce("ORI_Karlach_ReadsGortashLetter")
THEN
PROC_TryStartAD(MOO_Karlach_PAD_GortashLetter_3847aaf0-3335-f566-4360-7261fa3df8d6, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c);
//END_REGION Misc
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
