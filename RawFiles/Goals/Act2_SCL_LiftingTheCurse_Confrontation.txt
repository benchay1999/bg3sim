Version 1
SubGoalCombiner SGC_AND
INITSECTION
PROC_TriggerRegisterForPlayers((TRIGGER)S_SCL_OliverHouse_4286e8d8-a8f4-4cc3-be2f-0c3e937c9e40);
PROC_TriggerRegisterForParty((TRIGGER)S_SCL_OliverFight_SpottingArea_3c99c39d-c4a4-4e40-ab18-42e0918f8da6);

DB_SCL_OliverFight_PlazaNPCs((CHARACTER)S_TWN_CentralSquare_Shadows_Shadow_01_810ebf58-3b08-4664-9a52-3915f0801a8a);
DB_SCL_OliverFight_PlazaNPCs((CHARACTER)S_TWN_CentralSquare_Shadows_Shadow_02_b33cd839-9bbf-441e-bbba-a21b9ff2a33c);
DB_SCL_OliverFight_PlazaNPCs((CHARACTER)S_TWN_CentralSquare_Shadows_Shadow_03_1caca0a2-f41d-4699-a10b-4b961f39d529);
DB_SCL_OliverFight_PlazaNPCs((CHARACTER)S_TWN_CentralSquare_Shadows_ShadowCursed_01_b566eb96-bd49-4019-83f8-3769784fa12f);
DB_SCL_OliverFight_PlazaNPCs((CHARACTER)S_TWN_CentralSquare_Shadows_ShadowCursed_02_596ed5ad-ad80-45dd-9f8c-9263a70444b0);
DB_SCL_OliverFight_PlazaNPCs((CHARACTER)S_TWN_CentralSquare_Shadows_ShadowCursed_03_609fd646-31f3-47cd-acea-e9297fc7dad8);
DB_SCL_OliverFight_PlazaNPCs((CHARACTER)S_TWN_CentralSquare_Shadows_ShadowCursed_04_64b5c7f2-3635-4231-b72d-872307c9ff8d);

DB_GLO_DefeatCounter_AllDefeatedGlobalFlag("TWN_CentralSquare_Plaza", TWN_CentralSquare_State_AllPlazaEnemiesDefeated_80da8f15-038e-424f-a6b7-33205dc5b953);

// creature template, damage HP to nightdome
DB_SCL_OliverFight_PlushTemplate((CHARACTERROOT)UNI_TWN_LiftingTheCurse_Owlbear_b3a1a3c4-3d61-4b84-b1be-aa692a9d0081, 60);
DB_SCL_OliverFight_PlushSummonSpell("Shout_SCL_OliverFight_FluffyMemory");
DB_SCL_OliverFight_ShadowTemplate((CHARACTERROOT)UNI_TWN_LiftingTheCurse_Shadow_Summon_3d7f0ab6-7de5-4702-b0c7-d2e9da80af0b, 10);
DB_SCL_OliverFight_ShadowSummonSpell("Shout_SCL_OliverFight_ShadowCall");
DB_SCL_OliverFight_ShadowsLimit(15);
DB_SCL_OliverFight_MummyTemplate((CHARACTERROOT)UNI_TWN_LiftingTheCurse_MomShadow_a7eba062-92d1-4fd9-b28d-0905a51fa172, 60);
DB_SCL_OliverFight_DaddyTemplate((CHARACTERROOT)UNI_TWN_LiftingTheCurse_DadShadow_7a75ea78-27b0-44f5-9c95-0b3e62c34f46, 60);
DB_SCL_OliverFight_FamilySummonSpell("Shout_SCL_OliverFight_FamiliarMemory");

DB_SCL_OliverFight_NumberOfTurnsToSpawnPlush(1);
PROC_SetOnStage(S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5, 0);

DB_SCL_OliverFight_PlushRandomSpawnPoint((TRIGGER)NULL_00000000-0000-0000-0000-000000000000);
NOT DB_SCL_OliverFight_PlushRandomSpawnPoint((TRIGGER)NULL_00000000-0000-0000-0000-000000000000);
DB_SCL_OliverFight_PlushSpawnPoints((TRIGGER)S_SCL_OliverFight_OliversPlushSpawnPoint_001_72e679ef-f8a0-4119-88eb-64dcedd88cb4);
DB_SCL_OliverFight_PlushSpawnPoints((TRIGGER)S_SCL_OliverFight_OliversPlushSpawnPoint_002_2576d35c-653d-406e-a7a3-22d0c3e34730);
DB_SCL_OliverFight_PlushSpawnPoints((TRIGGER)S_SCL_OliverFight_OliversPlushSpawnPoint_003_fd07cbe5-dc19-43ac-a29e-6e8e9b781e67);
DB_SCL_OliverFight_MummySpawnPoint((TRIGGER)S_SCL_OliverFight_OliversMummySpawnpoint_3bf19fdb-f834-41d4-99cc-626e0f40b6b3);
DB_SCL_OliverFight_DaddySpawnPoint((TRIGGER)S_SCL_OliverFight_OliversDaddySpawnPoint_7d7b771a-fb3c-40ea-b499-ce6bb7dff3cb);

PROC_SCL_OliverFight_DefineFirstPlushSpawnPosition();

DB_SCL_OliverFight_SpawnedShadows(NULL_00000000-0000-0000-0000-000000000000);
NOT DB_SCL_OliverFight_SpawnedShadows(NULL_00000000-0000-0000-0000-000000000000);
DB_SCL_OliverFight_ShadowsSpawnPoints((TRIGGER)S_SCL_OliverFight_ShadowSpawnPoint_000_7801e60f-dbec-4285-8103-c35e8555ff28, 1);
DB_SCL_OliverFight_ShadowsSpawnPoints((TRIGGER)S_SCL_OliverFight_ShadowSpawnPoint_001_667a26a0-15d7-43a7-8ae6-bbac9aa67881, 1);
DB_SCL_OliverFight_ShadowsSpawnPoints((TRIGGER)S_SCL_OliverFight_ShadowSpawnPoint_002_5792ccc1-a175-4995-b12a-4aef2569474e, 1);
DB_SCL_OliverFight_ShadowsSpawnPoints((TRIGGER)S_SCL_OliverFight_ShadowSpawnPoint_003_01ae6e0e-0ed8-47c2-8a2a-d8d8df67015c, 1);
DB_SCL_OliverFight_ShadowsSpawnPoints((TRIGGER)S_SCL_OliverFight_ShadowSpawnPoint_004_8ac31910-77d7-4047-a196-ed26f817d1d3, 1);
DB_SCL_OliverFight_ShadowsSpawnPoints((TRIGGER)S_SCL_OliverFight_ShadowSpawnPoint_005_2f9748b1-78b9-4f58-86f0-fb0f131983dc, 2);
DB_SCL_OliverFight_ShadowsSpawnPoints((TRIGGER)S_SCL_OliverFight_ShadowSpawnPoint_006_590639cf-6c5c-40ac-b48e-27295920a928, 2);
DB_SCL_OliverFight_ShadowsSpawnPoints((TRIGGER)S_SCL_OliverFight_ShadowSpawnPoint_007_19cf8261-921f-4883-88a1-e677da274e58, 2);
DB_SCL_OliverFight_ShadowsSpawnPoints((TRIGGER)S_SCL_OliverFight_ShadowSpawnPoint_008_ecc17752-d342-4ae5-a044-d24675a6f5fc, 2);
DB_SCL_OliverFight_ShadowsSpawnPoints((TRIGGER)S_SCL_OliverFight_ShadowSpawnPoint_009_0ded5fea-31ca-4fde-a693-414ec1997dac, 2);
DB_SCL_OliverFight_ShadowsSpawnPoints((TRIGGER)S_SCL_OliverFight_ShadowSpawnPoint_010_235b56bf-91a6-4ef0-854b-2c8df7c9b638, 3);
DB_SCL_OliverFight_ShadowsSpawnPoints((TRIGGER)S_SCL_OliverFight_ShadowSpawnPoint_011_b2d4e14a-a752-40c3-9b45-798dd1128e3f, 3);
DB_SCL_OliverFight_ShadowsSpawnPoints((TRIGGER)S_SCL_OliverFight_ShadowSpawnPoint_012_e6697de5-a403-45ce-bc4a-682b0b975731, 3);
DB_SCL_OliverFight_ShadowsSpawnPoints((TRIGGER)S_SCL_OliverFight_ShadowSpawnPoint_013_af251f01-6150-450c-ac0d-90aab0486792, 3);
DB_SCL_OliverFight_ShadowsSpawnPoints((TRIGGER)S_SCL_OliverFight_ShadowSpawnPoint_014_9fbb01df-dec9-40b4-beef-ab0bbf8854f3, 3);

DB_DropMutingStatussesDialog((DIALOGRESOURCE)SCL_OliverShadow_Acceptance_f268fd19-252c-9e06-5f77-996a975ecc59);
DB_GlobalFlagReactionAfterDialog((FLAG)SCL_LiftingTheCurse_State_OliverAgreedToReturnToThaniel_3073ae92-8e87-42d1-a426-fe3e6c5e6deb, (DIALOGRESOURCE)SCL_OliverShadow_Acceptance_f268fd19-252c-9e06-5f77-996a975ecc59);


SetOnStage(S_SCL_OliverPortal_a45b9f90-0a29-4777-acc1-7f154d07f104, 0);

//Item Difficulty Changes DB
DB_Combat_DifficultyItems((ITEM)S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5,"OLIVER_DOME_HARDCORE", "HARD"); //Oliver's Nightdome
KBSECTION
//REGION Debug
IF
FlagSet((FLAG)GLO_LiftingTheCurse_Debug_OliverFledToTown_e3751b26-fe1b-4311-aebd-fd378f33c664, _, _)
THEN
PROC_GLO_LiftingTheCurse_Debug_HalsinSetUp("GLO_LiftingTheCurse_Debug_FledToTown");

PROC
PROC_GLO_LiftingTheCurse_Debug_HalsinSetUp_State("GLO_LiftingTheCurse_Debug_FledToTown")
THEN
SetFlag((FLAG)SCL_LiftingTheCurse_State_OliverFledToTown_47018478-a3ef-42cc-8334-1516c6ed40f7, NULL_00000000-0000-0000-0000-000000000000);
PROC_SCL_OliverFight_TeleportOliverToPlaza();
PROC_TeleportPartiesTo(S_SCL_OliverPortalTarget_06acac93-1308-4fa5-a666-469e051ab50e, "");
//END_REGION Debug

//REGION Halsin Knows Oliver support
IF
DialogActorJoined(SCL_OliversDiary_OliverShadow_7924b3e2-0329-7e1d-616c-6d2df0c43b68, _, S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, _)
AND
NOT DB_GlobalFlag(SCL_ShadowCurse_Knows_Oliver_Is_Thaniel_56af8bbd-eafe-3861-22a2-c253f16952e2)
THEN
PROC_GlobalSetFlagAndCache(SCL_ShadowCurse_Knows_Oliver_Is_Thaniel_56af8bbd-eafe-3861-22a2-c253f16952e2);
//END_REGION

//REGION Initial Plaza enemies
IF
DB_SCL_OliverFight_PlazaNPCs(_NPC)
THEN
DB_GLO_DefeatCounter(_NPC, "TWN_CentralSquare_Plaza");
//END_REGION

//REGION Spawning Portal for players to follow to town
IF
FlagSet((FLAG)SCL_LiftingTheCurse_State_OliverFledToTown_47018478-a3ef-42cc-8334-1516c6ed40f7, _, _)
THEN
SetOnStage(S_SCL_OliverPortal_a45b9f90-0a29-4777-acc1-7f154d07f104, 1);
MusicPlayGeneral("Explo_Theme_WMPA_Dramatic_Signal");

IF
UseFinished(_Player, S_SCL_OliverPortal_a45b9f90-0a29-4777-acc1-7f154d07f104, 1)
AND
DB_Players(_Player)
THEN
ObjectTimerLaunch(_Player, "SCL_PlayerUsedOliverPortal_ADStart", 1500, 0);

IF
ObjectTimerFinished(_, "SCL_PlayerUsedOliverPortal_ADStart")
THEN
PROC_TryStartAD(SCL_OliverShadow_AD_StartOfFight_0235ed34-cadb-5096-b73b-27c2505a3aa4, S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a);

//END_REGION

//REGION Fight Preparation
PROC
PROC_State_Changed(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, "SCL_LiftingTheCurse", "SCL_LiftingTheCurse_State_HalsinAtPostPortal")
THEN
PROC_GlobalSetFlagAndCache((FLAG)GLO_LiftingtheCurse_State_ThanielReleased_a27f02bf-29cf-48b8-887c-547ae4108384);



IF
AttackedBy(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _, _, _, _, _, _)
AND
DB_GlobalFlag((FLAG)GLO_LiftingtheCurse_State_ThanielReleased_a27f02bf-29cf-48b8-887c-547ae4108384)
AND
NOT DB_GlobalFlag((FLAG)SCL_LiftingTheCurse_State_OliverAlerted_8e9107c8-7b2d-414b-99e4-fe7996d7b163)
AND
NOT DB_GlobalFlag((FLAG)SCL_LiftingTheCurse_State_OliverFledToTown_47018478-a3ef-42cc-8334-1516c6ed40f7)
THEN
PROC_GlobalSetFlagAndCache((FLAG)SCL_LiftingTheCurse_State_OliverAlerted_8e9107c8-7b2d-414b-99e4-fe7996d7b163);
PROC_SCL_LiftingTheCurse_ChangeState_RetreatToTown();
 
PROC
PROC_SCL_LiftingTheCurse_ChangeState_RetreatToTown()
AND
DB_Players(_Player)
AND
DB_InRegion(_Player, (TRIGGER)S_SCL_OliverHouse_4286e8d8-a8f4-4cc3-be2f-0c3e937c9e40)
AND
NOT DB_OnlyOnce("SCL_LiftingTheCurse_RetreatSeen")
AND
QRY_StartDialog((DIALOGRESOURCE)SCL_OliversDiary_OliverShadow_7924b3e2-0329-7e1d-616c-6d2df0c43b68, S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _Player)
AND
QRY_OnlyOnce("SCL_LiftingTheCurse_RetreatSeen")
THEN
DB_NOOP(1);

IF
DialogEnded((DIALOGRESOURCE)SCL_OliversDiary_OliverShadow_7924b3e2-0329-7e1d-616c-6d2df0c43b68, _)
AND
DB_GlobalFlag((FLAG)SCL_LiftingTheCurse_State_OliverFledToTown_47018478-a3ef-42cc-8334-1516c6ed40f7)
THEN
PROC_State_Progress(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, "SCL_LiftingTheCurse", "SCL_LiftingTheCurse_State_HuntingForBreath");
PROC_SCL_OliverFight_TeleportOliverToPlaza();

PROC
PROC_SCL_OliverFight_TeleportOliverToPlaza()
THEN
SetStoryDisplayName(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "TWN_LiftingTheCurse_Spirit");

PROC
PROC_SCL_OliverFight_TeleportOliverToPlaza()
THEN
PROC_Poof(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a);
TeleportTo(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, S_SCL_EchoesOfOliver_OliversPosition_4da2be9f-f252-4055-815b-977c9777ec08, "SCL_OliverFight_TeleportedToPlaze", 0, 0, 0, 1, 1);

IF
EntityEvent(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "SCL_OliverFight_TeleportedToPlaze")
THEN
ApplyStatus(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "GROUNDED", -1.0, 1, NULL_00000000-0000-0000-0000-000000000000);
DB_Dialogs(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, (DIALOGRESOURCE)SCL_OliverShadow_Acceptance_f268fd19-252c-9e06-5f77-996a975ecc59);
RemoveStatus((CHARACTER)S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "INVISIBLE", NULL_00000000-0000-0000-0000-000000000000);
PROC_SetOnStage(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, 1);
SetCanJoinCombat(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, 1);
SetCanFight(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, 1);
PROC_SetRelationToPlayers((FACTION)ACT2_SCL_ShadowOliver_2b648156-0d79-4541-998d-5120fd1b22e4, 0);
ApplyStatus(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "INVULNERABLE_NOT_SHOWN", -1.0);
ApplyStatus(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "AI_HELPER_BLOCKRESOURCES", -1.0);
PROC_SCL_OliverConfrontation_ActivateNightdome();
DB_SCL_OliverConfrontation(1);
SetFlag((FLAG)SCL_LiftingTheCurse_State_OliverLeftHome_0bde59b3-ccc2-4747-8def-ad4209d418f8);

// removing old enemies from the plaza to make space for the final fight
IF
EntityEvent(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "SCL_OliverFight_TeleportedToPlaze")
AND
DB_SCL_OliverFight_PlazaNPCs(_PlazaNPC)
AND
NOT DB_Defeated(_PlazaNPC)
THEN
PROC_Poof(_PlazaNPC);

PROC
PROC_SCL_OliverConfrontation_ActivateNightdome()
AND
DB_SCL_OliverFight_MummyTemplate(_MummyTemplate, _)
AND
DB_SCL_OliverFight_DaddyTemplate(_DaddyTemplate, _)
AND
DB_SCL_OliverFight_MummySpawnPoint(_MummySpawnPoint)
AND
DB_SCL_OliverFight_DaddySpawnPoint(_DaddySpawnPoint)
AND
CreateAtObject(_MummyTemplate, _MummySpawnPoint, 0, 0, "", 1, _SpawnedMummyShadow)
AND
CreateAtObject(_DaddyTemplate, _DaddySpawnPoint, 0, 0, "", 1, _SpawnedDaddyShadow)
//AND
//DB_Is_InCombat(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _CombatID)
THEN
PROC_Foop(S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5); //TODO: Cutscene which pushes away all the players in the area
DB_SCL_OliverFight_SpawnedShadowParents(_SpawnedMummyShadow);
ApplyStatus((CHARACTER)_SpawnedMummyShadow, "SCL_OLIVER_DOME_BEAM", -1.0, 0, (ITEM)S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5);
DB_SCL_OliverFight_SpawnedShadowParents(_SpawnedDaddyShadow);
ApplyStatus((CHARACTER)_SpawnedDaddyShadow, "SCL_OLIVER_DOME_BEAM", -1.0, 0, (ITEM)S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5);

IF
EnteredLevel(_Shadow, _, "SCL_Main_A")
AND
DB_SCL_OliverFight_SpawnedShadowParents(_Shadow)
AND
GetFaction(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _Faction)
THEN
SetCombatGroupID(_Shadow, "SCL_OliverFight");
SetFaction(_Shadow, _Faction);
//END_REGION Fight Preparation

//REGION Combat
IF
AttackedBy(S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5, _Player, _, _, _, _, _)
AND
NOT DB_Is_InCombat(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _)
THEN
PROC_EnterCombat(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _Player);

IF
DB_InRegion(_PartyMember, (TRIGGER)S_SCL_OliverFight_SpottingArea_3c99c39d-c4a4-4e40-ab18-42e0918f8da6)
AND
DB_SCL_OliverConfrontation(1)
AND
NOT DB_HiddenCharacters(_PartyMember, _)
AND
NOT DB_Is_InconspicuousPartyMember(_PartyMember)
AND
NOT DB_Is_InCombat(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _)
THEN
PROC_EnterCombat(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _PartyMember);

IF
CastedSpell(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _Spell, _, _, _)
AND
DB_SCL_OliverFight_ShadowSummonSpell(_Spell)
AND
SysCount("DB_SCL_OliverFight_SpawnedShadows", 1, _SpawnedShadows)
AND
DB_SCL_OliverFight_ShadowsLimit(_ShadowsLimit)
AND
_SpawnedShadows < _ShadowsLimit
THEN
PROC_SCL_OliverFight_SpawnShadows();

PROC
PROC_SCL_OliverFight_SpawnShadows()
AND
DB_SCL_OliverFight_ShadowTemplate(_ShadowTemplate, _)
AND
Random(3, _Random)
AND
IntegerSum(_Random, 1, _IndexOneBased)
AND
DB_SCL_OliverFight_ShadowsSpawnPoints(_SpawnPoint, _IndexOneBased)
AND
CreateAtObject(_ShadowTemplate, _SpawnPoint, 0, 0, "", 1, _SpawnedShadow)
AND
DB_Is_InCombat(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _CombatID)
THEN
DB_SCL_OliverFight_SpawnedShadows(_SpawnedShadow);

IF
EnteredLevel(_SpawnedShadow, _, "SCL_Main_A")
AND
DB_SCL_OliverFight_SpawnedShadows(_SpawnedShadow)
AND
GetFaction(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _Faction)
THEN
SetCombatGroupID(_SpawnedShadow, "SCL_OliverFight");
RequestSetSwarmGroup(_SpawnedShadow, "SCL_OliverFight_Shadows");
SetFaction(_SpawnedShadow, _Faction);
ApplyStatus((CHARACTER)_SpawnedShadow, "SCL_OLIVER_DOME_BEAM", -1.0, 0, (ITEM)S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5);

IF
DB_PermaDefeated(_SpawnedShadow)
AND
DB_SCL_OliverFight_SpawnedShadows(_SpawnedShadow)
AND
DB_SCL_OliverFight_ShadowTemplate(_, _Damage)
THEN
NOT DB_SCL_OliverFight_SpawnedShadows(_SpawnedShadow);
PROC_SCL_OliverFight_DamageNightdome(_Damage, _SpawnedShadow);
//END_REGION Olivers Shadows

//REGION Olivers Plush
IF
DB_Is_InCombat(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a,_ID)
AND
DB_GlobalFlag((FLAG)SCL_LiftingTheCurse_State_OliverFledToTown_47018478-a3ef-42cc-8334-1516c6ed40f7)
AND
NOT DB_GlobalFlag((FLAG)SCL_LiftingTheCurse_State_NightdomeDestroyed_ea76f9be-6d12-441d-8c63-de999d5b3e0c)
THEN
DB_SCL_OliversDiary_ConfrontationCombatID(_ID);

IF
SwitchedCombat(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _ID, _NewID)
AND
DB_SCL_OliversDiary_ConfrontationCombatID(_ID)
THEN
NOT DB_SCL_OliversDiary_ConfrontationCombatID(_ID);
DB_SCL_OliversDiary_ConfrontationCombatID(_NewID);

IF
CombatRoundStarted(_ID,2)
AND
DB_SCL_OliversDiary_ConfrontationCombatID(_ID)
THEN
ApplyStatus(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "SCL_OLIVERFIGHT_CANSUMMONPLUSH", -1.0, 1, NULL_00000000-0000-0000-0000-000000000000);

PROC
PROC_SCL_OliverFight_DefineFirstPlushSpawnPosition()
AND
QRY_GetRandom("DB_SCL_OliverFight_PlushSpawnPoints", 1, "DB_SCL_OliverFight_PlushRandomSpawnPoint")
AND
DB_SCL_OliverFight_PlushRandomSpawnPoint((TRIGGER)_RandomSpawnPoint)
THEN
DB_SCL_OliverFight_NewPlushPosition((TRIGGER)_RandomSpawnPoint);

IF
CastedSpell(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _Spell, _, _, _)
AND
DB_SCL_OliverFight_PlushSummonSpell(_Spell)
THEN
PROC_SCL_OliverFight_SpawnPlush();

PROC
PROC_SCL_OliverFight_SpawnPlush()
THEN
//PROC_TryStartAD(SCL_OliverShadow_AD_SummoningPlush_09747fb4-10d5-e4a5-0795-584a592d6a3d, S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a);
DB_NOOP(1);

PROC
PROC_SCL_OliverFight_SpawnPlush()
AND
DB_SCL_OliverFight_NewPlushPosition(_RandomSpawnPoint)
AND
DB_SCL_OliverFight_PlushTemplate(_PlushTemplate, _)
AND
CreateAtObject(_PlushTemplate, _RandomSpawnPoint, 0, 0, "", 1, _SpawnedPlush)
AND
DB_Is_InCombat(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _CombatID)
AND
GetFaction(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _Faction)
THEN
PROC_SCL_OliveFight_UpdateCurrentPlush(_SpawnedPlush);
SetCombatGroupID(_SpawnedPlush, "SCL_OliverFight");
SetFaction(_SpawnedPlush, _Faction);
ApplyStatus((CHARACTER)_SpawnedPlush, "SCL_OLIVER_DOME_BEAM", -1.0, 0, (ITEM)S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5);

IF
EnteredLevel(_SpawnedPlush, _, "SCL_Main_A")
AND
DB_SCL_OliverFight_CurrentPlush(_SpawnedPlush)
AND
GetFaction(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _Faction)
THEN
SetCombatGroupID(_SpawnedPlush, "SCL_OliverFight");
SetFaction(_SpawnedPlush, _Faction);
ApplyStatus((CHARACTER)_SpawnedPlush, "SCL_OLIVER_DOME_BEAM", -1.0, 0, (ITEM)S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5);

PROC
PROC_SCL_OliverFight_SpawnPlush()
AND
DB_SCL_OliverFight_NewPlushPositionVFX((TRIGGER)_RandomSpawnPoint)
THEN
PROC_StopLoopEffect(_RandomSpawnPoint, "NewPlushPositionVFX");
NOT DB_SCL_OliverFight_NewPlushPositionVFX(_RandomSpawnPoint);

PROC
PROC_SCL_OliverFight_SpawnPlush()
AND
DB_SCL_OliverFight_NewPlushPosition(_RandomSpawnPoint)
THEN
NOT DB_SCL_OliverFight_NewPlushPosition(_RandomSpawnPoint);

PROC
PROC_SCL_OliveFight_UpdateCurrentPlush((GUIDSTRING)_NewPlush)
AND
DB_SCL_OliverFight_CurrentPlush(_CurrentPlush)
THEN
NOT DB_SCL_OliverFight_CurrentPlush(_CurrentPlush);

PROC
PROC_SCL_OliveFight_UpdateCurrentPlush((GUIDSTRING)_NewPlush)
THEN
DB_SCL_OliverFight_CurrentPlush(_NewPlush);

IF
DB_PermaDefeated(_Plush)
AND
DB_SCL_OliverFight_CurrentPlush(_Plush)
AND
DB_SCL_OliverFight_PlushTemplate(_, _Damage)
THEN
PROC_SCL_OliverFight_DamageNightdome(_Damage, _Plush);

IF
TurnEnded(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a)
AND
DB_SCL_OliverFight_CurrentPlush(_Plush)
AND
DB_PermaDefeated(_Plush)
AND
DB_SCL_OliverFight_PlushDeadTurnsPassed(_TurnsPassed)
AND
IntegerSum(_TurnsPassed, 1, _NewTurnsPassed)
THEN
NOT DB_SCL_OliverFight_PlushDeadTurnsPassed(_TurnsPassed);
DB_SCL_OliverFight_PlushDeadTurnsPassed(_NewTurnsPassed);
PROC_SCL_OliveFight_CheckSpawnPlushConditions();

IF
TurnStarted(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a)
AND
DB_SCL_OliverFight_CurrentPlush(_Plush)
AND
DB_PermaDefeated(_Plush)
AND
NOT DB_SCL_OliverFight_NewPlushPosition(_)
AND
QRY_GetRandom("DB_SCL_OliverFight_PlushSpawnPoints", 1, "DB_SCL_OliverFight_PlushRandomSpawnPoint")
AND
DB_SCL_OliverFight_PlushRandomSpawnPoint((TRIGGER)_RandomSpawnPoint)
AND
GetPosition(_RandomSpawnPoint, _X, _Y, _Z)
THEN
NOT DB_SCL_OliverFight_PlushRandomSpawnPoint((TRIGGER)_RandomSpawnPoint);
DB_SCL_OliverFight_NewPlushPosition(_RandomSpawnPoint);
PROC_LoopEffectAtPosition(VFX_Script_Light_Blue_01_363cc665-373b-fb22-4be5-e9efb3d7f90a, _RandomSpawnPoint, "NewPlushPositionVFX", _X, _Y, _Z, "SCL_Main_A");
DB_SCL_OliverFight_NewPlushPositionVFX((TRIGGER)_RandomSpawnPoint);


IF
TurnEnded(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a)
AND
DB_SCL_OliverFight_CurrentPlush(_Plush)
AND
DB_PermaDefeated(_Plush)
AND
NOT DB_SCL_OliverFight_PlushDeadTurnsPassed(_)
THEN
DB_SCL_OliverFight_PlushDeadTurnsPassed(1);

PROC
PROC_SCL_OliveFight_CheckSpawnPlushConditions()
AND
DB_SCL_OliverFight_PlushDeadTurnsPassed(_TurnsPassed)
AND
DB_SCL_OliverFight_NumberOfTurnsToSpawnPlush(_NumberOfTurnsToSpawnPlush)
AND
_TurnsPassed >= _NumberOfTurnsToSpawnPlush
THEN
ApplyStatus(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "SCL_OLIVERFIGHT_CANSUMMONPLUSH", -1.0, 1, NULL_00000000-0000-0000-0000-000000000000);

PROC
PROC_SCL_OliverFight_DamageNightdome((INTEGER)_Damage, (GUIDSTRING)_Summon)
AND
Exists(S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5, 1)
THEN
ApplyDamage(S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5, _Damage, "Force", _Summon);

PROC
PROC_SCL_OliverFight_DamageNightdome((INTEGER)_Damage, (GUIDSTRING)_Summon)
THEN
RemoveStatus(_Summon, "SCL_OLIVER_DOME_BEAM");
RealtimeObjectTimerLaunch(_Summon, "SCL_Oliver_RemoveSummon", 2500);

IF
ObjectTimerFinished(_Summon, "SCL_Oliver_RemoveSummon")
AND
HasAppliedStatus(_Summon, "CONTROL_UNDEAD", 1) 
THEN
DB_SCL_OliverFight_RemoveSummonOnLostControl(_Summon);

IF
ObjectTimerFinished(_Summon, "SCL_Oliver_RemoveSummon")
AND
HasAppliedStatus(_Summon, "CONTROL_UNDEAD", 0)
THEN
PROC_SCL_OliverFight_PoofSummon(_Summon);

IF
StatusRemoved(_Summon, "CONTROL_UNDEAD", _, _)
AND
DB_SCL_OliverFight_RemoveSummonOnLostControl(_Summon)
THEN
NOT DB_SCL_OliverFight_RemoveSummonOnLostControl(_Summon);
PROC_SCL_OliverFight_PoofSummon(_Summon);

PROC
PROC_SCL_OliverFight_PoofSummon((GUIDSTRING)_Summon)
AND
GetPosition(_Summon, _X, _Y, _Z)
THEN
PlayEffectAtPosition(VFX_Combat_Death_UniqueShdw_01_039ec0ce-33cb-8304-50c3-c814536ab546, _X, _Y, _Z, 1.0);
RealtimeObjectTimerLaunch(_Summon, "SCL_Oliver_RemoveSummonOffStage", 200); // Wait a bit for the VFX to kick in and hide the NPC pop.

IF
ObjectTimerFinished(_Summon, "SCL_Oliver_RemoveSummonOffStage")
THEN
PROC_SetOnStage(_Summon, 0);

IF
DB_PermaDefeated(S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5)
THEN
PROC_SCL_OliverFight_KillOliversMinions();

PROC
PROC_SCL_OliverFight_KillOliversMinions()
AND
DB_SCL_OliverFight_SpawnedShadows(_SpawnedShadow) // Don't remove them here from the DB, we want to do it when they are permadefeated
THEN
Die(_SpawnedShadow, DEATHTYPE.DoT, NULL_00000000-0000-0000-0000-000000000000, 0, 0, 1.0);

PROC
PROC_SCL_OliverFight_KillOliversMinions()
AND
DB_SCL_OliverFight_CurrentPlush(_Plush) // Don't remove plush here from the DB, we want to do it when they are permadefeated
THEN
DB_SCL_OliverFight_PlushADBlocked(1);
Die(_Plush, DEATHTYPE.DoT, NULL_00000000-0000-0000-0000-000000000000, 0, 0, 1.0);

PROC
PROC_SCL_OliverFight_KillOliversMinions()
AND
DB_SCL_OliverFight_RemoveSummonOnLostControl(_Summon)
THEN
Die(_Summon, DEATHTYPE.DoT, NULL_00000000-0000-0000-0000-000000000000, 0, 0, 1.0);

PROC
PROC_SCL_OliverFight_KillOliversMinions()
AND
DB_SCL_OliverFight_SpawnedShadowParents(_SpawnedShadowParent)
THEN
Die(_SpawnedShadowParent, DEATHTYPE.DoT, NULL_00000000-0000-0000-0000-000000000000, 0, 0, 1.0);
//END_REGION Combat

//REGION Olivers Shadow Parents
IF
CastedSpell(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _Spell, _, _, _)
AND
DB_SCL_OliverFight_FamilySummonSpell(_Spell)
AND
DB_SCL_OliverFight_MummyTemplate(_MummyTemplate, _)
AND
DB_SCL_OliverFight_DaddyTemplate(_DaddyTemplate, _)
AND
DB_SCL_OliverFight_MummySpawnPoint(_MummySpawnPoint)
AND
DB_SCL_OliverFight_DaddySpawnPoint(_DaddySpawnPoint)
AND
CreateAtObject(_MummyTemplate, _MummySpawnPoint, 0, 0, "", 1, _SpawnedMummyShadow)
AND
CreateAtObject(_DaddyTemplate, _DaddySpawnPoint, 0, 0, "", 1, _SpawnedDaddyShadow)
AND
GetFaction(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _Faction)
AND
DB_Is_InCombat(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _CombatID)
THEN
SetCombatGroupID(_SpawnedMummyShadow, "SCL_OliverFight");
SetCombatGroupID(_SpawnedDaddyShadow, "SCL_OliverFight");
SetFaction(_SpawnedMummyShadow, _Faction);
SetFaction(_SpawnedDaddyShadow, _Faction);
DB_SCL_OliverFight_SpawnedShadowParents(_SpawnedMummyShadow);
DB_SCL_OliverFight_SpawnedShadowParents(_SpawnedDaddyShadow);
ApplyStatus((CHARACTER)_SpawnedMummyShadow, "SCL_OLIVER_DOME_BEAM", -1.0, 0, (ITEM)S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5);
ApplyStatus((CHARACTER)_SpawnedDaddyShadow, "SCL_OLIVER_DOME_BEAM", -1.0, 0, (ITEM)S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5);

IF
DB_PermaDefeated(_SpawnedMummyShadow)
AND
DB_SCL_OliverFight_SpawnedShadowParents(_SpawnedMummyShadow)
AND
DB_SCL_OliverFight_MummyTemplate(_, _Damage)
THEN
NOT DB_SCL_OliverFight_SpawnedShadowParents(_SpawnedMummyShadow);
PROC_SCL_OliverFight_DamageNightdome(_Damage, _SpawnedMummyShadow);

IF
DB_PermaDefeated(_SpawnedDaddyShadow)
AND
DB_SCL_OliverFight_SpawnedShadowParents(_SpawnedDaddyShadow)
AND
DB_SCL_OliverFight_DaddyTemplate(_, _Damage)
THEN
NOT DB_SCL_OliverFight_SpawnedShadowParents(_SpawnedDaddyShadow);
PROC_SCL_OliverFight_DamageNightdome(_Damage, _SpawnedDaddyShadow);
//END_REGION

//REGION Olivers cursed domes
IF
EnteredLevel(_CursedDome, (ITEMROOT)UNI_SCL_OliverFight_Cursedome_fcde62b2-e95f-44b2-be74-6b3bafce80ef, _)
THEN
DB_SCL_OliverFight_CursedDomes((GUIDSTRING)_CursedDome);

IF
DB_PermaDefeated(S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5)
AND
DB_SCL_OliverFight_CursedDomes(_CursedDome)
THEN
Die((ITEM)_CursedDome);

IF
DB_PermaDefeated(_CursedDome)
AND
DB_SCL_OliverFight_CursedDomes(_CursedDome)
THEN
NOT DB_SCL_OliverFight_CursedDomes(_CursedDome);
//END_REGION

//REGION Oliver's Reactivity
// Killing Plush makes Oliver play an AD
// Reducing the HP of the Nightdome below 75% / 50% / 25% also makes Oliver play an AD
// We must only play one AD at a time
// Priority:
//	25% AD
//	50% AD
// 	75% AD 
//	Plush Died AD
IF
HitpointsChanged(_Plush, _RemainingPercentage)
AND
DB_SCL_OliverFight_CurrentPlush(_Plush)
AND
NOT DB_SCL_OliverFight_PlushADBlocked(1)
AND
_RemainingPercentage <= 0
AND
NOT DB_SCL_OliverFight_PlayPlushDiedAD(1)
THEN
DB_SCL_OliverFight_PlayPlushDiedAD(1);

IF
HitpointsChanged(S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5, _RemainingPercentage)
AND
NOT DB_SCL_OliveFight_CheckOliverReactivityTimerActive(1)
AND
_RemainingPercentage < 100
THEN
DB_SCL_OliveFight_CheckOliverReactivityTimerActive(1);
RealtimeObjectTimerLaunch(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "SCL_OliveFight_CheckOliverReactivity", 500);

IF
ObjectTimerFinished(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "SCL_OliveFight_CheckOliverReactivity")
AND
GetHitpointsPercentage(S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5, _RemainingPercentage)
THEN
NOT DB_SCL_OliveFight_CheckOliverReactivityTimerActive(1);
PROC_SCL_OliveFight_CheckOliverReactivity(_RemainingPercentage);

PROC
PROC_SCL_OliveFight_CheckOliverReactivity((REAL)_RemainingPercentage)
AND
_RemainingPercentage < 25.0
AND
QRY_OnlyOnce("SCL_OliverFight_Played25PercentageAD")
THEN
DB_SCL_OliverFight_PlayHPPercentageAD((DIALOGRESOURCE)SCL_OliverShadow_AD_Dome25PercentHP_a947c2ee-a89c-7a22-4924-7722cf818145);

PROC
PROC_SCL_OliveFight_CheckOliverReactivity((REAL)_RemainingPercentage)
AND
_RemainingPercentage < 50.0
AND
NOT DB_OnlyOnce("SCL_OliverFight_Played25PercentageAD") // in case we did massive AOE damage and we went from let's say 60% to 20% in one attack (for example - fireball)
AND
QRY_OnlyOnce("SCL_OliverFight_Played50PercentageAD")
THEN
DB_SCL_OliverFight_PlayHPPercentageAD((DIALOGRESOURCE)SCL_OliverShadow_AD_Dome50PercentHP_91f86d25-60a0-bf4b-fe32-19189c3fb0b0);

PROC
PROC_SCL_OliveFight_CheckOliverReactivity((REAL)_RemainingPercentage)
AND
_RemainingPercentage < 75.0
AND
NOT DB_OnlyOnce("SCL_OliverFight_Played25PercentageAD")
AND
NOT DB_OnlyOnce("SCL_OliverFight_Played50PercentageAD")
AND
QRY_OnlyOnce("SCL_OliverFight_Played75PercentageAD")
THEN
DB_SCL_OliverFight_PlayHPPercentageAD((DIALOGRESOURCE)SCL_OliverShadow_AD_Dome75PercentHP_e80aecbf-56d6-d2b3-10a1-caed4b287686);

IF
DB_SCL_OliverFight_PlayHPPercentageAD(_AD)
THEN
PROC_TryStartAD(_AD, S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a);

PROC
PROC_SCL_OliveFight_CheckOliverReactivity((REAL)_RemainingPercentage)
AND
NOT DB_SCL_OliverFight_PlayHPPercentageAD(_)
AND
DB_SCL_OliverFight_PlayPlushDiedAD(1)
THEN
PROC_TryStartAD((DIALOGRESOURCE)SCL_OliverShadow_AD_PlushDied_6caa67af-bbea-2fba-eba9-daad2a5d6deb, S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a);

PROC
PROC_SCL_OliveFight_CheckOliverReactivity((REAL)_RemainingPercentage)
AND
DB_SCL_OliverFight_PlayHPPercentageAD(_AD)
THEN
NOT DB_SCL_OliverFight_PlayHPPercentageAD(_AD);

PROC
PROC_SCL_OliveFight_CheckOliverReactivity((REAL)_RemainingPercentage)
AND
DB_SCL_OliverFight_PlayPlushDiedAD(1)
THEN
NOT DB_SCL_OliverFight_PlayPlushDiedAD(1);
//END_REGION

//REGION After Fight
IF
DB_PermaDefeated(S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5)
THEN
RemoveStatus(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "INVULNERABLE_NOT_SHOWN", NULL_00000000-0000-0000-0000-000000000000);
RemoveStatus(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "AI_HELPER_BLOCKRESOURCES", NULL_00000000-0000-0000-0000-000000000000);
SetFlag((FLAG)SCL_LiftingTheCurse_State_NightdomeDestroyed_ea76f9be-6d12-441d-8c63-de999d5b3e0c, NULL_00000000-0000-0000-0000-000000000000);
DB_IgnoreMutingStatussesDialog((DIALOGRESOURCE)SCL_OliverShadow_Acceptance_f268fd19-252c-9e06-5f77-996a975ecc59);
DB_SpotPlayers_IgnoreCombat(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "SCL_OliverFight_DomeDestroyed");
DB_SpotPlayers_TargetIgnoreCantTalk(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "SCL_OliverFight_DomeDestroyed");
DB_SpotPlayers_SpotterIgnoreCantTalk(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "SCL_OliverFight_DomeDestroyed");
DB_SpotPlayers((CHARACTER)S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "SCL_OliverFight_DomeDestroyed", NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);
DB_SpotPlayers_CustomRadius((CHARACTER)S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "SCL_OliverFight_DomeDestroyed", 25.0);
PROC_ORI_SetupCamp(S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a);
DB_InCamp(S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a);
SetOnStage(S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a, 1);
PROC_SetOnStage(S_SCL_OliverPortal_a45b9f90-0a29-4777-acc1-7f154d07f104, 0);
PROC_CharacterEnableAllCrimes(S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a);
DB_SpotPlayers((CHARACTER)S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a, "SCL_ThanielCampSpot", NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);
DB_SpotPlayers_Continuous((CHARACTER)S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a, "SCL_ThanielCampSpot");
DB_SpotPlayers_CustomRadius((CHARACTER)S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a, "SCL_ThanielCampSpot", 5.0);

PROC
PROC_SpotPlayers_Spotted((CHARACTER)S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "SCL_OliverFight_DomeDestroyed", _Player)
AND
QRY_StartDialogCustom((DIALOGRESOURCE)SCL_OliverShadow_Acceptance_f268fd19-252c-9e06-5f77-996a975ecc59, S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, _Player, 1, 1, 0, 0)
THEN
DB_NOOP(1);

IF
DialogStarted((DIALOGRESOURCE)SCL_OliverShadow_Acceptance_f268fd19-252c-9e06-5f77-996a975ecc59, _)
AND
NOT DB_PermaDefeated(S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5)
THEN
Die(S_SCL_OliverFight_Nightdome_d5ca84a9-98a4-47e4-ab5f-ee8b245c48d5);

IF
DialogStarted((DIALOGRESOURCE)SCL_OliverShadow_Acceptance_f268fd19-252c-9e06-5f77-996a975ecc59, _)
AND
DB_SCL_OliverFight_NewPlushPositionVFX(_RandomSpawnPoint)
THEN
PROC_StopLoopEffect(_RandomSpawnPoint, "NewPlushPositionVFX");
NOT DB_SCL_OliverFight_NewPlushPositionVFX(_RandomSpawnPoint);

PROC
PROC_GlobalFlagReactionAfterDialog(SCL_LiftingTheCurse_State_OliverAgreedToReturnToThaniel_3073ae92-8e87-42d1-a426-fe3e6c5e6deb)
THEN
PROC_GlobalSetFlagAndCache((FLAG)GLO_LiftingTheCurse_State_BreathHasBeenRestored_2113b54e-a140-4aa0-97ac-219fa7b7d038);
LeaveCombat(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a);
PROC_Poof(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a);
PROC_State_Progress(S_GLO_Halsin_7628bc0e-52b8-42a7-856a-13a6fd413323, "SCL_LiftingTheCurse", "SCL_LiftingTheCurse_State_ThanielRestored");


//Start Thaniel's camp dialog when he sees players in camp after merging with Oliver
PROC
PROC_SpotPlayers_Spotted((CHARACTER)S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a, "SCL_ThanielCampSpot", _Avatar)
AND
DB_Avatars(_Avatar)
AND
QRY_StartDialog((DIALOGRESOURCE)HAV_HalsinQuest_Spirit_85232c87-9912-7c37-c068-3196688f88b3, S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a, _Avatar)
THEN
PROC_SpotPlayers_StopSpotting(S_HAV_SpiritOfTheLand_1403cdfd-0660-4592-90c6-bfe4607ccd3a, "SCL_ThanielCampSpot");
//END_REGION After Fight

//REGION Party Banters
PROC
PROC_LongRest()
AND
DB_GlobalFlag(TWN_CentralSquare_State_AllPlazaEnemiesDefeated_80da8f15-038e-424f-a6b7-33205dc5b953)
AND
NOT DB_WorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_TWN_OutdoorsCleared_f8e1e665-6ba6-4447-93ef-cab74e1cc88c, _, _)
THEN
PROC_RegisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_TWN_OutdoorsCleared_f8e1e665-6ba6-4447-93ef-cab74e1cc88c);

IF
EntityEvent(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a, "SCL_OliverFight_TeleportedToPlaze")
THEN
PROC_UnregisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_TWN_OutdoorsCleared_f8e1e665-6ba6-4447-93ef-cab74e1cc88c);

PROC
PROC_LongRest()
AND
DB_GlobalFlag((FLAG)SCL_LiftingTheCurse_State_OliverAgreedToReturnToThaniel_3073ae92-8e87-42d1-a426-fe3e6c5e6deb)
AND
NOT DB_WorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_TWN_OutdoorsCleared_f8e1e665-6ba6-4447-93ef-cab74e1cc88c, _, _)
THEN
PROC_RegisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_TWN_OutdoorsCleared_f8e1e665-6ba6-4447-93ef-cab74e1cc88c);

PROC
PROC_LongRest()
AND
DB_PermaDefeated(S_SCL_OliversDiary_OliverShadow_4f6e63a1-b143-46b1-ac0e-834494dfdc6a)
AND
DB_SCL_OliverConfrontation(1)
AND
NOT DB_WorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_TWN_OutdoorsCleared_f8e1e665-6ba6-4447-93ef-cab74e1cc88c, _, _)
THEN
PROC_RegisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_TWN_OutdoorsCleared_f8e1e665-6ba6-4447-93ef-cab74e1cc88c);
//END_REGION
EXITSECTION
SysClear("DB_SCL_OliverFight_CursedDomes", 1);
ENDEXITSECTION
ParentTargetEdge "Act2"
