Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(S_SHA_SpiderMeatHunk_e5b96310-ee05-44d1-afb2-8cde9758e67b, (DIALOGRESOURCE)SHA_SpiderMeatHunk_9e191cf1-75cc-319e-c421-c79b44adab30);
DB_GlobalFlagReactionAfterDialog((FLAG)SHA_Displacer_State_AlliesPlayers_c8dc3aee-5b61-e434-18d8-cee73a073698, (DIALOGRESOURCE)SHA_Displacer_618b26dd-25a2-f1f1-be8f-79b6dcdcfab6);
DB_FlagReactionAfterDialog((FLAG)SHA_SpiderMeatHunk_Event_PoisonPlayer_8d37409f-ae04-6161-bfc6-fd538bcdc382, (DIALOGRESOURCE)SHA_SpiderMeatHunk_9e191cf1-75cc-319e-c421-c79b44adab30);
KBSECTION
PROC
PROC_GlobalFlagReactionAfterDialog(SHA_Displacer_State_AlliesPlayers_c8dc3aee-5b61-e434-18d8-cee73a073698)
THEN
SetFaction(S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, (FACTION)ACT2_SHA_DisplacerAllied_79459079-d3b1-421c-a4b7-ebd0e0df97fe);

IF
DialogStarted(SHA_Displacer_618b26dd-25a2-f1f1-be8f-79b6dcdcfab6, _DialogID)
AND
DB_DialogPlayers(_DialogID, _Player, 1)
THEN
DB_GLO_DieFlag((FLAG)SHA_OrthonLair_Event_PlayerKillsDisplacer_1f7eb788-3eb6-14b2-64a1-3fbd8450be1a, (CHARACTER)S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, DEATHTYPE.Physical, _Player);

IF
DialogEnded(SHA_Displacer_618b26dd-25a2-f1f1-be8f-79b6dcdcfab6, _)
AND
DB_GLO_DieFlag((FLAG)SHA_OrthonLair_Event_PlayerKillsDisplacer_1f7eb788-3eb6-14b2-64a1-3fbd8450be1a, (CHARACTER)S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, DEATHTYPE.Physical, _Player)
THEN
NOT DB_GLO_DieFlag((FLAG)SHA_OrthonLair_Event_PlayerKillsDisplacer_1f7eb788-3eb6-14b2-64a1-3fbd8450be1a, (CHARACTER)S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, DEATHTYPE.Physical, _Player);

// Orthon and merregons are allied towards the displacer, they will only fight against each other if the player is hostile towards the merregons/orthon but not the displacer.
IF
EnteredCombat((CHARACTER)_OrthonPartyMember, _CombatID)
AND
_OrthonPartyMember != S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2
AND
DB_SHA_OrthonLair_Party(_OrthonPartyMember)
AND
DB_GlobalFlag(SHA_Displacer_State_AlliesPlayers_c8dc3aee-5b61-e434-18d8-cee73a073698)
AND
QRY_IsEnemyToAnyPlayerInCombat(_OrthonPartyMember, _CombatID)
AND
NOT QRY_IsEnemyToAnyPlayerInCombat(S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, _CombatID)
AND
QRY_OnlyOnce("SHA_DisplacerAttacksOrthon")
THEN
PROC_SetRelationMutual(ACT2_SHA_DisplacerAllied_79459079-d3b1-421c-a4b7-ebd0e0df97fe, ACT2_SHA_Orthon_eda9b693-579a-43de-90fc-f4684f06c3dd, 0);
PROC_SetRelationMutual(ACT2_SHA_DisplacerAllied_79459079-d3b1-421c-a4b7-ebd0e0df97fe, ACT2_SHA_OrthonParty_216e6942-98f2-405b-93b0-425197b56606, 0);

// Displacer becomes ally - peaceful resolution
IF
FlagSet((FLAG)SHA_Displacer_State_AlliesPlayers_c8dc3aee-5b61-e434-18d8-cee73a073698, _, _)
AND
NOT DB_PermaDefeated(S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2)
THEN
ApplyStatus(S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, "PEACEFUL_RESOLUTION_XP", -1.0, 1);

PROC
PROC_FlagReactionAfterDialog(_Player, (FLAG)SHA_SpiderMeatHunk_Event_PoisonPlayer_8d37409f-ae04-6161-bfc6-fd538bcdc382)
THEN
ClearFlag((FLAG)SHA_SpiderMeatHunk_Event_PoisonPlayer_8d37409f-ae04-6161-bfc6-fd538bcdc382, _Player);
ApplyStatus(_Player, "SHA_SPIDERMEAT_POISONED", 60.0, 1, S_SHA_SpiderMeatHunk_e5b96310-ee05-44d1-afb2-8cde9758e67b);

//REGION Charm status
IF
FlagSet(SHA_Displacer_Knows_CharmedFromMeat_8f5a0df8-2271-5878-47d1-d0324fe23e19, _, _)
AND
HasAppliedStatus(S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, "SHA_DISPLACER_CHARMED_HIDDEN", 1)
THEN
ApplyStatus(S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, "SHA_DISPLACER_CHARMED", -1.0, 1, NULL_00000000-0000-0000-0000-000000000000);

IF
StatusRemoved(S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, "SHA_DISPLACER_CHARMED_HIDDEN", _Player, _)
THEN
SetFlag(SHA_Displacer_State_RemovedPoisonWithSpell_eb66dd4c-06c7-4506-9fa0-6b2ea6fe0a7f, NULL_00000000-0000-0000-0000-000000000000);
RemoveStatus(S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, "SHA_DISPLACER_CHARMED");

IF
StatusRemoved(S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, "SHA_DISPLACER_CHARMED_HIDDEN", (CHARACTER)_Player, _)
AND
DB_Players(_Player)
AND
QRY_StartDialogWithAvailableSpeakerInRange_TryTargetFirst(SHA_Displacer_618b26dd-25a2-f1f1-be8f-79b6dcdcfab6, (CHARACTER)S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, _Player, 15.0)
THEN
DB_NOOP(1);
//END_REGION Charm status

//REGION Growl

PROC
PROC_SHA_OrthonLair_EndAmbushNeutral()
THEN
DB_SpotPlayers(S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, "SHA_BeastmasterSecret_Approach", NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);
DB_SpotPlayers_TargetIgnoreCantTalk(S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, "SHA_BeastmasterSecret_Approach");
DB_SpotPlayers_CustomRadius(S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, "SHA_BeastmasterSecret_Approach", 3.0);
DB_SpotPlayers_IncludeFollowers(S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, "SHA_BeastmasterSecret_Approach");
DB_SpotPlayers_IncludeWildshapedPlayers(S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, "SHA_BeastmasterSecret_Approach");

PROC
PROC_SpotPlayers_Spotted(S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, "SHA_BeastmasterSecret_Approach", _Char)
THEN
LookAtEntity(S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, _Char, 0.5);
PROC_TryStartAD((DIALOGRESOURCE)SHA_Orthon_AD_DisplacerGrowl_ca42ff00-7a33-8ab0-29ec-d2efcd73ade8, S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2);
PROC_SpotPlayers_StopSpotting("SHA_BeastmasterSecret_Approach");

IF
EnteredCombat(S_SHA_Displacer_0d5e3e88-972b-4577-b6b2-b0946f38fce2, _)
THEN
PROC_SpotPlayers_StopSpotting("SHA_BeastmasterSecret_Approach");

IF
LevelLoaded("INT_Main_A")
THEN
PROC_SpotPlayers_StopSpotting("SHA_BeastmasterSecret_Approach");

PROC
PROC_SHA_OrthonLair_OrthonAndHisPartyDisappear()
THEN
PROC_SpotPlayers_StopSpotting("SHA_BeastmasterSecret_Approach");

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
