Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION Light trial
DB_SHA_Trials_LightTrialDoors((ITEM)S_SHA_LightTial_InteriorDoor_000_3905aa99-03c9-423a-b998-eb4252eec300);
DB_SHA_Trials_LightTrialDoors((ITEM)S_SHA_LightTial_InteriorDoor_001_32b1b747-cfbb-495d-8f50-2adc2cd3f851);
DB_SHA_Trials_LightTrialDoors((ITEM)S_SHA_LightTial_InteriorDoor_002_5073dc5d-7037-4913-a8ac-17c934be0b84);
DB_SHA_Trials_LightTrialDoors((ITEM)S_SHA_LightTial_InteriorDoor_003_c52299f5-e91e-42f8-896c-5be6f5c774f4);

// Roaming shadow
DB_SHA_Trials_LightTrialShadow((CHARACTER)S_SHA_LightTrial_Shadow_000_fb9095d7-e399-4cdb-b90a-e4dba3451882);
DB_SHA_Trials_LightTrialShadow((CHARACTER)S_SHA_LightTrial_Shadow_001_f4ef2c8e-02fd-4382-a395-4bebc674881d);

PROC_TriggerRegisterForParty((TRIGGER)S_SHA_LightTrial_MazeArea_44bbe72b-6eb4-41db-a093-04bcc9194f7b);
PROC_Poof((ITEM)S_SHA_TrialGem_000_5e3d0d3b-6fcf-48b8-a10c-580ff4efd1ff, (EFFECTRESOURCE)VFX_Script_SCL_Trial_Shar_Teleport_Disappear_BodyFX_01_0f13f03e-107a-22af-ef1f-5a7a8967d5b9);

DB_SHA_Trials_LightTrialSecretDoors_Open(-1);
//DB_SHA_Trials_LightTrialSecretDoors(_Door, _StartPos, _EndPos, _Moving)
DB_SHA_Trials_LightTrialSecretDoors((ITEM)S_SHA_LightTrial_SecretDoor_000_80e08f57-9619-4c30-9c52-e75020262ad4, (TRIGGER)S_SHA_LightTrial_SecretDoor_000_Start_19ecc426-cf83-4eec-bf81-930eaef1b430, (TRIGGER)S_SHA_LightTrial_SecretDoor_000_End_3df8ffc2-914d-4aa5-8da2-b919d4dedbf6, 0);
DB_SHA_Trials_LightTrialSecretDoors((ITEM)S_SHA_LightTrial_SecretDoor_001_df4c4827-5127-43cb-9f15-87631279441b, (TRIGGER)S_SHA_LightTrial_SecretDoor_001_Start_6e07ac4c-64ef-4b7a-927e-4c50471a00b1, (TRIGGER)S_SHA_LightTrial_SecretDoor_001_End_f5ae0740-b060-4f39-8d25-568961b22967, 0);
//END_REGION

//REGION Mental torture trial
SetOnStage(S_SHA_TrialGem_001_3c29b3b3-65ec-4d47-b689-e5eca053602b, 0);

DB_SHA_Trials_MirrorCharacters((CHARACTER)S_SHA_PlayerMirrorCharacter_000_9b6e66e8-3832-4bbf-b714-ffc2b0d7a911, (TRIGGER)S_SHA_PlayerMirrorCharacter_000_StartPos_2de7aa25-cfec-4a63-b7d6-62bdb52f5c40);
DB_SHA_Trials_MirrorCharacters((CHARACTER)S_SHA_PlayerMirrorCharacter_001_6b8c8710-2e62-4cb2-b4cb-93710d1b65eb, (TRIGGER)S_SHA_PlayerMirrorCharacter_001_StartPos_e42fa9ab-b3c8-4ecb-9e85-915c33a9f48b);
DB_SHA_Trials_MirrorCharacters((CHARACTER)S_SHA_PlayerMirrorCharacter_002_c2165721-f927-4a5c-a0a5-71c12b8b640f, (TRIGGER)S_SHA_PlayerMirrorCharacter_002_StartPos_f752361e-ba42-481e-8ead-08655d0787a7);
DB_SHA_Trials_MirrorCharacters((CHARACTER)S_SHA_PlayerMirrorCharacter_003_31c2dc34-ec63-4ecc-97f7-fdc0cf86a18a, (TRIGGER)S_SHA_PlayerMirrorCharacter_003_StartPos_ad8c9fc3-dad6-4150-b7ef-d38bcb858566);
PROC_SHA_Trials_InitMirrorCharacters();
//END_REGION

//REGION Invisible platforms trial
DB_SHA_Trials_InvisiblePlatformsCandles((ITEM)S_SHA_WarningLight_Platform0_ThreeTriesLeft_a9941c61-327d-4c5e-8cab-c80f500fcab2, 3);
DB_SHA_Trials_InvisiblePlatformsCandles((ITEM)S_SHA_WarningLight_Platform1_ThreeTriesLeft_089e2736-3487-4474-9660-ea2b2981d5bf, 3);
DB_SHA_Trials_InvisiblePlatformsCandles((ITEM)S_SHA_WarningLight_Platform2_ThreeTriesLeft_957ea83c-f968-499f-8811-ab5413b8156c, 3);
DB_SHA_Trials_InvisiblePlatformsCandles((ITEM)S_SHA_WarningLight_Platform3_ThreeTriesLeft_e5d81128-c241-4af5-9cb1-889645e5fcf0, 3);

DB_SHA_Trials_InvisiblePlatformsCandles((ITEM)S_SHA_WarningLight_Platform0_TwoTriesLeft_42a3aa6c-a4b3-40f5-8b5b-9153991691bb, 2);
DB_SHA_Trials_InvisiblePlatformsCandles((ITEM)S_SHA_WarningLight_Platform1_TwoTriesLeft_a48255cf-1300-4c2f-9c28-d028bd940b93, 2);
DB_SHA_Trials_InvisiblePlatformsCandles((ITEM)S_SHA_WarningLight_Platform2_TwoTriesLeft_9a1d077e-9341-4fce-b828-ae961ff7d6d9, 2);
DB_SHA_Trials_InvisiblePlatformsCandles((ITEM)S_SHA_WarningLight_Platform3_TwoTriesLeft_5e9f8768-e422-4050-adac-8cabce0a3c8b, 2);

DB_SHA_Trials_InvisiblePlatformsCandles((ITEM)S_SHA_WarningLight_Platform0_LastTryLeft_19411e95-2005-433b-ad0e-8986f1735a69, 1);
DB_SHA_Trials_InvisiblePlatformsCandles((ITEM)S_SHA_WarningLight_Platform1_LastTryLeft_805564f9-87d7-4419-b818-6b54417de8a4, 1);
DB_SHA_Trials_InvisiblePlatformsCandles((ITEM)S_SHA_WarningLight_Platform2_LastTryLeft_610cd35c-5ee2-4ee4-b648-0d4fbfbcd672, 1);
DB_SHA_Trials_InvisiblePlatformsCandles((ITEM)S_SHA_WarningLight_Platform3_LastTryLeft_eb68ad2a-505e-4be4-8923-40956cebc0e4, 1);

PROC_TriggerRegisterForParty(S_SHA_InvisiblePlatformsTrigger_30c88142-8cc0-4e71-842d-6f64f00a9f5e);
PROC_Poof((ITEM)S_SHA_TrialGem_002_b8dda6e3-8cb8-4287-81a4-0ac5260859c7, (EFFECTRESOURCE)VFX_Script_SCL_Trial_Shar_Teleport_Disappear_BodyFX_01_0f13f03e-107a-22af-ef1f-5a7a8967d5b9);

DB_SHA_Trials_AltDeathTriggerTeleport(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, S_SHA_PathOfDarknessTrial_DeathTriggerTP_000_6a28f53d-3819-4869-b961-fc564615962a);

DB_SHA_Trials_PlatformStatue((TRIGGER)S_SHA_PathOfDarknessTrial_DeathTriggerTP_000_6a28f53d-3819-4869-b961-fc564615962a, (ITEM)S_SHA_VoiceOfShar_Platform0_9bc7ee51-334e-48d5-8581-f0a67bbce472);
DB_SHA_Trials_PlatformStatue((TRIGGER)S_SHA_PathOfDarknessTrial_DeathTriggerTP_001_9c2f8c45-a159-4932-ba8e-183d0f3b6ef7, (ITEM)S_SHA_VoiceOfShar_Platform1_8b0c9e96-9a60-49dc-8ba7-c7cf6ac9d9c6);
DB_SHA_Trials_PlatformStatue((TRIGGER)S_SHA_PathOfDarknessTrial_DeathTriggerTP_002_791bd805-8200-488d-a97b-ed8de55eb524, (ITEM)S_SHA_VoiceOfShar_Platform3_006a50af-14d6-4d05-b28c-8756993cd846);

DB_SHA_Trials_TriesLeftWarning(3, (DIALOGRESOURCE)SHA_Trials_PAD_PathOfDarknessFirstWarning_2d731f84-0806-8bbb-c95b-43b91e854741);
DB_SHA_Trials_TriesLeftWarning(2, (DIALOGRESOURCE)SHA_Trials_PAD_PathOfDarknessSecondWarning_52afc0ef-f2f5-09a9-2b4d-38dd295d31d4);

//END_REGION
KBSECTION
//REGION Debug
IF
TextEvent("SHA_Teleport_Trials")
AND
DB_Players(_Player)
THEN
TeleportTo(_Player, S_SHA_Trials_OETeleportTo_eb5197fb-423b-4dea-80a8-69cc2433ce19);
//END_REGION Debug

//REGION Mental torture
IF
DualEntityEvent(_, _, "SHA_StopDoublesEncounterTrial")
AND
GetFlag(SHA_Trials_State_TrialInProgress_87772420-50d7-4937-85ec-95c42fd9fd14, S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe, 1)
THEN
SetFlag(SHA_Trials_Event_TrialFailed_e80850ea-ab84-4043-9809-960d4db885f6, S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe);

IF
FlagSet(SHA_Trials_State_TrialInProgress_87772420-50d7-4937-85ec-95c42fd9fd14, S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe, _)
THEN
DB_AmbushTrigger((TRIGGER)S_SHA_CloneAmbushTrigger_76fe7265-7cc8-4022-b990-397f86713cb7, (TRIGGER)S_SHA_CloneAmbushPerceptionTrigger_8e358cd2-34e5-4620-9cfe-33e3f4746d78, (DIFFICULTYCLASS)HiddenPerception_Medium_cd1800ab-1b11-4c7a-9f50-fdeb8d35481f);

IF
DB_SHA_Trials_MirrorCharacters(_Double, _)
THEN
DB_DoNotRemoveKnockedOutCharacterOnLongRest(_Double);

PROC
PROC_SHA_Trials_RestartTrial(S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe)
AND
DB_SHA_Trials_MirrorCharacters(_Double, _StartPos)
THEN
TeleportTo(_Double, _StartPos);
PROC_SHA_Trials_ResetCharacter(_Double);

PROC
PROC_SHA_Trials_ResetCharacter((CHARACTER)_Char)
AND
DB_Dead(_Char)
THEN
Resurrect(_Char);

PROC
PROC_SHA_Trials_ResetCharacter((CHARACTER)_Char)
THEN
RemoveStatusesWithGroup(_Char, "SG_Unconscious");
SetHitpointsPercentage(_Char, 100.0);

//clear owner statuse before clearing DB
PROC
PROC_SHA_Trials_RestartTrial(S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe)
AND
DB_SHA_MirrorEncounter_Pairs((CHARACTER)_Double,_)
THEN
RemoveStatus(_Double,"SHA_TORTURETRIAL_DOUBLE_OWNER");

PROC
PROC_SHA_Trials_RestartTrial(S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe)
AND
DB_SHA_MirrorEncounter_Pairs(_MirrorCharacter, _Player)
THEN
NOT DB_SHA_MirrorEncounter_Pairs(_MirrorCharacter, _Player);

PROC
PROC_SHA_Trials_InitMirrorCharacters()
AND
DB_SHA_Trials_MirrorCharacters(_Double, _StartPos)
THEN
TeleportTo(_Double, _StartPos);
PROC_CharacterDisableAllCrimes(_Double);
SetOnStage(_Double, 0);

PROC
PROC_SHA_Trials_MirrorCharactersAppear()
AND
DB_SHA_Trials_MirrorCharacters(_Double, _)
AND
DB_SHA_Trials_PartyMembersInTrialRooms((CHARACTER)_Player, (TRIGGER)S_SHA_MentalTortureTrigger_e272b659-024a-457d-a5f5-a9317d60e1eb)
AND
DB_Players(_Player)
AND
NOT DB_SHA_MirrorEncounter_Pairs(_Double, _)
AND
NOT DB_SHA_MirrorEncounter_Pairs(_, _Player)
THEN
DB_Tadpole_TadpoleBurstException(_Double);
DB_SHA_MirrorEncounter_Pairs(_Double, _Player);
PROC_SHA_Trials_ClearClassTags(_Double); // First clear any class tags
PROC_SHA_Trials_ApplyClassTags(_Double, _Player); // Then assign them
Transform(_Double, _Player, (SHAPESHIFTRULE)SHA_MirrorTrialDouble_07ec0764-5890-4d16-8522-4b838a741982);
CopyCharacterEquipment(_Double, _Player);
DB_GLO_DefeatCounter(_Double, "SHA_TrialDoubles");
DB_GLO_DefeatCounter(_Player, "SHA_TrialPlayers");
DB_AmbushTrigger_Ambusher((TRIGGER)S_SHA_CloneAmbushTrigger_76fe7265-7cc8-4022-b990-397f86713cb7, _Double, 1);
PROC_Foop(_Double);

QRY
QRY_BlockPickpocket(_, _Double)
AND
DB_SHA_Trials_MirrorCharacters(_Double, _)
THEN
DB_NOOP(1);

// Apply correct tags to clones.
PROC
PROC_SHA_Trials_ApplyClassTags((CHARACTER)_Double, (CHARACTER)_Player)
AND
DB_ClassTags(_Tag)
AND
IsTagged(_Player, _Tag, 1)
THEN
SetTag(_Double, _Tag);
DB_SHA_Trials_DoublesAppliedTags(_Double, _Tag);

PROC
PROC_SHA_Trials_ApplyClassTags((CHARACTER)_MirrorCharacter, (CHARACTER)_Player)
AND
QRY_SHA_Trials_HasGodTags(_Player)
AND
DB_GodClassAlignTags(_GodTag,_GodClericTag,_GodPaladinTag,_AlignTag)
AND
QRY_SHA_Trials_TryApplyTag(_MirrorCharacter, _Player, _GodTag)
AND
QRY_SHA_Trials_TryApplyTag(_MirrorCharacter, _Player, _GodClericTag)
AND
QRY_SHA_Trials_TryApplyTag(_MirrorCharacter, _Player, _GodPaladinTag)
AND
QRY_SHA_Trials_TryApplyTag(_MirrorCharacter, _Player, _AlignTag)
THEN
DB_NOOP(1);

PROC
PROC_SHA_Trials_ApplyClassTags((CHARACTER)_MirrorCharacter, (CHARACTER)_Player)
AND
QRY_SHA_Trials_HasGodTags(_Player)
AND
DB_AlignClassTags(_AlignTag, _ClericAlignTag, _PaladinAlignTag) // No need to try to apply align tag again
AND
QRY_SHA_Trials_TryApplyTag(_MirrorCharacter, _Player, _ClericAlignTag)
AND
QRY_SHA_Trials_TryApplyTag(_MirrorCharacter, _Player, _PaladinAlignTag)
THEN
DB_NOOP(1);

QRY
QRY_SHA_Trials_TryApplyTag((CHARACTER)_Double, (CHARACTER)_Player, (TAG)_Tag)
THEN
DB_NOOP(1);

QRY
QRY_SHA_Trials_TryApplyTag((CHARACTER)_Double, (CHARACTER)_Player, (TAG)_Tag)
AND
IsTagged(_Player, _Tag, 1)
THEN
SetTag(_Double, _Tag);
DB_SHA_Trials_DoublesAppliedTags(_Double, _Tag);

QRY
QRY_SHA_Trials_HasGodTags((CHARACTER)_Player)
AND
DB_ClassHasGodTags(_Tag)
AND
IsTagged(_Player, _Tag, 1)
THEN
DB_NOOP(1);

IF
AddedTo(_Item, (CHARACTER)_Double,_)
AND
DB_SHA_Trials_MirrorCharacters(_Double, _)
AND //Block weapons picked up from being deleted
NOT DB_Is_InCombat(_Double, _)
AND
IsItem(_Item, 1)
THEN
DB_SHA_Trials_ItemsToRemove(_Double, (ITEM)_Item);

PROC
PROC_SHA_Trials_ClearClassTags((CHARACTER)_Double)
AND
DB_SHA_Trials_ItemsToRemove(_Double, _Item)
AND
IsDestroyed(_Item, 0)
AND
Exists(_Item, 1)
THEN
NOT DB_SHA_Trials_ItemsToRemove(_Double, _Item);
RequestDelete(_Item);

// Clear tags from clones.
PROC
PROC_SHA_Trials_ClearClassTags((CHARACTER)_Double)
AND
DB_SHA_Trials_DoublesAppliedTags(_Double, _Tag)
THEN
NOT DB_SHA_Trials_DoublesAppliedTags(_Double, _Tag);
ClearTag(_Double, _Tag);

// Trial starts, unlock door to cell
IF
FlagSet(SHA_Trials_State_TrialInProgress_87772420-50d7-4937-85ec-95c42fd9fd14, S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe, _)
THEN
PROC_ItemUnlockAndOpen(S_SHA_MentalTortureCellDoor_f6d12215-6e1f-49de-9fa9-be50a549e6e7);
PROC_SHA_Trials_ClearDefeatCounter("SHA_TrialDoubles", SHA_Trials_Event_ClonesWinCombat_150017e2-0ee4-43d5-88a7-fef1baab8366);
PROC_SHA_Trials_ClearDefeatCounter("SHA_TrialPlayers", SHA_Trials_Event_PlayersWinCombat_dea864a4-3c94-4315-8b02-c2f813cd57d2);
PROC_SHA_Trials_MirrorCharactersAppear();


PROC
PROC_SHA_Trials_ClearDefeatCounter((STRING)_ID, (FLAG)_Flag)
AND
DB_GLO_DefeatCounter_State(_Object, _ID, _State)
THEN
NOT DB_GLO_DefeatCounter_State(_Object, _ID, _State);

PROC
PROC_SHA_Trials_ClearDefeatCounter((STRING)_ID, (FLAG)_Flag)
AND
DB_GLO_DefeatCounter(_Char, _ID)
THEN
NOT DB_GLO_DefeatCounter(_Char, _ID);

PROC
PROC_SHA_Trials_ClearDefeatCounter((STRING)_ID, (FLAG)_Flag)
THEN
ClearFlag(_Flag, NULL_00000000-0000-0000-0000-000000000000);

PROC
PROC_GLO_DefeatCounter_AllDefeated("SHA_TrialDoubles")
THEN
SetFlag(SHA_Trials_Event_PlayersWinCombat_dea864a4-3c94-4315-8b02-c2f813cd57d2, NULL_00000000-0000-0000-0000-000000000000);
PROC_SHA_Trials_TrialPassed(S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe);

PROC
PROC_GLO_DefeatCounter_AllDefeated("SHA_TrialPlayers")
THEN
SetFlag(SHA_Trials_Event_ClonesWinCombat_150017e2-0ee4-43d5-88a7-fef1baab8366, NULL_00000000-0000-0000-0000-000000000000);
PROC_SHA_Trials_TrialFailed(S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe);

IF
TemplateRemovedFrom(_Template, (ITEM)_Item, _Double)
AND
DB_SHA_Trials_MirrorCharacters((CHARACTER)_Double, _)
AND
_Template != MAG_Critical_CriticalExecution_Ring_2f2d4bf3-6a14-43f5-81fe-e14aa9871215
AND
DB_SHA_Trials_ItemsToRemove(_Double, (ITEM)_Item)
THEN
PROC_Poof(_Item);
RequestDelete(_Item);

PROC //Incase AI picked up the player weapon.
PROC_StateSet_PermaDefeated(_Double)
AND
DB_SHA_Trials_MirrorCharacters((CHARACTER)_Double, _)
AND
GetEquippedWeapon(_Double, _Weapon)
AND
NOT DB_SHA_Trials_ItemsToRemove(_Double, (ITEM)_Weapon)
THEN
Drop(_Weapon);

PROC
PROC_StateSet_PermaDefeated(_Double)
AND
DB_SHA_Trials_MirrorCharacters((CHARACTER)_Double, _)
THEN
PROC_Poof(_Double);

IF
AddedTo(S_SHA_TrialGem_001_3c29b3b3-65ec-4d47-b689-e5eca053602b, _Player,_)
AND
DB_Players((CHARACTER)_Player)
AND
DB_SHA_Trials_GemsNotTaken(S_SHA_TrialGem_001_3c29b3b3-65ec-4d47-b689-e5eca053602b)
THEN
SetFlag(SHA_Trials_Event_TrialPassed_a2bf1d24-4f81-4bae-ac3f-68a5760d122c, (ITEM)S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe); 

// Handle trial end
PROC
PROC_SHA_Trials_CustomTrialOutcome(S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe)
AND
DB_SHA_MirrorEncounter_Pairs(_MirrorCharacter, _Player)
THEN
PROC_Poof(_MirrorCharacter);

IF
ShapeshiftChanged((CHARACTER)_Double, _, _, _)
AND
DB_SHA_Trials_MirrorCharacters(_Double, _)
THEN
DB_NOOP(1);

PROC
PROC_SHA_Trials_TrialPassed((ITEM)S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe)
THEN
PROC_SHA_Trials_ActivateTeleportStone((ITEM)S_SHA_MentalTortureTrial_Goal_d2476415-7639-480e-b7da-7afa8c71e4b6);

IF
UseStarted(_Char, (ITEM)S_SHA_MentalTortureTrial_Goal_d2476415-7639-480e-b7da-7afa8c71e4b6)
THEN
PROC_SHA_Trials_MagicTeleport((CHARACTER)_Char, (TRIGGER)S_SHA_MentalTortureTrial_Start_e7437490-85e3-42fb-b5db-30c1e1b18532, (ITEM)S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe);


//REGION Class and Spell Data for Mirror Doubles
//Class Data, stats are applied as boosts to characters based off their class tags
//Note that at the moment this makes characters who multiclass stronger in some cases
PROC
PROC_SHA_Trials_InitMirrorCharacters()
THEN
DB_SHA_MirrorEncounter_ClassData((TAG)BARBARIAN_02913f9a-f696-40cf-acdf-32032afab32c,"SHA_TortureTrial_Barbarian","SHA_TORTURETRIAL_DOUBLE_BARBARIAN");
DB_SHA_MirrorEncounter_ClassData((TAG)BARD_d93434bd-6b71-4789-b128-ee24156057cc,"SHA_TortureTrial_Bard","SHA_TORTURETRIAL_DOUBLE_BARD");
DB_SHA_MirrorEncounter_ClassData((TAG)CLERIC_1671b4bf-4f47-4bb7-9cb9-80bb1f6009d5,"SHA_TortureTrial_Cleric","SHA_TORTURETRIAL_DOUBLE_CLERIC");
DB_SHA_MirrorEncounter_ClassData((TAG)DRUID_44ac4317-4d38-4d28-80e2-94024c6e42f0,"SHA_TortureTrial_Druid","SHA_TORTURETRIAL_DOUBLE_DRUID");
DB_SHA_MirrorEncounter_ClassData((TAG)FIGHTER_1ae7017c-4884-4a43-bc4a-742fa0d201c0,"SHA_TortureTrial_Fighter","SHA_TORTURETRIAL_DOUBLE_FIGHTER");
DB_SHA_MirrorEncounter_ClassData((TAG)PALADIN_6d85ab2d-5c23-498c-a61e-98f05a00177a,"SHA_TortureTrial_Paladin","SHA_TORTURETRIAL_DOUBLE_PALADIN");
DB_SHA_MirrorEncounter_ClassData((TAG)MONK_e1e460bb-d0ae-4452-8529-c9e176558731,"SHA_TortureTrial_Monk","SHA_TORTURETRIAL_DOUBLE_MONK");
DB_SHA_MirrorEncounter_ClassData((TAG)RANGER_37a733c1-a862-4157-b92a-9cff46232c6a,"SHA_TortureTrial_Ranger","SHA_TORTURETRIAL_DOUBLE_RANGER");
DB_SHA_MirrorEncounter_ClassData((TAG)ROGUE_f8a0608b-666c-4be6-a49c-03b369c10bd2,"SHA_TortureTrial_Rogue","SHA_TORTURETRIAL_DOUBLE_ROGUE");
DB_SHA_MirrorEncounter_ClassData((TAG)SORCERER_18266c0b-efbc-4c80-8784-ada4a37218d7,"SHA_TortureTrial_Sorcerer","SHA_TORTURETRIAL_DOUBLE_SORCERER");
DB_SHA_MirrorEncounter_ClassData((TAG)WARLOCK_5804f55a-93f7-4281-9512-8d548a9e2a22,"SHA_TortureTrial_Warlock","SHA_TORTURETRIAL_DOUBLE_WARLOCK");
DB_SHA_MirrorEncounter_ClassData((TAG)WIZARD_6fe3ae27-dc6c-4fc9-9245-710c790c396c,"SHA_TortureTrial_Wizard","SHA_TORTURETRIAL_DOUBLE_WIZARD");

PROC
PROC_SHA_Trials_ApplyClassTags((CHARACTER)_Double,(CHARACTER)_Target)
AND
NOT QRY_SHA_MirrorEncounter_HasOfficialClass((CHARACTER)_Double,(CHARACTER)_Target)
THEN
PROC_SHA_MirrorEncounter_ApplyClassStatus(_Double, "SHA_TORTURETRIAL_DOUBLE_FALLBACK");

QRY
QRY_SHA_MirrorEncounter_HasOfficialClass((CHARACTER)_Double,(CHARACTER)_Target)
AND
DB_SHA_MirrorEncounter_ClassData((TAG)_ClassTag,_SpellSet,_Status)
AND
IsTagged(_Target,_ClassTag,1)
THEN
PROC_SHA_MirrorEncounter_ApplyClassStatus(_Double,_Status);

PROC
PROC_SHA_MirrorEncounter_ApplyClassStatus((CHARACTER)_Double,(STRING)_Status)
THEN
ApplyStatus(_Double,_Status,-1.0,1);

//Clean up
PROC
PROC_SHA_Trials_ClearClassTags((CHARACTER)_Double)
AND
DB_SHA_MirrorEncounter_ClassData(_,_,_Status)
THEN
RemoveStatus(_Double,_Status);

PROC
PROC_SHA_Trials_ClearClassTags((CHARACTER)_Double)
AND
DB_SHA_MirrorEncounter_AddedSpells(_Double,_Action)
THEN
RemoveSpell(_Double,_Action);
NOT DB_SHA_MirrorEncounter_AddedSpells(_Double,_Action);

//REGION Choose best spawn for gem to appear at
IF
DB_Defeated(_Char)
AND
DB_SHA_Trials_MirrorCharacters((CHARACTER)_Char, _)
THEN
PROC_SHA_Trials_UpdateLastCloneToBeDefeated((CHARACTER)_Char);

PROC
PROC_SHA_Trials_UpdateLastCloneToBeDefeated((CHARACTER)_Char)
AND
DB_SHA_Trials_LastCloneToBeDefeated(_OtherChar)
THEN
NOT DB_SHA_Trials_LastCloneToBeDefeated(_OtherChar);

PROC
PROC_SHA_Trials_UpdateLastCloneToBeDefeated((CHARACTER)_Char)
THEN
DB_SHA_Trials_LastCloneToBeDefeated(_Char);

PROC
PROC_StateCleared_Defeated(_Char)
AND
DB_SHA_Trials_LastCloneToBeDefeated((CHARACTER)_Char)
THEN
NOT DB_SHA_Trials_LastCloneToBeDefeated((CHARACTER)_Char);

IF
EnteredChasm((CHARACTER)_Char, (CHARACTER)_Cause, _, _X, _Y, _Z)
AND
DB_SHA_Trials_MirrorCharacters((CHARACTER)_Char, _)
THEN
DB_SHA_Trials_CloneEnteredChasm(_Char, _Cause, _X, _Y, _Z);

// Desired case: clone killed by player without being thrown into a chasm.
PROC
PROC_SHA_Trials_TrialPassed((ITEM)S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe)
AND
DB_SHA_Trials_LastCloneToBeDefeated(_Char)
AND
NOT DB_SHA_Trials_CloneEnteredChasm(_Char, _, _, _, _)
AND
QRY_OnlyOnce("SHA_CloneCharacterFellIntoChasm")
THEN
TeleportTo(S_SHA_TrialGem_001_3c29b3b3-65ec-4d47-b689-e5eca053602b, _Char, "SHA_ClonesGemAppears");

// Chasm cases:
// 1. Thrown by player? Spawn gem next to him.
PROC
PROC_SHA_Trials_TrialPassed((ITEM)S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe)
AND
NOT DB_OnlyOnce("SHA_CloneCharacterFellIntoChasm")
AND
DB_SHA_Trials_LastCloneToBeDefeated(_Char)
AND
DB_SHA_Trials_CloneEnteredChasm(_Char, _Cause, _X, _Y, _Z)
AND
DB_Players(_Cause)
AND
QRY_OnlyOnce("SHA_CloneCharacterFellIntoChasm")
THEN
TeleportTo(S_SHA_TrialGem_001_3c29b3b3-65ec-4d47-b689-e5eca053602b, _Cause, "SHA_ClonesGemAppears");

// 2. Thrown by summon? Spawn gem next to summon's owner.
PROC
PROC_SHA_Trials_TrialPassed((ITEM)S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe)
AND
NOT DB_OnlyOnce("SHA_CloneCharacterFellIntoChasm")
AND
DB_SHA_Trials_LastCloneToBeDefeated(_Char)
AND
DB_SHA_Trials_CloneEnteredChasm(_Char, _Cause, _X, _Y, _Z)
AND
DB_PlayerSummons(_Cause)
AND
CharacterGetOwner(_Cause, _Owner)
AND
QRY_OnlyOnce("SHA_CloneCharacterFellIntoChasm")
THEN
TeleportTo(S_SHA_TrialGem_001_3c29b3b3-65ec-4d47-b689-e5eca053602b, _Owner, "SHA_ClonesGemAppears");

// 3. Spawn next to valid player
PROC
PROC_SHA_Trials_TrialPassed((ITEM)S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe)
AND
NOT DB_OnlyOnce("SHA_CloneCharacterFellIntoChasm")
AND
DB_Players(_Player)
AND
DB_InRegion(_Player, S_SHA_MentalTortureTrigger_e272b659-024a-457d-a5f5-a9317d60e1eb)
AND
QRY_OnlyOnce("SHA_CloneCharacterFellIntoChasm")
THEN
TeleportTo(S_SHA_TrialGem_001_3c29b3b3-65ec-4d47-b689-e5eca053602b, _Player, "SHA_ClonesGemAppears");

// 4. Can't spawn near player: spawn gem at closest valid point inside the 
PROC
PROC_SHA_Trials_TrialPassed((ITEM)S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe)
AND
NOT DB_OnlyOnce("SHA_CloneCharacterFellIntoChasm")
AND
DB_SHA_Trials_LastCloneToBeDefeated(_Char)
AND
DB_SHA_Trials_CloneEnteredChasm(_Char, _, _X, _Y, _Z)
AND
PositionIsInTrigger(_X, _Y, _Z, S_SHA_MentalTortureTrigger_e272b659-024a-457d-a5f5-a9317d60e1eb, 1)
AND
QRY_OnlyOnce("SHA_CloneCharacterFellIntoChasm")
THEN
TeleportToPosition(S_SHA_TrialGem_001_3c29b3b3-65ec-4d47-b689-e5eca053602b, _X, _Y, _Z, "SHA_ClonesGemAppears", 0);

// 5. Everything fails, teleport to fallback position.
PROC
PROC_SHA_Trials_TrialPassed((ITEM)S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe)
AND
NOT DB_OnlyOnce("SHA_CloneCharacterFellIntoChasm")
THEN
TeleportTo(S_SHA_TrialGem_001_3c29b3b3-65ec-4d47-b689-e5eca053602b, S_SHA_TrialGem_001_FallbackPos_d54d8bb6-1e1b-440a-83cf-aec48974142a, "SHA_ClonesGemAppears");

IF
EntityEvent(_Gem, "SHA_ClonesGemAppears")
THEN
PROC_Foop(_Gem);

IF
EntityEvent(_Gem, "SHA_ClonesGemAppears")
AND
DB_SHA_Trials_CloneEnteredChasm(_Char, _Cause, _X, _Y, _Z)
THEN
NOT DB_SHA_Trials_CloneEnteredChasm(_Char, _Cause, _X, _Y, _Z);
//END_REGION

//END_REGION

//REGION Mechanic for damaging players who dont kill their own double first

PROC
PROC_SHA_Trials_ApplyClassTags((CHARACTER)_Double,(CHARACTER)_Target)
THEN
ApplyStatus(_Double,"SHA_TORTURETRIAL_DOUBLE_OWNER",-1.0,1);
ApplyStatus(_Target,"SHA_TORTURETRIAL_DOUBLE_TARGET",-1.0,1,_Double);
DB_SHA_Trials_CharactersWithDoubleStatus(_Double);
DB_SHA_Trials_CharactersWithDoubleStatus(_Target);

IF
StatusRemoved((CHARACTER)_Char, "SHA_TORTURETRIAL_DOUBLE_OWNER",_, _)
AND
DB_SHA_Trials_CharactersWithDoubleStatus(_Char)
THEN
NOT DB_SHA_Trials_CharactersWithDoubleStatus(_Char);

IF
StatusRemoved((CHARACTER)_Char, "SHA_TORTURETRIAL_DOUBLE_TARGET",_, _)
AND
DB_SHA_Trials_CharactersWithDoubleStatus(_Char)
THEN
NOT DB_SHA_Trials_CharactersWithDoubleStatus(_Char);

IF
StatusRemoved((CHARACTER)_Double,"SHA_TORTURETRIAL_DOUBLE_OWNER",_,_)
AND
DB_SHA_MirrorEncounter_Pairs(_Double,_Target)
THEN
PROC_SHA_Trials_RemoveDoubleStatuses(_Target);

PROC
PROC_SHA_Trials_RemoveDoubleStatuses((CHARACTER)_Char)
AND
HasAppliedStatus(_Char, "SHA_TORTURETRIAL_DOUBLE_OWNER", 1)
THEN
RemoveStatus(_Char, "SHA_TORTURETRIAL_DOUBLE_OWNER");

PROC
PROC_SHA_Trials_RemoveDoubleStatuses((CHARACTER)_Char)
AND
HasAppliedStatus(_Char, "SHA_TORTURETRIAL_DOUBLE_TARGET", 1)
THEN
RemoveStatus(_Char, "SHA_TORTURETRIAL_DOUBLE_TARGET");

PROC
PROC_SHA_Trials_CustomTrialOutcome(S_SHA_MentalTortureTrial_98811843-1086-4931-a3bd-3fef817347fe)
AND
DB_SHA_Trials_CharactersWithDoubleStatus(_Char)
THEN
PROC_SHA_Trials_RemoveDoubleStatuses(_Char);

//END_REGION

//END_REGION

//REGION Lights trial
IF
DB_SHA_Trials_LightTrialShadow(_Shadow)
THEN
DB_SpotPlayers(_Shadow, "SHA_LightTrial_Shadow", NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);
DB_SpotPlayers_Continuous(_Shadow, "SHA_LightTrial_Shadow");
DB_SpotPlayers_SpotTrigger(_Shadow, "SHA_LightTrial_Shadow", (TRIGGER)S_SHA_LightTrial_SpotTrigger_ed62bf09-4f87-4fb7-993a-ced7453ebe77);
DB_SpotPlayers_SpotterIgnoreCantTalk(_Shadow, "SHA_LightTrial_Shadow");
DB_SpotPlayers_IncludeWildshapedPlayers(_Shadow, "SHA_LightTrial_Shadow");
DB_SpotPlayers_IncludeSummons(_Shadow, "SHA_LightTrial_Shadow");
DB_SpotPlayers_IncludeInconspicuous(_Shadow, "SHA_LightTrial_Shadow");
DB_SpotPlayers_IncludeFollowers(_Shadow, "SHA_LightTrial_Shadow");
SetCanJoinCombat(_Shadow, 0);
SetOnStage(_Shadow, 0);
TriggerRegisterForCharacter(S_SHA_LightTrial_SecretRoom_335cae58-3416-41c0-874d-78b16f6bcb45, _Shadow);
PROC_CharacterDisableAllCrimes(_Shadow);
DB_IgnoreAssault(_Shadow);


PROC
PROC_SHA_Trials_RestartTrial(S_SHA_ObjectOfDarknessTrial_3892d0a6-24f4-402e-8b51-03ba8cfb4429)
THEN
PROC_SHA_Trials_LockAndCloseLighTrialDoors();

PROC
PROC_SHA_Trials_RestartTrial(S_SHA_ObjectOfDarknessTrial_3892d0a6-24f4-402e-8b51-03ba8cfb4429)
AND
DB_SHA_Trials_LightTrialShadow(_Shadow)
THEN
PROC_SHA_Trials_ResetCharacter(_Shadow);

PROC
PROC_SHA_Trials_LockAndCloseLighTrialDoors()
AND
DB_SHA_Trials_LightTrialDoors(_Door)
THEN
Close(_Door);
Lock(_Door, "storylock");

// Trial progress
IF
FlagSet(SHA_Trials_State_TrialInProgress_87772420-50d7-4937-85ec-95c42fd9fd14, S_SHA_ObjectOfDarknessTrial_3892d0a6-24f4-402e-8b51-03ba8cfb4429, _)
THEN
PROC_Foop((ITEM)S_SHA_TrialGem_000_5e3d0d3b-6fcf-48b8-a10c-580ff4efd1ff, (EFFECTRESOURCE)VFX_Script_SCL_Trial_Shar_Teleport_Appear_BodyFX_01_520e41d0-3515-f40c-1ebc-9b6f899ec782);
PROC_SHA_Trials_UnlockAndOpenLighTrialDoors();
PROC_TriggerRegisterForPlayers(S_SHA_LightTrial_GoalTrigger_1d7b2ec2-f9a4-4739-9f72-532fa7159584);

IF
FlagSet(SHA_Trials_State_TrialInProgress_87772420-50d7-4937-85ec-95c42fd9fd14, S_SHA_ObjectOfDarknessTrial_3892d0a6-24f4-402e-8b51-03ba8cfb4429, _)
AND
DB_SHA_Trials_LightTrialShadow(_Shadow)
THEN
PROC_Foop(_Shadow);

IF
AddedTo((ITEM)S_SHA_TrialGem_000_5e3d0d3b-6fcf-48b8-a10c-580ff4efd1ff, _Player,_)
AND
DB_Players((CHARACTER)_Player)
AND
DB_SHA_Trials_GemsNotTaken((ITEM)S_SHA_TrialGem_000_5e3d0d3b-6fcf-48b8-a10c-580ff4efd1ff)
AND
GetFlag(SHA_Trials_State_TrialInProgress_87772420-50d7-4937-85ec-95c42fd9fd14, S_SHA_ObjectOfDarknessTrial_3892d0a6-24f4-402e-8b51-03ba8cfb4429, 1)
THEN
SetFlag(SHA_Trials_Event_TrialPassed_a2bf1d24-4f81-4bae-ac3f-68a5760d122c, S_SHA_ObjectOfDarknessTrial_3892d0a6-24f4-402e-8b51-03ba8cfb4429); 

PROC
PROC_SHA_Trials_CustomTrialOutcome(S_SHA_ObjectOfDarknessTrial_3892d0a6-24f4-402e-8b51-03ba8cfb4429)
AND
DB_SHA_Trials_LightTrialShadow(_Shadow)
THEN
PROC_Poof(_Shadow);

PROC
PROC_SHA_Trials_CustomTrialOutcome(S_SHA_ObjectOfDarknessTrial_3892d0a6-24f4-402e-8b51-03ba8cfb4429)
THEN
PROC_TriggerUnregisterForPlayers(S_SHA_LightTrial_GoalTrigger_1d7b2ec2-f9a4-4739-9f72-532fa7159584);

PROC
PROC_SHA_Trials_TrialPassed(S_SHA_ObjectOfDarknessTrial_3892d0a6-24f4-402e-8b51-03ba8cfb4429)
THEN
PROC_SHA_Trials_ActivateTeleportStone((ITEM)S_SHA_LightTrial_GoalStatue_dfddbf3e-7ab8-4b65-bc2c-2e50a6ed2e04);

PROC
PROC_SHA_Trials_UnlockAndOpenLighTrialDoors()
AND
DB_SHA_Trials_LightTrialDoors(_Door)
THEN
PROC_ItemUnlockAndOpen(_Door);

//Shadow spotting events
PROC
PROC_SpotPlayers_Spotted(_Shadow, "SHA_LightTrial_Shadow", _Char)
AND
NOT DB_Defeated(_Char)
THEN
PROC_SHA_Trials_ShadowSpotsPlayer(_Char, _Shadow);

PROC
PROC_SHA_Trials_ShadowSpotsPlayer((CHARACTER)_Char, (CHARACTER)_Shadow)
AND
DB_Players(_Char)
AND
QRY_StartDialog((DIALOGRESOURCE)SHA_Trials_AD_LightTrialShadow_fb7d5276-d521-6d02-8d50-0ac8aedf66b1, _Shadow)
AND
QRY_OncePerUserAndNearbyPlayers(_Char, "SHA_Trials_PlayerSpotted")
THEN
DB_SHA_Trials_TeleportedByShadowVB(_Char);

PROC
PROC_SHA_Trials_ShadowSpotsPlayer((CHARACTER)_Player, (CHARACTER)_Shadow)
THEN
ApplyDamage(_Player, 1, "Psychic", _Shadow);
PROC_SHA_Trials_MagicTeleport(_Player, S_SHA_LightTrial_Start_1a10d90d-bbe3-49c5-b046-56a7a34fceac, _Shadow);

IF
AutomatedDialogEnded(SHA_Trials_AD_LightTrialShadow_fb7d5276-d521-6d02-8d50-0ac8aedf66b1, _)
AND
DB_SHA_Trials_TeleportedByShadowVB(_Player)
AND
QRY_SpeakerIsAvailable(_Player)
AND
QRY_OncePerUserAndNearbyPlayers(_Player, "SHA_Trials_TeleportedByShadowVB")
THEN
StartVoiceBark(SHA_Trials_VB_TeleportedByShadow_af9826e6-1a85-9dcf-cf3c-d0def81d34fb, _Player);

IF
AutomatedDialogEnded(SHA_Trials_AD_LightTrialShadow_fb7d5276-d521-6d02-8d50-0ac8aedf66b1, _)
AND
DB_SHA_Trials_TeleportedByShadowVB(_Player)
THEN
NOT DB_SHA_Trials_TeleportedByShadowVB(_Player);

IF
DualEntityEvent(_, _, "SHA_StopObjectOfDarknessTrial")
AND
GetFlag(SHA_Trials_State_TrialInProgress_87772420-50d7-4937-85ec-95c42fd9fd14, S_SHA_ObjectOfDarknessTrial_3892d0a6-24f4-402e-8b51-03ba8cfb4429, 1)
THEN
PROC_Poof((ITEM)S_SHA_TrialGem_000_5e3d0d3b-6fcf-48b8-a10c-580ff4efd1ff, (EFFECTRESOURCE)VFX_Script_SCL_Trial_Shar_Teleport_Disappear_BodyFX_01_0f13f03e-107a-22af-ef1f-5a7a8967d5b9);
SetFlag(SHA_Trials_Event_TrialFailed_e80850ea-ab84-4043-9809-960d4db885f6, S_SHA_ObjectOfDarknessTrial_3892d0a6-24f4-402e-8b51-03ba8cfb4429);

// Secret doors
IF
DualEntityEvent(_, _, "SHA_LightTrial_SwitchSecretDoorsState")
AND
NOT QRY_SHA_Trials_AnySecretDoorMoving()
THEN
PROC_SHA_Trials_SwitchSecretRoomDoorsInLightTrialState();

// Switch states
PROC
PROC_SHA_Trials_SwitchSecretRoomDoorsInLightTrialState()
AND
DB_SHA_Trials_LightTrialSecretDoors_Open(_OldState)
AND
IntegerProduct(_OldState, -1, _NewState)
THEN
NOT DB_SHA_Trials_LightTrialSecretDoors_Open(_OldState);
DB_SHA_Trials_LightTrialSecretDoors_Open(_NewState);

PROC
PROC_SHA_Trials_SwitchSecretRoomDoorsInLightTrialState()
AND
DB_SHA_Trials_LightTrialSecretDoors_Open(-1)
AND
DB_SHA_Trials_LightTrialSecretDoors(_Door, _StartPos, _MoveTo, 0)
THEN
NOT DB_SHA_Trials_LightTrialSecretDoors(_Door, _StartPos, _MoveTo, 0);
DB_SHA_Trials_LightTrialSecretDoors(_Door, _StartPos, _MoveTo, 1);
ItemMoveTo(_Door, _StartPos, 3.0, 0.0, 0, "SHA_Trials_SecretDoorMovedToPosition", 1);

PROC
PROC_SHA_Trials_SwitchSecretRoomDoorsInLightTrialState()
AND
DB_SHA_Trials_LightTrialSecretDoors_Open(1)
AND
DB_SHA_Trials_LightTrialSecretDoors(_Door, _MoveTo, _EndPos, 0)
THEN
NOT DB_SHA_Trials_LightTrialSecretDoors(_Door, _MoveTo, _EndPos, 0);
DB_SHA_Trials_LightTrialSecretDoors(_Door, _MoveTo, _EndPos, 1);
ItemMoveTo(_Door, _EndPos, 3.0, 0.0, 0, "SHA_Trials_SecretDoorMovedToPosition", 1);

IF
EntityEvent((ITEM)_Door, "SHA_Trials_SecretDoorMovedToPosition")
AND
DB_SHA_Trials_LightTrialSecretDoors(_Door, _StartPos, _EndPos, 1)
THEN
NOT DB_SHA_Trials_LightTrialSecretDoors(_Door, _StartPos, _EndPos, 1);
DB_SHA_Trials_LightTrialSecretDoors(_Door, _StartPos, _EndPos, 0);

QRY
QRY_SHA_Trials_AnySecretDoorMoving()
AND
DB_SHA_Trials_LightTrialSecretDoors(_, _, _, 1)
THEN
DB_NOOP(1);

IF
DB_SHA_Trials_LightTrialSecretDoors_Open(-1)
THEN
SetFlag(SHA_Trials_State_SecretRoomClosed_224e42c9-4a5a-4be1-84dd-119e4929cc3b, NULL_00000000-0000-0000-0000-000000000000);

IF
DB_SHA_Trials_LightTrialSecretDoors_Open(1)
THEN
ClearFlag(SHA_Trials_State_SecretRoomClosed_224e42c9-4a5a-4be1-84dd-119e4929cc3b, NULL_00000000-0000-0000-0000-000000000000);

IF
DualEntityEvent(_, _, "SHA_LightTrial_ActivateSecretDoor")
AND
QRY_OnlyOnce_Reset("SHA_LightTrial_SecretDoorActivated")
AND
IsOpened(S_SHA_LightTrial_SecretSlidingWall_8b379499-f6c5-479a-9e2b-2e2b51868dde, 0)
AND
QRY_OnlyOnce("SHA_LightTrial_SecretDoorActivated")
THEN
Open(S_SHA_LightTrial_SecretSlidingWall_8b379499-f6c5-479a-9e2b-2e2b51868dde);

IF
DualEntityEvent(_, _, "SHA_LightTrial_ActivateSecretDoor")
AND
IsOpened(S_SHA_LightTrial_SecretSlidingWall_8b379499-f6c5-479a-9e2b-2e2b51868dde, 1)
AND
QRY_OnlyOnce("SHA_LightTrial_SecretDoorActivated")
THEN
Close(S_SHA_LightTrial_SecretSlidingWall_8b379499-f6c5-479a-9e2b-2e2b51868dde);

IF
UseStarted(_Char, S_SHA_LightTrial_GoalStatue_dfddbf3e-7ab8-4b65-bc2c-2e50a6ed2e04)
THEN
PROC_SHA_Trials_MagicTeleport(_Char, S_SHA_LightTrial_Start_1a10d90d-bbe3-49c5-b046-56a7a34fceac, S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e);
//END_REGION

//REGION Path of Darkness trial
IF
DualEntityEvent(_, _, "SHA_StopPathOfDarknessTrial")
AND
GetFlag(SHA_Trials_State_TrialInProgress_87772420-50d7-4937-85ec-95c42fd9fd14, S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, 1)
THEN
SetFlag(SHA_Trials_Event_TrialFailed_e80850ea-ab84-4043-9809-960d4db885f6, S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e);

IF
FlagSet((FLAG)SHA_Trials_Event_TrialFailed_e80850ea-ab84-4043-9809-960d4db885f6, S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, _)
AND
DB_SHA_Trials_GemsNotTaken(S_SHA_TrialGem_002_b8dda6e3-8cb8-4287-81a4-0ac5260859c7)
THEN
PROC_Poof((ITEM)S_SHA_TrialGem_002_b8dda6e3-8cb8-4287-81a4-0ac5260859c7, (EFFECTRESOURCE)VFX_Script_SCL_Trial_Shar_Teleport_Disappear_BodyFX_01_0f13f03e-107a-22af-ef1f-5a7a8967d5b9);

PROC
PROC_SHA_Trials_RestartTrial(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e)
AND
DB_SHA_Trials_GemsNotTaken(S_SHA_TrialGem_002_b8dda6e3-8cb8-4287-81a4-0ac5260859c7)
THEN
PROC_Poof((ITEM)S_SHA_TrialGem_002_b8dda6e3-8cb8-4287-81a4-0ac5260859c7, (EFFECTRESOURCE)VFX_Script_SCL_Trial_Shar_Teleport_Disappear_BodyFX_01_0f13f03e-107a-22af-ef1f-5a7a8967d5b9);

PROC
PROC_SHA_Trials_RestartTrial(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e)
AND
DB_SHA_Trials_InvisiblePlatformsTries(_Chances)
THEN
NOT DB_SHA_Trials_InvisiblePlatformsTries(_Chances);

PROC
PROC_SHA_Trials_RestartTrial(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e)
AND
DB_SHA_Trials_TrialRoomTriggers(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, S_SHA_PathOfDarknessTrigger_d535f17f-7d95-4996-b4f7-2f53c4a7348c, 1)
THEN
NOT DB_SHA_Trials_TrialRoomTriggers(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, S_SHA_PathOfDarknessTrigger_d535f17f-7d95-4996-b4f7-2f53c4a7348c, 1);
DB_SHA_Trials_TrialRoomTriggers(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, S_SHA_PathOfDarknessTrigger_d535f17f-7d95-4996-b4f7-2f53c4a7348c, 0);

PROC
PROC_SHA_Trials_RestartTrial(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e)
THEN
PROC_SHA_Trials_UpdateInvisiblePlatformsCandles();

// If trial starts and there is only one player inside, give him three tries
// Update candles which represent the number of tries left
IF
FlagSet(SHA_Trials_State_TrialInProgress_87772420-50d7-4937-85ec-95c42fd9fd14, S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, _)
THEN
NOT DB_SHA_Trials_TrialRoomTriggers(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, S_SHA_PathOfDarknessTrigger_d535f17f-7d95-4996-b4f7-2f53c4a7348c, 0);
DB_SHA_Trials_TrialRoomTriggers(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, S_SHA_PathOfDarknessTrigger_d535f17f-7d95-4996-b4f7-2f53c4a7348c, 1);
DB_SHA_Trials_InvisiblePlatformsTries(3);
PROC_Foop((ITEM)S_SHA_TrialGem_002_b8dda6e3-8cb8-4287-81a4-0ac5260859c7, (EFFECTRESOURCE)VFX_Script_SCL_Trial_Shar_Teleport_Appear_BodyFX_01_520e41d0-3515-f40c-1ebc-9b6f899ec782);
PROC_TriggerRegisterForPlayers(S_SHA_PathOfDarknessTrial_GoalTrigger_b4df1e7b-7d4f-42d2-b599-e1dae0c0d15e);
PROC_SHA_Trials_UpdateInvisiblePlatformsCandles();

// Update tries left
IF
DB_Defeated(_Char)
AND
DB_InRegion((CHARACTER)_Char, S_SHA_PathOfDarknessTrigger_d535f17f-7d95-4996-b4f7-2f53c4a7348c)
AND
GetFlag(SHA_Trials_State_TrialInProgress_87772420-50d7-4937-85ec-95c42fd9fd14, S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, 1)
THEN
PROC_SHA_Trials_UpdateInvisiblePlatformsTries();
PROC_SHA_Trials_UpdateInvisiblePlatformsCandles();

PROC
PROC_SHA_Trials_UpdateInvisiblePlatformsTries()
AND
DB_SHA_Trials_InvisiblePlatformsTries(_TriesLeft)
AND
IntegerSubtract(_TriesLeft, 1, _UpdatedTries)
THEN
NOT DB_SHA_Trials_InvisiblePlatformsTries(_TriesLeft);
DB_SHA_Trials_InvisiblePlatformsTries(_UpdatedTries);

PROC
PROC_SHA_Trials_UpdateInvisiblePlatformsTries()
AND
DB_SHA_Trials_InvisiblePlatformsTries(_TriesLeft)
AND
_TriesLeft < 1
AND
DB_SHA_Trials_TrialRoomTriggers(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, S_SHA_PathOfDarknessTrigger_d535f17f-7d95-4996-b4f7-2f53c4a7348c, 1)
THEN
NOT DB_SHA_Trials_TrialRoomTriggers(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, S_SHA_PathOfDarknessTrigger_d535f17f-7d95-4996-b4f7-2f53c4a7348c, 1);
DB_SHA_Trials_TrialRoomTriggers(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, S_SHA_PathOfDarknessTrigger_d535f17f-7d95-4996-b4f7-2f53c4a7348c, 0);

PROC
PROC_SHA_Trials_UpdateInvisiblePlatformsCandles()
AND
DB_SHA_Trials_InvisiblePlatformsTries(_TriesLeft)
AND
DB_SHA_Trials_InvisiblePlatformsCandles(_Candle, _Try)
AND
_TriesLeft >= _Try
AND
NOT DB_LoopEffect(_Candle, _, "SHA_PathOfDarkness_WarningLight", _, _, _, _)
THEN
PROC_LoopEffect((EFFECTRESOURCE)VFX_Status_DancingLights_DummyFX_01_3ba17bb8-7ee3-cf58-c5a9-9b0488cf96f0, _Candle, "SHA_PathOfDarkness_WarningLight", "SCL_Main_A", "", 0.5);

PROC
PROC_SHA_Trials_UpdateInvisiblePlatformsCandles()
AND
DB_SHA_Trials_InvisiblePlatformsTries(_TriesLeft)
AND
DB_SHA_Trials_InvisiblePlatformsCandles(_Candle, _Try)
AND
_TriesLeft < _Try
AND
DB_LoopEffect(_Candle, _, "SHA_PathOfDarkness_WarningLight", _, _, _, _)
THEN
PROC_StopLoopEffect(_Candle, "SHA_PathOfDarkness_WarningLight");

PROC
PROC_SHA_Trials_UpdateInvisiblePlatformsCandles()
AND
NOT DB_SHA_Trials_InvisiblePlatformsTries(_)
AND
DB_SHA_Trials_InvisiblePlatformsCandles(_Candle, _)
AND
DB_LoopEffect(_Candle, _, "SHA_PathOfDarkness_WarningLight", _, _, _, _)
THEN
PROC_StopLoopEffect(_Candle, "SHA_PathOfDarkness_WarningLight");

PROC
PROC_SHA_Trials_CharacterEnteredDeathTrigger_Hook((ITEM)S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, (TRIGGER)_TeleportTrigger, (CHARACTER)_Char)
AND
GetFlag(SHA_Trials_State_TrialInProgress_87772420-50d7-4937-85ec-95c42fd9fd14, S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, 1)
THEN
PROC_SHA_Trials_UpdateInvisiblePlatformsTries();
PROC_SHA_Trials_UpdateInvisiblePlatformsCandles();

QRY
QRY_SHA_Trials_BlockKillCharacter((ITEM)S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, (CHARACTER)_Char)
AND
GetFlag(SHA_Trials_State_TrialInProgress_87772420-50d7-4937-85ec-95c42fd9fd14, S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, 1)
AND
DB_SHA_Trials_InvisiblePlatformsTries(_TriesLeft)
AND
_TriesLeft > 0
THEN
PROC_SHA_Trials_DeathTriggerTeleport(_Char); // Just teleport them instead

PROC
PROC_SHA_Trials_MagicTeleport(_Char, _TeleportTrigger, (ITEM)S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e)
AND
DB_SHA_Trials_DeathTriggers((ITEM)S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, _, (TRIGGER)_TeleportTrigger)
AND
QRY_OnlyOnce("SHA_Trials_TrialWarning")
THEN
DB_SHA_Trials_TeleportingTo((TRIGGER)_TeleportTrigger);
TimerLaunch("SHA_Trials_TrialWarningDelay", 2500);

IF
TimerFinished("SHA_Trials_TrialWarningDelay")
AND
DB_SHA_Trials_TeleportingTo((TRIGGER)_TeleportTrigger)
AND
DB_SHA_Trials_PlatformStatue(_TeleportTrigger, _VoiceHelper)
AND
DB_SHA_Trials_InvisiblePlatformsTries(_TriesLeft)
AND
DB_SHA_Trials_TriesLeftWarning(_TriesLeft, _Warning)
THEN
NOT DB_OnlyOnce("SHA_Trials_TrialWarning");
NOT DB_SHA_Trials_TeleportingTo((TRIGGER)_TeleportTrigger);
PROC_TryStartAD(_Warning, _VoiceHelper);


// Character interacts with goal statue, pass the trial
IF
AddedTo((ITEM)S_SHA_TrialGem_002_b8dda6e3-8cb8-4287-81a4-0ac5260859c7, _Player,_)
AND
DB_Players((CHARACTER)_Player)
AND
DB_SHA_Trials_GemsNotTaken((ITEM)S_SHA_TrialGem_002_b8dda6e3-8cb8-4287-81a4-0ac5260859c7)
AND
GetFlag(SHA_Trials_State_TrialInProgress_87772420-50d7-4937-85ec-95c42fd9fd14, S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, 1)
THEN
SetFlag(SHA_Trials_Event_TrialPassed_a2bf1d24-4f81-4bae-ac3f-68a5760d122c, S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e);


PROC
PROC_SHA_Trials_TrialPassed(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e)
THEN
PROC_SHA_Trials_ActivateTeleportStone((ITEM)S_SHA_PathOfDarknessTrial_Goal_26c3ff42-4e78-4e4c-a700-bf17052714f6);
RemoveSurfaceLayer(S_SHA_PathOfDarknessTrial_GoalTrigger_b4df1e7b-7d4f-42d2-b599-e1dae0c0d15e, "Cloud", 6.0);

PROC
PROC_SHA_Trials_TrialPassed(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e)
AND
DB_SHA_Trials_InvisiblePlatformsCandles(_Candle, _)
AND
DB_LoopEffect(_Candle, _, "SHA_PathOfDarkness_WarningLight", _, _, _, _)
THEN
PROC_StopLoopEffect(_Candle, "SHA_PathOfDarkness_WarningLight");

PROC
PROC_SHA_Trials_CustomTrialOutcome(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e)
THEN
PROC_TriggerUnregisterForPlayers(S_SHA_PathOfDarknessTrial_GoalTrigger_b4df1e7b-7d4f-42d2-b599-e1dae0c0d15e);

IF
UseStarted(_Char, S_SHA_PathOfDarknessTrial_Goal_26c3ff42-4e78-4e4c-a700-bf17052714f6)
THEN
PROC_SHA_Trials_MagicTeleport(_Char, S_SHA_PathOfDarknessTrial_DeathTriggerTP_000_6a28f53d-3819-4869-b961-fc564615962a, S_SHA_PathOfDarknessTrial_Goal_26c3ff42-4e78-4e4c-a700-bf17052714f6);

IF
FlagSet(SHA_Trials_State_TrialInProgress_87772420-50d7-4937-85ec-95c42fd9fd14, S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, _)
THEN
NOT DB_SHA_Trials_AltDeathTriggerTeleport(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, S_SHA_PathOfDarknessTrial_DeathTriggerTP_000_6a28f53d-3819-4869-b961-fc564615962a);

IF
FlagCleared(SHA_Trials_State_TrialInProgress_87772420-50d7-4937-85ec-95c42fd9fd14, S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, _)
THEN
DB_SHA_Trials_AltDeathTriggerTeleport(S_SHA_PathOfDarknessTrial_ff2af557-dbc2-4e27-9c02-f13b25dbe86e, S_SHA_PathOfDarknessTrial_DeathTriggerTP_000_6a28f53d-3819-4869-b961-fc564615962a);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
