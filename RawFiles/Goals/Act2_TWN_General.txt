Version 1
SubGoalCombiner SGC_AND
INITSECTION
//Voicebarks in front of the buildings
DB_TWN_Buildings((TRIGGER)S_TWN_Tollhouse_BuildingArea_caff6009-bd31-4a5d-9f67-df1b874ce5df, (TRIGGER)S_TWN_Tollhouse_EntranceVoicebarkArea_a48d9922-091b-47e4-b4e2-39e193616371, (VOICEBARKRESOURCE)TWN_Tollhouse_VB_Entrance_0b6f6a1f-0552-8b00-e8b4-b20ea95bc9c2);
DB_TWN_Buildings((TRIGGER)S_TWN_Distillery_BuildingArea_bcc36ad6-263e-4809-8652-20cc9b8eee1b, (TRIGGER)S_TWN_Distillery_EntranceVoicebarkArea_c6a79f96-537d-4fce-bc19-74a2bf52d244, (VOICEBARKRESOURCE)TWN_Distillery_VB_Entrance_47de3e97-be60-b5c8-91b4-eece365218df);
DB_TWN_Buildings((TRIGGER)S_TWN_Hospital_BuildingArea_87268687-5554-4e08-a46d-485742b580f6, (TRIGGER)S_TWN_Hospital_EntranceVoicebarkArea_f3ba913c-6545-47b4-adb1-53a2a95f82ea, (VOICEBARKRESOURCE)TWN_Hospital_VB_Entrance_42656d47-99d0-eea9-d323-7f810e54fcf2);
DB_TWN_Buildings((TRIGGER)S_TWN_MasonsGuild_BuildingArea_5eeacbd2-beec-4e03-86ef-8dfb01c2923d, (TRIGGER)S_TWN_MasonsGuild_EntranceVoicebarkArea_74d27a8a-16b5-40dc-8fd9-f3332ccf3204, (VOICEBARKRESOURCE)TWN_MasonsGuild_VB_Entrance_9c507cfa-58d7-991b-71b8-84f56995f38f);

// Definitions
// DB_TWN_Bosses((CHARACTER)_Boss, (DIALOGRESOURCE)_Dialog, 
//	(STRING)_SpotID, (FLAG)_DeadFlag, 
//	(FLAG)PeacefulResolutionFlag, (FLAG)HostileResolutionFlag);
DB_TWN_Bosses((CHARACTER)S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, (DIALOGRESOURCE)TWN_Tollhouse_TollhouseMaster_1fc993a8-c338-7cd6-aef2-add52d540d28, 
	"TWN_Tollhouse_TollhouseMaster", (FLAG)TWN_Tollhouse_State_TollhouseMasterPermaDefeated_fbe5064b-60a5-4f71-af9b-b5ed5a2f610c, 
	(FLAG)TWN_Tollhouse_State_PeacefulResolution_2eb42178-c700-4742-9325-8fef38ed46ca, (FLAG)TWN_Tollhouse_State_HostileResolution_332f5654-b72d-4589-a715-18cd1c0ca5b9);

DB_TWN_Bosses((CHARACTER)S_TWN_Distillery_Brewer_4d9e3db3-9a78-4f4b-8101-1dd73c0f3be5, (DIALOGRESOURCE)TWN_Distillery_Brewer_c41d8459-3237-248f-c1bc-51e66892a65f, 
	"TWN_Distillery_Brewer", (FLAG)TWN_Distillery_State_BrewerPermaDefeated_f093cb1e-569e-458c-8412-f63be73e8b92, 
	(FLAG)TWN_Distillery_State_PeacefulResolution_ef40ccf6-a7d7-4fde-8fcf-82b947c29a44, (FLAG)TWN_Distillery_State_HostileResolution_9eb0881e-4818-4d3f-8580-238793666fc8);

DB_TWN_Bosses((CHARACTER)S_TWN_Hospital_Surgeon_e58b8b34-038b-4858-b817-c2a8096a9381, (DIALOGRESOURCE)TWN_Hospital_Surgeon_10b263cd-b704-9eb2-71f9-2c7f6eb4739b, 
	"TWN_Hospital_Surgeon", (FLAG)TWN_Hospital_State_SurgeonPermaDefeated_0820e94c-7007-4459-b935-2d33fbbf6aa9, 
	(FLAG)TWN_Hospital_State_PeacefulResolution_3830c1aa-6271-4af8-ba6c-0537bad1c624, (FLAG)TWN_Hospital_State_HostileResolution_b183f1f4-2935-4995-a9df-5731c0f65cf3);

// Boss, Minion, Spotting
DB_TWN_Bosses_Minions((CHARACTER)S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, (CHARACTER)S_TWN_Tollhouse_Face_Cowardice_efea0b12-68eb-4329-8fd9-14078e983607);
DB_TWN_Bosses_Minions((CHARACTER)S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, (CHARACTER)S_TWN_Tollhouse_Face_Greed_46e0f41a-6603-43ee-8e99-5dd91e7e9bb9);
DB_TWN_Bosses_Minions((CHARACTER)S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, (CHARACTER)S_TWN_Tollhouse_Face_Guilt_2321affc-6302-4800-87dc-bcd220642920);
DB_TWN_Bosses_Minions((CHARACTER)S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, (CHARACTER)S_TWN_Tollhouse_Face_Heartlessness_f335ce62-1d8b-4f70-969d-ef75f0dd78b1);
DB_TWN_Bosses_Minions((CHARACTER)S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, (CHARACTER)S_TWN_Tollhouse_Face_Obedience_0e35df6f-31f8-4ebb-a44f-e472c2e6a9c9);
DB_TWN_Bosses_Minions((CHARACTER)S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, (CHARACTER)S_TWN_Tollhouse_Face_Regret_f5206c52-e481-4c2c-b24b-f186904d768d);

DB_TWN_Bosses_Minions((CHARACTER)S_TWN_Hospital_Surgeon_e58b8b34-038b-4858-b817-c2a8096a9381, (CHARACTER)S_TWN_Hospital_ZealousAssistant_000_2250bda0-cf27-4567-8a71-b1a066113eb7);
DB_TWN_Bosses_Minions((CHARACTER)S_TWN_Hospital_Surgeon_e58b8b34-038b-4858-b817-c2a8096a9381, (CHARACTER)S_TWN_Hospital_ZealousAssistant_001_e2c2a930-2eb5-419f-9020-c141a0fd0753);
DB_TWN_Bosses_Minions((CHARACTER)S_TWN_Hospital_Surgeon_e58b8b34-038b-4858-b817-c2a8096a9381, (CHARACTER)S_TWN_Hospital_ZealousAssistant_002_a9e86a16-f426-4772-a187-39256c4568a1);
DB_TWN_Bosses_Minions((CHARACTER)S_TWN_Hospital_Surgeon_e58b8b34-038b-4858-b817-c2a8096a9381, (CHARACTER)S_TWN_Hospital_ZealousAssistant_003_2be9d074-245f-4f4f-9385-260d08f4ea73);

// boss, resolution (0 - peaceful, 1 - hostile)
DB_TWN_Bosses_MinionsRemoval((CHARACTER)S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, 0);
DB_TWN_Bosses_MinionsRemoval((CHARACTER)S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, 1);

DB_TWN_Bosses_SpotTrigger((CHARACTER)S_TWN_Tollhouse_TollhouseMaster_3b460226-8ca2-4bbc-9bd7-8bb947aa2c06, "TWN_Tollhouse_TollhouseMaster", (TRIGGER)S_TWN_Tollhouse_TollhouseMaster_SpotTrigger_c217c326-759b-4c19-a528-4aa8ec9eecf2);
DB_TWN_Bosses_SpotTrigger((CHARACTER)S_TWN_Hospital_Surgeon_e58b8b34-038b-4858-b817-c2a8096a9381, "TWN_Hospital_Surgeon", (TRIGGER)S_TWN_Hospital_Surgeon_SpotTrigger_bda58cc4-525c-4cdb-9165-09ffb3490c15);
DB_TWN_Bosses_SpotTrigger((CHARACTER)S_TWN_Distillery_Brewer_4d9e3db3-9a78-4f4b-8101-1dd73c0f3be5, "TWN_Distillery_Brewer", (TRIGGER)S_TWN_Distillery_Brewer_SpotTrigger_6b80c4fc-b8ac-450f-920e-1e10c5f7a041);

DB_TWN_Shadowblights((CHARACTER)S_TWN_Hospital_Surgeon_e58b8b34-038b-4858-b817-c2a8096a9381, (TRIGGER)S_ShadowCurse_BlightArea_9_f2091d2d-2ac6-4e7e-ba08-315ffdd3e40a);

PROC_TriggerRegisterForPlayers(S_TWN_MainSub_169edf4a-bd51-4964-adb0-4d47956d5fae);

DB_TWN_AlternativeSolutions((FLAG)TWN_Tollhouse_Event_GaveEnoughCoins_a387ad5b-5959-4626-b1b7-d4936eef8c72, (DIALOGRESOURCE)TWN_Tollhouse_TollhouseMaster_1fc993a8-c338-7cd6-aef2-add52d540d28);
DB_TWN_AlternativeSolutions((FLAG)TWN_Hospital_State_NursesKilledSurgeon_dc371af2-0788-43f9-990f-aaaf2192f8ca, (DIALOGRESOURCE)TWN_Hospital_Surgeon_10b263cd-b704-9eb2-71f9-2c7f6eb4739b);
DB_TWN_AlternativeSolutions((FLAG)TWN_Hospital_State_SurgeonKilledHimself_04969d92-f292-43c7-82a1-cd3ec967cded, (DIALOGRESOURCE)TWN_Hospital_Surgeon_10b263cd-b704-9eb2-71f9-2c7f6eb4739b);

//REGION signposts
DB_TWN_Signposts((ITEM)S_TWN_TollhouseSighpost_25076e99-4c20-4322-863c-918091a90c85, (DIALOGRESOURCE)TWN_TollhouseSignpost_AD_a38c2051-9808-cbf6-c912-a48950b56932);
//DB_TWN_Signposts(S_TWN_CentralSquareSignpost_f16fddd7-5437-4b2f-883d-e3ede92ff83a, (DIALOGRESOURCE)TWN_CentralSquareSignpost_AD_bd00d78f-5f6d-7978-e169-25919253d5a5);
//DB_TWN_Signposts(S_TWN_BGSignpost_0d1e8272-1e99-499c-93f3-03d5e5f29253, (DIALOGRESOURCE)TWN_BGSignpost_AD_853afc4d-643b-0994-d82c-1624de175a99);
//DB_TWN_Signposts(S_TWN_MasonsGuildSignpost_d4cc5ada-f10a-4dc7-bfc1-1003a5dae0d9, (DIALOGRESOURCE)TWN_MasonsGuildSignpost_AD_2a461212-4e92-c9fd-cfcf-7d5ec43b0c34);
//DB_TWN_Signposts(S_TWN_DistillerySignpost_bf84cc42-5773-43c7-8913-276c7fbffdf8, (DIALOGRESOURCE)TWN_DistillerySignpost_AD_c1f4db01-28e0-3b15-b7be-83fc8c11d572);
DB_TWN_Signposts(S_TWN_HospitalSignpost_fc83b2ce-ab18-4ec1-96a8-a7e1ead5d74b, (DIALOGRESOURCE)TWN_HospitalSignpost_AD_1ce58154-e5b3-6225-f43a-c19f211ccd25);
DB_TWN_Signposts(S_TWN_MausoleumSignpost_11ddb4c1-c948-4f83-a071-84843ec05eab, (DIALOGRESOURCE)TWN_MausoleumSignpost_AD_9386aebe-6186-c951-a8bc-8356e7ed172b);
DB_TWN_Signposts(S_TWN_ToMasonsGuildSingpost_4ee1a1c9-cda7-4f54-abb1-32c338420f72, (DIALOGRESOURCE)TWN_ToMasonsGuildSignpost_AD_73e2bdaf-c93e-ec16-8e90-6ddaf1817705);
DB_TWN_Signposts(S_TWN_ToHospitalSignpost_7600eb91-a019-4155-99cf-ba5616001fc9, (DIALOGRESOURCE)TWN_ToHospitalSignpost_AD_2d3344ba-f8cd-2d5a-86eb-fcbb7f4b1a11);
DB_TWN_Signposts(S_TWN_CrossroadsSignpost_c1baa029-13e9-4cb1-998b-972491a5fb9a, (DIALOGRESOURCE) SCL_Crossroads_AD_Sign_7993b2f4-add9-b32b-886b-66abf0629e55);
//END_REGION signposts

//REGION Justiciar corpses

DB_ItemDialog_NarratorAD((ITEM)S_TWN_JusticiarSkeleton1_23b32a55-018c-4964-af9b-7b4f33c8bbb3,(DIALOGRESOURCE)TWN_CentralSquare_AD_Justiciar_708c8e22-d202-566b-610a-f52f9b27fa11);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_JusticiarSkeleton2_0be699b2-b01d-4510-815b-e9e603d94172,(DIALOGRESOURCE)TWN_CentralSquare_AD_Justiciar_708c8e22-d202-566b-610a-f52f9b27fa11);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_JusticiarSkeleton3_e12f0745-db3f-49f4-a83a-25b83fbba7c9,(DIALOGRESOURCE)TWN_CentralSquare_AD_Justiciar_708c8e22-d202-566b-610a-f52f9b27fa11);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_JusticiarSkeleton4_25258340-0b53-403f-a538-a32d75f3a93c,(DIALOGRESOURCE)TWN_CentralSquare_AD_Justiciar_708c8e22-d202-566b-610a-f52f9b27fa11);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_JusticiarSkeleton5_5393487c-1dc5-4e10-8cfb-413d2209ffa9,(DIALOGRESOURCE)TWN_CentralSquare_AD_Justiciar_708c8e22-d202-566b-610a-f52f9b27fa11);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_JusticiarSkeleton5_5611e7c0-c4da-49a7-8a0a-5073d19cee8f,(DIALOGRESOURCE)TWN_CentralSquare_AD_Justiciar_708c8e22-d202-566b-610a-f52f9b27fa11);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_JusticiarSkeleton6_e58513f5-e4ff-4e30-98f6-410851d68edd,(DIALOGRESOURCE)TWN_CentralSquare_AD_Justiciar_708c8e22-d202-566b-610a-f52f9b27fa11);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_JusticiarSkeleton7_b4876dd0-14d0-44ac-993f-f8818ff34cca,(DIALOGRESOURCE)TWN_CentralSquare_AD_Justiciar_708c8e22-d202-566b-610a-f52f9b27fa11);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_JusticiarSkeleton8_e498284f-7dba-44be-9fd4-2d9f2362415e,(DIALOGRESOURCE)TWN_CentralSquare_AD_Justiciar_708c8e22-d202-566b-610a-f52f9b27fa11);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_JusticiarSkeleton9_2501024e-b0c0-4558-b261-abfaf7225dfa,(DIALOGRESOURCE)TWN_CentralSquare_AD_Justiciar_708c8e22-d202-566b-610a-f52f9b27fa11);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_JusticiarSkeleton10_351c468e-e4ad-4ed3-8cb8-f49889c94f6c,(DIALOGRESOURCE)TWN_CentralSquare_AD_Justiciar_708c8e22-d202-566b-610a-f52f9b27fa11);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_JusticiarSkeleton11_7c4c817d-dbd5-470e-a1c1-7b991cfea4b2,(DIALOGRESOURCE)TWN_CentralSquare_AD_Justiciar_708c8e22-d202-566b-610a-f52f9b27fa11);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_JusticiarSkeleton12_66138c61-37da-477b-8f00-8e833d631ec3,(DIALOGRESOURCE)TWN_CentralSquare_AD_Justiciar_708c8e22-d202-566b-610a-f52f9b27fa11);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_JusticiarSkeleton13_d3b3b206-c355-460a-9acc-39ddaa07a605,(DIALOGRESOURCE)TWN_CentralSquare_AD_Justiciar_708c8e22-d202-566b-610a-f52f9b27fa11);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_JusticiarSkeleton14_fdfa9aa6-3ab2-48fe-bb15-cd533c6f302b,(DIALOGRESOURCE)TWN_CentralSquare_AD_Justiciar_708c8e22-d202-566b-610a-f52f9b27fa11);
DB_ItemDialog_NarratorAD((ITEM)S_TWN_JusticiarSkeleton15_eb0d593f-76e8-45fe-88d3-04724b892a5c,(DIALOGRESOURCE)TWN_CentralSquare_AD_Justiciar_708c8e22-d202-566b-610a-f52f9b27fa11);

//END_REGON
KBSECTION
//REGION Voicebarks in front of the buildings
IF
DB_TWN_Buildings(_BuildingArea, _EntranceTrigger, _Voicebark)
THEN
PROC_TriggerRegisterForPlayers(_BuildingArea);
DB_OneShot_VoiceBarkTrigger(_EntranceTrigger, _Voicebark, "PerUserAndNearbyPlayers");

IF
EnteredTrigger(_Player, S_TWN_MasonsGuild_BuildingArea_5eeacbd2-beec-4e03-86ef-8dfb01c2923d)
THEN
SetFlag((FLAG)TWN_MasonsGuild_State_Entered_fa4cd759-10a3-40be-bd03-0e097a56c048, NULL_00000000-0000-0000-0000-000000000000);

// if entered the building in a non-regular way - cancel the voicebark
IF
EnteredTrigger(_Player, _BuildingArea)
AND
DB_TWN_Buildings(_BuildingArea, _EntranceTrigger, _Voicebark)
THEN
PROC_TriggerUnregisterForPlayers(_BuildingArea);
PROC_RemoveOneShotVoiceBarkTrigger(_EntranceTrigger, _Voicebark);

IF
DB_GlobalFlag(SCL_Drider_State_SidedWithCaravan_57a698da-0149-48d1-91fa-9a84a5dcc293) 
AND
DB_State_Current(S_SCL_Drider_aa59ce01-3d2e-42e8-a539-c07e0ea292ab, "SCL_TheDrider", "EscortingCaravan")
THEN
PROC_TriggerUnregisterForPlayers((TRIGGER)S_TWN_Tollhouse_BuildingArea_caff6009-bd31-4a5d-9f67-df1b874ce5df);
PROC_RemoveOneShotVoiceBarkTrigger((TRIGGER)S_TWN_Tollhouse_EntranceVoicebarkArea_a48d9922-091b-47e4-b4e2-39e193616371, (VOICEBARKRESOURCE)TWN_Tollhouse_VB_Entrance_0b6f6a1f-0552-8b00-e8b4-b20ea95bc9c2);
//END_REGION Voicebarks in front of the buildings

//REGION Signposts
IF
DB_TWN_Signposts(_Signpost, _Dialog)
AND
GUIDToString(_Signpost, _SignpostName)
THEN
DB_ItemDialog_NarratorAD(_Signpost, _Dialog);
//END_REGION Signposts

//REGION Cursed Town Bosses framework
IF
DB_TWN_Bosses(_Boss, _, _SpotID, _, _, _)
AND
DB_TWN_Bosses_SpotTrigger(_Boss, _SpotID, _SpotTrigger)
THEN
DB_SpotPlayers_SpotTrigger(_Boss, _SpotID, _SpotTrigger);

IF
DB_TWN_Bosses(_Boss, _Dialog, _SpotID, _PermaDefeatedFlag, _PeacefulResolutionFlag, _HostileResolutionFlag)
AND
GetPosition(_Boss, _X, _Y, _Z)
THEN
DB_Dialogs(_Boss, _Dialog);
DB_DialogBlockTradeButton(_Dialog);
DB_DropMutingStatussesDialog(_Dialog);
DB_PermaDefeatedFlag(_Boss, _PermaDefeatedFlag);
DB_GlobalFlagReactionAfterDialog(_PeacefulResolutionFlag, _Dialog);
DB_GlobalFlagReactionAfterDialog(_HostileResolutionFlag, _Dialog);
DB_SpotPlayers(_Boss, _SpotID, NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);
DB_SpotPlayers_IncludeWildshapedPlayers(_Boss, _SpotID);
DB_SpotPlayers_IncludeSummons(_Boss, _SpotID);
DB_SpotPlayers_IncludeFollowers(_Boss, _SpotID);
DB_SpotPlayers_TargetIgnoreCantTalk(_Boss, _SpotID);

IF
DB_TWN_AlternativeSolutions(_Flag, _Dialog)
THEN
DB_GlobalFlagReactionAfterDialog(_Flag, _Dialog);

PROC
PROC_SpotPlayers_Spotted(_BossOrMinion, _SpotID, _Char)
AND
DB_TWN_Bosses(_Boss, _Dialog, _SpotID, _, _, _)
AND
NOT DB_Players(_Char)
THEN
PROC_SetRelationTemporaryHostile(_Boss, _Char);

PROC
PROC_SpotPlayers_Spotted(_BossOrMinion, _SpotID, _Char)
AND
DB_TWN_Bosses(_Boss, _Dialog, _SpotID, _, _, _)
AND
DB_Players(_Char)
AND
NOT QRY_StartDialog(_Dialog, _Boss, _Char)
THEN
PROC_SetRelationTemporaryHostile(_Boss, _Char);

PROC
PROC_GlobalFlagReactionAfterDialog(_PeacefulResolutionFlag)
AND
DB_TWN_Bosses(_Boss, _Dialog, _SpotID, _DeadFlag, _PeacefulResolutionFlag, _HostileResolutionFlag)
THEN
PROC_TWN_RemoveBoss(_Boss, 1);

PROC
PROC_GlobalFlagReactionAfterDialog(_HostileResolutionFlag)
AND
DB_TWN_Bosses(_Boss, _Dialog, _SpotID, _DeadFlag, _PeacefulResolutionFlag, _HostileResolutionFlag)
AND
GetFaction(_Boss, _BossFaction)
THEN
PROC_SetRelationToPlayers(_BossFaction, 0);

IF
FlagSet(_DeadFlag, NULL_00000000-0000-0000-0000-000000000000, _)
AND
DB_TWN_Bosses(_Boss, _Dialog, _SpotID, _DeadFlag, _PeacefulResolutionFlag, _HostileResolutionFlag)
THEN
PROC_TWN_RemoveBoss(_Boss, 0);

PROC
PROC_TWN_RemoveBoss((CHARACTER)_Boss, (INTEGER)_PeacefulResolution)
THEN
PROC_TWN_RemoveMinions(_Boss, _PeacefulResolution);

IF
FlagSet((FLAG)TWN_Distillery_State_PeacefulResolution_ef40ccf6-a7d7-4fde-8fcf-82b947c29a44, _, _)
THEN
ApplyStatus(S_TWN_Distillery_Brewer_4d9e3db3-9a78-4f4b-8101-1dd73c0f3be5, "EXPLODE_DEATH", -1.0, 1, NULL_00000000-0000-0000-0000-000000000000);

PROC
PROC_TWN_RemoveBoss((CHARACTER)_Boss, (INTEGER)_PeacefulResolution)
AND
DB_TWN_Bosses(_Boss, _Dialog, _SpotID, _DeadFlag, _PeacefulResolutionFlag, _HostileResolutionFlag)
AND
GetFlag(_DeadFlag, NULL_00000000-0000-0000-0000-000000000000, 0)
AND
_Boss == S_TWN_Distillery_Brewer_4d9e3db3-9a78-4f4b-8101-1dd73c0f3be5
THEN
Die(_Boss, DEATHTYPE.Explode, NULL_00000000-0000-0000-0000-000000000000, 1, 0, 1.0);

PROC
PROC_TWN_RemoveBoss((CHARACTER)_Boss, (INTEGER)_PeacefulResolution)
AND
DB_TWN_Bosses(_Boss, _Dialog, _SpotID, _DeadFlag, _PeacefulResolutionFlag, _HostileResolutionFlag)
AND
GetFlag(_DeadFlag, NULL_00000000-0000-0000-0000-000000000000, 0)
AND
_Boss != S_TWN_Distillery_Brewer_4d9e3db3-9a78-4f4b-8101-1dd73c0f3be5
THEN
Die(_Boss, DEATHTYPE.DoT, NULL_00000000-0000-0000-0000-000000000000, 1, 0, 1.0);

PROC
PROC_TWN_RemoveBoss((CHARACTER)_Boss, _)
AND
DB_TWN_Shadowblights(_Boss, _ShadowblightTrigger)
THEN
PROC_ShadowCurse_RemoveShadowblightTrigger(_ShadowblightTrigger);

PROC
PROC_TWN_RemoveMinions((CHARACTER)_Boss, (INTEGER)_PeacefulResolution)
AND
DB_TWN_Bosses_MinionsRemoval(_Boss, _PeacefulResolution)
AND
DB_TWN_Bosses_Minions(_Boss, _Minion)
AND
NOT DB_Dead(_Minion)
THEN
Die(_Minion, DEATHTYPE.DoT, 0);
//END_REGION Cursed Town Bosses framework

//REGION Dialog interruption
IF
DialogEnded(_Dialog, _ID)
AND
DB_TWN_Bosses(_Boss, _Dialog, _, _, _PeacefulResolutionFlag, _HostileResolutionFlag)
AND
NOT DB_GlobalFlag(_PeacefulResolutionFlag)
AND
NOT DB_GlobalFlag(_HostileResolutionFlag)
AND
NOT QRY_TWN_UsedAlternativeSolution(_Dialog)
AND
GetFaction(_Boss, _BossFaction)
THEN
SetFlag(_HostileResolutionFlag, NULL_00000000-0000-0000-0000-000000000000);
PROC_SetRelationToPlayers(_BossFaction, 0);

QRY
QRY_TWN_UsedAlternativeSolution((DIALOGRESOURCE)_Dialog)
AND
DB_TWN_AlternativeSolutions(_Flag, _Dialog)
AND
DB_GlobalFlag(_Flag)
THEN
DB_NOOP(1);

IF
DialogStarted(_Dialog, _ID)
AND
DB_TWN_Bosses(_Boss, _Dialog, _SpotID, _, _PeacefulResolutionFlag, _HostileResolutionFlag)
THEN
PROC_SpotPlayers_StopSpotting(_Boss, _SpotID);
//END_REGION Dialog interruption

//REGION Party Banters
/*
	- Tollhouse
		- (Tollhouse Master died in the dialog or kill in combat) AND (Drider is dead or successfully escorted to Moonrise Towers)
	- Hospital
		- We cleared the reception area (Receptionist either dead or gave us access)
		- We cleared the operating room (Surgeon and Nurses are dead or Nurses killed Surgeon)
	- Masonry
		- Entering the building enables Party Banters
	- Distillery
		- Brewer exploded in the dialog or Brewer & Zombies are killed
*/

IF
FlagSet(TWN_Tollhouse_State_Safe_35ef29da-a7d1-435f-9fd9-7a03f828a903, _, _)
THEN
DB_TWN_Tollhouse_BuildingCleared(1);

IF
FlagSet((FLAG)TWN_Tollhouse_State_PeacefulResolution_2eb42178-c700-4742-9325-8fef38ed46ca, _, _)
THEN
DB_TWN_Tollhouse_BuildingCleared(1);

IF
DB_PermaDefeated(S_SCL_Drider_aa59ce01-3d2e-42e8-a539-c07e0ea292ab)
THEN
DB_TWN_Tollhouse_DriderIsNotAProblem(1);

PROC
PROC_State_Changed(S_SCL_Drider_aa59ce01-3d2e-42e8-a539-c07e0ea292ab, "SCL_TheDrider", _, "ReachedMoonriseTower")
THEN
DB_TWN_Tollhouse_DriderIsNotAProblem(1);

PROC
PROC_LongRest()
AND
DB_TWN_Tollhouse_BuildingCleared(1)
AND
DB_TWN_Tollhouse_DriderIsNotAProblem(1)
AND
NOT DB_WorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_TWN_TollhouseCleared_84c7fb87-89d9-44ab-9bdd-244882be35b1, _, _)
THEN
PROC_RegisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_TWN_TollhouseCleared_84c7fb87-89d9-44ab-9bdd-244882be35b1);

IF
FlagSet((FLAG)TWN_Distillery_State_PeacefulResolution_ef40ccf6-a7d7-4fde-8fcf-82b947c29a44, _, _)
THEN
DB_TWN_Distillery_Cleared(1);

IF
FlagSet(TWN_Distillery_State_Safe_ab5934e9-0f92-4aed-bcf2-e092e461396f, _, _)
THEN
DB_TWN_Distillery_Cleared(1);

PROC
PROC_LongRest()
AND
DB_TWN_Distillery_Cleared(1)
AND
NOT DB_WorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_TWN_DistilleryCleared_c36e2ff4-31c5-4d54-a575-cec7f2ac87c8, _, _)
THEN
PROC_RegisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_TWN_DistilleryCleared_c36e2ff4-31c5-4d54-a575-cec7f2ac87c8);

IF
FlagSet(TWN_Hospital_Reception_State_GainedAcess_f2f3f438-e6a4-4114-aaef-777db362be6a, _, _)
THEN
DB_TWN_Hospital_ReceptionAreaCleared(1);

IF
DB_PermaDefeated(S_TWN_Hospital_Reception_Receptionist_3baf6794-2c83-4bd3-b2dd-70b4a9f98503)
THEN
DB_TWN_Hospital_ReceptionAreaCleared(1);

IF
FlagSet((FLAG)TWN_Hospital_State_PeacefulResolution_3830c1aa-6271-4af8-ba6c-0537bad1c624, _, _)
THEN
DB_TWN_Hospital_OperatingRoomCleared(1);

IF
FlagSet((FLAG)TWN_Hospital_State_Safe_fd2c3932-9fe4-4e21-ab51-1c8e71e82e02, _, _)
THEN
DB_TWN_Hospital_OperatingRoomCleared(1);

PROC
PROC_LongRest()
AND
DB_TWN_Hospital_ReceptionAreaCleared(1)
AND
DB_TWN_Hospital_OperatingRoomCleared(1)
AND
NOT DB_WorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_TWN_HospitalCleared_981becfa-22a4-489c-816d-e9efdd7d8aeb, _, _)
THEN
PROC_RegisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_TWN_HospitalCleared_981becfa-22a4-489c-816d-e9efdd7d8aeb);

PROC
PROC_LongRest()
AND
DB_GlobalFlag(TWN_MasonsGuild_State_Entered_fa4cd759-10a3-40be-bd03-0e097a56c048)
AND
NOT DB_WorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_TWN_MasonsGuildCleared_6b74013c-e51a-407c-a29a-f8d4b9bdb646, _, _)
THEN
PROC_RegisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_TWN_MasonsGuildCleared_6b74013c-e51a-407c-a29a-f8d4b9bdb646);

PROC
PROC_LongRest()
AND
DB_GlobalFlag((FLAG)TWN_MasonsGuild_RebelHideout_State_Cleared_e67e4bf3-ba34-4411-8745-ea00c5d040a4)
AND
NOT DB_WorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_TWN_MasonsGuild_RebelHideoutCleared_c1cb0a66-4523-47ee-84f1-855787498ff0, _, _)
THEN
PROC_RegisterWorldGossipTrigger((TRIGGER)S_PartyBanterTrigger_TWN_MasonsGuild_RebelHideoutCleared_c1cb0a66-4523-47ee-84f1-855787498ff0);
//END_REGION Party Banters

IF
TemplateUseStarted(_,(ITEMROOT)UNI_TWN_WaningMoonSign_3f12408f-22b1-4a02-bd8c-e513c3407ccf,_Sign)
THEN
PROC_TryStartAD(TWN_Distillery_AD_WaningMoonSign_dfca9519-0ed2-bce9-c859-fb487aed1d03,_Sign);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
