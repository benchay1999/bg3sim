Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION Aasimar Survivor

//Directing the players to the booster, bc place is so big and we can afford it
DB_OneShot_VoiceBarkTrigger(S_TWN_AasimarSurvivor_DoorNoticeTrigger_68ef2709-25fe-4738-b318-f2780ffb8d49,TWN_AasimarSurvivor_VB_NoticedDoor_c6ca60a8-13d6-09ba-da30-4a5cbb5a5747);

DB_TWN_AasimarSurvivor_OchreJelly((CHARACTER)S_TWN_AasimarSurvivor_FetidOoze_002_2689011e-9c19-42a5-a44e-990ca571261a);
DB_TWN_AasimarSurvivor_OchreJelly(S_TWN_AasimarSurvivor_FetidOoze_003_41afd575-f2d1-4f05-98c4-baa140f684a4);
DB_TWN_AasimarSurvivor_OchreJelly(S_TWN_AasimarSurvivor_FetidOoze_001_95cd289a-33f3-4221-bf65-45efe3329762);

//TODO - Rework it to work with new activation range feature or hack it to only force update when someone is in the morgue
SetForceUpdate(S_TWN_AasimarSurvivor_AnimatedArmor_000_950ba3d5-6871-4be5-9e1a-38619c044f50, 1); //Temp fix for Armor being visually culled from the high cliffs - waiting on GUS-179376 for proper support

Die(S_TWN_AasimarSurvivor_AnimatedArmor_000_950ba3d5-6871-4be5-9e1a-38619c044f50,DEATHTYPE.DoT,NULL_00000000-0000-0000-0000-000000000000,0,1,1.0);

DB_SCL_ShadowCurse_Immune(S_TWN_AasimarSurvivor_AnimatedArmor_000_950ba3d5-6871-4be5-9e1a-38619c044f50);

DB_AmbushTrigger((TRIGGER)S_TWN_AasimarSurvivor_ArmorHostilityBox_d600c6b0-0279-487b-b065-37fae3763347, NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000);
//END_REGION

//REGION Morgue Zombies

DB_TWN_HospitalMorgue_Zombies(S_TWN_MorgueZombie_01_fdba6b05-c6de-4079-97dc-6abfddc5a75b);
DB_TWN_HospitalMorgue_Zombies(S_TWN_MorgueZombie_02_88dafec7-53e1-421c-865c-f4ef51f54fd2);
DB_TWN_HospitalMorgue_Zombies(S_TWN_MorgueZombie_03_a76458a6-e964-48ba-8305-3bbae695ce35);
DB_TWN_HospitalMorgue_Zombies(S_TWN_MorgueZombie_04_e81c4052-960c-4d06-b1dc-4fe5a973f3cc);
DB_TWN_HospitalMorgue_Zombies(S_TWN_MorgueZombie_05_57136905-ef79-4a05-a1bb-e26d221ffa2e);
DB_TWN_HospitalMorgue_Zombies(S_TWN_MorgueZombie_06_1fbcbafb-edd6-4bab-bcfc-61d649e9804a);

//END_REGION

//REGION Help Wanted Poster
DB_ItemDialog_NarratorAD(S_TWN_HelpWantedPoster_16b8555b-9688-4f87-9481-ee0879fda353, (DIALOGRESOURCE)TWN_HelpWantedPoster_AD_3883aa3a-d9ff-bc75-27a5-cf09c14ff3cd);
//END_REGION Help Wanted Poster

//REGION Fisherman Note
DB_ItemDialog_NarratorAD((ITEM)S_TWN_FishermanNote_c7455544-ded3-4500-8318-766863aa9b08, (DIALOGRESOURCE)TWN_FishermanNote_AD_7fb512de-63c6-e548-f3b5-955ebab8b359);
//END_REGION Fisherman Note

//REGION Confiscated Crates
DB_TWN_Tollhouse_ConfiscatedCrates((ITEM)S_TWN_DalelandsCrate_8e378444-78f6-4751-9448-d90f981f6a7f, (DIALOGRESOURCE)TWN_Tollhouse_PAD_DalelandsCrate_a06b02f9-8e41-6d01-d431-5e07130dcbd7);
DB_TWN_Tollhouse_ConfiscatedCrates((ITEM)S_TWN_CormyrCrate_6b33af1d-d030-4218-87f8-b3837e56025b, (DIALOGRESOURCE)TWN_Tollhouse_PAD_CormyrCrate_7ee5a591-aef8-708a-e570-30376afcc88a);
DB_TWN_Tollhouse_ConfiscatedCrates((ITEM)S_TWN_ThayCrate_968f2b5a-e741-403e-a8f9-3d3845f313cd, (DIALOGRESOURCE)TWN_Tollhouse_PAD_ThayCrate_aedb66ec-10b1-81c3-0f59-3241c04c8c83);
//END_REGION Confiscated Crates

//REGION Missing Pets
DB_ItemDialog_PlayerVB(S_TWN_MissingPetsCollarsPile_b7680bb0-4a59-4b66-ad76-f9d332265b9a, (VOICEBARKRESOURCE)TWN_MissingPetsCollarsPile_VB_46901677-75b9-b662-ce4d-8a3a9436802e);
DB_ItemDialog_PlayerVB(S_TWN_MissingPetsBoard_bea366ad-dd39-40e5-a287-cb98190f0763, (VOICEBARKRESOURCE)TWN_MissingPetsBoard_VB_bcd6a4c8-2c26-9537-ce48-fb9d8dc58ce2);
//END_REGION Missing Pets

//REGION Vlaakith Attack
PROC_TWN_VlaakithAttack_Init();

DB_TWN_VlaakithAttack_GithGroup((CHARACTER)S_TWN_VlaakithAttack_Melee_001_9aae708f-6fd1-453f-b5a6-959814f0672f);
DB_TWN_VlaakithAttack_GithGroup((CHARACTER)S_TWN_VlaakithAttack_Caster_000_e4141a02-f5e7-4a0c-a7af-d3dda6610c1b);
DB_TWN_VlaakithAttack_GithGroup((CHARACTER)S_TWN_VlaakithAttack_Melee_000_d29ba948-3631-4779-bd7a-400dcd7a51a3);
DB_TWN_VlaakithAttack_GithGroup((CHARACTER)S_TWN_VlaakithAttack_Ranger_000_b7e639ba-9e1d-42cd-8305-e8c02f00f1a1);
DB_TWN_VlaakithAttack_GithGroup((CHARACTER)S_TWN_VlaakithAttack_Ranger_001_dabc5b38-7606-48cb-99ef-e7edcc49e1a9);

DB_GLO_CharacterCorpseDialog((CHARACTER)S_TWN_VlaakithAttack_Melee_000_d29ba948-3631-4779-bd7a-400dcd7a51a3, (DIALOGRESOURCE)TWN_VlaakithAttack_Melee_000_Dead_79841c0b-79e1-4a01-6d92-58a80e80e062);
PROC_TriggerRegisterForParty((TRIGGER)S_TWN_GithRadarAmbushActivationArea_0506dfbf-aa7d-4e58-914a-858dd1525a75);

// Combat NPC Reactivity
DB_CombatReact_AD_OnEntered(S_TWN_VlaakithAttack_Caster_000_e4141a02-f5e7-4a0c-a7af-d3dda6610c1b, (DIALOGRESOURCE)TWN_VlaakithAttack_AD_Caster_000_CombatStarts_e5d7ae88-ff97-7e4b-74fc-7d136a227046);
DB_CombatReact_AD_OnNextTurn(S_TWN_VlaakithAttack_Caster_000_e4141a02-f5e7-4a0c-a7af-d3dda6610c1b, (DIALOGRESOURCE)TWN_VlaakithAttack_AD_Caster_000_TakingTurn_001_7b09b907-9742-a382-54fb-ac99e497f920);
DB_CombatReact_AD_OnNextTurn(S_TWN_VlaakithAttack_Caster_000_e4141a02-f5e7-4a0c-a7af-d3dda6610c1b, (DIALOGRESOURCE)TWN_VlaakithAttack_AD_Caster_000_TakingTurn_002_e71c74fe-d7ff-1a1d-a39e-1671d9bd5e5e);
DB_CombatReact_AD_OnNextTurn(S_TWN_VlaakithAttack_Caster_000_e4141a02-f5e7-4a0c-a7af-d3dda6610c1b, (DIALOGRESOURCE)TWN_VlaakithAttack_AD_Caster_000_TakingTurn_003_69558b1d-d5bf-2bbf-d139-a59d20675e48);

//END_REGION Vlaakith Attack

//REGION Misc VB Triggers
DB_OneShot_VoiceBarkTrigger(S_TWN_VBTrigger_MausoleumEntry_750d14ea-4e7a-4ed1-87ef-61af69c918a2, (VOICEBARKRESOURCE)TWN_Mausoleum_VB_Entrance_77694433-0d01-14e5-7005-f1057b5cc312, "PerUserAndNearbyPlayers");
//END_REGION

PROC_RegisterWorldGossipTrigger(S_PartyBanterTrigger_Hospital_Morgue_fd5b94c0-226b-4e96-8fdc-5de75dc9dbcd);
DB_WorldGossip_RegionTrigger(S_PartyBanterTrigger_Hospital_Morgue_fd5b94c0-226b-4e96-8fdc-5de75dc9dbcd, BANTERREGION_TWN_Morgue_6ed2181f-9eef-42dc-bc4e-7d51d92e520f);

DB_PartyProgress_Trigger((TRIGGER)S_TWN_MainSub_169edf4a-bd51-4964-adb0-4d47956d5fae, (FLAG)TWN_State_EverEnteredBefore_5d5b52ea-d366-4f56-a6f1-8aac8470a130);
KBSECTION
//REGION Aasimar Survivor

IF
DB_TWN_AasimarSurvivor_OchreJelly(_Ooze)
THEN
DB_AmbushTrigger_Ambusher((TRIGGER)S_TWN_AasimarSurvivor_ArmorHostilityBox_d600c6b0-0279-487b-b065-37fae3763347, _Ooze, 1);
DB_SCL_ShadowCurse_Immune(_Ooze);

PROC
PROC_LaunchAmbush(S_TWN_AasimarSurvivor_ArmorHostilityBox_d600c6b0-0279-487b-b065-37fae3763347,_Player)
THEN
Resurrect(S_TWN_AasimarSurvivor_AnimatedArmor_000_950ba3d5-6871-4be5-9e1a-38619c044f50);
PROC_SetRelationToPlayers((FACTION)Act2_TWN_AasimarSurvivor_AnimatedArmors_5bc4c87b-eb79-4d63-afff-ffde152fbc5f, 0);

PROC
PROC_CancelAmbush(S_TWN_AasimarSurvivor_ArmorHostilityBox_d600c6b0-0279-487b-b065-37fae3763347)
THEN
Resurrect(S_TWN_AasimarSurvivor_AnimatedArmor_000_950ba3d5-6871-4be5-9e1a-38619c044f50);
PROC_SetRelationToPlayers((FACTION)Act2_TWN_AasimarSurvivor_AnimatedArmors_5bc4c87b-eb79-4d63-afff-ffde152fbc5f, 0);

IF
Resurrected(S_TWN_AasimarSurvivor_AnimatedArmor_000_950ba3d5-6871-4be5-9e1a-38619c044f50)
THEN
ApplyStatus(S_TWN_AasimarSurvivor_AnimatedArmor_000_950ba3d5-6871-4be5-9e1a-38619c044f50, "TWN_ARMOUR_SHADOW", -1.0, 1, NULL_00000000-0000-0000-0000-000000000000);

IF
DB_AmbushRevealed(S_TWN_AasimarSurvivor_ArmorHostilityBox_d600c6b0-0279-487b-b065-37fae3763347)
AND
QRY_OnlyOnce("TWN_AasimarSurvivor_OozesSplash")
AND
DB_TWN_AasimarSurvivor_OchreJelly(_Ooze)
THEN
PlayEffect(_Ooze,VFX_Enemies_Fetid_Ooze_Spawn_Impact_Root_01_aef1bcfe-cb5c-b617-6bab-9ba5e56a46f5);

PROC
PROC_LaunchAmbush(S_TWN_AasimarSurvivor_ArmorHostilityBox_d600c6b0-0279-487b-b065-37fae3763347,_Player)
AND
QRY_OnlyOnce("TWN_AasimarSurvivor_OozesSplash")
AND
DB_TWN_AasimarSurvivor_OchreJelly(_Ooze)
THEN
PlayEffect(_Ooze,VFX_Enemies_Fetid_Ooze_Spawn_Impact_Root_01_aef1bcfe-cb5c-b617-6bab-9ba5e56a46f5);

PROC
PROC_CancelAmbush(S_TWN_AasimarSurvivor_ArmorHostilityBox_d600c6b0-0279-487b-b065-37fae3763347)
AND
QRY_OnlyOnce("TWN_AasimarSurvivor_OozesSplash")
AND
DB_TWN_AasimarSurvivor_OchreJelly(_Ooze)
THEN
PlayEffect(_Ooze,VFX_Enemies_Fetid_Ooze_Spawn_Impact_Root_01_aef1bcfe-cb5c-b617-6bab-9ba5e56a46f5);
//END_REGION

//REGION Morgue Zombies

IF
DB_TWN_HospitalMorgue_Zombies(_Zombie)
THEN
SetAmbushing(_Zombie,1);

IF
EnteredCombat(_Zombie, _)
AND
DB_TWN_HospitalMorgue_Zombies(_Zombie)
AND
HasActiveStatus(_Zombie, "TWN_MORGUE_MUMMYSLEEP", 1)
THEN
RemoveStatus(_Zombie, "TWN_MORGUE_MUMMYSLEEP");
PlayAnimation(_Zombie, STAT_KO_Peace_01_End_37fe9557-0310-4273-9e28-03c1613e4101, "");
SetAmbushing(_Zombie, 0);

//END_REGION

//REGION Envoy
IF
Moved(S_TWN_EnvoyMessage_d2b7c49d-78f6-4e03-8fa8-d71e7f95f30e)
THEN
SetGravity(S_TWN_EnvoyMessage_d2b7c49d-78f6-4e03-8fa8-d71e7f95f30e, GRAVITYTYPE.Enabled);

IF
PreMovedBy(S_TWN_EnvoySkull_b4409059-17c5-4656-8f2a-b21c2026e1c6, _)
THEN
SetGravity(S_TWN_EnvoyMessage_d2b7c49d-78f6-4e03-8fa8-d71e7f95f30e, GRAVITYTYPE.Enabled);
//END_REGION Envoy

//REGION Confiscated Crates
IF
UseFinished(_Player, _Crate, 1)
AND
DB_TWN_Tollhouse_ConfiscatedCrates(_Crate, _AD)
AND
GUIDToString(_AD, _ADString)
AND
QRY_OncePerUserAndNearbyPlayers(_Player, _ADString)
THEN
PROC_TryStartAD(_AD, _Player);
//END_REGION Confiscated Crates

//REGION Prayers on the wall
IF
UseFinished(_Player, S_TWN_PrayersAndCharms_95df1b04-2fc9-4c79-8971-c08f760085a1, 1)
AND
QRY_OncePerUserAndNearbyPlayers(_Player, "TWN_PrayersAndCharms")
THEN
StartVoiceBark(TWN_CharmAndPrayers_VB_c12648c9-aac7-5dbc-3681-c49877014849, _Player);
//END_REGION Prayers on the wall

//REGION Vlaakith Attack
IF
DB_TWN_VlaakithAttack_GithGroup(_GithAmbusher)
THEN
DB_GLO_DefeatCounter(_GithAmbusher, "TWN_VlaakithAttack");

IF
DB_TWN_VlaakithAttack_GithGroup(_Gith)
AND
NOT DB_TWN_VlaakithAttack_CastedLight(_Gith)
THEN
SetForceUpdate(_Gith, 1); // Force update giths for a moment so they can cast the light spell and it follows the rules.
UseSpell(_Gith, "Target_Light", _Gith);

IF
CastedSpell((CHARACTER)_Gith, "Target_Light", _, _, _)
AND
DB_TWN_VlaakithAttack_GithGroup(_Gith)
AND
NOT DB_TWN_VlaakithAttack_CastedLight(_Gith)
THEN
DB_TWN_VlaakithAttack_CastedLight(_Gith);
SetForceUpdate(_Gith, 0);
SetOnStage(_Gith, 0);

IF
CastSpellFailed((CHARACTER)_Gith, "Target_Light", _, _, _)
AND
DB_TWN_VlaakithAttack_GithGroup(_Gith)
AND
NOT DB_TWN_VlaakithAttack_CastedLight(_Gith)
THEN
DB_TWN_VlaakithAttack_CastedLight(_Gith);
SetForceUpdate(_Gith, 0);
SetOnStage(_Gith, 0);

//Case going back to CRE and back to SCL.
IF
DB_CurrentLevel("SCL_Main_A")
THEN
PROC_TWN_VlaakithAttack_Init();

PROC
PROC_TWN_VlaakithAttack_Init()
AND
DB_GlobalFlag((FLAG)CRE_ChainOfCommand_State_HuntedByVlaakith_dd45fe4c-894b-4c3a-8a81-f3ea3186cf82)
AND
QRY_OnlyOnce("TWN_VlaakithAttackSetUp")
THEN
PROC_TriggerRegisterForPlayers(S_TWN_VlaakithAttack_SpawnTrigger_0cff9ad6-11b6-423b-87a4-514431f67554);

//GithRadar logic
IF
EnteredTrigger(_Player, (TRIGGER)S_TWN_GithRadarAmbushActivationArea_0506dfbf-aa7d-4e58-914a-858dd1525a75)
AND
DB_Players(_Player)
AND
IsInInventoryOf((ITEM)S_GLO_GithRadar_4aca442e-5ecb-4611-b481-5eaf35596dea, _Player, 1)
AND
QRY_OnlyOnce("TWN_VlaakithAttack_RadarActivated")
THEN
PlaySound(_Player, "SE_Pendulum_Psionic_Detector"); 
StartVoiceBark((VOICEBARKRESOURCE)TWN_VlaakithAttack_VB_GithRadarGoesOffComment_e02a7f11-c79a-10dc-2942-88ab776ef8aa, _Player);
DB_TWN_VlaakithAttack_RadarWentOff(_Player);

PROC
PROC_GLO_DefeatCounter_AllDefeated("TWN_VlaakithAttack")
AND
DB_TWN_VlaakithAttack_RadarWentOff(_Player)
THEN
StartVoiceBark((VOICEBARKRESOURCE)TWN_VlaakithAttack_VB_RealizeGithRadarFunction_674fdb1a-706d-84c6-9289-ea260efaa08b, _Player);

//If the radar went off, the players get an advantage in the coming fight against the Gith Ambushers 
IF
EnteredTrigger(_, S_TWN_VlaakithAttack_SpawnTrigger_0cff9ad6-11b6-423b-87a4-514431f67554)
AND
DB_TWN_VlaakithAttack_RadarWentOff(_)
AND
QRY_OnlyOnce("TWN_VlaakithAttack_PlayersAlerted")
AND
DB_PartyMembers(_PartyMember)
AND
DB_InRegion(_PartyMember, (TRIGGER)S_TWN_GithRadarAmbushActivationArea_0506dfbf-aa7d-4e58-914a-858dd1525a75)
THEN
ApplyStatus(_PartyMember, "TWN_GITHRADAR_ALERT", -1.0, 1, NULL_00000000-0000-0000-0000-000000000000);
DB_TWN_VlaakithAttack_PartyMemberAlerted((CHARACTER)_PartyMember);

IF
EnteredTrigger(_, S_TWN_VlaakithAttack_SpawnTrigger_0cff9ad6-11b6-423b-87a4-514431f67554)
AND
DB_TWN_VlaakithAttack_GithGroup(_Gith)
THEN
PROC_Foop(_Gith); // TODO: Add VFX so it appears they come from another plane.

IF
EnteredTrigger(_Player, S_TWN_VlaakithAttack_SpawnTrigger_0cff9ad6-11b6-423b-87a4-514431f67554)
THEN
PROC_TriggerUnregisterForPlayers(S_TWN_VlaakithAttack_SpawnTrigger_0cff9ad6-11b6-423b-87a4-514431f67554);

IF
LeftCombat(_PartyMember, _)
AND
DB_TWN_VlaakithAttack_PartyMemberAlerted((CHARACTER)_PartyMember)
THEN
RemoveStatus(_PartyMember, "TWN_GITHRADAR_ALERT", NULL_00000000-0000-0000-0000-000000000000);

IF
StatusRemoved(_PartyMember, "TWN_GITHRADAR_ALERT", _, _)
THEN
NOT DB_TWN_VlaakithAttack_PartyMemberAlerted((CHARACTER)_PartyMember);

IF
TextEvent("TWN_SetUpGithAttack")
THEN
PROC_GlobalSetFlagAndCache((FLAG)CRE_ChainOfCommand_State_HuntedByVlaakith_dd45fe4c-894b-4c3a-8a81-f3ea3186cf82);
PROC_TWN_VlaakithAttack_Init();
//END_REGION Vlaakith Attack
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act2"
