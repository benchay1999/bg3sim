Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_RelationshipDialog_WRD_TriggerInCamp((DIALOGRESOURCE)Gale_InParty2_6beb1b10-845f-49fa-6d6d-f425eaa42574, (FLAG)ORI_Gale_IPRD_BeMyGod_0c7cb6c2-4097-9bb1-aa0e-6edfaa7abb2e);

KBSECTION
//REGION Tara handling

//Retry the origin moment.
IF
DialogEnded((DIALOGRESOURCE)WYR_Posthouse_Tara_OM_Gale_AOM_COM_OOM_999ecebb-967d-172b-0411-3d455cdf27e6, _ID)
AND
GetFlag((FLAG)COM_7075ec1a-70ad-bd25-3111-0a955cf07585, CAMP_Gale_Tressym_00706ce9-cbf7-95ee-912d-b42909b483a7, 0) //The COM flow actually account for the Avatar leaving well enough alone.
AND
NOT DB_GlobalFlag((FLAG)WYR_Posthouse_State_TressymHasGivenReward_939d0367-8bc7-487f-b8ef-15c5c9fd0faf)
THEN
PROC_DefineSingleOriginMoment((DIALOGRESOURCE)WYR_Posthouse_GalesTressym_6bbdb0cc-9a74-63c4-ec0b-21d26e65714e, (TAG)REALLY_GALE_9b0354c0-56d9-4723-8034-918ac9abab19, (DIALOGRESOURCE)WYR_Posthouse_Tara_OM_Gale_AOM_COM_OOM_999ecebb-967d-172b-0411-3d455cdf27e6);

IF
DialogEnded((DIALOGRESOURCE)WYR_Posthouse_Tara_OM_Gale_AOM_COM_OOM_999ecebb-967d-172b-0411-3d455cdf27e6, _ID)
THEN
DB_OnlyOnce("WYR_Posthouse_GalesTressym_OM_Gale_AOM_COM_OOM");

PROC
PROC_LOW_TressymTrade_NextPhase()
AND
QRY_OnlyOnce("WYR_Posthouse_GalesTressym_OM_Gale_AOM_COM_OOM")
THEN
PROC_DefineSingleOriginMoment((DIALOGRESOURCE)WYR_Posthouse_GalesTressym_6bbdb0cc-9a74-63c4-ec0b-21d26e65714e, (TAG)REALLY_GALE_9b0354c0-56d9-4723-8034-918ac9abab19, (DIALOGRESOURCE)WYR_Posthouse_Tara_OM_Gale_AOM_COM_OOM_999ecebb-967d-172b-0411-3d455cdf27e6);

IF
FlagSet(ORI_Gale_State_TaraKiller_08844f2a-aab5-4d93-b810-e952d24d76b7, _Player, _)
AND
DB_Avatars((CHARACTER)_Player)
AND
ChangeApprovalRating((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, _Player, 1, -20, _)
THEN
DB_NOOP(1);

QRY
QRY_ORI_Gale_TaraRecognised()
AND
DB_GlobalFlag(WYR_Posthouse_State_Tressym_PlayersSeesGaleSymbol_722a4065-ee43-4ee8-b235-bc4a904d62c3)
THEN
DB_NOOP(1);


//END_REGION

IF
FlagSet((FLAG)ORI_Gale_Knows_BrainHasCrownOfKarsus_FromRaphael_4451456b-6f9b-7de2-9fae-68fc5a86f9d9, _, _)
THEN
SetFlag((FLAG)ORI_Gale_State_ReactedElderBrain_be06e142-c83f-387b-b6e3-da74517f786c);

//REGION IPRDs

//REGION Be My God


PROC
PROC_Camp_SetModeToDay()
AND
NOT DB_Avatars((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
AND
DB_CampNight_Completed((FLAG)NIGHT_Gale_BeMyGod_2259ec28-b538-4895-a431-30dc7e2ddf66)
AND
QRY_OnlyOnce("ORI_Gale_IPRD_BeMyGod")
THEN
PROC_ORI_Gale_TriggerBeMyGodIPRD();

PROC
PROC_ORI_Gale_TriggerBeMyGodIPRD()
AND
DB_ORI_Partnered(_Avatar, (CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
AND
GetFlag((FLAG)ORI_Gale_State_AcceptedBeMyGodProposal_3d452ca4-14e6-176a-88ad-47d0ef8271d3, _Avatar, 1)
THEN
PROC_RelationshipDialog((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, (DIALOGRESOURCE)Gale_InParty2_6beb1b10-845f-49fa-6d6d-f425eaa42574, (FLAG)ORI_Gale_IPRD_BeMyGod_0c7cb6c2-4097-9bb1-aa0e-6edfaa7abb2e, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 1);

PROC
PROC_ORI_Gale_TriggerBeMyGodIPRD()
AND
DB_GlobalFlag((FLAG)ORI_Gale_State_RejectedBeMyGodProposal_de72eaa6-c6f4-014c-b1a2-213c243c1dba)
THEN
PROC_RelationshipDialog((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, (DIALOGRESOURCE)Gale_InParty2_6beb1b10-845f-49fa-6d6d-f425eaa42574, (FLAG)ORI_Gale_IPRD_BeMyGod_0c7cb6c2-4097-9bb1-aa0e-6edfaa7abb2e, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 1);

//END_REGION

//REGION Crown of Karsus related ones
IF
FlagSet((FLAG)ORI_Gale_Knows_BrainHasCrownOfKarsus_FromRaphael_4451456b-6f9b-7de2-9fae-68fc5a86f9d9, _, _)
AND
NOT DB_GlobalFlag((FLAG)ORI_Gale_State_KarsusNotesResolved_db5a6b70-d9b0-4470-8b7d-b08a52c1f905)
AND
NOT DB_Avatars((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
THEN
PROC_RelationshipDialog((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, (DIALOGRESOURCE)Gale_InParty2_6beb1b10-845f-49fa-6d6d-f425eaa42574, (FLAG)ORI_Gale_IPRD_BrainHasCrownOfKarsus_FromRaphael_0de0d760-e60f-8cab-6b0d-66c2ad67c638, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 0);

IF
FlagSet((FLAG)GLO_Monitor_State_MadeDeal_86f40f63-0bfa-4ab6-b3b0-d01475c3cbdc, _, _)
AND
NOT DB_GlobalFlag((FLAG)HoH_Warlock_State_ContractDestroyed_528a9080-f162-4a8c-bcc7-120db8fbd63f)
AND
DB_GlobalFlag((FLAG)ORI_Gale_Knows_ReadKarsusNotes_2b0e541f-f49e-7e82-27d6-15fccd6ff1ff)
AND
NOT DB_Avatars((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
THEN
PROC_RelationshipDialog((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, (DIALOGRESOURCE)Gale_InParty2_6beb1b10-845f-49fa-6d6d-f425eaa42574, (FLAG)ORI_Gale_IPRD_SignedContract_bdc786ed-fa49-abf3-407e-b94bfab30b3c, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 0);
PROC_CancelRelationshipDialog((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, (DIALOGRESOURCE)Gale_InParty2_6beb1b10-845f-49fa-6d6d-f425eaa42574, (FLAG)ORI_Gale_IPRD_BrainHasCrownOfKarsus_FromRaphael_0de0d760-e60f-8cab-6b0d-66c2ad67c638);


IF
FlagSet((FLAG)ORI_Gale_State_KarsusNotesResolved_db5a6b70-d9b0-4470-8b7d-b08a52c1f905, _, _)
THEN
PROC_CancelRelationshipDialog((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, (DIALOGRESOURCE)Gale_InParty2_6beb1b10-845f-49fa-6d6d-f425eaa42574, (FLAG)ORI_Gale_IPRD_BrainHasCrownOfKarsus_FromRaphael_0de0d760-e60f-8cab-6b0d-66c2ad67c638);

IF
FlagSet((FLAG)HoH_Warlock_State_ContractDestroyed_528a9080-f162-4a8c-bcc7-120db8fbd63f, _, _)
THEN
PROC_CancelRelationshipDialog((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, (DIALOGRESOURCE)Gale_InParty2_6beb1b10-845f-49fa-6d6d-f425eaa42574, (FLAG)ORI_Gale_IPRD_SignedContract_bdc786ed-fa49-abf3-407e-b94bfab30b3c);


//END_REGION


//REGION Drow Siblings

IF
FlagSet((FLAG)WYR_DapperDrow_State_PartnerAgreedGroup_b285bbaa-7b79-48c6-bf26-1b9f2de5cdb7, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 0)
AND
NOT DB_Avatars((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
THEN
PROC_RelationshipDialog((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, (DIALOGRESOURCE)Gale_InParty2_6beb1b10-845f-49fa-6d6d-f425eaa42574, (FLAG)ORI_Gale_IPRD_DrowSiblings_f9912803-da31-7f03-cd4c-ecf1d6baf076, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 1);

//END_REGION

//END_REGION

//REGION TGs

IF
FlagSet((FLAG)ORI_Gale_State_Met_ElminsterInWYR_82b0cbde-1d3b-1f67-86a8-d05744462209, _, _)
AND
DB_Avatars(S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
THEN
SetFlag(TG_ORI_GaleMetElminsterInWYR_36accdc7-ac1f-41fa-8f13-e7dac633dbb8, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 0);

IF
FlagSet((FLAG)ORI_Gale_State_Met_ElminsterInWYR_82b0cbde-1d3b-1f67-86a8-d05744462209, _, _)
AND
NOT DB_Avatars(S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
AND
DB_Avatars(_Avatar)
THEN
SetFlag(TG_ORI_GaleMetElminsterInWYR_36accdc7-ac1f-41fa-8f13-e7dac633dbb8, _Avatar, 0);

//END_REGION

//REGION Elminster sends his simulacra

PROC
PROC_StateSet_Defeated((CHARACTER)S_GLO_Elminster_75bb6396-1132-4064-bafe-205a3f156b9b)
AND
NOT DB_Dead((CHARACTER)S_GLO_Elminster_75bb6396-1132-4064-bafe-205a3f156b9b)
THEN
SetFlag((FLAG)ORI_Gale_State_ElminsterWasDefeated_23d0595f-e384-4071-ba9b-3e6899c4fd4b, NULL_00000000-0000-0000-0000-000000000000, 0);
PROC_Poof((CHARACTER)S_GLO_Elminster_75bb6396-1132-4064-bafe-205a3f156b9b);

IF
Dying((CHARACTER)S_GLO_Elminster_75bb6396-1132-4064-bafe-205a3f156b9b)
THEN
RealtimeObjectTimerLaunch((CHARACTER)S_GLO_Elminster_75bb6396-1132-4064-bafe-205a3f156b9b, "GLO_Elminster_DeathTimer", 1470);

IF
ObjectTimerFinished((CHARACTER)S_GLO_Elminster_75bb6396-1132-4064-bafe-205a3f156b9b, "GLO_Elminster_DeathTimer")
THEN
SetFlag((FLAG)ORI_Gale_State_ElminsterWasDefeated_23d0595f-e384-4071-ba9b-3e6899c4fd4b, NULL_00000000-0000-0000-0000-000000000000, 0);
SetOnStage(S_GLO_Elminster_75bb6396-1132-4064-bafe-205a3f156b9b, 0);

//END_REGION

//REGION 

PROC
PROC_CampNight_PreSelection_Hook()
AND
NOT DB_GlobalFlag(ORI_Gale_State_KarsusNotesResolved_db5a6b70-d9b0-4470-8b7d-b08a52c1f905)
AND
DB_PartyMembers((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
AND
NOT DB_Avatars((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
AND
NOT QRY_ORI_Gale_HasKarsusNotesCRD()
AND
DB_GlobalFlag(ORI_Gale_State_HasKarsusNotes_NightRequirement_3b1c8482-253b-47e6-998b-0551da43272b)
THEN
ClearFlag(ORI_Gale_State_HasKarsusNotes_NightRequirement_3b1c8482-253b-47e6-998b-0551da43272b, NULL_00000000-0000-0000-0000-000000000000);

QRY
QRY_ORI_Gale_HasKarsusNotesCRD()
AND
NOT DB_Players((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
AND
DB_Players(_Character)
AND
GetFlag(LOW_SorcerousSundries_Event_HasKarsusBook_c7019855-fcac-4bc5-bacf-9fad0213ab28, _Character, 1)
THEN
SetFlag(ORI_Gale_State_HasKarsusNotes_NightRequirement_3b1c8482-253b-47e6-998b-0551da43272b, NULL_00000000-0000-0000-0000-000000000000);

QRY
QRY_ORI_Gale_HasKarsusNotesCRD()
AND
DB_Players((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
AND
GetFlag(LOW_SorcerousSundries_Event_HasKarsusBook_c7019855-fcac-4bc5-bacf-9fad0213ab28, S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604, 1)
THEN
SetFlag(ORI_Gale_State_HasKarsusNotes_NightRequirement_3b1c8482-253b-47e6-998b-0551da43272b, NULL_00000000-0000-0000-0000-000000000000);


//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act3"
