Version 1
SubGoalCombiner SGC_AND
INITSECTION
SetStoryDisplayName(S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, "GLO_DevilishOx_PolymorphedName");
PROC_SetOnStage((ITEM)S_GLO_DevilishOx_AppleForm_2fb8359f-7fa7-4dab-8e0f-c1edef958a73, 0);

TriggerRegisterForCharacter((TRIGGER)S_WYR_OxArea_1cebc515-8593-4716-8e0f-0df2d4086e4f, (CHARACTER)S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56);

PROC_GLO_DevilishOx_TeleportIfAlive();

DB_DefeatedStateFlag(S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, GLO_DevilishOx_State_Defeated_4061b72f-ce89-4b86-b18b-ed9c5fbc7cc8);
DB_HasItemEvent(S_GLO_DevilishOx_AppleForm_2fb8359f-7fa7-4dab-8e0f-c1edef958a73, WYR_DevilishOx_State_AppleInInventory_5534ac16-ab3d-4dd9-8405-3a396cf628d4);
DB_InRegionFlag((TRIGGER)S_WYR_OxArea_1cebc515-8593-4716-8e0f-0df2d4086e4f, (FLAG)WYR_DevilishOx_State_InRegion_de7ab588-ec0f-4925-b1ab-8d8793897d9a);
KBSECTION
//REGION Debug
IF
TextEvent("KillDevilishOx")
AND
GetHostCharacter(_Player)
THEN
Die((CHARACTER)S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, DEATHTYPE.Physical, 0);

IF
TextEvent("MetOxIn_DruidsGrove")
THEN
SetFlag(DEN_DevilishOx_MeetAtDruidsGrove_HasMet_SwA_94637372-3365-4c0c-97fa-59b7d089cd53);

IF
TextEvent("MetOxIn_LastLightInn")
THEN
SetFlag(HAV_DevilishOx_LastLight_HasMet_SwA_12a0387f-f284-417b-a91b-53ab87ef9e85);

IF
TextEvent("MetOxIn_SmallBarn")
THEN
SetFlag(WYR_DevilishOx_SmallBarn_HasMet_SwA_435e36f8-8a47-4c9d-8304-d98b349af0ef);
//END_REGION


//REGION
//Check if the Ox is alive, and if so teleport it to the barn
PROC
PROC_GLO_DevilishOx_TeleportIfAlive()
AND
NOT DB_PermaDefeated(S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56)
THEN
PROC_CharacterFullRestore((CHARACTER)S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56);
PROC_SetOnStage(S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, 1);
TeleportTo(S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, S_WYR_TeleportDevilishOxPos_dce97914-6c24-4cea-83b8-6b1e8477fc04);
PROC_RemoveDialog(S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56); //removing previous dialog as it's no longer relevant at this point
DB_Dialogs((CHARACTER)S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, (DIALOGRESOURCE)HAV_LastLightOx002_5dee5628-5f90-1dfe-05fa-63cc1243c713);
RemoveStatus((CHARACTER)S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56,"HAV_PHASM_OX_DISGUISE_ACT1",NULL_00000000-0000-0000-0000-000000000000);
ApplyStatus((CHARACTER)S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56,"HAV_PHASM_OX_DISGUISE",-1.0,1,NULL_00000000-0000-0000-0000-000000000000);
PROC_SetAnubisConfig((CHARACTER)S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, "GLO_DevilishOx");
SetLevel((CHARACTER)S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, 9);
PROC_TriggerRegisterForPlayers((TRIGGER)S_WYR_OxKnowledgeCheck_6172ed9a-9078-4e5e-bbca-6a81ee7401fa);
DB_KnowledgeCheckTrigger("WYR_DevilishOx_Investigation", (TRIGGER)S_WYR_OxKnowledgeCheck_6172ed9a-9078-4e5e-bbca-6a81ee7401fa, "Insight", (DIFFICULTYCLASS)Act3_Medium_77cee1c4-384a-4217-b670-67db3c7add57);

PROC
PROC_StateSet_Defeated(S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56)
THEN
PROC_KnowledgeCheck_Clear((TRIGGER)S_WYR_OxKnowledgeCheck_6172ed9a-9078-4e5e-bbca-6a81ee7401fa);

//END_REGION


//REGION
//Play this first AD if the player didn't meet the ox in DEN or HAV
PROC
PROC_GLO_KnowledgeCheckSuccess(_Player, "WYR_DevilishOx_Investigation", S_WYR_OxKnowledgeCheck_6172ed9a-9078-4e5e-bbca-6a81ee7401fa)
AND
NOT DB_GlobalFlag(DEN_DevilishOx_State_InsightCheckSucceeded_36e186dc-ba92-4ac1-8dbd-f41a2b97d8d0)
AND
NOT DB_GlobalFlag(HAV_DevilishOx_State_InsightCheckSucceeded_0aa9dbad-3ff9-4956-a3e6-e5beaeb9caf4)
THEN
PROC_TryStartAD(DEN_DevilishOx_PAD_FirstInsight_ee3df178-2a9b-5fc9-e3fc-f9357e063bc1, _Player);
SetFlag(DEN_DevilishOx_State_InsightCheckSucceeded_36e186dc-ba92-4ac1-8dbd-f41a2b97d8d0);

//Play this third AD if the player has heard the insight check AD in HAV
PROC
PROC_GLO_KnowledgeCheckSuccess(_Player, "WYR_DevilishOx_Investigation", S_WYR_OxKnowledgeCheck_6172ed9a-9078-4e5e-bbca-6a81ee7401fa)
AND
DB_GlobalFlag(HAV_DevilishOx_State_InsightCheckSucceeded_0aa9dbad-3ff9-4956-a3e6-e5beaeb9caf4)
THEN
PROC_TryStartAD(WYR_DevilishOx_PAD_ThirdInsight_a48a456d-7d91-3d83-1c88-3c505a0758ef, _Player);
SetFlag(WYR_DevilishOx_State_InsightCheckSucceeded_86202033-1f7b-4727-aca9-e73bf4c6f952);
//END_REGION


//REGION
//If the player has spoken with the ox and it has asked to be taken through the gate, it turns into an apple
IF
FlagSet(WYR_DevilishOx_Event_PlayerAgreesToHelpOx_770092da-b4f4-40f5-bec8-aa9a6af0a2f4, NULL_00000000-0000-0000-0000-000000000000, _)
THEN
//Current solution for the ox tranforming into an apple.
PROC_SetOnStage((ITEM)S_GLO_DevilishOx_AppleForm_2fb8359f-7fa7-4dab-8e0f-c1edef958a73, 1);
PROC_SetOnStage((CHARACTER)S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, 0);
//END_REGION


//REGION
//If the Ox enters combat then it morphs into another creature
IF
EnteredCombat(S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, _)
AND
QRY_OnlyOnce("WYR_DevilishOx_EnteredCombat")
THEN
PROC_Foop((CHARACTER)S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56);
RemoveStatus(S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, "HAV_PHASM_OX_DISGUISE", NULL_00000000-0000-0000-0000-000000000000);
SetFlag(HAV_DevilishOx_State_MonsterRevealed_32e50fb0-ab45-44cf-86af-3984de34f338, NULL_00000000-0000-0000-0000-000000000000);

//If it leaves combat, it would disappear out of sight
IF
LeftCombat(S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, _)
THEN
PROC_DisappearOutOfSight(S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, "Run", 1,"");
SetFlag(HAV_DevilishOx_State_Fled_aa1fff72-16df-4269-abda-61e5120a8623, NULL_00000000-0000-0000-0000-000000000000);

IF
AttackedBy(S_GLO_DevilishOx_AppleForm_2fb8359f-7fa7-4dab-8e0f-c1edef958a73, _, _Player, _, _, _, _)
AND
DB_Players((CHARACTER)_Player)
AND
GetPosition(S_GLO_DevilishOx_AppleForm_2fb8359f-7fa7-4dab-8e0f-c1edef958a73, _X, _Y, _Z)
AND
QRY_OnlyOnce("WYR_DevilishOx_AppleProvoked")
THEN
PROC_SetOnStage((CHARACTER)S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, 1);
AppearAtPosition(S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, _X, _Y, _Z, 1, NULL_00000000-0000-0000-0000-000000000000, "WYR_DevilishOx_AppleAttacked");
PROC_MakeNPCHostile(S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, _Player);
PROC_WYR_MonsterRevealed_Reaction();

IF
DB_Destroyed(S_GLO_DevilishOx_AppleForm_2fb8359f-7fa7-4dab-8e0f-c1edef958a73)
AND
DB_Players((CHARACTER)_Player)
AND
GetPosition(S_GLO_DevilishOx_AppleForm_2fb8359f-7fa7-4dab-8e0f-c1edef958a73, _X, _Y, _Z)
AND
QRY_OnlyOnce("WYR_DevilishOx_AppleProvoked")
THEN
PROC_SetOnStage((CHARACTER)S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, 1);
AppearAtPosition(S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, _X, _Y, _Z, 1, NULL_00000000-0000-0000-0000-000000000000, "WYR_DevilishOx_AppleDestroyed");
PROC_MakeNPCHostile(S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, _Player);
PROC_WYR_MonsterRevealed_Reaction();

PROC
PROC_WYR_MonsterRevealed_Reaction()
AND
QRY_OnlyOnce("WYR_DevilishOx_MonsterRevealedReaction")
THEN
PROC_SetOnStage((ITEM)S_GLO_DevilishOx_AppleForm_2fb8359f-7fa7-4dab-8e0f-c1edef958a73, 0);
RemoveStatus(S_GLO_DevilishOx_49eb5b4b-c6ed-4d37-8f68-97b1128b7d56, "HAV_PHASM_OX_DISGUISE", NULL_00000000-0000-0000-0000-000000000000);
SetFlag(HAV_DevilishOx_State_MonsterRevealed_32e50fb0-ab45-44cf-86af-3984de34f338, NULL_00000000-0000-0000-0000-000000000000);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act3"
