Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Dialogs(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203, (DIALOGRESOURCE)LOW_Figaros_Naaber_acedefc7-6e71-a067-3ccf-c1d31f24e694);
PROC_TriggerRegisterForPlayers(S_WYR_FigaroSister_Naaber_StartSpot_Trigger_853ba17b-27c8-49b4-a46d-4fd85c808327);
DB_GlobalFlagReactionAfterDialog(LOW_SerialKiller_State_NaaberDialog_Finish_9b7e052e-d8d4-fb55-602f-0fbe47a1489b,LOW_Figaros_Naaber_acedefc7-6e71-a067-3ccf-c1d31f24e694);

DB_WYR_FigaroSister_ClothingReactivity("Figaro",(GUIDSTRING)S_LOW_Figaro_3ff2b7ea-3de2-4bdc-a600-c973f290bc09,(FLAG)WYR_FigaroSister_State_WearingFigaroClothes_f2d964ad-f362-42e2-9b77-c067e7b76a3b);
DB_WYR_FigaroSister_ClothingReactivity("Carmen",(GUIDSTRING)S_WYR_FigaroSister_Carmen_99c32d1b-838c-4db2-8150-d4112aedc636,WYR_FigaroSister_State_WearingCarmenClothes_f8e42a71-88f6-4400-86e9-fed63f6078cb);

DB_WYR_FigaroSister_Naaber_FollowTrigger((TRIGGER)S_WYR_FigaroSister_Naaber_Follow_Trigger_08deaef5-4f97-4679-926d-6d92dddfa842);

DB_GLO_CharacterCorpseDialog(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,WYR_FigaroSister_Naaber_Dead_44114beb-01c4-8701-d2cb-e86c10dd8f92);
PROC_CharacterDisableAllCrimes((CHARACTER)S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203);

DB_WYR_FigaroSister_ClothingReactivity_Slots("Breast",ARMOURSET.Normal);
DB_WYR_FigaroSister_ClothingReactivity_Slots("Boots",ARMOURSET.Normal);
DB_WYR_FigaroSister_ClothingReactivity_Slots("VanityBody",ARMOURSET.Vanity);
DB_WYR_FigaroSister_ClothingReactivity_Slots("VanityBoots",ARMOURSET.Vanity);
KBSECTION
//REGION Naaber

PROC
PROC_GlobalFlagReactionAfterDialog(LOW_SerialKiller_State_NaaberDialog_Finish_9b7e052e-d8d4-fb55-602f-0fbe47a1489b)
THEN
PROC_RemoveAllDialogEntriesForSpeaker(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203);
PROC_DisappearOutOfSight((CHARACTER)S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203, "Walk", 1, "");
PROC_SpotPlayers_StopSpotting("WYR_FigaroSister_Naaber");

IF
FlagSet(LOW_SerialKiller_State_NaaberDialog_Finish_9b7e052e-d8d4-fb55-602f-0fbe47a1489b,_,_DialogId)
AND
DB_DialogPlayers(_DialogId,_Player,_)
THEN
QuestUpdate((CHARACTER)_Player,"HIDDEN_BGO_Boosters", "WYR_FigaroSister_FinishedNaaberDialog");

IF
DB_InRegion(_,S_WYR_FigaroSister_Naaber_StartSpot_Trigger_853ba17b-27c8-49b4-a46d-4fd85c808327)
THEN
PROC_TriggerUnregisterForPlayers(S_WYR_FigaroSister_Naaber_StartSpot_Trigger_853ba17b-27c8-49b4-a46d-4fd85c808327);
PROC_TriggerRegisterForPlayers(S_WYR_FigaroSister_Naaber_Follow_Trigger_08deaef5-4f97-4679-926d-6d92dddfa842);
DB_SpotPlayers((CHARACTER)S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203, "WYR_FigaroSister_Naaber", (FLAG)LOW_SerialKiller_Event_NaaberSpotting_7692331d-6856-498a-9120-de07dd78c774,(FLAG)LOW_SerialKiller_Event_NaaberStopSpotting_24a0cca9-1b65-44c7-8e1b-002824f55612);

//Naaber spotting
PROC
PROC_SpotPlayers_Spotted(_Spotter, "WYR_FigaroSister_Naaber", (CHARACTER)_Target)
AND
DB_Players(_Target)
AND
IsControlled(_Target, 1)
AND
GetUUID(_Target, _UUID)
AND
Concatenate("WYR_NaaberRepeatingAD", _UUID, _TimerName)
AND
NOT DB_WYR_FigaroSister_Naaber(_, _)
THEN
PROC_WYR_FigaroSister_Naaber_StartFollow((CHARACTER)_Target,(STRING)_TimerName);

PROC
PROC_WYR_FigaroSister_Naaber_StartFollow((CHARACTER)_,(STRING)_)
AND
NOT DB_GlobalFlag(WYR_FigaroSister_State_NaaberSawPlayer_2c7f47d6-9924-40c1-ac0b-e312e61d46c6)
THEN
SetFlag(WYR_FigaroSister_State_NaaberSawPlayer_2c7f47d6-9924-40c1-ac0b-e312e61d46c6);

IF
FlagSet(WYR_FigaroSister_State_NaaberShovedIntoRiver_3c5f936b-d777-4b35-9ae0-f8319cd40f87,_,_)
AND
DB_GlobalFlag(WYR_FigaroSister_State_NaaberSawPlayer_2c7f47d6-9924-40c1-ac0b-e312e61d46c6)
THEN
ClearFlag(WYR_FigaroSister_State_NaaberSawPlayer_2c7f47d6-9924-40c1-ac0b-e312e61d46c6);

PROC
PROC_WYR_FigaroSister_Naaber_StartFollow((CHARACTER)_Target,(STRING)_TimerName)
THEN
PROC_Follow((CHARACTER)S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203, _Target);
ObjectTimerLaunch(_Target, _TimerName, 30000);
DB_WYR_FigaroSister_Naaber(_Target, _TimerName);

PROC
PROC_WYR_FigaroSister_Naaber_StartFollow((CHARACTER)_Target,(STRING)_TimerName)
AND
NOT DB_WYR_FigaroSister_NaaberRiversideState("Spotting")
AND
NOT DB_DialogSpeakers(_,S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,_)
THEN
PROC_TryStartAD((DIALOGRESOURCE)LOW_Figaros_Naaber_AD_991b0991-af6e-d3a8-4646-55feb37434b9, S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203);

IF
DB_WYR_FigaroSister_Naaber_QueueRestartSpot(1)
AND
DB_WYR_FigaroSister_Naaber_FollowTrigger(_Trigger)
AND
DB_InRegion(_,_Trigger)
THEN
NOT DB_WYR_FigaroSister_Naaber_QueueRestartSpot(1);
PROC_SpotPlayers_RestartSpotting(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,"WYR_FigaroSister_Naaber");
DebugText(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,"Restart spotting");

IF
ObjectTimerFinished(_Player, _TimerNaaber)
AND
DB_WYR_FigaroSister_Naaber((CHARACTER)_Player, _TimerNaaber)
AND
DB_WYR_FigaroSister_Naaber_FollowTrigger(_Trigger)
AND
DB_InRegion(_Player,_Trigger)
THEN
PROC_WYR_FigaroSister_TryAD(_Player, _TimerNaaber);

PROC
PROC_WYR_FigaroSister_TryAD((CHARACTER)_Player, (STRING)_TimerNaaber)
THEN
ObjectTimerLaunch(_Player, _TimerNaaber, 30000);

PROC
PROC_WYR_FigaroSister_TryAD((CHARACTER)_, (STRING)_)
AND
NOT DB_DialogSpeakers(_,S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,_)
THEN
PROC_TryStartAD((DIALOGRESOURCE)LOW_Figaros_Naaber_AD_991b0991-af6e-d3a8-4646-55feb37434b9, S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203);

IF
DB_Following(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,_Player)
AND
DB_WYR_FigaroSister_Naaber_FollowTrigger(_Trigger)
AND
DB_WYR_FigaroSister_Naaber(_Player, _TimerNaaber)
AND
NOT DB_InRegion(_Player,_Trigger)
THEN
PROC_StopFollow((CHARACTER)S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203);
ObjectTimerCancel(_Player, _TimerNaaber);
NOT DB_WYR_FigaroSister_Naaber(_Player, _TimerNaaber);
TimerLaunch("WYR_FigaroSister_Naaber_RestartSpot_Delay",6000);

IF
TimerFinished("WYR_FigaroSister_Naaber_RestartSpot_Delay")
AND
NOT DB_GlobalFlag(LOW_SerialKiller_State_NaaberDialog_Finish_9b7e052e-d8d4-fb55-602f-0fbe47a1489b)
THEN
DB_WYR_FigaroSister_Naaber_QueueRestartSpot(1);

IF
DialogStarted((DIALOGRESOURCE)LOW_Figaros_Naaber_acedefc7-6e71-a067-3ccf-c1d31f24e694,_)
THEN
TimerCancel("WYR_FigaroSister_Naaber_RestartSpot_Delay");

IF
DialogEnded((DIALOGRESOURCE)LOW_Figaros_Naaber_acedefc7-6e71-a067-3ccf-c1d31f24e694,_)
AND
DB_WYR_FigaroSister_Naaber(_Player, _TimerNaaber)
THEN
PROC_StopFollow((CHARACTER)S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203);
ObjectTimerCancel(_Player, _TimerNaaber);
NOT DB_WYR_FigaroSister_Naaber(_Player, _TimerNaaber);

IF
DialogEnded((DIALOGRESOURCE)LOW_Figaros_Naaber_acedefc7-6e71-a067-3ccf-c1d31f24e694,_)
THEN
TimerLaunch("WYR_FigaroSister_Naaber_RestartSpot_Delay",25000);
SetEntityEvent(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,"WYR_FigaroSister_Naaber_StopSleep",1);

//REGION Naaber shoved into river

IF
FlagSet(WYR_FigaroSister_Debug_NaaberChasm_2004ed99-c740-4698-f97d-73c5bb2c5444,_Player,_)
THEN
ClearFlag(WYR_FigaroSister_Debug_NaaberChasm_2004ed99-c740-4698-f97d-73c5bb2c5444,_Player);
PROC_WYR_FigaroSister_DEBUG_NaaberChasm((CHARACTER)_Player);

IF
TextEvent("Naaber_Chasm")
AND
GetHostCharacter(_Player)
THEN
PROC_WYR_FigaroSister_DEBUG_NaaberChasm(_Player);

PROC
PROC_WYR_FigaroSister_DEBUG_NaaberChasm((CHARACTER)_Player)
AND
DB_WYR_FigaroSister_NaaberRiversideState(_State)
THEN
NOT DB_WYR_FigaroSister_NaaberRiversideState(_State);

PROC
PROC_WYR_FigaroSister_DEBUG_NaaberChasm((CHARACTER)_Player)
THEN
PROC_CharacterFullRestore((CHARACTER)S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203);
SetOnStage(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,1);
TeleportTo(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,S_WYR_FigaroSister_DEBUG_Naaber_Chasm_Point_d398a72d-9e6c-4421-9b03-ddb874694be4,"",0,0,0,1,0);
TeleportTo(_Player,S_WYR_FigaroSister_DEBUG_Point_5d7b1276-f516-49f2-b3ea-3564b25cd35c,"",1,1,1,1,1);

IF
EnteredChasm(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,_,(TRIGGER)ChasmRegion_WC_Chiontar_8963a5a9-a4ef-44fe-8a72-0701f9774b9a,_,_,_)
AND
NOT DB_GlobalFlag(WYR_FigaroSister_State_NaaberShovedIntoRiver_3c5f936b-d777-4b35-9ae0-f8319cd40f87)
AND
NOT DB_SpotPlayers(_,"WYR_FigaroSister_Naaber",_,_)
THEN
PROC_TriggerUnregisterForPlayers(S_WYR_FigaroSister_Naaber_StartSpot_Trigger_853ba17b-27c8-49b4-a46d-4fd85c808327);
DB_SpotPlayers((CHARACTER)S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203, "WYR_FigaroSister_Naaber", (FLAG)LOW_SerialKiller_Event_NaaberSpotting_7692331d-6856-498a-9120-de07dd78c774,(FLAG)LOW_SerialKiller_Event_NaaberStopSpotting_24a0cca9-1b65-44c7-8e1b-002824f55612);

IF
EnteredChasm(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,_,(TRIGGER)ChasmRegion_WC_Chiontar_8963a5a9-a4ef-44fe-8a72-0701f9774b9a,_,_,_)
AND
NOT DB_GlobalFlag(WYR_FigaroSister_State_NaaberShovedIntoRiver_3c5f936b-d777-4b35-9ae0-f8319cd40f87)
AND
DB_WYR_FigaroSister_Naaber(_Target, _TimerName)
THEN
NOT DB_WYR_FigaroSister_Naaber(_Target, _TimerName);

IF
EnteredChasm(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,_,(TRIGGER)ChasmRegion_WC_Chiontar_8963a5a9-a4ef-44fe-8a72-0701f9774b9a,_,_,_)
AND
NOT DB_GlobalFlag(WYR_FigaroSister_State_NaaberShovedIntoRiver_3c5f936b-d777-4b35-9ae0-f8319cd40f87)
THEN
EnterChasmProcessed(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,1);
PROC_SpotPlayers_StopSpotting(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,"WYR_FigaroSister_Naaber");
PROC_StopFollow((CHARACTER)S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203);
TeleportTo(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,S_WYR_FigaroSister_Naaber_Riverside_Point_e8cf2752-8e2e-4318-a3cf-cf985d7b43cd,"",0,0,0,1,1);
SetFlag(WYR_FigaroSister_State_NaaberShovedIntoRiver_3c5f936b-d777-4b35-9ae0-f8319cd40f87);
SetOnStage(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,0);
DB_WYR_FigaroSister_NaaberRiversideState("Chasm");
PROC_TriggerRegisterForParty(S_WYR_FigaroSister_Naaber_Riverside_BlockAppear_Trigger_87532484-48f6-4bd9-82f9-29066f26e3a4);
SetFlag(WYR_FigaroSister_Naaber_HasMet_AD_93541945-f6ed-99ec-184e-8dae4ab0b37b);

IF
WentOnStage(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,0)
AND
DB_WYR_FigaroSister_NaaberRiversideState("Chasm")
THEN
NOT DB_WYR_FigaroSister_NaaberRiversideState("Chasm");
DB_WYR_FigaroSister_NaaberRiversideState("Offstage");

IF
DB_WYR_FigaroSister_NaaberRiversideState("Offstage")
AND
NOT DB_InRegion(_,S_WYR_FigaroSister_Naaber_Riverside_BlockAppear_Trigger_87532484-48f6-4bd9-82f9-29066f26e3a4)
THEN
NOT DB_WYR_FigaroSister_NaaberRiversideState("Offstage");
DB_WYR_FigaroSister_NaaberRiversideState("Spotting");
SetOnStage(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,1);
PROC_SpotPlayers_RestartSpotting(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,"WYR_FigaroSister_Naaber");
PROC_TriggerUnregisterForParty(S_WYR_FigaroSister_Naaber_Riverside_BlockAppear_Trigger_87532484-48f6-4bd9-82f9-29066f26e3a4);
NOT DB_WYR_FigaroSister_Naaber_FollowTrigger(S_WYR_FigaroSister_Naaber_Follow_Trigger_08deaef5-4f97-4679-926d-6d92dddfa842);
DB_WYR_FigaroSister_Naaber_FollowTrigger(S_WYR_FigaroSister_Naaber_Riverside_Follow_Trigger_62419598-5602-4b72-afb7-66aaccaeb23d);
PROC_TriggerRegisterForPlayers(S_WYR_FigaroSister_Naaber_Riverside_Follow_Trigger_62419598-5602-4b72-afb7-66aaccaeb23d);

PROC
PROC_SpotPlayers_Spotted((CHARACTER)S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,"WYR_FigaroSister_Naaber",_Player)
AND
DB_WYR_FigaroSister_NaaberRiversideState("Spotting")
THEN
NOT DB_WYR_FigaroSister_NaaberRiversideState("Spotting");
PROC_TryStartAD(WYR_FigaroSister_AD_Naaber_RiversideSpotted_87f94711-3988-88c8-2594-b230e8d79a5c,S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203);

IF
AutomatedDialogEnded(WYR_FigaroSister_AD_Naaber_RiversideSpotted_87f94711-3988-88c8-2594-b230e8d79a5c,_)
THEN
PROC_WYR_FigaroSister_Naaber_StartDefaultAD();

IF
AutomatedDialogRequestFailed(WYR_FigaroSister_AD_Naaber_RiversideSpotted_87f94711-3988-88c8-2594-b230e8d79a5c,_)
THEN
PROC_WYR_FigaroSister_Naaber_StartDefaultAD();

PROC
PROC_WYR_FigaroSister_Naaber_StartDefaultAD()
AND
DB_WYR_FigaroSister_Naaber((CHARACTER)_Player, _TimerNaaber)
THEN
PROC_TryStartAD((DIALOGRESOURCE)LOW_Figaros_Naaber_AD_991b0991-af6e-d3a8-4646-55feb37434b9, S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203);

PROC
PROC_WYR_FigaroSister_Naaber_StartDefaultAD()
AND
DB_WYR_FigaroSister_Naaber((CHARACTER)_Player, _TimerNaaber)
THEN
ObjectTimerCancel(_Player, _TimerNaaber);
ObjectTimerLaunch(_Player, _TimerNaaber, 8000);

//END_REGION


//Reaction to being attacked
IF
AttackedBy(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,_Attacker,_,_,_,_,_)
AND
DB_PartyMembers((CHARACTER)_Attacker)
AND
NOT DB_WYR_FigaroSister_NaaberAttackedAD_OnCooldown(1)
AND
NOT DB_DialogSpeakers(_,S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,_)
THEN
PROC_TryStartAD(WYR_FigaroSister_AD_Naaber_Attacked_08ab851d-757a-d01a-5a11-d073dd3ba5d5,S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203);
DB_WYR_FigaroSister_NaaberAttackedAD_OnCooldown(1);
RealtimeObjectTimerLaunch(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,"WYR_FigaroSister_NaaberAttackedAD",3500);

IF
ObjectTimerFinished(S_WYR_FigaroSister_Naaber_fa22f4a3-53ba-4a35-9dec-a402244a5203,"WYR_FigaroSister_NaaberAttackedAD")
THEN
NOT DB_WYR_FigaroSister_NaaberAttackedAD_OnCooldown(1);

//END_REGION

//REGION Figaro/Carmen clothing reactivity

//Register traded items from Figaro and Carmen
IF
MovedFromTo(_Item,_,_Seller,_)
AND
DB_WYR_FigaroSister_ClothingReactivity(_,_Seller,_)
AND
NOT DB_WYR_FigaroSister_BlockTradeRegister(_Item)
THEN
DB_WYR_FigaroSister_BlockTradeRegister(_Item);

IF
MovedFromTo(_Item,_Seller,_,1)
AND
DB_WYR_FigaroSister_ClothingReactivity(_Identifier,_Seller,_)
AND
NOT DB_WYR_FigaroSister_BlockTradeRegister(_Item)
THEN
DB_WYR_FigaroSister_TradedItem(_Item,_Identifier);

//DB_WYR_FigaroSister_WearingClothes
IF
Equipped(_Item,_Player)
AND
DB_WYR_FigaroSister_TradedItem(_Item,_Identifier)
AND
DB_WYR_FigaroSister_ClothingReactivity(_Identifier,_,_Flag)
AND
DB_Players(_Player)
THEN
DB_WYR_FigaroSister_WearingClothes((CHARACTER)_Player,(ITEM)_Item,(STRING)_Identifier);

IF
Unequipped(_Item,_Player)
AND
DB_WYR_FigaroSister_TradedItem(_Item,_Identifier)
AND
DB_WYR_FigaroSister_ClothingReactivity(_Identifier,_,_Flag)
AND
DB_Players(_Player)
THEN
NOT DB_WYR_FigaroSister_WearingClothes((CHARACTER)_Player,(ITEM)_Item,(STRING)_Identifier);

//DB_WYR_FigaroSister_VisualClothes
//Check whether newly added worn clothes are visually worn
IF
DB_WYR_FigaroSister_WearingClothes((CHARACTER)_Player,(ITEM)_Item,(STRING)_Identifier)
AND
QRY_WYR_FigaroSister_IsClothingVisuallyWorn(_Player,_Item)
THEN
DB_WYR_FigaroSister_VisualClothes((CHARACTER)_Player,(ITEM)_Item,(STRING)_Identifier);
PROC_WYR_FigaroSister_CheckVisualClothing(_Player,_Identifier);

//If visual clothes are no longer worn, remove
IF
DB_WYR_FigaroSister_VisualClothes((CHARACTER)_Player,(ITEM)_Item,(STRING)_Identifier)
AND
NOT DB_WYR_FigaroSister_WearingClothes((CHARACTER)_Player,(ITEM)_Item,(STRING)_Identifier)
THEN
NOT DB_WYR_FigaroSister_VisualClothes((CHARACTER)_Player,(ITEM)_Item,(STRING)_Identifier);
PROC_WYR_FigaroSister_CheckVisualClothing(_Player,_Identifier);

//Armor set changed - check whether visual clothes are still visual
IF
ArmorSetChanged(_Player,_)
AND
DB_WYR_FigaroSister_VisualClothes(_Player,_Item,_Identifier)
AND
NOT QRY_WYR_FigaroSister_IsClothingVisuallyWorn(_Player,_Item)
THEN
NOT DB_WYR_FigaroSister_VisualClothes((CHARACTER)_Player,(ITEM)_Item,(STRING)_Identifier);
PROC_WYR_FigaroSister_CheckVisualClothing(_Player,_Identifier);

IF
ArmorSetChanged(_Player,_)
AND
DB_WYR_FigaroSister_WearingClothes(_Player,_Item,_Identifier)
AND
QRY_WYR_FigaroSister_IsClothingVisuallyWorn(_Player,_Item)
THEN
DB_WYR_FigaroSister_VisualClothes((CHARACTER)_Player,(ITEM)_Item,(STRING)_Identifier);
PROC_WYR_FigaroSister_CheckVisualClothing(_Player,_Identifier);

//PROC_WYR_FigaroSister_CheckClothing
//Check whether to set the flag
PROC
PROC_WYR_FigaroSister_CheckVisualClothing((CHARACTER)_Player,(STRING)_Identifier)
AND
DB_WYR_FigaroSister_VisualClothes((CHARACTER)_Player,(ITEM)_Item,(STRING)_Identifier)
AND
NOT DB_WYR_FigaroSister_PlayerWearingClothing((CHARACTER)_Player,(STRING)_Identifier)
AND
DB_WYR_FigaroSister_ClothingReactivity(_Identifier,_,_Flag)
THEN
DB_WYR_FigaroSister_PlayerWearingClothing((CHARACTER)_Player,(STRING)_Identifier);
SetFlag(_Flag,_Player);

//Check whether to clear the flag
PROC
PROC_WYR_FigaroSister_CheckVisualClothing((CHARACTER)_Player,(STRING)_Identifier)
AND
NOT DB_WYR_FigaroSister_VisualClothes((CHARACTER)_Player,(ITEM)_,(STRING)_Identifier)
AND
DB_WYR_FigaroSister_ClothingReactivity(_Identifier,_,_Flag)
THEN
NOT DB_WYR_FigaroSister_PlayerWearingClothing((CHARACTER)_Player,(STRING)_Identifier);
ClearFlag(_Flag,_Player);

//QUERIES
//TODO? check for shapeshifting
//Problem: Wyll's Devil form is a shapeshift?
QRY
QRY_WYR_FigaroSister_IsClothingVisuallyWorn((CHARACTER)_Player,(ITEM)_Item)
AND
DB_EquippedItemSlots(_Slot)
AND
GetEquippedItem(_Player,_Slot,_Item)
AND
QRY_WYR_FigaroSister_CheckSlotVisible(_Player,_Slot)
THEN
DB_NOOP(1);

//If armour set-dependent slot, check whether armour set is visible
QRY
QRY_WYR_FigaroSister_CheckSlotVisible((CHARACTER)_Player,(STRING)_Slot)
AND
DB_WYR_FigaroSister_ClothingReactivity_Slots(_Slot,_ArmourSet)
AND
QRY_GetArmourSet(_Player)
AND
DB_QRYRTN_GetArmourSet(_ArmourSet)
THEN
DB_NOOP(1);

//Other slots are only visible in normal armour set
QRY
QRY_WYR_FigaroSister_CheckSlotVisible((CHARACTER)_Player,(STRING)_Slot)
AND
NOT DB_WYR_FigaroSister_ClothingReactivity_Slots(_Slot,_)
AND
QRY_GetArmourSet(_Player)
AND
DB_QRYRTN_GetArmourSet(ARMOURSET.Normal)
THEN
DB_NOOP(1);

//DEBUG
IF
FlagSet(_Flag,_Player,_)
AND
DB_WYR_FigaroSister_ClothingReactivity(_Name,_,_Flag)
AND
Concatenate("Wearing clothes from ",_Name,_DebugText)
THEN
DebugText(_Player,_DebugText);

IF
FlagCleared(_Flag,_Player,_)
AND
DB_WYR_FigaroSister_ClothingReactivity(_Name,_,_Flag)
AND
Concatenate("Stopped wearing clothes from ",_Name,_DebugText)
THEN
DebugText(_Player,_DebugText);

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act3"
