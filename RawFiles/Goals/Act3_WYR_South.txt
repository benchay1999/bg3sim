Version 1
SubGoalCombiner SGC_AND
INITSECTION
// South checkpoint of the Wyrm's bridge
DB_CheckPoint(S_WYR_South_Guard_004_e572c6d1-3330-46fd-9ecc-da318f499956,
	S_WYR_South_Guard_000_3e5c5389-46ac-4f3c-8970-7323f38efa51,
	S_WYR_South_PrimarySpotTrigger_c8076228-1f9d-4d8a-846c-e1b6cecf2f92,
	S_WYR_South_WarningTrigger_62a509e2-a814-480c-ac04-694a60564c2d,
	S_WYR_South_TrespassTrigger_eefe8bf7-73da-4954-abd2-38cbc1c4b9cd,
	"WYR_South",
	WYR_South_State_AccessGranted_3339af4f-3214-4d10-aaab-3d3511b9ab74,
	"WYR_South_Checkpoint_Trespass",
	S_WYR_South_CheckpointOut_9443e8cb-c32a-4b1b-bf04-5e9506dea31b);
DB_CheckPointPassTrigger("WYR_South","WYR_South_Access",S_WYR_South_PassTrigger_1ae4c8e2-7bb4-404b-89be-975c72aab7e9);

PROC_TriggerRegisterForPlayers(S_WYR_South_AddPlayerToDialog_Zone_d9ff0c05-6afa-4d58-88f6-4ab0cbc20f2d);
DB_AddCharactersInTriggerToDialog(WYR_South_Primary_f03a1685-b148-b70e-b773-b62404525308,S_WYR_South_AddPlayerToDialog_Zone_d9ff0c05-6afa-4d58-88f6-4ab0cbc20f2d,1,0);

DB_DialogMoneyTransfer(1,WYR_South_Primary_f03a1685-b148-b70e-b773-b62404525308,200,2,1);

DB_WYR_South_Guards((CHARACTER)S_WYR_South_Guard_000_3e5c5389-46ac-4f3c-8970-7323f38efa51);
DB_WYR_South_Guards(S_WYR_South_Guard_001_1b648dc2-fa93-43f4-b771-b3d18da7725b);
DB_WYR_South_Guards(S_WYR_South_Guard_002_2df4c38e-db78-4a08-b78f-68882ef55a6c);
DB_WYR_South_Guards(S_WYR_South_Guard_003_e93fa1a6-9999-45a9-9a91-3e7dc00908c1);
DB_WYR_South_Guards(S_WYR_South_Guard_004_e572c6d1-3330-46fd-9ecc-da318f499956);
DB_WYR_South_Guards(S_WYR_South_Guard_009_d0b1836e-6a9c-42b5-83b3-21ac5cc055bb);
DB_WYR_South_Guards(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635);
// Yes, intentionally not in DB
//DB_WYR_South_Guards(S_WYR_South_Guard_005_eb822a22-acb3-4648-9558-9bc6f1f830af);

DB_WYR_South_Refugees((CHARACTER)S_WYR_South_Civilian_000_fac3ed5f-b71b-4285-b05a-1d504c06666e);
DB_WYR_South_Refugees((CHARACTER)S_WYR_South_Civilian_001_4134d7c5-e8ba-4123-bd25-32094a9c3701);
DB_WYR_South_Refugees((CHARACTER)S_WYR_South_Civilian_002_b6a8b4ee-87a1-4129-b8f0-5d2ef7ec76ea);
DB_WYR_South_Refugees((CHARACTER)S_WYR_South_Civilian_003_2abb831b-3975-4447-a13d-2a0e13ac7b70);
DB_WYR_South_Refugees((CHARACTER)S_WYR_South_Civilian_004_df71e11e-d3d5-4430-9416-dcaca67860c1);
DB_WYR_South_Refugees((CHARACTER)S_WYR_South_Civilian_005_3215b076-c050-45d5-a068-45725f539b24);

DB_Children(S_WYR_South_Civilian_004_df71e11e-d3d5-4430-9416-dcaca67860c1);
DB_Children(S_WYR_South_Civilian_005_3215b076-c050-45d5-a068-45725f539b24);


DB_Dialogs(S_WYR_South_Guard_004_e572c6d1-3330-46fd-9ecc-da318f499956,WYR_South_Primary_f03a1685-b148-b70e-b773-b62404525308);
DB_Dialogs(S_WYR_South_Guard_000_3e5c5389-46ac-4f3c-8970-7323f38efa51,WYR_South_WarningGuard_fc38db3b-3e56-24cb-b84f-b0468193ce84);
DB_Dialogs(S_WYR_South_Guard_001_1b648dc2-fa93-43f4-b771-b3d18da7725b,WYR_South_Guard_001_c5bcf5c4-3170-3295-210b-4367671a0c44);
PROC_RemoveAllDialogEntriesForSpeaker(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635);
DB_GLO_SteelWatchers_CustomDialog((CHARACTER)S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635);
DB_Dialogs(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635,WYR_South_SteelWatch_7d8e4721-dd3e-ce25-243c-32104380566b);

DB_Dialogs(S_WYR_South_Guard_002_2df4c38e-db78-4a08-b78f-68882ef55a6c,WYR_South_Guard_002_8142c8ee-92a3-69e0-7d4c-47066c7c089e);
DB_Dialogs(S_WYR_South_Guard_003_e93fa1a6-9999-45a9-9a91-3e7dc00908c1,WYR_South_Guard_003_85fc21ad-932f-31bc-8210-ebe1be6f80de);
DB_Dialogs(S_WYR_South_Guard_005_eb822a22-acb3-4648-9558-9bc6f1f830af,WYR_South_Guard_005_a7b7e9db-1dde-8c3b-91ec-1cca1e197987);
DB_Dialogs(S_WYR_South_Guard_009_d0b1836e-6a9c-42b5-83b3-21ac5cc055bb,WYR_South_Guard_009_f97c4ad3-cf6c-ad7c-820d-93afc4deb62e);

DB_Dialogs(S_WYR_South_Civilian_000_fac3ed5f-b71b-4285-b05a-1d504c06666e,WYR_South_Civilian_000_497d5599-2052-8dd4-5f59-12846123ef27);
DB_Dialogs(S_WYR_South_Civilian_001_4134d7c5-e8ba-4123-bd25-32094a9c3701,WYR_South_Civilian_001_b22807c5-c1d9-847b-aa1d-377672b13455);
DB_Dialogs(S_WYR_South_Civilian_002_b6a8b4ee-87a1-4129-b8f0-5d2ef7ec76ea,WYR_South_Civilian_002_1e073016-aae2-1edf-d315-50b0774779dc);
DB_Dialogs(S_WYR_South_Civilian_003_2abb831b-3975-4447-a13d-2a0e13ac7b70,WYR_South_Civilian_003_55e3bb37-78df-615d-79dd-6cf51acb7a84);
DB_Dialogs(S_WYR_South_Civilian_004_df71e11e-d3d5-4430-9416-dcaca67860c1,WYR_South_Civilian_004_fb014c2a-0bfc-06ad-c4b5-3490fd15e7d1);
DB_Dialogs(S_WYR_South_Civilian_005_3215b076-c050-45d5-a068-45725f539b24,WYR_South_Civilian_005_d491fb03-e222-39de-7740-c3691e4cdc1a);

DB_FlagReactionAfterDialog(WYR_South_AgreedCustody_a85fefb5-6f59-4709-b310-b82a1e13411f,WYR_South_Primary_f03a1685-b148-b70e-b773-b62404525308);
DB_GlobalFlagReactionAfterDialog(WYR_Ironhand_Event_GnomesIntervent_06ad5471-b210-4ea0-a390-09bc7afdd332,WYR_South_Primary_f03a1685-b148-b70e-b773-b62404525308);
DB_FlagReactionAfterDialog(WYR_South_AgreedCustody_a85fefb5-6f59-4709-b310-b82a1e13411f,WYR_South_SteelWatch_7d8e4721-dd3e-ce25-243c-32104380566b);

DB_GLO_SteelWatcher_IgnoresCrime(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635);


DB_WYR_South_Panic(100,(CHARACTER)S_WYR_South_Guard_004_e572c6d1-3330-46fd-9ecc-da318f499956,(GUIDSTRING)S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635);
DB_WYR_South_Panic(1000,S_WYR_South_Guard_000_3e5c5389-46ac-4f3c-8970-7323f38efa51,S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635);
DB_WYR_South_Panic(500,S_WYR_South_Guard_001_1b648dc2-fa93-43f4-b771-b3d18da7725b,S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635);
DB_WYR_South_Panic(1500,S_WYR_South_Guard_002_2df4c38e-db78-4a08-b78f-68882ef55a6c,S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635);
DB_WYR_South_Panic(750,S_WYR_South_Guard_003_e93fa1a6-9999-45a9-9a91-3e7dc00908c1,S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635);
DB_WYR_South_Panic(1100,S_WYR_South_Guard_009_d0b1836e-6a9c-42b5-83b3-21ac5cc055bb,S_WYR_South_GuardRunTo_009_272d9941-0b05-47e9-9e3f-a99ee878cc5e);

DB_WYR_South_GuardPos((CHARACTER)S_WYR_South_Guard_000_3e5c5389-46ac-4f3c-8970-7323f38efa51,(TRIGGER)S_WYR_South_GuardPos_000_b3469255-d016-404c-b5cd-8b0dc20d2c6e);
DB_WYR_South_GuardPos(S_WYR_South_Guard_001_1b648dc2-fa93-43f4-b771-b3d18da7725b,S_WYR_South_GuardPos_001_71a00eb9-4695-4c8b-b881-5c1b65d21fd8);
DB_WYR_South_GuardPos(S_WYR_South_Guard_002_2df4c38e-db78-4a08-b78f-68882ef55a6c,S_WYR_South_GuardPos_002_ad84e12f-9ca2-4b7b-b3cb-bdf6a7a739a5);
DB_WYR_South_GuardPos(S_WYR_South_Guard_003_e93fa1a6-9999-45a9-9a91-3e7dc00908c1,S_WYR_South_GuardPos_003_73b1b206-2295-493b-b1f2-5ed0d1265c55);
DB_WYR_South_GuardPos(S_WYR_South_Guard_004_e572c6d1-3330-46fd-9ecc-da318f499956,S_WYR_South_GuardPos_004_8dbe2ecc-1c63-4181-a8e9-f9c65ddae4bd);
DB_WYR_South_GuardPos(S_WYR_South_Guard_009_d0b1836e-6a9c-42b5-83b3-21ac5cc055bb,S_WYR_South_GuardPos_009_93d6ff05-f80e-4652-a62a-1ccabe92446a);

DB_WYR_South_GuardGoingToLook((CHARACTER)S_WYR_South_Guard_000_3e5c5389-46ac-4f3c-8970-7323f38efa51,(TRIGGER)S_WYR_South_GuardsRunTo_000_31f43e24-8f85-4096-8940-4a4c10af0f21);
DB_WYR_South_GuardGoingToLook(S_WYR_South_Guard_001_1b648dc2-fa93-43f4-b771-b3d18da7725b,S_WYR_South_GuardsRunTo_001_52761170-9b2b-41f9-9fa4-7221a778794c);


DB_WYR_South_RefugeesSeenWhenTalkedTo((CHARACTER)S_WYR_South_Civilian_000_fac3ed5f-b71b-4285-b05a-1d504c06666e);
DB_WYR_South_RefugeesSeenWhenTalkedTo((CHARACTER)S_WYR_South_Civilian_001_4134d7c5-e8ba-4123-bd25-32094a9c3701);
DB_WYR_South_RefugeesSeenWhenTalkedTo((CHARACTER)S_WYR_South_Civilian_002_b6a8b4ee-87a1-4129-b8f0-5d2ef7ec76ea);
DB_WYR_South_RefugeesSeenWhenTalkedTo((CHARACTER)S_WYR_South_Civilian_003_2abb831b-3975-4447-a13d-2a0e13ac7b70);
DB_WYR_South_RefugeesSeenWhenTalkedTo((CHARACTER)S_WYR_South_Civilian_004_df71e11e-d3d5-4430-9416-dcaca67860c1);
DB_WYR_South_RefugeesSeenWhenTalkedTo((CHARACTER)S_WYR_South_Civilian_005_3215b076-c050-45d5-a068-45725f539b24);
DB_WYR_South_RefugeesSeenWhenTalkedTo((CHARACTER)S_WYR_South_Guard_002_2df4c38e-db78-4a08-b78f-68882ef55a6c);
DB_WYR_South_RefugeesSeenWhenTalkedTo((CHARACTER)S_WYR_South_Guard_003_e93fa1a6-9999-45a9-9a91-3e7dc00908c1);
KBSECTION
//REGION Debug
//END_REGION Debug


//REGION States and flags

IF
DB_WYR_South_Guards(_Guard)
THEN
DB_GLO_DefeatCounter(_Guard,"WYR_South_Guards");

// guards can't see this trigger - it's too far, disable checkpoint manually
IF
DB_InRegion(_Player,_Trigger)
AND
DB_CheckPointPassTrigger("WYR_South",_,_Trigger)
AND
DB_CheckPoint(_,_,_,_,_,"WYR_South",(FLAG)_PassFlag,_,_)
AND
NOT DB_GlobalFlag(_PassFlag)
AND
NOT DB_DialogName(WYR_South_Primary_f03a1685-b148-b70e-b773-b62404525308,_) // no ongoing checkpoint dialog
THEN
PROC_WYR_Ironhand_SkipScene();

IF
DB_InRegion(_Player,_Trigger)
AND
DB_CheckPointPassTrigger("WYR_South",_,_Trigger)
AND
DB_CheckPoint(_,_,_,_,_,"WYR_South",(FLAG)_PassFlag,_,_)
AND
NOT DB_GlobalFlag(_PassFlag)
THEN
SetFlag(_PassFlag);

PROC
PROC_GLO_DefeatCounter_AllPermaDefeated("WYR_South_Guards")
THEN
PROC_RemoveDBTrespassTrigger(S_WYR_South_TrespassTrigger_eefe8bf7-73da-4954-abd2-38cbc1c4b9cd,S_WYR_South_CheckpointOut_9443e8cb-c32a-4b1b-bf04-5e9506dea31b);

IF
FlagSet((FLAG)WYR_South_Event_AgreedCustody_a85fefb5-6f59-4709-b310-b82a1e13411f,_,_)
THEN
SetFlag(WYR_South_State_AgreedCustody_b160b41d-8e33-4d4c-acc8-9aa862fee1df);

IF
DB_Is_InCombat(_Guard,_CombatID)
AND
DB_WYR_South_Guards((CHARACTER)_Guard)
AND
DB_Is_InCombat(_Player,_CombatID)
AND
DB_Players((CHARACTER)_Player)
THEN
SetFlag(WYR_South_State_Combat_d048dfe1-1545-4698-beba-cf7ed94f5ba4);

IF //Turn guards permanently hostile if one of them perishes.
KilledBy(_Guard, _Player, _, _)
AND
DB_Players((CHARACTER)_Player)
AND
DB_WYR_South_Guards((CHARACTER)_Guard)
AND
QRY_OnlyOnce("WYR_South_Guards_PermaHostile")
AND
GetFaction(_Guard, _Faction)
THEN
PROC_SetRelationToPlayers(_Faction, 0);


//END_REGION States and flags


//REGION Checkpoint dialogs

PROC
PROC_CheckPointDialogue(_Player,S_WYR_South_Guard_004_e572c6d1-3330-46fd-9ecc-da318f499956)
AND
NOT DB_WYR_South_GuardsPanic(1)
AND
QRY_StartCheckpointWarningDialog("WYR_South",WYR_South_Primary_f03a1685-b148-b70e-b773-b62404525308,
	S_WYR_South_Guard_004_e572c6d1-3330-46fd-9ecc-da318f499956,
	_Player)
THEN
DB_NOOP(1);

PROC
PROC_StartDialog_AddExtraSpeakers(WYR_South_Primary_f03a1685-b148-b70e-b773-b62404525308,_DialogID)
AND
NOT DB_GlobalFlag(WYR_South_State_AccessGranted_3339af4f-3214-4d10-aaab-3d3511b9ab74)
THEN
PROC_DialogAddSpeakingActor(_DialogID,S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635,0,1);

PROC
PROC_StartDialog_AddExtraSpeakers(WYR_South_Primary_f03a1685-b148-b70e-b773-b62404525308,_DialogID)
AND
NOT DB_GlobalFlag(WYR_South_State_AccessGranted_3339af4f-3214-4d10-aaab-3d3511b9ab74)
AND
DB_WYR_Ironhand_SceneGnomes(_,_Gnome)
THEN
PROC_DialogAddSpeakingActor(_DialogId,_Gnome,1,1);

QRY
QRY_ShouldNotAddCharacterInTriggerToDialog(_Player,WYR_South_Primary_f03a1685-b148-b70e-b773-b62404525308,S_WYR_South_AddPlayerToDialog_Zone_d9ff0c05-6afa-4d58-88f6-4ab0cbc20f2d)
AND
QRY_CharacterIsHidden(_Player)
THEN
DB_NOOP(1);

PROC
PROC_CheckPointDialogue(_Player,_Guard)
AND
NOT DB_WYR_South_GuardsPanic(1)
AND
_Guard != S_WYR_South_Guard_004_e572c6d1-3330-46fd-9ecc-da318f499956
AND
DB_Dialogs(_Guard,_Dialog)
AND
QRY_StartCheckpointWarningDialog("WYR_South",_Dialog,_Guard,_Player)
THEN
DB_NOOP(1);

PROC
PROC_WYR_KillDirectorGortash_CalmSteelWatch()
THEN
PROC_GlobalSetFlagAndCache(WYR_South_State_AccessGranted_3339af4f-3214-4d10-aaab-3d3511b9ab74);
//END_REGION

//REGION Gnomes attack
IF
FlagSet(WYR_South_Event_DoInspection_a1e5ffe2-ffc4-4a7a-b7f2-1f5df21b7db1,_,_DialogID)
THEN
PROC_CharacterMoveTo(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635,S_WYR_South_SteelWatch_InspectionPoint_9e5932b8-c11e-42b4-b205-d42ff46456bb,"Walk","");

/*
IF
FlagSet(WYR_South_Event_DoInspection_a1e5ffe2-ffc4-4a7a-b7f2-1f5df21b7db1,_,_DialogID)
AND
DB_DialogPlayers(_DialogID,_Player,_)
THEN
PROC_CharacterMoveTo((CHARACTER)_Player,S_WYR_South_Player_InspectionPoint_3be6935e-93c8-49dd-9be7-6256bf94e981,"Walk","");
*/

IF
EntityEvent(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635,"WYR_South_SteelWatch_ReturnToSpot")
THEN
LookFromTrigger(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635,S_WYR_South_SteelWatch_Pos_eecfb94e-02de-4d8e-afd6-e4615542a2b1,0);

IF
FlagSet(WYR_South_State_AccessGranted_3339af4f-3214-4d10-aaab-3d3511b9ab74,_,_)
THEN
PROC_AddCharactersInTriggerToDialog_Clear(WYR_South_Primary_f03a1685-b148-b70e-b773-b62404525308,S_WYR_South_AddPlayerToDialog_Zone_d9ff0c05-6afa-4d58-88f6-4ab0cbc20f2d);
PROC_TriggerUnregisterForPlayers(S_WYR_South_AddPlayerToDialog_Zone_d9ff0c05-6afa-4d58-88f6-4ab0cbc20f2d);

IF
FlagSet(WYR_South_State_AccessGranted_3339af4f-3214-4d10-aaab-3d3511b9ab74,_,_)
AND
HasAppliedStatus(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635,"SUNFLASHED",0)
AND
NOT DB_GlobalFlag(WYR_Ironhand_Event_GnomesIntervent_06ad5471-b210-4ea0-a390-09bc7afdd332)
AND
DB_GLO_SteelWatcher_IgnoresCrime(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635,_CrimeID)
THEN
NOT DB_GLO_SteelWatcher_IgnoresCrime(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635);
NOT DB_GLO_SteelWatcher_IgnoresCrime(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635,_CrimeID);

// TODO:
// 1) make sure no one is in the hideout when they appear
// 2) use Poof or invisibility status so they do not appear out of nowhere
IF
FlagSet((FLAG)WYR_Ironhand_Event_GnomesIntervent_06ad5471-b210-4ea0-a390-09bc7afdd332,_,_)
AND
DB_WYR_Ironhand_SceneGnomes(_Id,_Gnome)
AND
DB_WYR_Ironhand_Pos_BridgeCheckpointDialog(_Id,_Pos)
THEN
RemoveSurfaceLayer(_Pos,"Ground",5.0);
RemoveSurfaceLayer(_Pos,"Cloud",5.0);
TeleportTo(_Gnome,_Pos);
//END_REGION Checkpoint dialogs


//REGION Custody for player

PROC
PROC_FlagReactionAfterDialog(_Player,WYR_South_AgreedCustody_a85fefb5-6f59-4709-b310-b82a1e13411f,_,_DialogID)
AND
DB_Players(_OtherPlayer)
AND
InSamePartyGroup((CHARACTER)_Player,_OtherPlayer,1)
AND
DB_DialogNPCs(_DialogID,_Guard,1)
AND
DB_RegionPrison("BGO_Main_A_WyrmRock",_PrisonCell,_PrisonSpawnPoint)
AND
DB_PlayerPrison(S_WYR_WyrmRockPrison_PlayerCell_6792230e-f319-4870-8778-5f7bfae74489,_,_,_FugitiveStatus)
THEN
DB_WYR_South_TeleportingToPrison(_Player);
PROC_CrimeTeleportCharacterToPrison((CHARACTER)_Guard,_OtherPlayer,_PrisonCell,_PrisonSpawnPoint,_FugitiveStatus);

PROC
PROC_FlagReactionAfterDialog(_Player, WYR_South_Event_AgreedCustody_a85fefb5-6f59-4709-b310-b82a1e13411f,_,_)
THEN
ClearFlag(WYR_South_Event_AgreedCustody_a85fefb5-6f59-4709-b310-b82a1e13411f, _Player);

IF
EntityEvent(_Player,"CRIME_TeleportedToPrison")
AND
DB_WYR_South_TeleportingToPrison((CHARACTER)_Player)
AND
QRY_OnlyOnce("WYR_South_VB_AfterSentToPrison")
THEN
StartVoiceBark(WYR_South_VB_AfterSentToPrison_42cb9caa-7005-bbd0-b9c8-5462c748e406,_Player);

IF
EntityEvent(_Player,"CRIME_TeleportedToPrison")
AND
DB_WYR_South_TeleportingToPrison((CHARACTER)_Player)
THEN
NOT DB_WYR_South_TeleportingToPrison(_Player);

//END_REGION Custody for player


//REGION Resisting arrest

//PROC
//PROC_GlobalFlagReactionAfterDialog(WYR_Ironhand_Event_GnomesIntervent_06ad5471-b210-4ea0-a390-09bc7afdd332,WYR_South_Primary_f03a1685-b148-b70e-b773-b62404525308,_DialogID)
//AND
//DB_DialogPlayers(_DialogID,_Player,1)
//AND
//DB_Players((CHARACTER)_Player)
//THEN
//ForceTurnBasedMode(_Player,1);

PROC
PROC_GlobalFlagReactionAfterDialog(WYR_Ironhand_Event_GnomesIntervent_06ad5471-b210-4ea0-a390-09bc7afdd332,WYR_South_Primary_f03a1685-b148-b70e-b773-b62404525308)
THEN
PROC_WYR_Ironhand_GnomesIntervent(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635);
SetFlag(WYR_South_State_AccessGranted_3339af4f-3214-4d10-aaab-3d3511b9ab74);
SetFlag(WYR_South_State_AccessGrantedThroughCheckpoint_b70fe001-d164-4d4a-a333-d63e1982b4cd);

//END_REGION Resisting arrest


//REGION Guards panic

PROC
PROC_WYR_South_GuardsPanic()
AND
GetStatusTurns(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635,"SUNFLASHED",_Turns)
AND
IntegerProduct(_Turns,6000,_TurnsMillisec)
AND
IntegerSum(_TurnsMillisec,1000,_Duration)
THEN
DB_WYR_South_GuardsPanic(1);
TimerLaunch("WYR_South_PanicFinish",_Duration);

PROC
PROC_WYR_Ironhand_GnomesRun()
AND
DB_WYR_Ironhand_SceneGnomes(1,_Gnome)
THEN
PROC_WYR_South_GuardsPanic();
ObjectTimerLaunch(_Gnome,"WYR_South_GuardsAfterSunflash_RunForGnomes",2000,0);
//TimerLaunch("WYR_South_GuardsAfterSunflash_RunForGnomes",2000);

PROC
PROC_WYR_South_GuardsPanic()
AND
DB_WYR_South_Panic(_Delay,_Guard,_RunTo)
THEN
RealtimeObjectTimerLaunch(_Guard,"WYR_South_PanicDelay",_Delay);

//END_REGION Guards panic


//REGION Guards after sunflash

IF
ObjectTimerFinished(_Gnome,"WYR_South_GuardsAfterSunflash_RunForGnomes")
THEN
PROC_TryStartAD(WYR_South_AD_CommandsAfterSunflash_c242ec92-de77-b9b3-aac6-e71fa560a6e5,S_WYR_South_Guard_004_e572c6d1-3330-46fd-9ecc-da318f499956);

IF
ObjectTimerFinished(_Gnome,"WYR_South_GuardsAfterSunflash_RunForGnomes")
AND
DB_WYR_South_GuardGoingToLook(_Guard,_LookOutPos)
THEN
PROC_CharacterMoveTo(_Guard,_LookOutPos,"Run","WYR_South_GuardRunsToCheck");

IF
EntityEvent(_Guard,"WYR_South_GuardRunsToCheck")
AND
_Guard != S_WYR_South_GoldQuestGiver_ad4a43f4-a86c-4946-a696-43a4ba4e6b45
THEN
PlayAnimation(_Guard,CUST_LookingAround_63bcacef-5631-4ab3-9400-81b23cb18ca7,"WYR_South_GuardLookedAround");

IF
EntityEvent(_Guard,"WYR_South_GuardLookedAround")
AND
DB_WYR_South_GuardPos((CHARACTER)_Guard,_Pos)
THEN
PROC_CharacterMoveTo(_Guard,_Pos,"Walk","");

IF
ObjectTimerFinished(_Guard,"WYR_South_PanicDelay")
AND
DB_WYR_South_Panic(_Delay,(CHARACTER)_Guard,_RunTo)
THEN
PROC_CharacterMoveTo((CHARACTER)_Guard,_RunTo,"Run","");
RealtimeObjectTimerLaunch(_Guard,"WYR_South_GuardReturns",12000);

IF
ObjectTimerFinished(_Guard,"WYR_South_GuardReturns")
AND
NOT DB_WYR_South_GuardGoingToLook((CHARACTER)_Guard,_)
AND
_Guard != S_WYR_South_Guard_004_e572c6d1-3330-46fd-9ecc-da318f499956
AND
DB_WYR_South_GuardPos((CHARACTER)_Guard,_Pos)
THEN
PROC_CharacterMoveTo(_Guard,_Pos,"Walk","");

IF
TimerFinished("WYR_South_PanicFinish")
THEN
NOT DB_WYR_South_GuardsPanic(1);
//PROC_SpotPlayers_RestartSpotting(S_WYR_South_Guard_004_e572c6d1-3330-46fd-9ecc-da318f499956,"WYR_South");

IF
StatusRemoved(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635,"SUNFLASHED",_,_)
AND
DB_WYR_Ironhand_SteelWatchSunflashed(_SteelWatch, _Gnome)
THEN
NOT DB_WYR_Ironhand_SteelWatchSunflashed(_SteelWatch, _Gnome);
PROC_CharacterMoveTo(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635,S_WYR_South_SteelWatch_Broken_Pos_3cdeffea-027f-4151-8714-e9224ff28bd9,"Stroll","WYR_South_SteelWatch_Event_Steering");
PROC_CharacterMoveTo(S_WYR_South_Guard_004_e572c6d1-3330-46fd-9ecc-da318f499956,S_WYR_South_GuardPos_Fixing_004_8c2c333a-1a99-41ab-8f00-92f6b840831e,"Stroll","WYR_South_Guard_Fixing");
ApplyStatus(_SteelWatch, "SUNFLASH_BROKENSCANNER", -1.0, 1, _Gnome);
DB_WYR_South_SteelWatchBrokenScanner((CHARACTER)_SteelWatch);

QRY
QRY_SelectCustomDialog_AfterGenerics((CHARACTER)S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635, _)
AND
IsTagged(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635, (TAG)STEEL_WATCHER_MALFUNCTION_62b8df63-c303-4956-97a9-5c9c97520ebf, 1)
THEN
DB_SelectedDialog((DIALOGRESOURCE)GLO_SteelWatcher_AD_Malfunction_6b4fb4c5-b34f-f10f-d035-9f3a8ec5a0b2, (CHARACTER)S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635);

IF
EntityEvent(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635, "WYR_South_SteelWatch_Event_Steering")
THEN
LookFromTrigger(S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635,S_WYR_South_SteelWatch_Broken_Pos_3cdeffea-027f-4151-8714-e9224ff28bd9, 1);

IF
EntityEvent((CHARACTER)S_WYR_South_Guard_004_e572c6d1-3330-46fd-9ecc-da318f499956,"WYR_South_Guard_Fixing")
THEN
LookAtEntity((CHARACTER)S_WYR_South_Guard_004_e572c6d1-3330-46fd-9ecc-da318f499956,S_WYR_South_SteelWatch_cb51332e-f9b9-4564-bda9-641bb1f03635,3.0);
PlayAnimation(S_WYR_South_Guard_004_e572c6d1-3330-46fd-9ecc-da318f499956,CUST_Pondering_01_c2a68c82-1b0b-4d0c-8f1a-c9f14984a5fb,"");

// Removing multifunctional SW
PROC
PROC_LongRestOrLevelUnloading("BGO_Main_A")
AND
DB_WYR_South_SteelWatchBrokenScanner(_SteelWatch)
AND
NOT DB_PermaDefeated(_SteelWatch)
THEN
NOT DB_WYR_South_SteelWatchBrokenScanner(_SteelWatch);
PROC_SetOnStage(_SteelWatch, 0);

//END_REGION Guards after sunflash


//REGION Civilians disappear in case of combat or after player left

IF
DB_Is_InCombat(_Guard,_CombatID)
AND
DB_WYR_South_Guards((CHARACTER)_Guard)
THEN
PROC_WYR_South_RefugeesDisappear();

PROC
PROC_WYR_Ironhand_GnomesRun()
THEN
PROC_WYR_South_RefugeesDisappear();

PROC
PROC_WYR_South_RefugeesDisappear()
AND
DB_WYR_South_Refugees(_Refugee)
AND
NOT DB_PermaDefeated(_Refugee)
THEN
PROC_DisappearOutOfSightTo(_Refugee,S_WYR_South_CiviliansDisappearTo_Pos_a24e5541-7305-429a-a58f-dd4dd7a016a6,"Run",0,"");

IF
DB_DialogNPCs(_DialogID,_Npc,_)
AND
DB_WYR_South_RefugeesSeenWhenTalkedTo((CHARACTER)_Npc)
AND
DB_DialogName(_Dialog,_DialogID)
AND
DB_Dialogs(_Npc,_Dialog)
THEN
DB_WYR_South_SeenRefugees(1);

IF
DialogStarted(WYR_South_Primary_f03a1685-b148-b70e-b773-b62404525308,_)
THEN
DB_WYR_South_SeenRefugees(1);

// refugees are in one Activation group => one event is enough
IF
Deactivated(_Refugee)
AND
DB_WYR_South_SeenRefugees(1)
AND
DB_WYR_South_Refugees((CHARACTER)_Refugee)
THEN
PROC_WYR_South_RefugeesRemove();

PROC
PROC_WYR_South_RefugeesRemove()
THEN
SetFlag(WYR_South_RefugeesLeft_e3945f51-32da-42f4-ab06-f58e61f55dbc,NULL_00000000-0000-0000-0000-000000000000,0);

PROC
PROC_WYR_South_RefugeesRemove()
AND
QRY_OnlyOnce("PROC_WYR_South_RefugeesRemove")
AND
DB_WYR_South_Refugees(_Refugee)
THEN
SetOnStage(_Refugee,0);

IF
EnteredTrigger(_Player,S_WYR_Fortress_SUB_94ca5a54-0813-48b4-9f55-4cb039a63c82)
AND
DB_Players((CHARACTER)_Player)
AND
IsActive(S_WYR_South_Civilian_000_fac3ed5f-b71b-4285-b05a-1d504c06666e,0)
THEN
PROC_WYR_South_RefugeesRemove();

//END_REGION Civilians disappear in case of combat or after player left

//REGION Peacful resolutions
IF
FlagSet((FLAG)WYR_South_State_AccessGranted_3339af4f-3214-4d10-aaab-3d3511b9ab74,_,_)
AND
NOT DB_GlobalFlag((FLAG)GLO_ScryingEyes_State_PartyActedHostile_d3c6d338-36d5-41e7-97d1-9541e0a9bbcf)
AND
NOT DB_GlobalFlag((FLAG)GLO_SteelWatchers_State_EnemyOfAbsolute_1e72bf66-e983-42e5-bd41-10b03eb9e713)
AND
DB_WYR_South_Guards(_Guard)
THEN
PROC_PeacefulResolve((CHARACTER)_Guard);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act3"
