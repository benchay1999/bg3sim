Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_LOW_BhaalTemple_OrinBattle_RitualHelper((CHARACTER)S_LOW_BhaalTemple_RitualHelper_d3fed835-7758-48a8-9641-af8eae197203);

DB_LOW_BhaalTemple_OrinBattle_RitualVFX_Tears((ITEM)S_LOW_BhaalTemple_RitualTear_000_87fe6084-f6ed-4094-b5aa-3cee6cb356ef);
DB_LOW_BhaalTemple_OrinBattle_RitualVFX_Tears((ITEM)S_LOW_BhaalTemple_RitualTear_001_bc9508f5-5e64-467b-8c14-debf8cb651f8);
DB_LOW_BhaalTemple_OrinBattle_RitualVFX_Tears((ITEM)S_LOW_BhaalTemple_RitualTear_002_70471e49-da76-43c6-82f9-381721b1c374);
DB_LOW_BhaalTemple_OrinBattle_RitualVFX_Tears((ITEM)S_LOW_BhaalTemple_RitualTear_003_aef605d3-1a57-44fd-856c-7d9a43a7d9d2);
DB_LOW_BhaalTemple_OrinBattle_RitualVFX_Tears((ITEM)S_LOW_BhaalTemple_RitualTear_004_d22f4e24-3319-44c1-a08a-b9f79e715ee3);
DB_LOW_BhaalTemple_OrinBattle_RitualVFX_Tears((ITEM)S_LOW_BhaalTemple_RitualTear_005_5dc08baa-cd8a-46e0-a069-7ec616808444);
DB_LOW_BhaalTemple_OrinBattle_RitualVFX_Tears((ITEM)S_LOW_BhaalTemple_RitualTear_006_ac74f7e7-cd3c-4b8b-b485-31d30eadba46);
DB_LOW_BhaalTemple_OrinBattle_RitualVFX_Tears((ITEM)S_LOW_BhaalTemple_RitualTear_007_bcf521c1-0edf-4d50-9e5b-185a461be894);
DB_LOW_BhaalTemple_OrinBattle_RitualVFX_Tears((ITEM)S_LOW_BhaalTemple_RitualTear_008_9a2d8e6f-0070-4abc-ba36-ae3fec287497);
DB_LOW_BhaalTemple_OrinBattle_RitualVFX_Tears((ITEM)S_LOW_BhaalTemple_RitualTear_009_ff5c1ea7-753f-414f-a882-ff7c6d545637);
DB_LOW_BhaalTemple_OrinBattle_RitualVFX_Tears((ITEM)S_LOW_BhaalTemple_RitualTear_010_09705252-0411-4d61-991b-193cf8320a83);
DB_LOW_BhaalTemple_OrinBattle_RitualVFX_Tears((ITEM)S_LOW_BhaalTemple_RitualTear_011_18bfb4d8-968a-41a4-9032-f240b1ca2554);


DB_LOW_BhaalTemple_RitualSpells("Target_LOW_BhaalTemple_OrinBuff","EASY");
DB_LOW_BhaalTemple_RitualSpells("Target_LOW_BhaalTemple_OrinBuff","MEDIUM");
DB_LOW_BhaalTemple_RitualSpells("Target_LOW_BhaalTemple_OrinBuff_Hardcore","HARD");
KBSECTION
//REGION Non-Dark Urge Variant

//REGION Setup

IF
DB_LOW_BhaalTemple_OrinBattle_RitualHelper(_RitualHelper)
THEN
SetOnStage(_RitualHelper,0);
SetCombatGroupID(_RitualHelper,"LOW_BhaalTemple_Ritual");
SetAmbushing(_RitualHelper, 1); //To hide the outline

IF
EnteredCombat(S_GLO_Orin_bf24e0ec-a3a6-4905-bd2d-45dc8edf8101,_)
AND
GetFlag((FLAG)LOW_BhaalTemple_State_OrinInTemple_494df8e1-a986-4744-958f-b33880dd5bb2,NULL_00000000-0000-0000-0000-000000000000, 1)
AND  
GetFlag((FLAG)LOW_BhaalTemple_State_DarkUrgeIsDuel_bcaa9b35-be89-4775-b493-7f8d40338a1b , NULL_00000000-0000-0000-0000-000000000000, 0)
THEN
PROC_LOW_BhaalTemple_OrinBattle_BeginRitual();

PROC
PROC_LOW_BhaalTemple_OrinBattle_BeginRitual()
AND
NOT DB_OnlyOnce("OW_BhaalTemple_OrinBattle_RitualEnded")
AND
DB_LOW_BhaalTemple_OrinBattle_RitualHelper(_RitualHelper)
THEN
DB_LOW_BhaalTemple_OrinBattle_RitualTarget((CHARACTER)S_GLO_Orin_bf24e0ec-a3a6-4905-bd2d-45dc8edf8101);
DB_LOW_BhaalTemple_OrinBattle_RitualChanter((CHARACTER)S_LOW_BhaalTemple_Reaper_01_72d472ba-5e32-4922-ab0b-b5a554c6dcab);
DB_LOW_BhaalTemple_OrinBattle_RitualChanter((CHARACTER)S_LOW_BhaalTemple_Reaper_02_ef16770b-0590-457f-8475-712f2c491be2);
DB_LOW_BhaalTemple_OrinBattle_RitualChanter((CHARACTER)S_LOW_BhaalTemple_Reaper_03_97c90688-1181-4893-a497-141e816a4c4a);
DB_LOW_BhaalTemple_OrinBattle_RitualChanter((CHARACTER)S_LOW_BhaalTemple_Reaper_04_c01e3dc3-4323-4a50-a378-ae6c0b8a062a);
DB_LOW_BhaalTemple_OrinBattle_RitualChanter((CHARACTER)S_LOW_BhaalTemple_Reaper_05_a2bcb302-7a7b-4206-ac93-54b755bcb4d8);
DB_LOW_BhaalTemple_OrinBattle_RitualChanter((CHARACTER)S_LOW_BhaalTemple_Reaper_06_646eaea2-2329-4d75-acc7-feddad951066);

IF
DB_LOW_BhaalTemple_OrinBattle_RitualChanter(_Chanter)
THEN
ApplyStatus(_Chanter,"LOW_BHAALTEMPLE_RITUALCHANNEL",-1.0,1);
SetCanFight(_Chanter,0);

//check done to ensure chanters are still alive/chanting properly
PROC
PROC_LOW_BhaalTemple_OrinBattle_BeginRitual()
THEN
PROC_LOW_BhaalTemple_OrinBattle_Check_RitualOver(); 

PROC
PROC_LOW_BhaalTemple_OrinBattle_BeginRitual()
AND
NOT DB_OnlyOnce("OW_BhaalTemple_OrinBattle_RitualEnded")
AND
DB_LOW_BhaalTemple_OrinBattle_RitualHelper(_RitualHelper)
THEN
SetOnStage(_RitualHelper,1);

//END_REGION

//REGION Ritual Logic

PROC
PROC_LOW_BhaalTemple_OrinBattle_BeginRitual()
AND
NOT DB_OnlyOnce("OW_BhaalTemple_OrinBattle_RitualEnded")
AND
CheckRulesetModifierString(SCRIPTED_COMBAT_MECHANICS_cac2d8bd-c197-4a84-9df1-f86f54ad4521,"HARD",0)
THEN
ApplyStatus(S_GLO_Orin_bf24e0ec-a3a6-4905-bd2d-45dc8edf8101,"DEATHSHEAD_UNSTOPPABLE_7",-1.0,1);

PROC
PROC_LOW_BhaalTemple_OrinBattle_BeginRitual()
AND
NOT DB_OnlyOnce("OW_BhaalTemple_OrinBattle_RitualEnded")
AND
CheckRulesetModifierString(SCRIPTED_COMBAT_MECHANICS_cac2d8bd-c197-4a84-9df1-f86f54ad4521,"HARD",1)
THEN
ApplyStatus(S_GLO_Orin_bf24e0ec-a3a6-4905-bd2d-45dc8edf8101,"DEATHSHEAD_UNSTOPPABLE_12",-1.0,1);

PROC
PROC_LOW_BhaalTemple_OrinBattle_BeginRitual()
AND
NOT DB_OnlyOnce("OW_BhaalTemple_OrinBattle_RitualEnded")
AND
DB_LOW_BhaalTemple_OrinBattle_RitualHelper(_RitualHelper)
AND
DB_LOW_BhaalTemple_OrinBattle_RitualTarget(_Target)
THEN
SetAmbushing(S_LOW_BhaalTemple_RitualHelper_d3fed835-7758-48a8-9641-af8eae197203, 1); //To hide the outline
SetOnStage(_RitualHelper,1);
EnterCombat(_RitualHelper,_Target);
SetCombatGroupID(_Target,"LOW_BhaalTemple_Ritual");

PROC
PROC_LOW_BhaalTemple_OrinBattle_BeginRitual()
AND
NOT DB_OnlyOnce("OW_BhaalTemple_OrinBattle_RitualEnded")
THEN
SetCombatGroupID(S_LOW_BhaalTemple_NightBlade_01_7a82cf11-57a2-4796-8b3a-d3409c0cbe9e,"LOW_BhaalTemple_Ritual");
SetCombatGroupID(S_LOW_BhaalTemple_DeathsHead_02_adf3d591-4442-4543-86fd-9ca5b1cb73d2,"LOW_BhaalTemple_Ritual");

IF
StatusRemoved((CHARACTER)_Chanter,"LOW_BHAALTEMPLE_RITUALCHANNEL",_,_)
AND
DB_LOW_BhaalTemple_OrinBattle_RitualChanter(_Chanter)
THEN
SetCanFight(_Chanter,1);
NOT DB_LOW_BhaalTemple_OrinBattle_RitualChanter(_Chanter);
PROC_LOW_BhaalTemple_OrinBattle_Check_RitualOver();

//Fallback check, should also fix savegame older savegames with the issue of dead chanters before the encounter
PROC
PROC_LOW_BhaalTemple_OrinBattle_Check_RitualOver()
AND
DB_LOW_BhaalTemple_OrinBattle_RitualChanter(_Chanter)
AND
HasAppliedStatus(_Chanter,"LOW_BHAALTEMPLE_RITUALCHANNEL",0)
THEN
NOT DB_LOW_BhaalTemple_OrinBattle_RitualChanter(_Chanter);

PROC
PROC_LOW_BhaalTemple_OrinBattle_Check_RitualOver()
AND
SysCount("DB_LOW_BhaalTemple_OrinBattle_RitualChanter",1,_Count)
AND
_Count < 1
AND
QRY_OnlyOnce("OW_BhaalTemple_OrinBattle_RitualEnded")
THEN
PROC_LOW_BhaalTemple_OrinBattle_RitualEnd();

PROC
PROC_LOW_BhaalTemple_OrinBattle_RitualEnd()
AND
DB_LOW_BhaalTemple_OrinBattle_RitualChanter(_Chanter)
THEN
SetCanFight(_Chanter,1);
RemoveStatus(_Chanter,"LOW_BHAALTEMPLE_RITUALCHANNEL");
EnterCombat(_Chanter,S_GLO_Orin_bf24e0ec-a3a6-4905-bd2d-45dc8edf8101);

PROC
PROC_LOW_BhaalTemple_OrinBattle_RitualEnd()
AND
DB_LOW_BhaalTemple_OrinBattle_RitualHelper(_RitualHelper)
THEN
SetOnStage(_RitualHelper,0);

PROC
PROC_LOW_BhaalTemple_OrinBattle_RitualEnd()
THEN
ApplyStatus(S_GLO_Orin_bf24e0ec-a3a6-4905-bd2d-45dc8edf8101,"LOW_BHAALTEMPLE_ORIN_PHASE2",-1.0,1);

//Ritual Helper
//Find a target every turn for Deathbringer Assault
IF
TurnStarted((CHARACTER)_RitualHelper)
AND
DB_LOW_BhaalTemple_OrinBattle_RitualHelper(_RitualHelper)
AND
DB_LOW_BhaalTemple_OrinBattle_RitualTarget(_)
AND
QRY_OnlyOnce_Reset("LOW_OrinRitual_DeathTarget_TurnEndDelay")
THEN
SetEntityEventReal(_RitualHelper, "GLO_CombatWait", 6.0);
RealtimeObjectTimerLaunch(_RitualHelper,"LOW_OrinRitual_DeathTarget_VFXDelay",600);
RealtimeObjectTimerLaunch(_RitualHelper,"LOW_OrinRitual_DeathTarget_CastDelay",1000);
PROC_LOW_BhaalTemple_ArenaCamera_Start((CHARACTER)S_GLO_Orin_bf24e0ec-a3a6-4905-bd2d-45dc8edf8101);

//REGION Bhaal Effigy VFX during the ritual
IF
ObjectTimerFinished(_RitualHelper,"LOW_OrinRitual_DeathTarget_VFXDelay")
THEN
PROC_LOW_BhaalTemple_RitualHelper_VfxActivate();

PROC
PROC_LOW_BhaalTemple_RitualHelper_VfxActivate()
THEN
PROC_LoopEffect(VFX_Script_LOW_BhaalTemple_Glow_Eyes_01_c64237ad-1677-49be-765c-dfb685a9e499, S_LOW_BhaalTemple_RitualEye_Left_32b174c3-13f1-4f01-bd84-35ae76031ed9, "LOW_BhaalTemple_RitualHelper_VFX", "CTY_Main_A", "", 0.2);
PROC_LoopEffect(VFX_Script_LOW_BhaalTemple_Glow_Eyes_01_c64237ad-1677-49be-765c-dfb685a9e499, S_LOW_BhaalTemple_RitualEye_Right_7b99a163-8391-48d0-a8e3-aed202d94111, "LOW_BhaalTemple_RitualHelper_VFX", "CTY_Main_A", "", 0.2);
TimerLaunch("LOW_BhaalTemple_RitualHelper_VFX_Timer", 5000);

PROC
PROC_LOW_BhaalTemple_RitualHelper_VfxActivate()
AND
DB_LOW_BhaalTemple_OrinBattle_RitualVFX_Tears(_Tear)
THEN
PROC_LoopEffect(VFX_Script_LOW_BhaalTemple_Glow_Tears_01_b0a3369d-3c1f-b685-41c9-6764f8ec6896, _Tear, "LOW_BhaalTemple_RitualHelper_VFX", "CTY_Main_A", "", 0.2);

IF
TimerFinished("LOW_BhaalTemple_RitualHelper_VFX_Timer")
THEN
PROC_StopLoopEffect(S_LOW_BhaalTemple_RitualEye_Left_32b174c3-13f1-4f01-bd84-35ae76031ed9, "LOW_BhaalTemple_RitualHelper_VFX");
PROC_StopLoopEffect(S_LOW_BhaalTemple_RitualEye_Right_7b99a163-8391-48d0-a8e3-aed202d94111, "LOW_BhaalTemple_RitualHelper_VFX");

IF
TimerFinished("LOW_BhaalTemple_RitualHelper_VFX_Timer")
AND
DB_LOW_BhaalTemple_OrinBattle_RitualVFX_Tears(_Tear)
THEN
PROC_StopLoopEffect(_Tear, "LOW_BhaalTemple_RitualHelper_VFX");

IF
TextEvent("LOW_BhaalTemple_RitualHelper_VfxActivate")
THEN
PROC_LOW_BhaalTemple_RitualHelper_VfxActivate();

//END_REGION

IF
ObjectTimerFinished(_RitualHelper,"LOW_OrinRitual_DeathTarget_CastDelay")
THEN
PROC_LOW_BhaalTemple_CastRitualSpell();

PROC
PROC_LOW_BhaalTemple_CastRitualSpell()
AND
DB_LOW_BhaalTemple_OrinBattle_RitualHelper((CHARACTER)_RitualHelper)
AND
DB_LOW_BhaalTemple_OrinBattle_RitualTarget(_Char)
AND
DB_LOW_BhaalTemple_RitualSpells(_Spell,_Difficulty)
AND
CheckRulesetModifierString(SCRIPTED_COMBAT_MECHANICS_cac2d8bd-c197-4a84-9df1-f86f54ad4521,_Difficulty,1)
THEN
UseSpell(_RitualHelper,_Spell,_Char);

IF
CastedSpell((CHARACTER)_RitualHelper,_Spell,_,_,_)
AND
DB_LOW_BhaalTemple_RitualSpells(_Spell,_)
AND
DB_LOW_BhaalTemple_OrinBattle_RitualHelper(_RitualHelper)
THEN
PROC_LOW_OrinRitual_DeathTarget_TriggerTurnEndDelay(_RitualHelper);

PROC
PROC_LOW_OrinRitual_DeathTarget_TriggerTurnEndDelay((CHARACTER)_RitualHelper)
AND
DB_HonorMode_Enabled(1)
THEN
RealtimeObjectTimerLaunch(_RitualHelper,"LOW_OrinRitual_DeathTarget_TurnEndDelay",3000);

PROC
PROC_LOW_OrinRitual_DeathTarget_TriggerTurnEndDelay((CHARACTER)_RitualHelper)
AND
NOT DB_HonorMode_Enabled(1)
THEN
RealtimeObjectTimerLaunch(_RitualHelper,"LOW_OrinRitual_DeathTarget_TurnEndDelay",2000);

IF
ObjectTimerFinished(_RitualHelper,"LOW_OrinRitual_DeathTarget_TurnEndDelay")
AND
HasActiveStatus(S_GLO_Orin_bf24e0ec-a3a6-4905-bd2d-45dc8edf8101,"LEGENDARYACTION_LOW_BLOODSWIPE_ORIN",0)
THEN
EndTurn(_RitualHelper);

IF
CastSpellFailed((CHARACTER)_RitualHelper,_Spell,_,_,_)
AND
DB_LOW_BhaalTemple_OrinBattle_RitualHelper(_RitualHelper)
AND
DB_LOW_BhaalTemple_RitualSpells(_Spell,_)
AND
HasActiveStatus(S_GLO_Orin_bf24e0ec-a3a6-4905-bd2d-45dc8edf8101,"LEGENDARYACTION_LOW_BLOODSWIPE_ORIN",0)
AND
QRY_OnlyOnce("LOW_OrinRitual_DeathTarget_TurnEndDelay")
THEN
EndTurn(_RitualHelper);

//End Ritual if Orin is somehow setoffstage, killed or banished before event ends
IF
DB_PermaDefeatedOrOffstage(S_GLO_Orin_bf24e0ec-a3a6-4905-bd2d-45dc8edf8101)
AND
DB_LOW_BhaalTemple_OrinBattle_RitualTarget(_Target)
THEN
PROC_LOW_BhaalTemple_AchievementHook();
PROC_LOW_BhaalTemple_OrinBattle_RitualEnd();

IF
StatusApplied(_Target,"BANISHED",_,_)
AND
DB_LOW_BhaalTemple_OrinBattle_RitualTarget((CHARACTER)_Target)
THEN
PROC_LOW_BhaalTemple_OrinBattle_RitualEnd();

//Clear DBs
PROC
PROC_LOW_BhaalTemple_OrinBattle_RitualEnd()
AND
DB_LOW_BhaalTemple_OrinBattle_RitualHelper(_RitualHelper)
THEN
NOT DB_LOW_BhaalTemple_OrinBattle_RitualHelper(_RitualHelper);

PROC
PROC_LOW_BhaalTemple_OrinBattle_RitualEnd()
AND
DB_LOW_BhaalTemple_OrinBattle_RitualTarget(_Char)
THEN
NOT DB_LOW_BhaalTemple_OrinBattle_RitualTarget(_Char);

PROC
PROC_LOW_BhaalTemple_OrinBattle_RitualEnd()
AND
DB_LOW_BhaalTemple_OrinBattle_RitualChanter(_Char)
THEN
NOT DB_LOW_BhaalTemple_OrinBattle_RitualChanter(_Char);

//END_REGION

//END_REGION

//REGION Dark Urge Variant

IF
EnteredCombat(S_GLO_Orin_bf24e0ec-a3a6-4905-bd2d-45dc8edf8101,_)
AND
GetFlag((FLAG)LOW_BhaalTemple_State_OrinInTemple_494df8e1-a986-4744-958f-b33880dd5bb2,NULL_00000000-0000-0000-0000-000000000000, 1)
AND
HasActiveStatus(S_GLO_Orin_bf24e0ec-a3a6-4905-bd2d-45dc8edf8101,"LOW_BHAALTEMPLE_ORINSLAYER",0)
AND
QRY_OnlyOnce("LOW_OrinDarkUrgeHumanCombat_Setup_OnlyOnce")
THEN
ApplyStatus(S_GLO_Orin_bf24e0ec-a3a6-4905-bd2d-45dc8edf8101,"LOW_ORIN_DEATHBRINGER_COUNTER_0",-1.0,1);

//END_REGION

//REGION Arena Cam Camera Handling
PROC
PROC_LOW_BhaalTemple_ArenaCamera_Start((CHARACTER)_Target)
AND
DB_Players(_Player)
AND
DB_InRegion(_Player, (TRIGGER)S_LOW_BhaalTemple_AltarArea_DialogTrigger_0a93c977-cdc9-40bf-b433-854084739104)
THEN
SendArenaCameraEvent(_Player, _Target, "LOW_BhaalTemple_Ritual_ArenaCamEvent");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act3b"
