Version 1
SubGoalCombiner SGC_AND
INITSECTION
PROC_Stack_AddLast("LOW_TressymTrade_TressymPhase_Loot", S_LOW_TressymTrade_Item_01_e104ad81-84b8-441d-a913-764dbdf45d05);
PROC_Stack_AddLast("LOW_TressymTrade_TressymPhase_Loot", S_LOW_TressymTrade_Item_02_7941ee85-f204-4414-b22e-51b4c2c776d3);

PROC_Stack_AddLast("LOW_TressymTrade_TressymPhase_Location", S_LOW_TressymTrade_Location_01_a7925195-0a4f-4402-8fb1-9fa559daae61);
PROC_Stack_AddLast("LOW_TressymTrade_TressymPhase_Location", S_LOW_TressymTrade_Location_02_877c8d50-b08d-4109-bb14-3604272de02a);

PROC_Stack_AddLast("LOW_TressymTrade_TressymPhase_Flag", LOW_TressymTrade_State_Tressym_Item1Traded_cd6e0e2e-c37d-4f99-abf6-37870acf4739);
PROC_Stack_AddLast("LOW_TressymTrade_TressymPhase_Flag", LOW_TressymTrade_State_Tressym_Item2Traded_c5e6115c-ecdd-4bf8-815c-78b0ccdffe7e);

PROC_LOW_TressymTrade_Init();
KBSECTION
//REGION Tressym init
PROC
PROC_LOW_TressymTrade_Init()
AND
DB_Stack("LOW_TressymTrade_TressymPhase_Loot", _Item)
THEN
SetOnStage(_Item, 0);

PROC
PROC_LOW_TressymTrade_Init()
AND
NOT DB_WYR_Posthouse_Tressym(_)
THEN
DB_WYR_Posthouse_Tressym((CHARACTER)S_GLO_Gale_Tressym_ada7ee81-829d-48c7-ba73-9c9ec191d236);

// Debug flag set before loading LOW
PROC
PROC_LOW_TressymTrade_Init()
AND
DB_GlobalFlag((FLAG)LOW_TressymTrade_Debug_MoveTressymToLOW_87b6df86-9ca2-fcf5-5f27-80b1d49bd90d)
THEN
ClearFlag((FLAG)LOW_TressymTrade_Debug_MoveTressymToLOW_87b6df86-9ca2-fcf5-5f27-80b1d49bd90d);
PROC_LOW_TressymTrade_DebugSetup();

// Debug flag set after loading LOW
IF
FlagSet((FLAG)LOW_TressymTrade_Debug_MoveTressymToLOW_87b6df86-9ca2-fcf5-5f27-80b1d49bd90d, _, _)
THEN
ClearFlag((FLAG)LOW_TressymTrade_Debug_MoveTressymToLOW_87b6df86-9ca2-fcf5-5f27-80b1d49bd90d);
PROC_LOW_TressymTrade_DebugSetup();
PROC_LOW_TressymTrade_TressymInit();

// Debug set tressym to Tara
PROC
PROC_LOW_TressymTrade_DebugSetup()
AND
DB_WYR_Posthouse_Tressym(_Tressym)
THEN
NOT DB_WYR_Posthouse_Tressym(_Tressym);
DB_WYR_Posthouse_Tressym((CHARACTER)S_GLO_Gale_Tressym_ada7ee81-829d-48c7-ba73-9c9ec191d236);
SetFlag((FLAG)WYR_Posthouse_State_TressymHasGivenReward_939d0367-8bc7-487f-b8ef-15c5c9fd0faf);
DB_LOW_TressymTrade_InitialSetup(1);

IF
LevelLoaded("CTY_Main_A")
AND
DB_LOW_TressymTrade_InitialSetup(1)
THEN
NOT DB_LOW_TressymTrade_InitialSetup(1);
PROC_LOW_TressymTrade_TressymInit();

PROC
PROC_LOW_TressymTrade_TressymInit()
AND
DB_WYR_Posthouse_Tressym(_Tressym)
AND
QRY_LOW_TressymGetsNextLoot((CHARACTER)_Tressym)
AND
QRY_LOW_TressymMoveToNextLocation((CHARACTER)_Tressym)
THEN
SetFlag((FLAG)LOW_TressymTrade_State_Tressym_MovedToLOW_d1b28b14-9a4c-45f6-a6af-2d21ac722cd7);
PROC_RemoveAllDialogEntriesForSpeaker(S_GLO_Gale_Tressym_ada7ee81-829d-48c7-ba73-9c9ec191d236);
DB_Dialogs(S_GLO_Gale_Tressym_ada7ee81-829d-48c7-ba73-9c9ec191d236, (DIALOGRESOURCE)LOW_TressymTrade_GalesTressym_6bbdb0cc-9a74-63c4-ec0b-21d26e65714e);
PROC_ClearOriginMoment((DIALOGRESOURCE)WYR_Posthouse_Tara_OM_Gale_AOM_COM_OOM_999ecebb-967d-172b-0411-3d455cdf27e6);
SetOnStage(_Tressym, 1);
//END_REGION

//REGION Tressym rotates
PROC
PROC_LongRest()
AND
DB_GlobalFlag(LOW_TressymTrade_State_Tressym_ReadyToMoveToNextLocation_ba8501c2-bde5-41e1-80ea-d8c38440dde8)
THEN
DB_ExecuteInLevel("LOW_TressymTrade_MoveToNextLocation","CTY_Main_A");
ClearFlag(LOW_TressymTrade_State_Tressym_ReadyToMoveToNextLocation_ba8501c2-bde5-41e1-80ea-d8c38440dde8);

PROC
PROC_ExecuteInLevel("LOW_TressymTrade_MoveToNextLocation","CTY_Main_A")
THEN
PROC_LOW_TressymTrade_RemoveTradedItems();
PROC_LOW_TressymTrade_NextPhase();

PROC
PROC_LOW_TressymTrade_RemoveTradedItems()
AND
DB_LOW_TressymTrade_TradedItem(_Item)
THEN
SetOnStage(_Item, 0);

PROC
PROC_LOW_TressymTrade_NextPhase()
AND
DB_WYR_Posthouse_Tressym(_Tressym)
AND
QRY_LOW_TressymSetNextPhaseFlag()
AND
QRY_LOW_TressymGetsNextLoot((CHARACTER)_Tressym)
AND
QRY_LOW_TressymMoveToNextLocation((CHARACTER)_Tressym)
THEN
ClearFlag(LOW_TressymTrade_State_Tressym_ReadyToMoveToNextLocation_ba8501c2-bde5-41e1-80ea-d8c38440dde8);

// After last flag is set, Tara goes to camp, if she was previously there.
PROC
PROC_LOW_TressymTrade_NextPhase()
AND
NOT DB_Stack("LOW_TressymTrade_TressymPhase_Flag", _)
AND
NOT QRY_LOW_TressymTrade_ToCamp()
THEN
SetOnStage(S_GLO_Gale_Tressym_ada7ee81-829d-48c7-ba73-9c9ec191d236, 0);

QRY
QRY_LOW_TressymTrade_ToCamp()
AND
DB_Avatars((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
AND
NOT DB_GlobalFlag((FLAG)ORI_Gale_State_TressymJoined_c46cfbb6-2aad-47a9-81a1-bdd28a7919fb)
AND
NOT DB_ORI_Gale_SetupCurseProgression(2) //Debug flow - skipped act 1 entirely.
THEN
PROC_CampNight_GaleTressym_ToCamp(1);

QRY
QRY_LOW_TressymTrade_ToCamp()
AND
DB_Avatars((CHARACTER)S_Player_Gale_ad9af97d-75da-406a-ae13-7071c563f604)
AND
NOT DB_GlobalFlag((FLAG)ORI_Gale_State_TressymJoined_c46cfbb6-2aad-47a9-81a1-bdd28a7919fb)
THEN
PROC_CampNight_GaleTressym_ToCamp(1);

QRY
QRY_LOW_TressymSetNextPhaseFlag()
AND
QRY_Stack_ExtractFirst("LOW_TressymTrade_TressymPhase_Flag")
AND
DB_QRYRTN_Stack_ExtractFirst(_Flag)
THEN
SetFlag((FLAG)_Flag);

QRY
QRY_LOW_TressymMoveToNextLocation((CHARACTER)_Tressym)
AND
QRY_Stack_ExtractFirst("LOW_TressymTrade_TressymPhase_Location")
AND
DB_QRYRTN_Stack_ExtractFirst(_Location)
THEN
TeleportTo(_Tressym, (ITEM)_Location, "LOW_TressymTrade_TressymTeleported");

QRY
QRY_LOW_TressymGetsNextLoot((CHARACTER)_Tressym)
AND
QRY_Stack_ExtractFirst("LOW_TressymTrade_TressymPhase_Loot")
AND
DB_QRYRTN_Stack_ExtractFirst(_Loot)
THEN
DB_LOW_TressymTrade_Tressym_CurrentLoot(_Loot);

IF
EntityEvent(_Tressym, "LOW_TressymTrade_TressymTeleported")
AND
DB_LOW_TressymTrade_Tressym_CurrentLoot(_Loot)
THEN
PROC_ToInventoryAndOnStage((ITEM)_Loot, _Tressym);

IF
DB_LOW_TressymTrade_Tressym_CurrentLoot(_NewValue)
AND
DB_LOW_TressymTrade_Tressym_CurrentLoot(_OldValue)
AND
_NewValue != _OldValue
THEN
NOT DB_LOW_TressymTrade_Tressym_CurrentLoot(_OldValue);

// Tressym will always accept the deal if the player offered anything
QRY
QRY_AlternateEvaluateTradeDeal(_Player,_Tressym,_ValuePlayer,_)
AND
DB_WYR_Posthouse_Tressym(_Tressym)
AND
DB_Players(_Player)
AND
_ValuePlayer > 0
AND
DB_LOW_TressymTrade_Tressym_CurrentLoot(_TressymLoot)
THEN
ExecuteDeal(_Player, 1, 0);

// Tressym will only reject the deal if the player offered nothing
QRY
QRY_AlternateEvaluateTradeDeal(_Player,_Tressym,0,_)
AND
DB_WYR_Posthouse_Tressym(_Tressym)
AND
DB_Players(_Player)
AND
DB_LOW_TressymTrade_Tressym_CurrentLoot(_TressymLoot)
THEN
ExecuteDeal(_Player, 0, -5);

// Tressym traded the item to the player
IF
MovedFromTo((ITEM)_Item, (CHARACTER)_Player, (CHARACTER)_Tressym, 1)
AND
DB_WYR_Posthouse_Tressym(_Tressym)
AND
DB_Players(_Player)
THEN
DB_LOW_TressymTrade_TradedItem(_Item);

IF
MovedFromTo((ITEM)_Item, (CHARACTER)_Tressym, (CHARACTER)_Player, 1)
AND
DB_LOW_TressymTrade_Tressym_CurrentLoot(_Item)
AND
DB_WYR_Posthouse_Tressym(_Tressym)
AND
DB_Players(_Player)
THEN
SetFlag(LOW_TressymTrade_State_Tressym_ReadyToMoveToNextLocation_ba8501c2-bde5-41e1-80ea-d8c38440dde8);
//END_REGION

//REGION Tressym leaves
// Tressym leaves after dialog
IF
DialogEnded((DIALOGRESOURCE)WYR_Posthouse_GalesTressym_6bbdb0cc-9a74-63c4-ec0b-21d26e65714e, _)
AND
DB_GlobalFlag((FLAG)WYR_Tara_Event_ToCamp_976bb6a2-e5ad-50c3-5283-5e818dcbd6e3)
AND
QRY_OnlyOnce("LOW_TressymTrade_TressymLeaves")
THEN
PROC_CampNight_GaleTressym_ToCamp(1);

// Tressym goes to camp after long rest
PROC
PROC_LongRest()
AND
DB_GlobalFlag((FLAG)LOW_TressymTrade_State_Tressym_Item2Traded_c5e6115c-ecdd-4bf8-815c-78b0ccdffe7e)
AND
DB_GlobalFlag((FLAG)ORI_Gale_State_TressymJoined_c46cfbb6-2aad-47a9-81a1-bdd28a7919fb)
AND
QRY_OnlyOnce("LOW_TressymTrade_TressymLeaves")
THEN
PROC_CampNight_GaleTressym_ToCamp(1);

// Tressym leaves after long rest
PROC
PROC_LongRest()
AND
DB_GlobalFlag((FLAG)LOW_TressymTrade_State_Tressym_Item2Traded_c5e6115c-ecdd-4bf8-815c-78b0ccdffe7e)
AND
DB_WYR_Posthouse_Tressym(_Tressym)
AND
QRY_OnlyOnce("LOW_TressymTrade_TressymLeaves")
THEN
PROC_SetOnStage(_Tressym, 0);
//END_REGION

//REGION Debug
IF
TextEvent("LOW_Tressym_Init")
THEN
SetFlag((FLAG)WYR_Posthouse_State_TressymHasGivenReward_939d0367-8bc7-487f-b8ef-15c5c9fd0faf);
PROC_LOW_TressymTrade_TressymInit();

IF
TextEvent("LOW_Tressym_NextPhase")
THEN
PROC_LOW_TressymTrade_NextPhase();
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act3b"
