Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION Karlach's tavern
DB_Dialogs((CHARACTER)S_LOW_Bartender_626fc1ed-c1ff-4dcd-a986-068a12cb7974, (DIALOGRESOURCE)LOW_KarlachsRomance_Bartender_d949094e-237b-de80-784d-8b262b21679f);
DB_Dialogs((CHARACTER)S_LOW_ElfLover_000_b15d3b0d-eddd-4156-9dcc-77775d7c5975, (DIALOGRESOURCE)LOW_ElfLover_000_06c8bef9-f4e5-e5a6-211a-9342a9f4616c);
DB_Dialogs((CHARACTER)S_LOW_ElfLover_001_47437f3d-70c5-4b88-a276-9a498280dc04, (DIALOGRESOURCE)LOW_ElfLover_001_af13c133-4082-d013-eef7-00af06dc0f72);


DB_ItemOwnerShipTriggers("CTY_Main_A", (TRIGGER)S_LOW_KarlachsTavern_RoomArea_09201191-2f92-47f5-812d-27db140b997f, (CHARACTER)S_LOW_Bartender_626fc1ed-c1ff-4dcd-a986-068a12cb7974);
DB_ItemOwnerShipTriggers("CTY_Main_A", (TRIGGER)S_LOW_KarlachsTavern_Bar_Ownership_4db2627d-a9b0-430d-8f91-5f5030a4515f, (CHARACTER)S_LOW_Bartender_626fc1ed-c1ff-4dcd-a986-068a12cb7974);
DB_ItemOwnerShipTriggers("CTY_Main_A", (TRIGGER)S_LOW_KarlachsTavern_KitchenArea_10de6376-a5d1-4bf1-8c97-6af5b396924b, (CHARACTER)S_LOW_Bartender_626fc1ed-c1ff-4dcd-a986-068a12cb7974);
DB_ItemOwnerShipTriggersFallback((TRIGGER)S_LOW_KarlachsTavern_RoomArea_09201191-2f92-47f5-812d-27db140b997f, (CHARACTER)S_LOW_Bartender_RomanceSceneBackup_d3e6a655-b69d-4db7-9b59-dc0a9cdbfc26);
DB_ItemOwnerShipTriggersFallback((TRIGGER)S_LOW_KarlachsTavern_Bar_Ownership_4db2627d-a9b0-430d-8f91-5f5030a4515f, (CHARACTER)S_LOW_Bartender_RomanceSceneBackup_d3e6a655-b69d-4db7-9b59-dc0a9cdbfc26);
DB_ItemOwnerShipTriggersFallback((TRIGGER)S_LOW_KarlachsTavern_KitchenArea_10de6376-a5d1-4bf1-8c97-6af5b396924b, (CHARACTER)S_LOW_Bartender_RomanceSceneBackup_d3e6a655-b69d-4db7-9b59-dc0a9cdbfc26);
//END_REGION Karlach's tavern

//REGION Forging of the heart
DB_CharacterInRegionFlag((CHARACTER)S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79, (TRIGGER)S_LOW_City_SUB_dd83ddd3-80ff-4ff9-8f07-f3ede71b283f, (FLAG)GLO_ForgingOfTheHeart_State_DammonInAct3_d7ce5c6b-95d4-46c5-a96f-6a4ffe445606);
// Setting up Dammon
PROC_LOW_ForgingOfTheHeart_SettingUpDammon();
//END_REGION Forging of the heart

//REGION A Graveyard Hello
DB_LOW_AGraveyardHello_Tombstones((ITEM)S_LOW_KarlachParents_Father_0b5f891e-88b9-4632-8191-5dbe65e4ca64, (DIALOGRESOURCE)LOW_AGraveyardHello_AD_FathersGrave_73059d7d-257c-e45a-2d43-15c23d8fdaa0, "LOW_Karlach_FathersTombstoneReveal");
DB_LOW_AGraveyardHello_Tombstones((ITEM)S_LOW_KarlachParents_Mother_7ad21119-f63d-4518-9fcb-4560296df1a3, (DIALOGRESOURCE)LOW_AGraveyardHello_AD_MothersGrave_3ef9c2a0-5e01-61eb-e173-95a176b2d315, "LOW_Karlach_MothersTombstoneReveal");
PROC_TriggerRegisterForPlayers((TRIGGER)S_LOW_KarlachParentsTombs_888c9cfe-8e8f-43e3-8c89-832d6f27490e);
//END_REGION A Graveyard Hello

//REGION An Old Friend
PROC_DefineSingleOriginMoment((DIALOGRESOURCE)LOW_SteepsTrader_Weapons_eb3d9bd3-7ccd-7f35-8533-8e86f20d4d0d, (TAG)REALLY_KARLACH_1a2f70d6-8ead-4eb5-a824-79ee1971764a, (DIALOGRESOURCE)LOW_AnOldFriend_OM_Karlach_AOM_OOM_52d8f04a-4f11-804f-ccfd-b15d3a4ab00a, (DIALOGRESOURCE)LOW_AnOldFriend_OM_Karlach_COM_df3f060c-9abe-3ba4-d702-e966f23fa868, (DIALOGRESOURCE)LOW_AnOldFriend_OM_Karlach_AOM_OOM_52d8f04a-4f11-804f-ccfd-b15d3a4ab00a);
PROC_TriggerRegisterForPlayers((TRIGGER)S_LOW_AnOldFriend_KarlachSpotArea_0d55e45d-2bf6-4490-9525-55aa9ab75d06);
//END_REGION An Old Friend

//REGION Misc IPRDs
DB_RelationshipDialog_WRD_TriggerInCamp((DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_LearnsGortashWasSlave_ef743672-e022-4451-9fe1-52a47c1eea51);

DB_ORI_Karlach_InPartyOMFlags((FLAG)ORI_Karlach_IPRD_OutOfHell_decd7d2d-dd26-47cb-b8a8-77d70052cfdb, (FLAG)ORI_Karlach_State_LeftHoH_e757bcae-8f55-4f77-9753-4d94d62988aa);
//END_REGION Misc IPRDs
KBSECTION
//REGION Forging of the heart
//Checking if Dammon still alive
PROC
PROC_LOW_ForgingOfTheHeart_SettingUpDammon()
AND
NOT DB_PermaDefeated(S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79)
AND
HasAppliedStatus(S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79, "SCL_SHADOW_CURSE_UNDEAD", 0)
AND
HasAppliedStatus(S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79, "SCL_SHADOW_CURSE_UNDEAD_NEW", 0)
THEN
TeleportTo(S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79, S_LOW_WeaponsmithPos_90b415de-4552-413b-a4f5-8cb8d0e06c2d);
SetOnStage(S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79, 1);
SetVisible(S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79, 1);
PROC_SetCustomTradeTreasure((CHARACTER)S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79, "LOW_Weaponsmith_Trade");
PROC_RemoveAllDialogEntriesForSpeaker(S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79);
DB_Dialogs(S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79, (DIALOGRESOURCE)LOW_Dammon_028c720e-ece2-d8e4-de6c-cd4fa8faad26);
DB_ItemOwnerShipTriggers("CTY_Main_A", (TRIGGER)S_LOW_WeaponsmithShop_7c674999-a7e5-4c92-9a68-92bee2a72fc7, (CHARACTER)S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79);
PROC_DefineSingleOriginMoment((DIALOGRESOURCE)LOW_Dammon_028c720e-ece2-d8e4-de6c-cd4fa8faad26, (TAG)REALLY_KARLACH_1a2f70d6-8ead-4eb5-a824-79ee1971764a, (DIALOGRESOURCE)GLO_ForgingOfTheHeart_OM_Karlach_AOM_OOM_COM_e31f3936-c198-cf82-bd8f-e26efba691f1);

PROC
PROC_LOW_ForgingOfTheHeart_SettingUpDammon()
AND
DB_PermaDefeated(S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79)
THEN
RequestDelete((ITEM)S_LOW_Dammon_InfernalArmsJournal_000_c098174d-01c0-4196-b1e7-c66135cf3944);

// If Karlach got the upgrade in Act 1 or Act 2 unlock the second from the start.
PROC
PROC_LOW_ForgingOfTheHeart_SettingUpDammon()
AND
DB_GlobalFlag((FLAG)GLO_ForgingOfTheHeart_State_KarlachUpgraded_a818e2f5-9e0c-4ab3-8c1e-00765d3b892f)
THEN
SetFlag((FLAG)HAV_ForgingOfTheHeart_State_ReadyForSecondUpgrade_edce0ffd-21d1-7a6c-2b55-a3e749d06fca, NULL_00000000-0000-0000-0000-000000000000);

QRY
QRY_OriginMoment_PreventRelaunchDialog(GLO_ForgingOfTheHeart_OM_Karlach_AOM_OOM_COM_e31f3936-c198-cf82-bd8f-e26efba691f1, S_DEN_Weaponsmith_e2ad06ec-8034-479a-9f69-b86faea6dc79, _Player)
THEN
DB_NOOP(1);

//END_REGION


//REGION Date
PROC
PROC_ORI_KarlachDate_TryReplaceBartender()
AND
DB_PermaDefeated(S_LOW_Bartender_626fc1ed-c1ff-4dcd-a986-068a12cb7974)
THEN
SetOnStage(S_LOW_Bartender_626fc1ed-c1ff-4dcd-a986-068a12cb7974, 0);
RemoveSurfaceLayer(S_LOW_Bartender_Pos_9b09481a-1e11-435a-86a3-6164aa981b79, "Ground", 5.0);
DB_Dialogs(S_LOW_Bartender_RomanceSceneBackup_d3e6a655-b69d-4db7-9b59-dc0a9cdbfc26, (DIALOGRESOURCE)LOW_Bartender_Replace_0012debc-6cee-bc2e-d1e5-71ee2ee8ba6d);
TeleportTo(S_LOW_Bartender_RomanceSceneBackup_d3e6a655-b69d-4db7-9b59-dc0a9cdbfc26, S_LOW_Bartender_Pos_9b09481a-1e11-435a-86a3-6164aa981b79);
//END_REGION

//REGION House of Hope InParty branches
IF
FlagSet((FLAG)LOW_HouseOfHope_State_InRegion_216b82aa-2d9a-47d2-9793-2638226c8f61, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _)
AND
NOT DB_Avatars((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
PROC_RelationshipDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_BackInHell_f326fd18-8c4e-47c3-9742-8fe8b8f11401, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 0);

IF
FlagSet((FLAG)LOW_HouseOfHope_State_InRegion_216b82aa-2d9a-47d2-9793-2638226c8f61, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _)
AND
DB_Avatars((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
QRY_OnlyOnce("LOW_Karlach_AvatarInHouseOfHope")
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)LOW_Karlach_AvatarInHouseOfHope_1aa81775-5458-c54b-d090-7aa5a522e13b, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
DB_NOOP(1);

IF
LeftTrigger((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Trigger)
AND
DB_TriggerEvents_TriggerSet("LOW_HouseOfHope", _Trigger)
AND
NOT DB_Avatars((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
DB_PartOfTheTeam((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
NOT QRY_TriggerEvents_InTriggerSet((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, "LOW_HouseOfHope")
AND
QRY_OnlyOnce("LOW_Karlach_LeavingHouseOfHope")
THEN
PROC_RelationshipDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_OutOfHell_decd7d2d-dd26-47cb-b8a8-77d70052cfdb, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 0);

IF
TextEvent("Karlach_TestAvatarHoH")
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)LOW_Karlach_AvatarInHouseOfHope_1aa81775-5458-c54b-d090-7aa5a522e13b, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
DB_NOOP(1);
//END_REGION House of Hope InParty branches

//REGION A Graveyard Hello
IF
DB_LOW_AGraveyardHello_Tombstones(_Tombstone, _FallbackAD, _)
THEN
DB_Dialogs(_Tombstone, _FallbackAD);
PROC_DefineSingleOriginMoment(_FallbackAD, (TAG)REALLY_KARLACH_1a2f70d6-8ead-4eb5-a824-79ee1971764a, (DIALOGRESOURCE)LOW_AGraveyardHello_OM_Karlach_AOM_OOM_d65d3904-0580-d867-6b47-6a036a193d82, (DIALOGRESOURCE)LOW_AGraveyardHello_OM_Karlach_COM_ef138e4b-07f6-df7f-6263-6b187c8ab18b, (DIALOGRESOURCE)LOW_AGraveyardHello_OM_Karlach_AOM_OOM_d65d3904-0580-d867-6b47-6a036a193d82);

IF
DB_InRegion((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (TRIGGER)S_LOW_KarlachParentsTombs_888c9cfe-8e8f-43e3-8c89-832d6f27490e)
AND
NOT DB_CantTalk(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c) // There are a couple of big fights (kobolds and zombies) nearby, trigger only outside of combat so players don't miss it. 
THEN
PROC_LOWAGraveyardHello_RevealParentsGraves();

PROC
PROC_LOWAGraveyardHello_RevealParentsGraves()
THEN
PROC_TryStartAD((DIALOGRESOURCE)LOW_AGraveyardHello_PAD_SpottedParentsGraves_c257f7fc-e4c3-521a-04b7-2d69e4d40fd8, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c);
PROC_TriggerUnregisterForPlayers((TRIGGER)S_LOW_KarlachParentsTombs_888c9cfe-8e8f-43e3-8c89-832d6f27490e);

PROC
PROC_LOWAGraveyardHello_RevealParentsGraves()
AND
DB_LOW_AGraveyardHello_Tombstones(_Tombstone, _, _ID)
THEN
SetCanInteract(_Tombstone, 1);
PROC_PlayPerceptionRevealEffect(_Tombstone, _ID);

IF
DialogEnded((DIALOGRESOURCE)LOW_AGraveyardHello_OM_Karlach_AOM_OOM_d65d3904-0580-d867-6b47-6a036a193d82, _DialogID)
AND
NOT DB_DialogRequestFailed((DIALOGRESOURCE)LOW_AGraveyardHello_OM_Karlach_AOM_OOM_d65d3904-0580-d867-6b47-6a036a193d82, _DialogID)
THEN
SetFlag((FLAG)LOW_Karlach_State_VisitedParentsGrave_010b0502-962f-4927-aecb-6b539c528afb, NULL_00000000-0000-0000-0000-000000000000);

IF
DialogEnded((DIALOGRESOURCE)LOW_AGraveyardHello_OM_Karlach_COM_ef138e4b-07f6-df7f-6263-6b187c8ab18b, _DialogID)
AND
NOT DB_DialogRequestFailed((DIALOGRESOURCE)LOW_AGraveyardHello_OM_Karlach_COM_ef138e4b-07f6-df7f-6263-6b187c8ab18b, _DialogID)
THEN
SetFlag((FLAG)LOW_Karlach_State_VisitedParentsGrave_010b0502-962f-4927-aecb-6b539c528afb, NULL_00000000-0000-0000-0000-000000000000);

// Attacking Karlach parents' graves will drop her approval towards the attack owner.
IF
AttackedBy((ITEM)_Tombstone, _Player, _Summon, _, _, _, _)
AND
DB_LOW_AGraveyardHello_Tombstones(_Tombstone, _, _)
AND
DB_Players((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
NOT DB_Avatars((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
QRY_GetCharacterOwnerIfItemSummon(_Player, _Summon)
AND
DB_QRYRTN_GetCharacterOwnerIfItemSummon(_Attacker)
AND
NOT DB_Is_InCombat(_Attacker, _) // Avoid during combat, accidental attacks might happen
AND
DB_Avatars(_Attacker)
AND
DB_Sees((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Attacker)
AND
ChangeApprovalRating((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, _Attacker, 1, -10, _)
THEN
DB_NOOP(1);
//END_REGION A Graveyard Hello

//REGION An Old Friend
IF
DB_Sees((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (CHARACTER)S_LOW_SteepsTrader_Weapons_3c4ebba4-d2ba-4f07-98ed-bc523845ce3b)
AND
DB_InRegion((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (TRIGGER)S_LOW_AnOldFriend_KarlachSpotArea_0d55e45d-2bf6-4490-9525-55aa9ab75d06)
AND
NOT DB_CantTalk(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
NOT DB_Defeated(S_LOW_SteepsTrader_Weapons_3c4ebba4-d2ba-4f07-98ed-bc523845ce3b)
AND
QRY_OnlyOnce("LOW_AnOldFriend_KarlachSpotsFytz")
THEN
PROC_TryStartAD((DIALOGRESOURCE)LOW_AnOldFriend_PAD_KarlachSpotsFrytz_d4d52eef-91a3-3225-902b-ac73b49ebefb, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c);
PROC_TriggerUnregisterForPlayers((TRIGGER)S_LOW_AnOldFriend_KarlachSpotArea_0d55e45d-2bf6-4490-9525-55aa9ab75d06);

QRY
QRY_OriginMoment_PreventRelaunchDialog((DIALOGRESOURCE)LOW_AnOldFriend_OM_Karlach_COM_df3f060c-9abe-3ba4-d702-e966f23fa868, (CHARACTER)S_LOW_SteepsTrader_Weapons_3c4ebba4-d2ba-4f07-98ed-bc523845ce3b, _Player)
THEN
DB_NOOP(1);

QRY
QRY_OriginMoment_PreventRelaunchDialog((DIALOGRESOURCE)LOW_AnOldFriend_OM_Karlach_AOM_OOM_52d8f04a-4f11-804f-ccfd-b15d3a4ab00a, (CHARACTER)S_LOW_SteepsTrader_Weapons_3c4ebba4-d2ba-4f07-98ed-bc523845ce3b, _Player)
THEN
DB_NOOP(1);

QRY
QRY_OM_BlockDisablingTrade((DIALOGRESOURCE)LOW_AnOldFriend_OM_Karlach_AOM_OOM_52d8f04a-4f11-804f-ccfd-b15d3a4ab00a, _, _)
THEN
DB_NOOP(1);

QRY
QRY_OM_BlockDisablingTrade((DIALOGRESOURCE)LOW_AnOldFriend_OM_Karlach_COM_df3f060c-9abe-3ba4-d702-e966f23fa868, _, _)
THEN
DB_NOOP(1);

IF
DialogEnded((DIALOGRESOURCE)LOW_AnOldFriend_OM_Karlach_AOM_OOM_52d8f04a-4f11-804f-ccfd-b15d3a4ab00a, _DialogID)
AND
NOT DB_DialogRequestFailed((DIALOGRESOURCE)LOW_AnOldFriend_OM_Karlach_AOM_OOM_52d8f04a-4f11-804f-ccfd-b15d3a4ab00a, _DialogID)
THEN
PROC_RelationshipDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_MetFytz_1d51e4a5-3f57-b3f5-78c5-c508c43204c7, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 0);

IF
DialogEnded((DIALOGRESOURCE)LOW_AnOldFriend_OM_Karlach_COM_df3f060c-9abe-3ba4-d702-e966f23fa868, _DialogID)
AND
NOT DB_DialogRequestFailed((DIALOGRESOURCE)LOW_AnOldFriend_OM_Karlach_COM_df3f060c-9abe-3ba4-d702-e966f23fa868, _DialogID)
THEN
PROC_RelationshipDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_MetFytz_1d51e4a5-3f57-b3f5-78c5-c508c43204c7, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 0);
//END_REGION An Old Friend

//REGION Misc IPRDs
IF
DB_Sees((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (CHARACTER)S_Player_Minsc_0de603c5-42e2-4811-9dad-f652de080eba)
AND
DB_GlobalFlag((FLAG)ORI_Minsc_HasMet_70ddd64b-6f91-1d6c-b3e6-8ecddc30ab04)
AND
NOT DB_PartOfTheTeam((CHARACTER)S_Player_Minsc_0de603c5-42e2-4811-9dad-f652de080eba)
AND
NOT DB_OnlyOnce("ORI_Karlach_SpotsMinsc")
THEN
DB_OnlyOnce("ORI_Karlach_SpotsMinsc");
PROC_RelationshipDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_MetMinsc_ecec9b80-1625-797d-2498-6815bd390490, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 0);

// Meeting hope
IF
DialogEnded((DIALOGRESOURCE)LOW_HouseOfHope_HopeSpirit01_32a22a1a-6052-0556-509b-a30105e5f4dc, _DialogID)
AND
NOT DB_DialogRequestFailed((DIALOGRESOURCE)LOW_HouseOfHope_HopeSpirit01_32a22a1a-6052-0556-509b-a30105e5f4dc, _DialogID)
AND
DB_Players((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
QRY_SpeakerIsAvailableAndInDialogRange(S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, S_LOW_HouseOfHope_HopeSpirit01Trigger_ce6d730f-eb77-49ad-9f1c-db859990b036)
AND
QRY_OnlyOnce("ORI_Karlach_SawHope")
THEN
PROC_TryStartAD((DIALOGRESOURCE)LOW_HouseOfHope_PAD_KarlachReactsToHope_c4562399-29d0-b014-4047-74602a570501, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c);

//Karlach learns about Gortash backstory IPRD
IF
FlagSet((FLAG)LOW_HouseOfHope_Knows_GortashWasSlave_54d9e707-e5b9-e801-6d4a-9d4141275473, _, _)
THEN
PROC_RelationshipDialog((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, (DIALOGRESOURCE)Karlach_InParty_12459660-b66e-9b0b-9963-670e0993543d, (FLAG)ORI_Karlach_IPRD_LearnsGortashWasSlave_ef743672-e022-4451-9fe1-52a47c1eea51, S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c, 0);
//END_REGION Misc IPRDs

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act3b"
