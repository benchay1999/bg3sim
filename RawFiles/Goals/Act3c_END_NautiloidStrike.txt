Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_END_NautiloidStrike_Helpers(_TargetHelper)
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_001_d9f6374e-f2ec-42e1-8be8-59f24e9c0091);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_002_db1d24d4-1e92-4b33-ac86-32ef586bed5f);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_003_c2fe1566-ba04-4f7e-8d98-298df5e2e765);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_004_65299ba6-5221-4d72-8a0a-09cbd3bdcac0);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_005_1d2ce6a9-936f-4824-91ec-372f845227e8);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_006_97e5f46b-afd3-4d4f-a9b9-4edad2a30bb5);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_007_62280193-5e9a-40f0-bae5-b7ffcbe50242);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_008_c228aee0-0c9b-4df9-b6c9-8178ffbcc7e9);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_009_44f10724-2d92-4a32-9598-79264b9cee1a);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_010_24be470c-0c59-4063-81a3-6602e73528fe);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_011_5469a61f-6408-48c0-b162-cb7a6db5970d);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_012_1bf6855f-3510-4d92-a34e-90702a43eddd);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_013_a6996023-b1e4-4610-8faf-6d2f312b2641);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_014_3abfb659-dd78-4c2c-99bf-60d31874c988);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_015_bb25f420-f264-4d4e-8da3-17e518ba681a);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_017_ab7a3a9a-3a28-4344-809d-e773f68e6498);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_018_5ec4d250-03ed-4247-a4f4-97369f88c7fe);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_019_645a2250-75ef-4c88-aa18-3b9e2d7f7894);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_016_e7b45e22-93da-4858-bad4-d2cb9622a9fe);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_021_0131a23e-6331-4e74-9e43-34cceec48ec5);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_022_1e1b5011-f234-4ba0-ba10-3b1db036a879);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_023_369030f6-b062-4118-bd0e-4843f08cc8d7);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_024_c40f6321-c6c3-4e09-995a-06e9bc31c049);
DB_END_NautiloidStrike_Helpers(S_END_NautiloidStrikeTargetHelper_020_c6b7f0a5-7c52-4e04-962b-fa7acca56b6e);

//DB_END_NautiloidStrike_DropPodTypes(_PodType, _TimerID, _Spell)
DB_END_NautiloidStrike_DropPodTypes(0, "END_NautiloidStrike_IntDevPod", "ProjectileStrike_END_HighHallInterior_IntDevPodStrike");
DB_END_NautiloidStrike_DropPodTypes(1, "END_NautiloidStrike_MindFlayerPod", "ProjectileStrike_END_HighHallInterior_MindFlayerPodStrike");

//To allow enough time for the camera to jump to each hit
DB_END_NautiloidStrike_CastSpacingTime(200);
DB_END_NautiloidStrike_TotalBufferTime(0);
DB_END_NautiloidStrike_EntityHitTime(0);

KBSECTION
//REGION DEBUG
IF
TextEvent("end_strikeclear")
THEN
PROC_END_NautiloidStrike_ClearAll();

IF
TextEvent("end_strikefire")
AND
NOT DB_END_NautiloidStrike_PrimedStrikeHelpers(_)
AND
GetHostCharacter(_Player)
THEN
DebugText(_Player, "No targets primed!");

//END_REGION

//REGION Set Strike Target Position
PROC
PROC_END_NautiloidStrike_SetTarget((REAL)_, (REAL)_)
THEN
PROC_END_NautiloidStrike_ResetSelectedHelpers();

PROC
PROC_END_NautiloidStrike_SetTarget((REAL)_, (REAL)_, (INTEGER)_)
THEN
PROC_END_NautiloidStrike_ResetSelectedHelpers();

PROC
PROC_END_NautiloidStrike_ResetSelectedHelpers()
AND
DB_OnlyOnce("END_NautiloidStrike_ResetOldestHelper")
THEN
NOT DB_OnlyOnce("END_NautiloidStrike_ResetOldestHelper");

PROC
PROC_END_NautiloidStrike_ResetSelectedHelpers()
AND
DB_OnlyOnce("END_NautiloidStrike_SelectedHelper")
THEN
NOT DB_OnlyOnce("END_NautiloidStrike_SelectedHelper");

PROC
PROC_END_NautiloidStrike_SetTarget((REAL)_X, (REAL)_Z)
THEN
PROC_END_NautiloidStrike_SelectPlaceAndReadyHelper(_X, _Z, 0);

PROC
PROC_END_NautiloidStrike_SetTarget((REAL)_X, (REAL)_Z, (INTEGER)_IsDropPod)
THEN
PROC_END_NautiloidStrike_SelectPlaceAndReadyHelper((REAL)_X, (REAL)_Z, _IsDropPod);

PROC
PROC_END_NautiloidStrike_SelectPlaceAndReadyHelper((REAL)_, (REAL)_, 0)
AND
NOT QRY_END_NautiloidStrike_HelperIsAvailable()
AND
DB_END_NautiloidStrike_PrimedStrikeHelpers(_TargetHelper)
AND
QRY_OnlyOnce("END_NautiloidStrike_ResetOldestHelper")
THEN
PROC_END_NautiloidStrike_Clear(_TargetHelper);

PROC
PROC_END_NautiloidStrike_SelectPlaceAndReadyHelper((REAL)_, (REAL)_, 1)
AND
NOT QRY_END_NautiloidStrike_HelperIsAvailable()
AND
DB_END_NautiloidStrike_PrimedDropPodHelpers(_TargetHelper)
AND
QRY_OnlyOnce("END_NautiloidStrike_ResetOldestHelper")
THEN
PROC_END_NautiloidStrike_Clear(_TargetHelper);

QRY
QRY_END_NautiloidStrike_HelperIsAvailable()
AND
DB_END_NautiloidStrike_Helpers(_TargetHelper)
AND
NOT DB_END_NautiloidStrike_PrimedStrikeHelpers(_TargetHelper)
AND
NOT DB_END_NautiloidStrike_PrimedDropPodHelpers(_TargetHelper)
THEN
DB_NOOP(1);

PROC
PROC_END_NautiloidStrike_SelectPlaceAndReadyHelper((REAL)_X, (REAL)_Z, (INTEGER)_IsDropPod)
AND
QRY_END_NautiloidStrike_GetAvailableHelpers()
AND
DB_QRY_RTN_END_SelectedHelper(_TargetHelper)
THEN
NOT DB_QRY_RTN_END_SelectedHelper(_TargetHelper);
PROC_END_NautiloidStrike_MoveTargetHelperToPos(_TargetHelper, _X, _Z);
ApplyStatus(_TargetHelper, "END_BOMBARDMENT_AI_AVOIDAREA", -1.0, 1, S_END_NautiloidCannonHelper_5f06accf-0a2e-46bb-b3d2-8219e5748623);
PROC_END_NautiloidStrike_SetStrikeType((GUIDSTRING)_TargetHelper, _IsDropPod);
SetForceUpdate(_TargetHelper, 1);

PROC
PROC_END_NautiloidStrike_SetStrikeType((GUIDSTRING)_TargetHelper, 0)
THEN
DB_END_NautiloidStrike_PrimedStrikeHelpers(_TargetHelper);
ApplyStatus(_TargetHelper, "END_HIGHHALLINTERIOR_NAUTILOIDTARGET_VFX", -1.0, 1, S_END_NautiloidCannonHelper_5f06accf-0a2e-46bb-b3d2-8219e5748623);

PROC
PROC_END_NautiloidStrike_SetStrikeType((GUIDSTRING)_TargetHelper, 1)
THEN
DB_END_NautiloidStrike_PrimedDropPodHelpers(_TargetHelper);
ApplyStatus(_TargetHelper, "END_HIGHHALLINTERIOR_DROPPODTARGET_VFX", -1.0, 1, S_END_NautiloidCannonHelper_5f06accf-0a2e-46bb-b3d2-8219e5748623);

QRY
QRY_END_NautiloidStrike_GetAvailableHelpers()
AND
DB_END_NautiloidStrike_Helpers(_TargetHelper)
AND
NOT DB_END_NautiloidStrike_PrimedStrikeHelpers(_TargetHelper)
AND
NOT DB_END_NautiloidStrike_PrimedDropPodHelpers(_TargetHelper)
AND
NOT DB_OnlyOnce("END_NautiloidStrike_SelectedHelper")
THEN
DB_OnlyOnce("END_NautiloidStrike_SelectedHelper");
DB_QRY_RTN_END_SelectedHelper(_TargetHelper);

PROC
PROC_END_NautiloidStrike_MoveTargetHelperToPos((GUIDSTRING)_TargetHelper,(REAL)_X,(REAL)_Z)
THEN
TeleportToPosition(_TargetHelper, _X, 200.0, _Z, "", 0, 0, 0, 0, 1);

//A current safety measure since ForceUpdate calls are not binary, meaning mutltiple false calls and one true call will still result in false
PROC
PROC_END_NautiloidStrike_SetForceUpdate((GUIDSTRING)_Object, 1)
AND
NOT DB_END_NautiloidStrike_ForceUpdatingObject(_Object)
THEN
DB_END_NautiloidStrike_ForceUpdatingObject(_Object);
SetForceUpdate(_Object, 1);

PROC
PROC_END_NautiloidStrike_SetForceUpdate((GUIDSTRING)_Object, 0)
AND
DB_END_NautiloidStrike_ForceUpdatingObject(_Object)
THEN
NOT DB_END_NautiloidStrike_ForceUpdatingObject(_Object);
SetForceUpdate(_Object, 0);

PROC
PROC_END_NautiloidStrike_ClearForceUpdatedObjects()
AND
DB_END_NautiloidStrike_ForceUpdatingObject(_Object)
THEN
NOT DB_END_NautiloidStrike_ForceUpdatingObject(_Object);
SetForceUpdate(_Object, 0);

//END_REGION

//REGION Fire At All Targets

PROC
PROC_END_NautiloidStrike_FireAll()
AND
DB_END_NautiloidStrike_PrimedStrikeHelpers(_TargetHelper)
AND
Random(1500, _Time) //Stagger the fire
THEN
RealTimeObjectTimerLaunch(_TargetHelper, "END_NautiloidStrike_Fire", _Time);

IF
ObjectTimerFinished(_TargetHelper, "END_NautiloidStrike_Fire")
AND
DB_END_NautiloidStrike_PrimedStrikeHelpers(_TargetHelper)
THEN
PROC_END_NautiloidStrike_FireStrike("ProjectileStrike_END_HighHallInterior_NautiloidStrike", _TargetHelper);
PROC_END_NautiloidStrike_Clear(_TargetHelper);
PROC_END_NautiloidStrike_StikeCasted();

//Fire shots in sequence
PROC
PROC_END_NautiloidStrike_FireSequence()
AND
DB_OnlyOnce("END_NautiloidStrike_FireThenWaitForNext")
THEN
NOT DB_OnlyOnce("END_NautiloidStrike_FireThenWaitForNext");

PROC
PROC_END_NautiloidStrike_FireSequence()
AND
NOT DB_GlobalFlag(END_NautiloidStrike_State_CurrentlyFiring_dc0e8671-348f-45a9-b09f-f7627b7ceee7)
THEN
SetFlag(END_NautiloidStrike_State_CurrentlyFiring_dc0e8671-348f-45a9-b09f-f7627b7ceee7);
PROC_END_NautiloidStrike_FireNextInSequence();

PROC
PROC_END_NautiloidStrike_FireNextInSequence()
AND
DB_END_NautiloidStrike_CastSpacingTime(_CastTime)
AND
DB_END_NautiloidStrike_EntityHitTime(_HitTime)
AND
IntegerSum(_CastTime, _HitTime, _TotalTime)
AND
DB_END_NautiloidStrike_TotalBufferTime(_OldTime)
THEN
NOT DB_END_NautiloidStrike_TotalBufferTime(_OldTime);
DB_END_NautiloidStrike_TotalBufferTime(_TotalTime);
PROC_END_NautiloidStrike_ResetEntityHitTime();

PROC
PROC_END_NautiloidStrike_ResetEntityHitTime()
AND
DB_END_NautiloidStrike_EntityHitTime(_HitTime)
THEN
NOT DB_END_NautiloidStrike_EntityHitTime(_HitTime);
DB_END_NautiloidStrike_EntityHitTime(0);

PROC
PROC_END_NautiloidStrike_FireNextInSequence()
AND
DB_END_NautiloidStrike_PrimedStrikeHelpers(_TargetHelper)
AND
DB_END_NautiloidStrike_TotalBufferTime(_BufferTime)
AND
NOT DB_OnlyOnce("END_NautiloidStrike_FireThenWaitForNext")
THEN
DB_OnlyOnce("END_NautiloidStrike_FireThenWaitForNext");
DB_END_NautiloidStrike_PrimedStrikeQueue(_TargetHelper);
RealTimeObjectTimerLaunch(_TargetHelper, "END_NautiloidStrike_NextFire", _BufferTime);

IF
ObjectTimerFinished(_TargetHelper, "END_NautiloidStrike_NextFire")
AND
DB_END_NautiloidStrike_PrimedStrikeHelpers(_TargetHelper)
AND
DB_END_NautiloidStrike_PrimedStrikeQueue(_TargetHelper)
THEN
PROC_END_NautiloidStrike_FireStrike("ProjectileStrike_END_HighHallInterior_NautiloidStrike", _TargetHelper);
RealTimeObjectTimerLaunch(_TargetHelper, "END_NautiloidStrike_ClearHelper", 1400);
RealTimeObjectTimerLaunch(_TargetHelper, "END_NautiloidStrike_WaitForHit", 1800); //This time adds onto the time between each cast
PROC_END_NautiloidStrike_StikeCasted();

//This is to allow time to see if the camera should wait a bit longer after hitting a player
IF
ObjectTimerFinished(_, "END_NautiloidStrike_WaitForHit")
AND
QRY_END_NautiloidStrike_FireSequenceReachedEnd()
THEN
ClearFlag(END_NautiloidStrike_State_CurrentlyFiring_dc0e8671-348f-45a9-b09f-f7627b7ceee7);
PROC_END_NautiloidStrike_ResetEntityHitTime(); //In the case the time was increased for the last hit, so it does not carry over

IF
ObjectTimerFinished(_, "END_NautiloidStrike_WaitForHit")
AND
NOT QRY_END_NautiloidStrike_FireSequenceReachedEnd()
AND
DB_OnlyOnce("END_NautiloidStrike_FireThenWaitForNext")
THEN
NOT DB_OnlyOnce("END_NautiloidStrike_FireThenWaitForNext");
PROC_END_NautiloidStrike_FireNextInSequence();

//Camera Timing
//The only things casting these spells are Nautiloids
IF
CastedSpell(_, "ProjectileStrike_END_HighHallInterior_NautiloidStrike", _, _, _)
AND
DB_END_NautiloidStrike_StrikeCastID(_ID)
THEN
NOT DB_END_NautiloidStrike_StrikeCastID(_ID);

IF
CastedSpell(_, "ProjectileStrike_END_HighHallInterior_NautiloidStrike", _, _, _ID)
AND
NOT DB_END_NautiloidStrike_StrikeCastID(_ID)
THEN
DB_END_NautiloidStrike_StrikeCastID(_ID);

//If an entity is hit by a spell, the combat camera will focus on that entity as a higher priority than the next cast, so add a bit of time if a entity is hit
IF
AttackedBy(_, S_END_NautiloidCannonHelper_5f06accf-0a2e-46bb-b3d2-8219e5748623, S_END_NautiloidCannonHelper_5f06accf-0a2e-46bb-b3d2-8219e5748623, _, _, _, _ID)
AND
DB_END_NautiloidStrike_StrikeCastID(_ID)
THEN
NOT DB_END_NautiloidStrike_StrikeCastID(_ID);
PROC_END_NautiloidStrike_EntityHitCastBuffer();

QRY
QRY_END_NautiloidStrike_FireSequenceReachedEnd()
AND
NOT DB_END_NautiloidStrike_PrimedStrikeHelpers(_)
THEN
DB_NOOP(1);

PROC
PROC_END_NautiloidStrike_EntityHitCastBuffer()
AND
DB_END_NautiloidStrike_EntityHitTime(_HitTime)
AND
_HitTime == 0
THEN
NOT DB_END_NautiloidStrike_EntityHitTime(_HitTime);
DB_END_NautiloidStrike_EntityHitTime(400);

IF
ObjectTimerFinished(_TargetHelper, "END_NautiloidStrike_ClearHelper")
AND
DB_END_NautiloidStrike_PrimedStrikeQueue(_TargetHelper)
THEN
NOT DB_END_NautiloidStrike_PrimedStrikeQueue(_TargetHelper);
PROC_END_NautiloidStrike_Clear(_TargetHelper);

//Cautionary
IF
FlagCleared(END_NautiloidStrike_State_CurrentlyFiring_dc0e8671-348f-45a9-b09f-f7627b7ceee7, _, _)
AND
DB_END_NautiloidStrike_PrimedStrikeHelpers(_TargetHelper)
THEN
NOT DB_END_NautiloidStrike_PrimedStrikeHelpers(_TargetHelper);

PROC
PROC_END_NautiloidStrike_FireStrike((STRING)_Spell,(GUIDSTRING)_Target)
AND
NOT DB_GlobalFlag(END_General_State_CurrentlyInBrainBattle_0d7205b2-0d55-4540-8737-543253873cd6)
THEN
DB_END_NautiloidStrike_SpellAndTargetAfterTeleport(_Spell, _Target);
TeleportTo(S_END_NautiloidCannonHelper_5f06accf-0a2e-46bb-b3d2-8219e5748623, _Target, "END_HighHallInterior_ArrivedAtFirePosition");

IF
EntityEvent(S_END_NautiloidCannonHelper_5f06accf-0a2e-46bb-b3d2-8219e5748623, "END_HighHallInterior_ArrivedAtFirePosition")
AND
DB_END_NautiloidStrike_SpellAndTargetAfterTeleport(_Spell, _Target)
THEN
NOT DB_END_NautiloidStrike_SpellAndTargetAfterTeleport(_Spell, _Target);
UseSpell(S_END_NautiloidCannonHelper_5f06accf-0a2e-46bb-b3d2-8219e5748623, _Spell, _Target);

PROC
PROC_END_NautiloidStrike_FireStrike((STRING)_Spell,(GUIDSTRING)_Target)
AND
DB_GlobalFlag(END_General_State_CurrentlyInBrainBattle_0d7205b2-0d55-4540-8737-543253873cd6)
THEN
CreateProjectileStrikeAt(_Target, _Spell);

PROC
PROC_END_NautiloidStrike_StikeCasted()
THEN
DB_NOOP(1);

//Intellect Devourers = 0
//Mind Flayer = 1
PROC
PROC_END_NautiloidStrike_FireDropPods((INTEGER)_PodType)
AND
DB_END_NautiloidStrike_PrimedDropPodHelpers(_TargetHelper)
AND
DB_END_NautiloidStrike_DropPodTypes(_PodType, _TimerID, _Spell)
AND
Random(1500, _Time) 
THEN
RealTimeObjectTimerLaunch(_TargetHelper, _TimerID, _Time);

IF
ObjectTimerFinished(_TargetHelper, _TimerID)
AND
DB_END_NautiloidStrike_PrimedDropPodHelpers(_TargetHelper)
AND
DB_END_NautiloidStrike_DropPodTypes(_, _TimerID, _Spell)
THEN
PROC_END_NautiloidStrike_FireStrike(_Spell, _TargetHelper);
PROC_END_NautiloidStrike_Clear(_TargetHelper);

IF
StatusApplied(_IntDev, "END_NAUTILOID_POD_INTELLECT_DEVOURER", _, _)
THEN
PROC_END_NautiloidStrike_IntDevPodCharSet((CHARACTER)_IntDev);
PROC_END_NautiloidStrike_DropPodCharSet((CHARACTER)_IntDev);

PROC
PROC_END_NautiloidStrike_IntDevPodCharSet((CHARACTER)_IntDev)
THEN
DB_NOOP(1);

IF
StatusApplied(_IntDev, "END_NAUTILOID_POD_INTELLECT_DEVOURER_DETONATOR", _, _)
THEN
PROC_END_NautiloidStrike_IntDevPodDetonatorCharSet((CHARACTER)_IntDev);
PROC_END_NautiloidStrike_DropPodCharSet((CHARACTER)_IntDev);

PROC
PROC_END_NautiloidStrike_IntDevPodDetonatorCharSet((CHARACTER)_IntDev)
THEN
DB_NOOP(1);

IF
StatusApplied(_MindFlayer, "END_NAUTILOID_POD_MIND_FLAYER", _, _)
THEN
PROC_END_NautiloidStrike_MindFlayerPodCharSet((CHARACTER)_MindFlayer);
PROC_END_NautiloidStrike_DropPodCharSet((CHARACTER)_MindFlayer);

PROC
PROC_END_NautiloidStrike_MindFlayerPodCharSet((CHARACTER)_MindFlayer)
THEN
PlayAnimation(_MindFlayer, UTIL_Spawn_01_510ff37e-0618-40e4-8ce0-236e3dc9c291, "");

//For shared behaviour
PROC
PROC_END_NautiloidStrike_DropPodCharSet((CHARACTER)_Char)
THEN
DB_NOOP(1);

IF
CastedSpell(_, _Spell, _, _, _)
AND
DB_END_NautiloidStrike_DropPodTypes(_, _, _Spell)
THEN
PROC_END_NautiloidStrike_DropPodCasted();

PROC
PROC_END_NautiloidStrike_DropPodCasted()
THEN
DB_NOOP(1);

//END_REGION


//REGION Clean Up
PROC
PROC_END_NautiloidStrike_ClearAll()
AND
DB_END_NautiloidStrike_PrimedStrikeHelpers(_TargetHelper)
THEN
NOT DB_END_NautiloidStrike_PrimedStrikeHelpers(_TargetHelper);

PROC
PROC_END_NautiloidStrike_ClearAll()
AND
DB_END_NautiloidStrike_PrimedDropPodHelpers(_TargetHelper)
THEN
NOT DB_END_NautiloidStrike_PrimedDropPodHelpers(_TargetHelper);

PROC
PROC_END_NautiloidStrike_ClearAll()
AND
DB_END_NautiloidStrike_Helpers(_TargetHelper)
THEN
RemoveStatus(_TargetHelper, "END_HIGHHALLINTERIOR_NAUTILOIDTARGET_VFX");
RemoveStatus(_TargetHelper, "END_HIGHHALLINTERIOR_DROPPODTARGET_VFX");
RemoveStatus(_TargetHelper, "END_BOMBARDMENT_AI_AVOIDAREA");
SetForceUpdate(_TargetHelper, 0);

PROC
PROC_END_NautiloidStrike_ClearAll()
AND
DB_GlobalFlag(END_NautiloidStrike_State_CurrentlyFiring_dc0e8671-348f-45a9-b09f-f7627b7ceee7)
THEN
ClearFlag(END_NautiloidStrike_State_CurrentlyFiring_dc0e8671-348f-45a9-b09f-f7627b7ceee7);

PROC
PROC_END_NautiloidStrike_Clear((GUIDSTRING)_TargetHelper)
AND
DB_END_NautiloidStrike_PrimedStrikeHelpers(_TargetHelper)
THEN
NOT DB_END_NautiloidStrike_PrimedStrikeHelpers(_TargetHelper);
RemoveStatus(_TargetHelper, "END_HIGHHALLINTERIOR_NAUTILOIDTARGET_VFX");
RemoveStatus(_TargetHelper, "END_BOMBARDMENT_AI_AVOIDAREA");



PROC
PROC_END_NautiloidStrike_Clear((GUIDSTRING)_TargetHelper)
AND
DB_END_NautiloidStrike_PrimedDropPodHelpers(_TargetHelper)
THEN
NOT DB_END_NautiloidStrike_PrimedDropPodHelpers(_TargetHelper);
RemoveStatus(_TargetHelper, "END_HIGHHALLINTERIOR_DROPPODTARGET_VFX");
RemoveStatus(_TargetHelper, "END_BOMBARDMENT_AI_AVOIDAREA");

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act3c"
