Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Daisy_IdleTeleport("CRE_Main_A", (TRIGGER)S_CAMP_Act1b_DaisyTP_ed390955-547b-40dd-a949-b433de9721be);
DB_Daisy_IdleTeleport("SCL_Main_A", (TRIGGER)S_CAMP_Act2_DaisyTP_54c96387-2824-4686-8e93-bb374edcade0);
DB_Daisy_IdleTeleport("INT_Main_A", (TRIGGER)S_INT_DaisyIdlePos_746aa4c2-de9d-422a-ba3b-e3cdc43cbab2);
DB_Daisy_IdleTeleport("BGO_Main_A", (TRIGGER)S_BGO_DaisyIdlePos_1803ae68-8166-4abf-9208-4a35b694ae5c);
DB_Daisy_IdleTeleport("CTY_Main_A", (TRIGGER)S_CTY_DaisyIdlePos_d75387e1-6f61-4c27-98ae-8d93e0694782);
DB_Daisy_IdleTeleport("IRN_Main_A", (TRIGGER)S_IRN_DaisyIdlePos_b7c3216c-340b-494c-8c2c-3aa54e4ff4fb);
DB_Daisy_IdleTeleport("END_Main", (TRIGGER)S_END_DaisyIdlePos_bc62b5cf-f4cb-458f-9e10-fa7ebcff8093);
DB_Daisy_IdleTeleport("EPI_Main_A", (TRIGGER)S_EPI_DaisyIdlePos_3ba06d3a-2777-4930-bbcc-601965414fc3);

DB_GLO_Daisy_TrackTags((TAG)DAISY_DESIRETRACK_8f815f65-6fe5-4671-8e2b-63575e1f2e88);
DB_GLO_Daisy_TrackTags((TAG)DAISY_REASONTRACK_6ea65c6d-3172-4478-9530-b74cbdcbb870);
DB_GLO_Daisy_TrackTags((TAG)DAISY_FEARTRACK_c1400eed-4e63-4cb6-8ae1-f616505fda1a);

DB_GLO_DaisyDreams_Emperors((CHARACTER)S_GLO_Emperor_Dream1_16d9b440-d51c-469d-9739-2f4be068bdf1);
DB_GLO_DaisyDreams_Emperors((CHARACTER)S_GLO_Emperor_Dream2_780fc58b-e9ec-42d7-9551-7e19f3b8902b);
DB_GLO_DaisyDreams_Emperors((CHARACTER)S_GLO_Emperor_Dream3_271bfbb2-52a3-4f54-a766-5178057ed167);
DB_GLO_DaisyDreams_Emperors((CHARACTER)S_GLO_Emperor_Dream4_bc39b001-3813-44d8-b5fc-47130b78b5ab);

DB_GLO_EmperorBackups((CHARACTER)S_GLO_Emperor_Backup1_4cc11aec-c50f-41c9-9ded-39c963133404);
DB_GLO_EmperorBackups((CHARACTER)S_GLO_Emperor_Backup2_8fb6ea5e-2a3b-49dd-91a3-1676eb94d0a3);
DB_GLO_EmperorBackups((CHARACTER)S_GLO_Emperor_Backup3_154a092b-4bc4-40ef-a5ff-9ac15dae69a4);

DB_CampNight((FLAG)NIGHT_DaisyAcknowledgement_4965d83f-4b23-4ddf-8dfb-b7df21b2b8f6,7030);
DB_CampNight_Camp((FLAG)NIGHT_DaisyAcknowledgement_4965d83f-4b23-4ddf-8dfb-b7df21b2b8f6,"ELFSONG");
DB_CampNight_Camp((FLAG)NIGHT_DaisyAcknowledgement_4965d83f-4b23-4ddf-8dfb-b7df21b2b8f6,"FARM");
DB_CampNight_Camp((FLAG)NIGHT_DaisyAcknowledgement_4965d83f-4b23-4ddf-8dfb-b7df21b2b8f6,"SLUMS");
DB_CampNight_Requirement((FLAG)NIGHT_DaisyAcknowledgement_4965d83f-4b23-4ddf-8dfb-b7df21b2b8f6,(FLAG)GLO_Daisy_State_TriggerDaisyAcknowledgement_502407ba-c92f-47fa-b3fc-c00d1fecbf9d);
DB_CampNight_AvatarDream((FLAG)NIGHT_DaisyAcknowledgement_4965d83f-4b23-4ddf-8dfb-b7df21b2b8f6,(DIALOGRESOURCE)CAMP_DaisyAcknowledgement_AvD_ROM_58e20a32-e432-1c93-9c75-97bebafc8b1b);
DB_GLO_DaisyAvatarDialog((DIALOGRESOURCE)CAMP_DaisyAcknowledgement_AvD_ROM_58e20a32-e432-1c93-9c75-97bebafc8b1b);
DB_GLO_DaisyDream_Vanity((DIALOGRESOURCE)CAMP_DaisyAcknowledgement_AvD_ROM_58e20a32-e432-1c93-9c75-97bebafc8b1b);

DB_CampNight((FLAG)NIGHT_DaisyCourseCorrection_6c56a05e-ad97-4100-9969-e10cd35c8860,7020);
DB_CampNight_Camp((FLAG)NIGHT_DaisyCourseCorrection_6c56a05e-ad97-4100-9969-e10cd35c8860,"SCLMAIN");
DB_CampNight_Camp((FLAG)NIGHT_DaisyCourseCorrection_6c56a05e-ad97-4100-9969-e10cd35c8860,"SHARTEMPLE");
DB_CampNight_Camp((FLAG)NIGHT_DaisyCourseCorrection_6c56a05e-ad97-4100-9969-e10cd35c8860,"MOONRISE");
DB_CampNight_Camp((FLAG)NIGHT_DaisyCourseCorrection_6c56a05e-ad97-4100-9969-e10cd35c8860,"HAVEN");
DB_CampNight_AvatarDream((FLAG)NIGHT_DaisyCourseCorrection_6c56a05e-ad97-4100-9969-e10cd35c8860,(DIALOGRESOURCE)CAMP_DaisyCourseCorrection_AvD_ab34160d-e291-d961-9488-811fa68f2101);
DB_CampNight_CancelledBy((FLAG)NIGHT_DaisyCourseCorrection_6c56a05e-ad97-4100-9969-e10cd35c8860,(FLAG)GLO_Ketheric_State_Dead_668558ed-0052-0777-64f7-a8a197c40cc5);
DB_Camp_DaisyAvatarDreamDialog((DIALOGRESOURCE)CAMP_DaisyCourseCorrection_AvD_ab34160d-e291-d961-9488-811fa68f2101);
DB_GLO_DaisyDream_Vanity((DIALOGRESOURCE)CAMP_DaisyCourseCorrection_AvD_ab34160d-e291-d961-9488-811fa68f2101);

DB_CampNight((FLAG)NIGHT_DaisyStelmane_7d379735-d643-40a3-aabb-7a180f792817,7040);
DB_CampNight_Camp((FLAG)NIGHT_DaisyStelmane_7d379735-d643-40a3-aabb-7a180f792817,"FARM");
DB_CampNight_Camp((FLAG)NIGHT_DaisyStelmane_7d379735-d643-40a3-aabb-7a180f792817,"ELFSONG");
DB_CampNight_Camp((FLAG)NIGHT_DaisyStelmane_7d379735-d643-40a3-aabb-7a180f792817,"SLUMS");
DB_CampNight_Requirement((FLAG)NIGHT_DaisyStelmane_7d379735-d643-40a3-aabb-7a180f792817,(FLAG)GLO_Daisy_State_TriggerDaisyStelmane_0ea9e817-7b69-4ced-97ec-d579740198e3);
DB_CampNight_AvatarDream((FLAG)NIGHT_DaisyStelmane_7d379735-d643-40a3-aabb-7a180f792817,(DIALOGRESOURCE)CAMP_DaisyStelmane_AvD_01ac98d2-b839-61ba-1d4c-f40cde0bc6f4);
DB_Camp_EmperorAvatarDreamDialog((DIALOGRESOURCE)CAMP_DaisyStelmane_AvD_01ac98d2-b839-61ba-1d4c-f40cde0bc6f4);

DB_CampNight((FLAG)NIGHT_DaisyMissionStatement_287c7434-5031-46f2-9b7b-4999a2349081,7090);
DB_CampNight_Camp((FLAG)NIGHT_DaisyMissionStatement_287c7434-5031-46f2-9b7b-4999a2349081,"WLDMAIN");
DB_CampNight_Camp((FLAG)NIGHT_DaisyMissionStatement_287c7434-5031-46f2-9b7b-4999a2349081,"WLDCAVGRN");
DB_CampNight_Camp((FLAG)NIGHT_DaisyMissionStatement_287c7434-5031-46f2-9b7b-4999a2349081,"WLDCAVSND");
DB_CampNight_Camp((FLAG)NIGHT_DaisyMissionStatement_287c7434-5031-46f2-9b7b-4999a2349081,"WLDDUNABB");
DB_CampNight_Camp((FLAG)NIGHT_DaisyMissionStatement_287c7434-5031-46f2-9b7b-4999a2349081,"WLDDUNSHA");
DB_CampNight_Camp((FLAG)NIGHT_DaisyMissionStatement_287c7434-5031-46f2-9b7b-4999a2349081,"WLDUND");
DB_CampNight_Camp((FLAG)NIGHT_DaisyMissionStatement_287c7434-5031-46f2-9b7b-4999a2349081,"WLDBAS");
DB_CampNight_Camp((FLAG)NIGHT_DaisyMissionStatement_287c7434-5031-46f2-9b7b-4999a2349081,"CREMAIN");
DB_CampNight_Camp((FLAG)NIGHT_DaisyMissionStatement_287c7434-5031-46f2-9b7b-4999a2349081,"CREINSIDE");
DB_CampNight_Requirement((FLAG)NIGHT_DaisyMissionStatement_287c7434-5031-46f2-9b7b-4999a2349081,(FLAG)NIGHT_DaisyDream1_52669b07-9584-4c9d-8219-49679811fd4f,(FLAG)GLO_Hag_State_FailedTadpole_155a938a-5fb7-725b-9b00-6d6f44c451ef); //Hag
DB_CampNight_Requirement((FLAG)NIGHT_DaisyMissionStatement_287c7434-5031-46f2-9b7b-4999a2349081,(FLAG)NIGHT_DaisyDream1_52669b07-9584-4c9d-8219-49679811fd4f,(FLAG)GLO_Daisy_State_TriggerDaisyMissionStatement_516981b9-db79-45e6-be54-93f27b6c9def); //Omeluum or Halsin
DB_CampNight_Requirement((FLAG)NIGHT_DaisyMissionStatement_287c7434-5031-46f2-9b7b-4999a2349081,(FLAG)NIGHT_DaisyDream1_52669b07-9584-4c9d-8219-49679811fd4f,(FLAG)NIGHT_GoblinHunt_RaiderCelebration_86fee25f-1069-4f17-89fa-4c5b69f82e0b); //After Goblin Hunt Celebration
DB_CampNight_Requirement((FLAG)NIGHT_DaisyMissionStatement_287c7434-5031-46f2-9b7b-4999a2349081,(FLAG)NIGHT_DaisyDream1_52669b07-9584-4c9d-8219-49679811fd4f,(FLAG)NIGHT_GoblinHunt_TieflingCelebration_1ad8c357-2695-4d5c-b5f9-8b8c07803121); //After Goblin Hunt Celebration
DB_CampNight_Requirement((FLAG)NIGHT_DaisyMissionStatement_287c7434-5031-46f2-9b7b-4999a2349081,(FLAG)NIGHT_DaisyDream1_52669b07-9584-4c9d-8219-49679811fd4f,(FLAG)GLO_Underdark_EverEnteredBefore_dd5cd5b4-2ad5-4dbd-4972-2afaa7538994); //Been to the underdark
DB_CampNight_CancelledBy((FLAG)NIGHT_DaisyMissionStatement_287c7434-5031-46f2-9b7b-4999a2349081, (FLAG)CRE_PartyProgress_EnteredAstralPlane_9eadff68-8e63-49a6-83ea-418816503a8f);
DB_CampNight_AvatarDream((FLAG)NIGHT_DaisyMissionStatement_287c7434-5031-46f2-9b7b-4999a2349081,(DIALOGRESOURCE)CAMP_DaisyMissionStatement_AvD_5a65da2b-7a95-7c52-df19-eba5b4946f72);
DB_Camp_DaisyAvatarDreamDialog((DIALOGRESOURCE)CAMP_DaisyMissionStatement_AvD_5a65da2b-7a95-7c52-df19-eba5b4946f72);

DB_CRE_DaisyGithyankiWarningAreas((TRIGGER)S_CRE_DaisyGithyankiWarningArea_dbd73a45-994b-4f90-9e03-0f4acad566d4);
DB_CRE_DaisyGithyankiWarningAreas((TRIGGER)S_CRE_DaisyGithyankiWarningArea2_0f96ef98-619d-4c49-8034-944ebddf9855);

DB_Dialog_AddAllNearbyPlayersAtStart((DIALOGRESOURCE)WYR_DaisyEmperorBalduran_fe2393a8-08d6-4be5-e669-55562b193275);

KBSECTION
//DB_DaisyAD_Triggered has been migrated to DB_DaisyAD_TriggeredOnProfile
//DB_EmperorAD_Triggered has been migrated to DB_EmperorAD_TriggeredOnProfile
//The old DB was only using UserID, which is not consistant and can change on each avatar every time they join
//To reduce any risks with patching and since UserID is an Integer and ProfileID is a GUID, the content of the old DB has been migrated to the new DB, applying the triggered dialogs to all currently active avatars on patch application.

//REGION Daisy AD
PROC
PROC_DaisyAD((CHARACTER)_Player,(DIALOGRESOURCE)_DaisyAD)
AND
GetReservedUserID(_Player,_UserID)
AND
GetUserProfileID(_UserID, _Profile)
AND
NOT DB_DaisyAD_TriggeredOnProfile(_DaisyAD,_Profile)
THEN
DB_DaisyAD_TriggeredOnProfile(_DaisyAD,_Profile);
PROC_DaisyAD_PlayInternal(_Player,_DaisyAD);

PROC
PROC_DaisyAD_PlayInternal((CHARACTER)_Player,(DIALOGRESOURCE)_DaisyAD)
AND
QRY_GLO_DaisyDreams_GetDaisy(_Player)
AND
DB_QRYRTN_GLO_DaisyDreams_GetDaisy((CHARACTER)_Daisy)
THEN
PROC_TryStartAD(_DaisyAD,_Daisy,_Player);

IF
AutomatedDialogStarted(_DaisyAD,_ID)
AND
DB_DaisyAD_TriggeredOnProfile(_DaisyAD,_)
AND
DB_DialogPlayers(_ID,_Character,1)
THEN
DB_DaisyAD_Played((CHARACTER)_Character,_DaisyAD);

// Remove a Daisy AD if you need to play the same dialogue more than one time
PROC
PROC_RemoveDaisyAD((CHARACTER)_Character, (DIALOGRESOURCE)_DaisyAD)
AND
GetReservedUserID(_Character,_UserID)
AND
GetUserProfileID(_UserID, _Profile)
AND
DB_DaisyAD_TriggeredOnProfile(_DaisyAD,_Profile)
THEN
NOT DB_DaisyAD_TriggeredOnProfile(_DaisyAD,_Profile);
//END_REGION

//REGION Emperor AD
PROC
PROC_EmperorAD((CHARACTER)_Player,(DIALOGRESOURCE)_EmperorAD)
AND
GetReservedUserID(_Player,_UserID)
AND
GetUserProfileID(_UserID, _Profile)
AND
NOT DB_EmperorAD_TriggeredOnProfile(_EmperorAD,_Profile)
AND
NOT QRY_GLO_IsEmperorADPlaying(_Profile)
THEN
DB_EmperorAD_TriggeredOnProfile(_EmperorAD,_Profile);
PROC_EmperorAD_PlayInternal(_Player,_EmperorAD);


//Check if other Emperor AD is playing at the moment.
QRY
QRY_GLO_IsEmperorADPlaying((STRING)_Profile)
AND
DB_DialogName(_EmperorAD, _)
AND
DB_EmperorAD_TriggeredOnProfile(_EmperorAD,_Profile)
THEN
DB_NOOP(1);

PROC
PROC_EmperorAD_PlayInternal((CHARACTER)_Player,(DIALOGRESOURCE)_EmperorAD)
AND
QRY_GLO_DaisyDreams_GetEmperor(_Player)
AND
DB_QRYRTN_GLO_DaisyDreams_GetEmperor((CHARACTER)_Emperor)
THEN
PROC_TryStartAD(_EmperorAD,_Emperor,_Player);

// Remove a Emperor AD if you need to play the same dialogue more than one time
PROC
PROC_ResetEmperorAD((CHARACTER)_Character, (DIALOGRESOURCE)_EmperorAD)
AND
GetReservedUserID(_Character,_UserID)
AND
GetUserProfileID(_UserID, _Profile)
AND
DB_EmperorAD_TriggeredOnProfile(_EmperorAD,_Profile)
THEN
NOT DB_EmperorAD_TriggeredOnProfile(_EmperorAD,_Profile);
//END_REGION

//REGION Test Daisy ADs
IF
TextEvent("DaisyAD")
AND
GetHostCharacter(_Player)
THEN
PROC_DaisyAD(_Player,(DIALOGRESOURCE)CRE_DaisyGithyankiWarning_856f6467-5afc-7561-7af9-03e8b9b8e283);

IF
TextEvent("DaisyADReset")
AND
DB_DaisyAD_TriggeredOnProfile(_DaisyAD,_UserID)
THEN
NOT DB_DaisyAD_TriggeredOnProfile(_DaisyAD,_UserID);

IF
TextEvent("DaisyADAll")
AND
DB_Players(_Player)
THEN
PROC_DaisyAD(_Player,(DIALOGRESOURCE)TEST_Daisy_AD_91ecf2ab-86fe-4eb0-d3c6-aae6e50a1a28);
//END_REGION

//REGION Daisy Desire/Reason/Fear Track
//Start off on the Desire Track
PROC
PROC_DaisyDesire_SetDaisyFor((CHARACTER)_Char, (CHARACTER)_Daisy, (STRING)_Gender)
THEN
SetTag(_Char,(TAG)DAISY_DESIRETRACK_8f815f65-6fe5-4671-8e2b-63575e1f2e88);
SetTag(_Daisy,(TAG)DAISY_DESIRETRACK_8f815f65-6fe5-4671-8e2b-63575e1f2e88);

//Setting a Track tag on Daisy sets it on the avatar as well
IF
TagSet(_Daisy,_DaisyTrackTag)
AND
DB_GLO_Daisy_TrackTags(_DaisyTrackTag)
AND
DB_GLO_DaisyDreams_Relationships(_Avatar, (CHARACTER)_Daisy)
THEN
SetTag(_Avatar,_DaisyTrackTag);

//Setting a Track tag on an avatar sets it on the Daisy as well
IF
TagSet(_Avatar,_DaisyTrackTag)
AND
DB_GLO_Daisy_TrackTags(_DaisyTrackTag)
AND
DB_GLO_DaisyDreams_Relationships((CHARACTER)_Avatar, _Daisy)
THEN
SetTag(_Daisy,_DaisyTrackTag);

//Setting a Track tag, clears the other ones. (You can only be on one track at a time)
IF
TagSet(_Character,_DaisyTrackTag)
AND
DB_GLO_Daisy_TrackTags(_DaisyTrackTag)
AND
DB_GLO_Daisy_TrackTags(_OldTrackTag)
AND
_OldTrackTag != _DaisyTrackTag
THEN
ClearTag(_Character,_OldTrackTag);

IF
TextEvent("daisydesire")
AND
GetHostCharacter(_Avatar)
AND
DB_GLO_DaisyDreams_Relationships(_Avatar, _Daisy)
THEN
SetTag(_Avatar,(TAG)DAISY_DESIRETRACK_8f815f65-6fe5-4671-8e2b-63575e1f2e88);

IF
TextEvent("daisyreason")
AND
GetHostCharacter(_Avatar)
AND
DB_GLO_DaisyDreams_Relationships(_Avatar, _Daisy)
THEN
SetTag(_Avatar,(TAG)DAISY_REASONTRACK_6ea65c6d-3172-4478-9530-b74cbdcbb870);

IF
TextEvent("daisyfear")
AND
GetHostCharacter(_Avatar)
AND
DB_GLO_DaisyDreams_Relationships(_Avatar, _Daisy)
THEN
SetTag(_Avatar,(TAG)DAISY_FEARTRACK_c1400eed-4e63-4cb6-8ae1-f616505fda1a);

IF
FlagSet((FLAG)GLO_Daisy_Event_SwapTrack_Desire_74a03dc6-4521-4781-a1a9-615e29d3813c,_PlayerOrDaisy,_Inst)
THEN
SetTag(_PlayerOrDaisy,(TAG)DAISY_DESIRETRACK_8f815f65-6fe5-4671-8e2b-63575e1f2e88);
ClearFlag((FLAG)GLO_Daisy_Event_SwapTrack_Desire_74a03dc6-4521-4781-a1a9-615e29d3813c,_PlayerOrDaisy,_Inst);

IF
FlagSet((FLAG)GLO_Daisy_Event_SwapTrack_Reason_27c31264-0ae1-4c7a-8391-942a5fd5a694,_PlayerOrDaisy,_Inst)
THEN
SetTag(_PlayerOrDaisy,(TAG)DAISY_REASONTRACK_6ea65c6d-3172-4478-9530-b74cbdcbb870);
ClearFlag((FLAG)GLO_Daisy_Event_SwapTrack_Reason_27c31264-0ae1-4c7a-8391-942a5fd5a694,_PlayerOrDaisy,_Inst);

IF
FlagSet((FLAG)GLO_Daisy_Event_SwapTrack_Fear_bd34a8fe-fc25-4155-98da-2436b032f8d2,_PlayerOrDaisy,_Inst)
THEN
SetTag(_PlayerOrDaisy,(TAG)DAISY_FEARTRACK_c1400eed-4e63-4cb6-8ae1-f616505fda1a);
ClearFlag((FLAG)GLO_Daisy_Event_SwapTrack_Fear_bd34a8fe-fc25-4155-98da-2436b032f8d2,_PlayerOrDaisy,_Inst);

//END_REGION

//REGION Daisy/Emperor Inclusion
PROC
PROC_StartDialog_AddInclusionSpeakers(_DialogResource,_Inst)
AND
NOT DB_GLO_DaisyAvatarDialog(_DialogResource) //Daisy dreams don't use the inclusion system. They already have Daisy or Emperor added as required speaker.
AND
NOT QRY_GLO_DaisyDreams_BlockInclusion((DIALOGRESOURCE)_DialogResource)
AND
DB_DialogRequestCache_SpeakerList_Players(_DialogResource,_Inst,_Player,1)
AND
QRY_GLO_DaisyDreams_GetDaisy((CHARACTER)_Player)
AND
DB_QRYRTN_GLO_DaisyDreams_GetDaisy((CHARACTER)_Daisy)
AND
QRY_GLO_DaisyDreams_GetEmperor((CHARACTER)_Player)
AND
DB_QRYRTN_GLO_DaisyDreams_GetEmperor((CHARACTER)_Emperor)
THEN
PROC_StartDialog_TryAddDaisyOrEmperor(_Inst,_Daisy);
PROC_StartDialog_TryAddDaisyOrEmperor(_Inst,_Emperor);

PROC
PROC_StartDialog_TryAddDaisyOrEmperor((INTEGER)_Inst,(CHARACTER)_DaisyOrEmperor)
AND
NOT DB_DialogRequestCache_SpeakerList_Speakers(_,_Inst,_DaisyOrEmperor,_)
THEN
PROC_DialogAddSpeakingActor(_Inst,_DaisyOrEmperor,0,1);

QRY
QRY_GLO_DaisyDreams_BlockInclusion((DIALOGRESOURCE)_DialogResource)
AND
1 == 0
THEN
DB_NOOP(1);


//END_REGION

//REGION Test Daisy/Emperor Inclusions
IF
TextEvent("daisyincl")
AND
GetHostCharacter(_Player)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)Test_DaisyInclusion_1d7dc2f1-13b5-1a20-b63f-26f0e9ab815a,_Player)
THEN
DB_NOOP(1);
//END_REGION

//REGION Dream Emperors
//If you don't have a dream emperor assigned, grab a free one
IF
DB_GLO_DaisyDreams_Relationships(_Avatar, _Daisy)
AND
DB_GLO_DaisyDreams_Emperors(_Emperor)
AND
NOT DB_GLO_EmperorDreams_Relationships(_Avatar, _)
AND
NOT DB_GLO_EmperorDreams_Relationships(_,_Emperor)
THEN
DB_GLO_EmperorDreams_Relationships(_Avatar,_Emperor);

IF
DB_GLO_EmperorDreams_Relationships(_,_Emperor)
AND
DB_CurrentLevel(_CurrentLevel)
AND
DB_Daisy_IdleTeleport(_CurrentLevel, _TeleportPos)
THEN
TeleportTo(_Emperor,_TeleportPos);

IF
DB_GLO_EmperorBackups(_Daisy)
AND
DB_CurrentLevel(_CurrentLevel)
AND
DB_Daisy_IdleTeleport(_CurrentLevel, _TeleportPos)
THEN
TeleportTo(_Daisy,_TeleportPos);

IF
DB_Camp_EmperorAvatarDreamDialog(_Dialog)
THEN
DB_GLO_DaisyAvatarDialog(_Dialog);

QRY
QRY_Camp_StartAvatarDream_CustomDialogStart((DIALOGRESOURCE)_DaisyDialog,(CHARACTER)_Player)
AND
DB_Camp_EmperorAvatarDreamDialog(_DaisyDialog)
AND
QRY_GLO_DaisyDreams_GetEmperor(_Player)
AND
DB_QRYRTN_GLO_DaisyDreams_GetEmperor((CHARACTER)_Emperor)
AND
QRY_StartDialogCustom_Fixed(_DaisyDialog,_Emperor,_Player, 0, 0, 0, 0)
THEN
DB_NOOP(1);

QRY
QRY_Camp_StartAvatarDream_CustomDialogStart((DIALOGRESOURCE)_DaisyDialog,(CHARACTER)_Player)
AND
DB_Camp_EmperorAvatarDreamDialog(_DaisyDialog)
THEN
DB_NOOP(1);

//Clean return value
QRY
QRY_GLO_DaisyDreams_GetEmperor((CHARACTER)_Player)
AND
DB_QRYRTN_GLO_DaisyDreams_GetEmperor(_Daisy)
THEN
NOT DB_QRYRTN_GLO_DaisyDreams_GetEmperor(_Daisy);

//Existing Daisy found
QRY
QRY_GLO_DaisyDreams_GetEmperor((CHARACTER)_Avatar)
AND
DB_GLO_EmperorDreams_Relationships(_Avatar, _Daisy)
THEN
PROC_GLO_DaisyDreams_GetEmperor_Attempt(_Daisy,_Avatar);

//If companion, return bonded avatar's Daisy
QRY
QRY_GLO_DaisyDreams_GetEmperor((CHARACTER)_Companion)
AND
NOT DB_GLO_EmperorDreams_Relationships(_Companion, _)
AND
QRY_GetBestAvatarForCompanion(_Companion)
AND
DB_QRYRTN_GetBestAvatarForCompanion(_Companion,_Avatar)
AND
DB_GLO_EmperorDreams_Relationships(_Avatar, _Daisy)
THEN
PROC_GLO_DaisyDreams_GetEmperor_Attempt(_Daisy,_Avatar);

PROC
PROC_GLO_DaisyDreams_GetEmperor_Attempt((CHARACTER)_Daisy,(CHARACTER)_Avatar)
AND
QRY_SpeakerIsAvailable(_Daisy)
THEN
DB_QRYRTN_GLO_DaisyDreams_GetEmperor(_Daisy);
DB_GLO_DaisyDreams_DaisyVoice(_Daisy,_Avatar);

PROC
PROC_GLO_DaisyDreams_GetEmperor_Attempt((CHARACTER)_Daisy,(CHARACTER)_Avatar)
AND
DB_GLO_EmperorBackups(_DaisyBackup)
AND
NOT DB_QRYRTN_GLO_DaisyDreams_GetEmperor(_)
AND
QRY_SpeakerIsAvailable(_DaisyBackup)
THEN
DB_QRYRTN_GLO_DaisyDreams_GetEmperor(_DaisyBackup);
DB_GLO_DaisyDreams_DaisyVoice(_DaisyBackup,_Avatar);

//If no Emperor found, throw error.
QRY
QRY_GLO_DaisyDreams_GetEmperor((CHARACTER)_Character)
AND
NOT DB_QRYRTN_GLO_DaisyDreams_GetEmperor(_)
AND
GUIDToString(_Character,_CharacterUUID)
AND
Concatenate("Could not find Emperor for: ",_CharacterUUID,_EmperorError)
THEN
DebugBreak(_EmperorError);
//END_REGION

//REGION GLO_DaisyADs

IF
FlagSet((FLAG)GLO_Daisy_State_Unlocked_100c65da-7fab-4643-9630-db143451edc9,_,_)
AND
NOT DB_LevelUnreachable("WLD_Main_A")
THEN
DB_GLO_DaisyADs_DoAttackADs(1);

//Casted tadpole power spell Daisy AD
//Player needs to be tadpoled, in combat, not in dialog, not played all
IF
CastedSpell(_Player,_Spell,_,_,_)
AND
//Check tadpole power spell conditions
DB_TadpolePowers_DebugTags(_,_Spell)
AND
_Spell != "Projectile_TAD_Levitate"
AND
_Spell != "Rush_ForceTunnel"
AND
//Check AD conditions
DB_GLO_DaisyADs_DoAttackADs(1)
AND
DB_Players((CHARACTER)_Player)
AND
NOT DB_GLO_DaisyADs_BlockAttackAD(_Player,"GLO_DaisyAD_AttackSuccess")
AND
GetFlag(GLO_DaisyAD_AttackSuccess_PlayedAll_c7957dc0-443d-07c1-48ed-e9a61dc433bc,_Player,0)
AND
IsTagged(_Player,(TAG)ILLITHID_1eec74e8-3673-4500-abec-57b7ed8469ed,1)
AND
//Character conditions
DB_Is_InCombat(_Player,_)
AND
NOT DB_DialogSpeakers(_,_Player,_)
THEN
DB_GLO_DaisyADs_BlockAttackAD(_Player,"GLO_DaisyAD_AttackSuccess");
PROC_DaisyAD(_Player,(DIALOGRESOURCE)GLO_DaisyAD_AttackSuccess_a1e8e75e-2073-e8ae-4cf5-c9adb70bc700);
PROC_RemoveDaisyAD(_Player,(DIALOGRESOURCE)GLO_DaisyAD_AttackSuccess_a1e8e75e-2073-e8ae-4cf5-c9adb70bc700);

//AttackFail Daisy AD
//Needs to be enemy, in combat, not killed, missed
//Player needs to be tadpoled, not in dialog, not played all
IF
MissedBy(_Defender,_,_Player,_StoryActionAD)
AND
//Check AD conditions
DB_GLO_DaisyADs_DoAttackADs(1)
AND
DB_Players((CHARACTER)_Player)
AND
NOT DB_GLO_DaisyADs_BlockAttackAD(_Player,"GLO_DaisyAD_AttackFail")
AND
GetFlag(GLO_DaisyAD_AttackFail_PlayedAll_956b2bc9-0a97-83e3-5fd1-83a2fd34e410,_Player,0)
AND
IsTagged(_Player,(TAG)ILLITHID_1eec74e8-3673-4500-abec-57b7ed8469ed,1)
AND
//Character conditions
NOT DB_Dead((CHARACTER)_Defender)
AND
DB_Is_InCombat(_Player,_)
AND
NOT DB_DialogSpeakers(_,_Player,_)
AND
IsEnemy((CHARACTER)_Defender,_Player,1)
THEN
DB_GLO_DaisyADs_BlockAttackAD(_Player,"GLO_DaisyAD_AttackFail");
PROC_DaisyAD(_Player,(DIALOGRESOURCE)GLO_DaisyAD_AttackFail_46ce77cb-f9ed-0933-e9a6-8095200031d8);
PROC_RemoveDaisyAD(_Player,(DIALOGRESOURCE)GLO_DaisyAD_AttackFail_46ce77cb-f9ed-0933-e9a6-8095200031d8);

//Timeout
IF
DB_GLO_DaisyADs_BlockAttackAD((CHARACTER)_Player,(STRING)_Identifier)
THEN
RealtimeObjectTimerLaunch(_Player,_Identifier,50000);

IF
ObjectTimerFinished(_Player,_Identifier)
AND
DB_GLO_DaisyADs_BlockAttackAD((CHARACTER)_Player,_Identifier)
THEN
NOT DB_GLO_DaisyADs_BlockAttackAD(_Player,_Identifier);

//Stop attack ADs after act 2 point of no return
IF
DB_LevelUnreachable("WLD_Main_A")
AND
DB_GLO_DaisyADs_DoAttackADs(1)
THEN
NOT DB_GLO_DaisyADs_DoAttackADs(1);

//Cleanup
IF
DB_GLO_DaisyADs_BlockAttackAD((CHARACTER)_Player,(STRING)_Identifier)
AND
NOT DB_GLO_DaisyADs_DoAttackADs(1)
THEN
NOT DB_GLO_DaisyADs_BlockAttackAD(_Player,_Identifier);

//END_REGION

//REGION NIGHT_DaisyAcknowledgement
//This is an avatar dream in which avatars NOT on the desire track get to see Daisy sleep with Astarion (Vampirelord), Minthara, Karlach or Shadowheart (Shar Path)
//Avatars on the desire track get to have sex with Daisy BUT will eventually be spied on by available companions.
//Order of assignment:
// 1) NOT Desire Track + In a relationship with Astarion (Vampirelord), Minthara, Karlach or Shadowheart (Shar Path)
// 2) NOT Desire Track + NOT In a relationship with Astarion (Vampirelord), Minthara, Karlach or Shadowheart (Shar Path)
// 3) Desire Track: Assign one companion to each avatar (if available)
// 4) Desire Track: Divide the remaining companions (max 2 per avatar) as extra peanut voyeurs.

//1) NOT Desire + Lovers
PROC
PROC_CampNight_StartSleepMoments_PreHook()
AND
DB_Camp_QueuedAvatarDream((DIALOGRESOURCE)CAMP_DaisyAcknowledgement_AvD_ROM_58e20a32-e432-1c93-9c75-97bebafc8b1b)
AND
DB_Avatars(_Avatar)
AND
IsTagged(_Avatar,(TAG)DAISY_DESIRETRACK_8f815f65-6fe5-4671-8e2b-63575e1f2e88,0) //Does NOT have Desire Track = Reason Track or Fear Track
THEN
PROC_CAMP_DaisyAcknowledgement_FindPrimaryPartner_Lover(_Avatar);

//2) NOT Desire + NOT Lovers
PROC
PROC_CampNight_StartSleepMoments_PreHook()
AND
DB_Camp_QueuedAvatarDream((DIALOGRESOURCE)CAMP_DaisyAcknowledgement_AvD_ROM_58e20a32-e432-1c93-9c75-97bebafc8b1b)
AND
DB_Avatars(_Avatar)
AND
IsTagged(_Avatar,(TAG)DAISY_DESIRETRACK_8f815f65-6fe5-4671-8e2b-63575e1f2e88,0) //Does NOT have Desire Track = Reason Track or Fear Track
THEN
PROC_CAMP_DaisyAcknowledgement_FindPrimaryPartner(_Avatar);

//3-1) Desire (Try to give each avatar one companion) (Three times!)
PROC
PROC_CampNight_StartSleepMoments_PreHook()
AND
DB_Camp_QueuedAvatarDream((DIALOGRESOURCE)CAMP_DaisyAcknowledgement_AvD_ROM_58e20a32-e432-1c93-9c75-97bebafc8b1b)
AND
DB_Avatars(_Avatar)
AND
IsTagged(_Avatar,(TAG)DAISY_DESIRETRACK_8f815f65-6fe5-4671-8e2b-63575e1f2e88,1) //Has Desire Track
THEN
PROC_CAMP_DaisyAcknowledgement_FindSecondaryPartner(_Avatar,8);

//3-2) Desire (Try to give each avatar one companion) (Three times!)
PROC
PROC_CampNight_StartSleepMoments_PreHook()
AND
DB_Camp_QueuedAvatarDream((DIALOGRESOURCE)CAMP_DaisyAcknowledgement_AvD_ROM_58e20a32-e432-1c93-9c75-97bebafc8b1b)
AND
DB_Avatars(_Avatar)
AND
IsTagged(_Avatar,(TAG)DAISY_DESIRETRACK_8f815f65-6fe5-4671-8e2b-63575e1f2e88,1) //Has Desire Track
THEN
PROC_CAMP_DaisyAcknowledgement_FindSecondaryPartner(_Avatar,9);

//3-3) Desire (Try to give each avatar one companion) (Three times!)
PROC
PROC_CampNight_StartSleepMoments_PreHook()
AND
DB_Camp_QueuedAvatarDream((DIALOGRESOURCE)CAMP_DaisyAcknowledgement_AvD_ROM_58e20a32-e432-1c93-9c75-97bebafc8b1b)
AND
DB_Avatars(_Avatar)
AND
IsTagged(_Avatar,(TAG)DAISY_DESIRETRACK_8f815f65-6fe5-4671-8e2b-63575e1f2e88,1) //Has Desire Track
THEN
PROC_CAMP_DaisyAcknowledgement_FindSecondaryPartner(_Avatar,10);

//Lover VampireLord Astarion
PROC
PROC_CAMP_DaisyAcknowledgement_FindPrimaryPartner_Lover((CHARACTER)_Avatar)
AND
NOT DB_CAMP_DaisyAcknowledgement_Partner(_Avatar,_,_)
AND
DB_ORI_Partnered(_Avatar,(CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255)
AND
DB_InCamp((CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255)
AND
NOT DB_CantTalk((CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255)
AND
DB_GlobalFlag((FLAG)ORI_Astarion_State_BecameVampireLord_c446ce94-efd8-45d5-b407-284177b6b57e)
THEN
DB_CAMP_DaisyAcknowledgement_Partner(_Avatar,(CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255,4);

//Lover Minthara
PROC
PROC_CAMP_DaisyAcknowledgement_FindPrimaryPartner_Lover((CHARACTER)_Avatar)
AND
NOT DB_CAMP_DaisyAcknowledgement_Partner(_Avatar,_,_)
AND
DB_ORI_Partnered(_Avatar,(CHARACTER)S_GOB_DrowCommander_25721313-0c15-4935-8176-9f134385451b)
AND
DB_InCamp((CHARACTER)S_GOB_DrowCommander_25721313-0c15-4935-8176-9f134385451b)
AND
NOT DB_CantTalk((CHARACTER)S_GOB_DrowCommander_25721313-0c15-4935-8176-9f134385451b)
THEN
DB_CAMP_DaisyAcknowledgement_Partner(_Avatar,(CHARACTER)S_GOB_DrowCommander_25721313-0c15-4935-8176-9f134385451b,5);

//Lover Karlach
PROC
PROC_CAMP_DaisyAcknowledgement_FindPrimaryPartner_Lover((CHARACTER)_Avatar)
AND
NOT DB_CAMP_DaisyAcknowledgement_Partner(_Avatar,_,_)
AND
DB_ORI_Partnered(_Avatar,(CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
DB_InCamp((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
NOT DB_CantTalk((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
DB_CAMP_DaisyAcknowledgement_Partner(_Avatar,(CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c,6);

//Lover Shadowheart on Shar Path
PROC
PROC_CAMP_DaisyAcknowledgement_FindPrimaryPartner_Lover((CHARACTER)_Avatar)
AND
NOT DB_CAMP_DaisyAcknowledgement_Partner(_Avatar,_,_)
AND
DB_ORI_Partnered(_Avatar,(CHARACTER)S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679)
AND
DB_InCamp((CHARACTER)S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679)
AND
NOT DB_CantTalk((CHARACTER)S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679)
AND
DB_GlobalFlag((FLAG)ORI_Shadowheart_State_SharPath_bf9ae334-ff6a-458d-b898-3074bca0bdfb)
THEN
DB_CAMP_DaisyAcknowledgement_Partner(_Avatar,(CHARACTER)S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679,7);

//VampireLord Astarion
PROC
PROC_CAMP_DaisyAcknowledgement_FindPrimaryPartner((CHARACTER)_Avatar)
AND
NOT DB_CAMP_DaisyAcknowledgement_Partner(_Avatar,_,_)
AND
NOT DB_CAMP_DaisyAcknowledgement_Partner(_,(CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255,_)
AND
NOT DB_Avatars((CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255)
AND
DB_InCamp((CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255)
AND
NOT DB_CantTalk((CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255)
AND
DB_GlobalFlag((FLAG)ORI_Astarion_State_BecameVampireLord_c446ce94-efd8-45d5-b407-284177b6b57e)
THEN
DB_CAMP_DaisyAcknowledgement_Partner(_Avatar,(CHARACTER)S_Player_Astarion_c7c13742-bacd-460a-8f65-f864fe41f255,4);

//Minthara
PROC
PROC_CAMP_DaisyAcknowledgement_FindPrimaryPartner((CHARACTER)_Avatar)
AND
NOT DB_CAMP_DaisyAcknowledgement_Partner(_Avatar,_,_)
AND
NOT DB_CAMP_DaisyAcknowledgement_Partner(_,(CHARACTER)S_GOB_DrowCommander_25721313-0c15-4935-8176-9f134385451b,_)
AND
DB_InCamp((CHARACTER)S_GOB_DrowCommander_25721313-0c15-4935-8176-9f134385451b)
AND
NOT DB_CantTalk((CHARACTER)S_GOB_DrowCommander_25721313-0c15-4935-8176-9f134385451b)
THEN
DB_CAMP_DaisyAcknowledgement_Partner(_Avatar,(CHARACTER)S_GOB_DrowCommander_25721313-0c15-4935-8176-9f134385451b,5);

//Karlach
PROC
PROC_CAMP_DaisyAcknowledgement_FindPrimaryPartner((CHARACTER)_Avatar)
AND
NOT DB_CAMP_DaisyAcknowledgement_Partner(_Avatar,_,_)
AND
NOT DB_CAMP_DaisyAcknowledgement_Partner(_,(CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c,_)
AND
NOT DB_Avatars((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
DB_InCamp((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
AND
NOT DB_CantTalk((CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c)
THEN
DB_CAMP_DaisyAcknowledgement_Partner(_Avatar,(CHARACTER)S_Player_Karlach_2c76687d-93a2-477b-8b18-8a14b549304c,6);

//Shadowheart on Shar Path
PROC
PROC_CAMP_DaisyAcknowledgement_FindPrimaryPartner((CHARACTER)_Avatar)
AND
NOT DB_CAMP_DaisyAcknowledgement_Partner(_Avatar,_,_)
AND
NOT DB_CAMP_DaisyAcknowledgement_Partner(_,(CHARACTER)S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679,_)
AND
NOT DB_Avatars((CHARACTER)S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679)
AND
DB_InCamp((CHARACTER)S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679)
AND
NOT DB_CantTalk((CHARACTER)S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679)
AND
DB_GlobalFlag((FLAG)ORI_Shadowheart_State_SharPath_bf9ae334-ff6a-458d-b898-3074bca0bdfb)
THEN
DB_CAMP_DaisyAcknowledgement_Partner(_Avatar,(CHARACTER)S_Player_ShadowHeart_3ed74f06-3c60-42dc-83f6-f034cb47c679,7);

PROC
PROC_CAMP_DaisyAcknowledgement_FindSecondaryPartner((CHARACTER)_Avatar,(INTEGER)_SpeakerSlot)
AND
QRY_OnlyOnce_Reset("CAMP_DaisyAcknowledgement_FindSecondaryPartner")
AND
DB_InCamp(_Companion)
AND
DB_Origins(_Companion)
AND
NOT DB_Avatars(_Companion)
AND
NOT DB_CantTalk(_Companion)
AND
NOT DB_CAMP_DaisyAcknowledgement_Partner(_,_Companion,_)
AND
QRY_OnlyOnce("CAMP_DaisyAcknowledgement_FindSecondaryPartner")
THEN
DB_CAMP_DaisyAcknowledgement_Partner(_Avatar,_Companion,_SpeakerSlot);

QRY
QRY_Inclusion_BlockCompanionInclusion((GUIDSTRING)_Companion,(DIALOGRESOURCE)CAMP_DaisyAcknowledgement_AvD_ROM_58e20a32-e432-1c93-9c75-97bebafc8b1b)
THEN
DB_NOOP(1);

QRY
QRY_Camp_StartAvatarDream_CustomDialogStart((DIALOGRESOURCE)CAMP_DaisyAcknowledgement_AvD_ROM_58e20a32-e432-1c93-9c75-97bebafc8b1b,(CHARACTER)_Player)
AND
QRY_GLO_DaisyDreams_GetDaisy(_Player)
AND
DB_QRYRTN_GLO_DaisyDreams_GetDaisy((CHARACTER)_Daisy)
AND
QRY_GLO_DaisyDreams_GetEmperor(_Player)
AND
DB_QRYRTN_GLO_DaisyDreams_GetEmperor((CHARACTER)_Emperor)
AND
QRY_StartDialogCustom((DIALOGRESOURCE)CAMP_DaisyAcknowledgement_AvD_ROM_58e20a32-e432-1c93-9c75-97bebafc8b1b,_Emperor,_Daisy,_Player, 0, 0, 0, 0)
THEN
PROC_GLO_DaisyDreams_ChangeClothing((DIALOGRESOURCE)CAMP_DaisyAcknowledgement_AvD_ROM_58e20a32-e432-1c93-9c75-97bebafc8b1b,_Daisy);

PROC
PROC_StartDialog_AddExtraSpeakers((DIALOGRESOURCE)CAMP_DaisyAcknowledgement_AvD_ROM_58e20a32-e432-1c93-9c75-97bebafc8b1b,_Inst)
AND
DB_DialogRequestCache_SpeakerList_Players((DIALOGRESOURCE)CAMP_DaisyAcknowledgement_AvD_ROM_58e20a32-e432-1c93-9c75-97bebafc8b1b,_Inst,_Avatar,1)
AND
DB_CAMP_DaisyAcknowledgement_Partner((CHARACTER)_Avatar,_Companion,_SpeakerSlot)
THEN
PROC_DialogAddSpeakingActorAt(_Inst,_Companion,_SpeakerSlot);

//If the custom start doesn't trigger, we'd rather have no dream than a dream without Daisy
QRY
QRY_Camp_StartAvatarDream_CustomDialogStart((DIALOGRESOURCE)CAMP_DaisyAcknowledgement_AvD_ROM_58e20a32-e432-1c93-9c75-97bebafc8b1b,(CHARACTER)_Player)
THEN
DB_NOOP(1);

//END_REGION

//REGION CRE_DaisyGithyankiWarning
IF
DB_CurrentLevel("CRE_Main_A")
AND
QRY_OnlyOnce("CRE_DaisyGithyankiWarningArea_Register")
AND
DB_CRE_DaisyGithyankiWarningAreas(_Trigger)
THEN
PROC_TriggerRegisterForPlayers(_Trigger);

IF
EnteredTrigger(_Player,_DaisyGithyankiWarningArea)
AND
DB_CRE_DaisyGithyankiWarningAreas(_DaisyGithyankiWarningArea)
THEN
PROC_DaisyAD(_Player,(DIALOGRESOURCE)CRE_DaisyGithyankiWarning_856f6467-5afc-7561-7af9-03e8b9b8e283);

//Cutoff point: Player visited Astral Prison
IF
EnteredTrigger(_Player,(TRIGGER)S_CRE_AstralPlaneArea_cc0ba8c0-ae50-4f12-8ea4-7db1ff8e62b0)
AND
DB_CRE_DaisyGithyankiWarningAreas(_Trigger)
THEN
PROC_TriggerUnregisterForPlayers(_Trigger);
//END_REGION

//REGION WYR_DaisyEmperorBalduran
/*
IF
DialogEnded(WYR_AnsurGhost_a4d1b437-aa2f-b343-bf27-a336b42b5dee,_Inst)
AND
DB_State_Current(S_WYR_SkeletalDragon_67770922-5e0a-40c5-b3f0-67e8eb50493a, "WYR_SkeletalDragon", "DeadAgain")
AND
DB_DialogPlayers(_Inst,_Player,1)
THEN
TeleportTo(S_GLO_Emperor_73d49dc5-8b8b-45dc-a98c-927bb4e3169b,_Player);
PROC_WYR_TryStart_DaisyEmperorBalduran((CHARACTER)_Player);
*/

//Attempt 1: controlled player = avatar
PROC
PROC_WYR_TryStart_DaisyEmperorBalduran((CHARACTER)_Player)
AND
NOT DB_DaisyEmperorBalduran_Happened(1)
AND
DB_Avatars(_Player)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)WYR_DaisyEmperorBalduran_fe2393a8-08d6-4be5-e669-55562b193275,S_GLO_Emperor_73d49dc5-8b8b-45dc-a98c-927bb4e3169b,_Player)
THEN
DB_DaisyEmperorBalduran_Happened(1);

//Attempt 2: Nearby Avatar assigned to same player
PROC
PROC_WYR_TryStart_DaisyEmperorBalduran((CHARACTER)_Player)
AND
NOT DB_DaisyEmperorBalduran_Happened(1)
AND
DB_Avatars(_Avatar)
AND
QRY_SameUser(_Player,_Avatar)
AND
QRY_SpeakerIsAvailableAndInDialogRange(_Avatar,_Player)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)WYR_DaisyEmperorBalduran_fe2393a8-08d6-4be5-e669-55562b193275,S_GLO_Emperor_73d49dc5-8b8b-45dc-a98c-927bb4e3169b,_Avatar)
THEN
DB_DaisyEmperorBalduran_Happened(1);

//Attempt 3: Nearby Avatar NOT assigned to same player
PROC
PROC_WYR_TryStart_DaisyEmperorBalduran((CHARACTER)_Player)
AND
NOT DB_DaisyEmperorBalduran_Happened(1)
AND
DB_Avatars(_Avatar)
AND
QRY_SpeakerIsAvailableAndInDialogRange(_Avatar,_Player)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)WYR_DaisyEmperorBalduran_fe2393a8-08d6-4be5-e669-55562b193275,S_GLO_Emperor_73d49dc5-8b8b-45dc-a98c-927bb4e3169b,_Avatar)
THEN
DB_DaisyEmperorBalduran_Happened(1);

//Attempt 4: The player that was talking to Ansur/Ghost
PROC
PROC_WYR_TryStart_DaisyEmperorBalduran((CHARACTER)_Player)
AND
NOT DB_DaisyEmperorBalduran_Happened(1)
AND
QRY_StartDialog_Fixed((DIALOGRESOURCE)WYR_DaisyEmperorBalduran_fe2393a8-08d6-4be5-e669-55562b193275,S_GLO_Emperor_73d49dc5-8b8b-45dc-a98c-927bb4e3169b,_Player)
THEN
DB_DaisyEmperorBalduran_Happened(1);

//Fallback
PROC
PROC_WYR_TryStart_DaisyEmperorBalduran((CHARACTER)_Player)
AND
NOT DB_DaisyEmperorBalduran_Happened(1)
THEN
PROC_Poof(S_GLO_Emperor_73d49dc5-8b8b-45dc-a98c-927bb4e3169b);

IF
DialogEnded((DIALOGRESOURCE)WYR_DaisyEmperorBalduran_fe2393a8-08d6-4be5-e669-55562b193275,_)
THEN
PROC_Poof(S_GLO_Emperor_73d49dc5-8b8b-45dc-a98c-927bb4e3169b);

IF
DialogRequestFailed((DIALOGRESOURCE)WYR_DaisyEmperorBalduran_fe2393a8-08d6-4be5-e669-55562b193275,_)
THEN
PROC_Poof(S_GLO_Emperor_73d49dc5-8b8b-45dc-a98c-927bb4e3169b);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ModWrapper_Gustav"
